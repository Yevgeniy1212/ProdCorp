
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("ПечатныйДокумент") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,Параметры,"ОбъектПечати,ПечатныйДокумент,Заголовок");
	ОбновитьДанные();
	ПолеДокумента.ОтображатьЗаголовки = Ложь;
	ПолеДокумента.ОтображатьСетку     = Ложь;
	ПолеДокумента.Защита              = Ложь;
	ПолеДокумента.ТолькоПросмотр      = Истина;
	ПолеДокумента.ИмяПараметровПечати = ПечатныйДокумент.ИмяПараметровПечати;
	ПолеДокумента.ЧерноБелыйПросмотр  = Ложь;
	ПолеДокумента.ЧерноБелаяПечать    = Истина;
	ПолеДокумента.ПовторятьПриПечатиСтроки = ПечатныйДокумент.ПовторятьПриПечатиСтроки;
	Элементы.ТабличныйДокумент.ТолькоПросмотр = Истина;
	Элементы.ТабличныйДокумент.Редактирование = Ложь;
	Элементы.ТолькоПросмотр.Пометка = Истина;
	КоличествоКопий = Макс(ПолеДокумента.КоличествоЭкземпляров, 1);
    фин_ОбщегоНазначенияСервер.НастроитьКнопкиОтправки(ЭтотОбъект,Элементы.ГруппаКнопок);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаПочту(Кнопка)
	общ_ОбщегоНазначенияКлиент.ОтправкаПечатнойФормыПоЭлПочте(ЭтотОбъект, ПолеДокумента);
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	ОбновитьДанные();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанные()
	ПолеДокумента.Очистить();
	ПолеДокумента.ВставитьОбласть(ПечатныйДокумент.Область());
КонецПроцедуры


&НаКлиенте
Процедура ТолькоПросмотр(Команда)
	Элементы.ТолькоПросмотр.Пометка	= НЕ Элементы.ТолькоПросмотр.Пометка;
	ПолеДокумента.ТолькоПросмотр 	= НЕ Элементы.ТолькоПросмотр.Пометка;
	Элементы.ТабличныйДокумент.Редактирование = НЕ Элементы.ТабличныйДокумент.Редактирование;
	Элементы.ТабличныйДокумент.ТолькоПросмотр = НЕ Элементы.ТабличныйДокумент.ТолькоПросмотр;
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	ПолеДокумента.КоличествоЭкземпляров = КоличествоКопий; 
	ПолеДокумента.Напечатать(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКопию(Команда)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = "Save as";
	Диалог.ПолноеИмяФайла = СтрЗаменить(ЭтотОбъект.Заголовок,".","");
	Диалог.ПредварительныйПросмотр = Ложь;
	Диалог.Фильтр = ПолучитьФильтрФайлов();
	ОписаниеОбработкаВыбораФайла = Новый ОписаниеОповещения("ОбработкаВыбораФайла",ЭтотОбъект);
	Диалог.Показать(ОписаниеОбработкаВыбораФайла);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораФайла(ВыбранныеФайлы,ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы<>Неопределено И ВыбранныеФайлы.Количество()>0 Тогда
		ИмяФайла = ВыбранныеФайлы[0];//Диалог.ПолноеИмяФайла;
		Попытка
			ПолеДокумента.Записать(ИмяФайла, ?(Прав(ИмяФайла,3) = "xls", ТипФайлаТабличногоДокумента.XLS, ТипФайлаТабличногоДокумента.MXL));
		Исключение
			ПоказатьПредупреждение(,"Ошибка при записи. Файл не записан.");
		КонецПопытки;		
	КонецЕсли;
КонецПроцедуры

// Формирует строку фильтра для диалога выбора файла с типами файлов.
//
// Параметры
//  Нет.
//
// Возвращаемое значение:
//   Строка – фильтр по типам файлов для диалога выбора файла.
//
&НаКлиенте
Функция ПолучитьФильтрФайлов() Экспорт

	Возврат "Табличный документ (*.mxl)|*.mxl|"
	      + "Документ Microsoft Excel (*.xls)|*.xls|";

КонецФункции // ПолучитьФильтрФайлов()


