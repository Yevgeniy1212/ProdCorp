
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Список") Тогда
		//Список.ЗагрузитьЗначения(Параметры.Список.ВыгрузитьЗначения());
		Список=Параметры.Список;
		Если Список.Количество()=0 И Параметры.ПервоначальноеЗаполнение Тогда
			Список.Добавить("ЗначениеПоля","Значение разреза");
		КонецЕсли;
	Иначе
		Список.Добавить("ЗначениеПоля","Значение разреза");
	КонецЕсли;
	Элементы.СписокПометка.Видимость=Параметры.Свойство("ОтображениеПометки",Элементы.СписокПометка.Заголовок);
	Если Параметры.Свойство("Группировки") Тогда
		Группировки = Параметры.Группировки;
		ТипыГруппировок = Параметры.ТипыГруппировок;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикВыбораПоля",ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.фин_ДеревоДополнительныхПолей",Новый Структура("Измерение,БазовыйПуть,Группировки,ТипыГруппировок",Параметры.Разрез,"ЗначениеПоля",Группировки,ТипыГруппировок),Элемент,УникальныйИдентификатор,ВариантОткрытияОкна.ОтдельноеОкно,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораПоля(РезультатВыбора,ДополнительныеПараметры) Экспорт
	Если РезультатВыбора<>Неопределено Тогда
		Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока).Значение = РезультатВыбора;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Если ПроверитьСписокПолей() Тогда
		Закрыть(Список);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПроверитьСписокПолей()
	Для Каждого ЭлементСписка Из Список Цикл
		Если ТипЗнч(ЭлементСписка.Значение)=Тип("Строка") И Найти(ЭлементСписка.Значение,".")<>0 Тогда
			ИмяКорня = Лев(ЭлементСписка.Значение,Найти(ЭлементСписка.Значение,".")-1);
			Если ИмяКорня = "ЗначениеПоля" Тогда
				Продолжить;
			КонецЕсли;
			НайденоРодительскоеПоле = Ложь;
			РодительскоеПоле = Неопределено;
			Для Каждого ЭлементВыбора Из Группировки Цикл
				Если ТипЗнч(ЭлементВыбора.Значение)<>Тип("Строка") И фин_ОбщегоНазначенияСервер.ПолучитьИмяЭлементаПеречисленияПоЗначению(ЭлементВыбора.Значение)=ИмяКорня Тогда
					РодительскоеПоле = ЭлементВыбора.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если РодительскоеПоле = Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Некорректное поле "+ЭлементСписка.Значение);
				Возврат Ложь;
			Иначе
				НайденоРодительскоеПоле = Список.НайтиПоЗначению(РодительскоеПоле)<>Неопределено;
				Если НайденоРодительскоеПоле = Ложь Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В список полей не включено родительское поле для "+ЭлементСписка.Значение);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура СписокЗначениеПриИзменении(Элемент)
	Список.НайтиПоИдентификатору(Элементы.Список.ТекущаяСтрока).Представление = "";	
КонецПроцедуры
