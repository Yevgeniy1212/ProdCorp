
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресСервераСЛК 				= Константы.общ_АдресСервераСЛК.Получить();
	ПортСервераСЛК 					= Константы.общ_ПортСервераСЛК.Получить();
	Серия 							= общ_ЗащитаКлиентСервер.Серия();
	ВерсияБиблиотекЗащиты 			= общ_ПроцедурыМеханизмаЗащиты.ВерсияБиблиотекЗащиты();
	ВерсияКонфигурацииПоставщика 	= общ_ОбщегоНазначения.ВерсияКонфигурации();
	ВерсияИнформационнойБазыКлиента = общ_ОбщегоНазначения.ВерсияКонфигурацииИБ();
	
	ОбновитьИнформациюОСоединении(ЭтаФорма);
	
	Если АдресСервераСЛК = "" ИЛИ НРег(АдресСервераСЛК) = НРег("localhost") Тогда
		Расположение = Перечисления.общ_ВариантыРасположенияСервераСЛК.НаЛокальномКомпьютере;
		АдресСервераСЛК = "localhost";
		ПортСервераСЛК = 9099;
	Иначе
		Расположение = Перечисления.общ_ВариантыРасположенияСервераСЛК.НаКомпьютереВСети;
	КонецЕсли;
	
	НастроитьДоступностьПараметровСвязи(ЭтаФорма); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РасположениеПриИзменении(Элемент)
	
	Если Расположение = ПредопределенноеЗначение("Перечисление.общ_ВариантыРасположенияСервераСЛК.НаЛокальномКомпьютере") Тогда
		АдресСервераСЛК = "localhost";
	Иначе
		АдресСервераСЛК = "";
	КонецЕсли;
	
	ПортСервераСЛК = 9099;
	
	НастроитьДоступностьПараметровСвязи(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьСоединение(Команда)
	
	Результат = УстановитьСоединениеССервером();
	
	Если Результат И ЭтаФорма.Открыта() Тогда
		Закрыть(Истина);
	КонецЕсли;
	
	ОбновитьИнформациюОСоединении(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КонсольСервераСЛК(Команда)
	
	Попытка
		АдресКонсолиСЛК = общ_ЗащитаКлиентСервер.АдресКонсоли();
	Исключение
	КонецПопытки;
	
	Если Найти(АдресКонсолиСЛК, ?(АдресСервераСЛК = "localhost", "127.0.0.1", АдресСервераСЛК)) = 0 
		И Найти(АдресКонсолиСЛК, ПортСервераСЛК) = 0 Тогда
		Если УстановитьСоединениеССервером() Тогда
			ОбновитьПовторноИспользуемыеЗначения();
			АдресКонсолиСЛК = общ_ЗащитаКлиентСервер.АдресКонсоли();
		Иначе	
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ЗапуститьПриложение(АдресКонсолиСЛК);
	#Иначе
		ПерейтиПоНавигационнойСсылке(АдресКонсолиСЛК);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьДоступностьПараметровСвязи(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ПортСервераСЛК.ТолькоПросмотр = (Форма.Расположение = ПредопределенноеЗначение("Перечисление.общ_ВариантыРасположенияСервераСЛК.НаЛокальномКомпьютере")); 
	Элементы.АдресСервераСЛК.ТолькоПросмотр = (Форма.Расположение = ПредопределенноеЗначение("Перечисление.общ_ВариантыРасположенияСервераСЛК.НаЛокальномКомпьютере")); 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИнформациюОСоединении(Форма)

	Попытка
		Форма.ВерсияКомпонентыСЛК = общ_ЗащитаКлиентСервер.Версия();
	Исключение
	КонецПопытки;
	
	Попытка
		Форма.ДанныеСеанса = общ_ЗащитаКлиентСервер.ДанныеСеанса();
	Исключение
	КонецПопытки;
	
	Попытка
		Форма.ЛицензияДоступна = общ_ЗащитаКлиентСервер.ПроверитьЛицензию();
	Исключение
	КонецПопытки;
	
	Форма.Элементы.ОписаниеЛицензии.Видимость = Форма.ЛицензияДоступна;
	
	Попытка
		Если Форма.ЛицензияДоступна Тогда
			Форма.ОписаниеЛицензии = общ_ОбщегоНазначения.ОписаниеЛицензии(общ_ЗащитаКлиентСервер.ПолучитьЛицензию(), Ложь);
			Форма.ИнформацияДляОтладки = общ_ЗащитаКлиентСервер.ПолучитьОтладочнуюСтроку();
		КонецЕсли;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция УстановитьСоединениеССервером()
	
	Константы.общ_АдресСервераСЛК.Установить(СокрЛП(АдресСервераСЛК));
	Константы.общ_ПортСервераСЛК.Установить(ПортСервераСЛК);
	
	Если СокрЛП(АдресСервераСЛК) = "" Тогда
		общ_ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не задан адрес сервера лицензий!'"));
		Возврат Ложь;
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();

	Возврат общ_ЗащитаКлиентСервер.ЗапускМенеджераЛицензий();
	
КонецФункции

#КонецОбласти
