#Если Клиент Тогда

Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок = Ложь, ВысотаЗаголовка = 0, ТолькоЗаголовок = Ложь) Экспорт

	СписокСпособовУдержанийПоИЛ = Новый СписокЗначений;
	СписокСпособовУдержанийПоИЛ.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ИсполнительныйЛистПроцентом);
	СписокСпособовУдержанийПоИЛ.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ИсполнительныйЛистПроцентомДоПредела);
	СписокСпособовУдержанийПоИЛ.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ИсполнительныйЛистФиксСуммойДоПредела);
	СписокСпособовУдержанийПоИЛ.Добавить(Перечисления.СпособыРасчетаОплатыТруда.УдержаниеФиксированнойСуммой);
	СписокСпособовУдержанийПоИЛ.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ПоМесячномуРасчетномуПоказателю);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("СписокСпособовУдержанийПоИЛ", СписокСпособовУдержанийПоИЛ);
		
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("парамПриход", ВидДвиженияНакопления.Приход);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("парамВидСтроки", Перечисления.РасчетыСБюджетомФондамиВидСтроки.Исчисление);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("парамВидПлатежа", Перечисления.ВидыПлатежейВБюджетИФонды.Налог);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Уволен", Перечисления.ПричиныИзмененияСостояния.Увольнение);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ВнутреннееСовместительство", Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ПочтовыйСбор", Перечисления.СпособыРасчетаОплатыТруда.ПочтовыйСбор);
	
	ОбщийОтчет.СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок);
	
КонецПроцедуры

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОбщийОтчет.ИмяРегистра = "-";
	ОбщийОтчет.мНазваниеОтчета = "Свод удержаний по исполнительным листам";
	
	ПостроительОтчета = ОбщийОтчет.ПостроительОтчета;
	
	// структура представлений полей
	СтруктураПредставлениеПолей = Новый Структура();
	
	УдержанияПоИсполнительнымЛистамТекст = 
	"ВЫБРАТЬ
	|	УдержанияОрганизаций.ОбособленноеПодразделение КАК Организация,
	|	УдержанияОрганизаций.ФизЛицо КАК ФизЛицо,
	|	УдержанияОрганизаций.ВидРасчета КАК ВидРасчета,
	|	УдержанияОрганизаций.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	УдержанияОрганизаций.ДокументОснование КАК ДокументОснование,
	|	УдержанияОрганизаций.ДокументОснование.Получатель КАК Получатель,
	|	УдержанияОрганизаций.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	УдержанияОрганизаций.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	УдержанияОрганизаций.Результат КАК Результат
	|ИЗ
	|	РегистрРасчета.УдержанияРаботниковОрганизаций КАК УдержанияОрганизаций
	|
	|ГДЕ
	|	(УдержанияОрганизаций.ВидРасчета.СпособРасчета В (&СписокСпособовУдержанийПоИЛ) ИЛИ
	|	УдержанияОрганизаций.ВидРасчета.СпособРасчета = &ПочтовыйСбор) И
	|	УдержанияОрганизаций.ПериодРегистрации МЕЖДУ &ДатаНач И &ДатаКон И
	|	УдержанияОрганизаций.ДокументОснование ССЫЛКА Документ.ИсполнительныйЛист И
	|	УдержанияОрганизаций.Результат <> 0";
	
	ИсчисленныйИПНТекст = 
	"ВЫБРАТЬ
	|	ИПНРасчетыСБюджетом.Организация КАК Организация,
	|	ИПНРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|	СУММА(ИПНРасчетыСБюджетом.Налог) КАК ИсчисленныйНалог
	|ИЗ
	|	РегистрНакопления.ИПНРасчетыСБюджетом КАК ИПНРасчетыСБюджетом
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				УдержанияПоИсполнительнымЛистам.Организация,
	|				УдержанияПоИсполнительнымЛистам.ФизЛицо
	|			ИЗ
	|				("+УдержанияПоИсполнительнымЛистамТекст+") КАК УдержанияПоИсполнительнымЛистам) КАК СписокФизЛиц
	|		ПО СписокФизЛиц.Физлицо = ИПНРасчетыСБюджетом.ФизЛицо 
	|			И СписокФизЛиц.Организация = ИПНРасчетыСБюджетом.Организация
	|
	|ГДЕ
	|	(ИПНРасчетыСБюджетом.Период МЕЖДУ  &ДатаНач И &ДатаКон) И
	|	ИПНРасчетыСБюджетом.ВидДвижения = &парамПриход И 
	|	ИПНРасчетыСБюджетом.ВидСтроки = &парамВидСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ИПНРасчетыСБюджетом.Организация,
	|	ИПНРасчетыСБюджетом.ФизЛицо";
	
	ИсчисленныйОПВТекст =
	"ВЫБРАТЬ
	|	ОПВРасчетыСФондами.Организация КАК Организация,
	|	ОПВРасчетыСФондами.ФизЛицо КАК ФизЛицо,
	|	СУММА(ОПВРасчетыСФондами.Взнос) КАК ИсчисленныеВзносы
	|ИЗ
	|	РегистрНакопления.ОПВРасчетыСФондами КАК ОПВРасчетыСФондами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				УдержанияПоИсполнительнымЛистам.Организация,
	|				УдержанияПоИсполнительнымЛистам.ФизЛицо
	|			ИЗ
	|				("+УдержанияПоИсполнительнымЛистамТекст+") КАК УдержанияПоИсполнительнымЛистам) КАК СписокФизЛиц
	|		ПО СписокФизЛиц.Физлицо = ОПВРасчетыСФондами.ФизЛицо 
	|			И СписокФизЛиц.Организация = ОПВРасчетыСФондами.Организация
	|
	|ГДЕ
	|	(ОПВРасчетыСФондами.Период МЕЖДУ  &ДатаНач И &ДатаКон) И
	|	ОПВРасчетыСФондами.ВидДвижения = &парамПриход И 
	|	ОПВРасчетыСФондами.ВидСтроки = &парамВидСтроки И
	|	ОПВРасчетыСФондами.ВидПлатежа = &парамВидПлатежа
	|
	|СГРУППИРОВАТЬ ПО
	|	ОПВРасчетыСФондами.Организация,
	|	ОПВРасчетыСФондами.ФизЛицо";
	
	ИсчисленныйВОСМСТекст =
	"ВЫБРАТЬ
	|	ВОСМСРасчетыСФондами.Организация КАК Организация,
	|	ВОСМСРасчетыСФондами.ФизЛицо КАК ФизЛицо,
	|	СУММА(ВОСМСРасчетыСФондами.Взнос) КАК ИсчисленныеВзносы
	|ИЗ
	|	РегистрНакопления.ВОСМСРасчетыСФондами КАК ВОСМСРасчетыСФондами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				УдержанияПоИсполнительнымЛистам.Организация,
	|				УдержанияПоИсполнительнымЛистам.ФизЛицо
	|			ИЗ
	|				("+УдержанияПоИсполнительнымЛистамТекст+") КАК УдержанияПоИсполнительнымЛистам) КАК СписокФизЛиц
	|		ПО СписокФизЛиц.Физлицо = ВОСМСРасчетыСФондами.ФизЛицо 
	|			И СписокФизЛиц.Организация = ВОСМСРасчетыСФондами.Организация
	|
	|ГДЕ
	|	(ВОСМСРасчетыСФондами.Период МЕЖДУ  &ДатаНач И &ДатаКон) И
	|	ВОСМСРасчетыСФондами.ВидДвижения = &парамПриход И 
	|	ВОСМСРасчетыСФондами.ВидСтроки = &парамВидСтроки И
	|	ВОСМСРасчетыСФондами.ВидПлатежа = &парамВидПлатежа
	|
	|СГРУППИРОВАТЬ ПО
	|	ВОСМСРасчетыСФондами.Организация,
	|	ВОСМСРасчетыСФондами.ФизЛицо";

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УдержанияПоИЛ.Организация КАК Организация,
	|	УдержанияПоИЛ.ФизЛицо КАК ФизЛицо,
	|	РаботникиОрганизации.Сотрудник.Код КАК ТабельныйНомер,
	|	УдержанияПоИЛ.Получатель КАК Получатель,
	|	УдержанияПоИЛ.ДокументОснование КАК ДокументОснование,
	|	УдержанияПоИЛ.СуммаУдержанийВсего КАК СуммаУдержанийВсего,
	|	УдержанияПоИЛ.СуммаУдержанийПоИЛ КАК СуммаУдержанийПоИЛ,
	|	УдержанияПоИЛ.СуммаПочтовогоСбора КАК СуммаПочтовогоСбора,
	|	ИсчисленныйИПН.ИсчисленныйНалог КАК Налог,
	|	ИсчисленныйОПВ.ИсчисленныеВзносы КАК Взнос,
	|	ИсчисленныйВОСМС.ИсчисленныеВзносы КАК ВОСМС,
	|	ОблагаемаяБазаИЛ.Результат КАК ОблагаемаяБазаИЛ
	|ИЗ
	|	(ВЫБРАТЬ
	|		Удержания.Организация КАК Организация,
	|		Удержания.ФизЛицо КАК ФизЛицо,
	|		Удержания.ДокументОснование КАК ДокументОснование,
	|		Удержания.Получатель КАК Получатель,
	|		СУММА(Удержания.СуммаУдержанийВсего) КАК СуммаУдержанийВсего,
	|		СУММА(Удержания.СуммаУдержанийПоИЛ) КАК СуммаУдержанийПоИЛ,
	|		СУММА(Удержания.СуммаПочтовогоСбора) КАК СуммаПочтовогоСбора
	|	ИЗ
	|	(ВЫБРАТЬ
	|		УдержанияОрганизаций.Организация КАК Организация,
	|		УдержанияОрганизаций.ФизЛицо КАК ФизЛицо,
	|		УдержанияОрганизаций.Получатель КАК Получатель,
	|		УдержанияОрганизаций.ДокументОснование КАК ДокументОснование,
	|		УдержанияОрганизаций.Результат КАК СуммаУдержанийВсего,
	|		ВЫБОР 
	|			КОГДА УдержанияОрганизаций.СпособРасчета В (&СписокСпособовУдержанийПоИЛ) 
	|				ТОГДА УдержанияОрганизаций.Результат 
	|			ИНАЧЕ 0 
	|		КОНЕЦ КАК СуммаУдержанийПоИЛ,
	|		ВЫБОР 
	|			КОГДА УдержанияОрганизаций.СпособРасчета = &ПочтовыйСбор 
	|				ТОГДА УдержанияОрганизаций.Результат 
	|			ИНАЧЕ 0 
	|		КОНЕЦ КАК СуммаПочтовогоСбора
	|	ИЗ
	|		(" + УдержанияПоИсполнительнымЛистамТекст + ") КАК УдержанияОрганизаций) КАК Удержания
	|
	|	СГРУППИРОВАТЬ ПО
	|		Удержания.Организация,
	|		Удержания.ФизЛицо,
	|		Удержания.ДокументОснование,
	|		Удержания.Получатель) КАК УдержанияПоИЛ
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (" + ИсчисленныйИПНТекст + ") КАК ИсчисленныйИПН
	|	  ПО ИсчисленныйИПН.ФизЛицо = УдержанияПоИЛ.ФизЛицо 
	|			И ИсчисленныйИПН.Организация = УдержанияПоИЛ.Организация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (" + ИсчисленныйОПВТекст + ") КАК ИсчисленныйОПВ
	|	  ПО ИсчисленныйОПВ.ФизЛицо = УдержанияПоИЛ.ФизЛицо 
	|			И ИсчисленныйОПВ.Организация = УдержанияПоИЛ.Организация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (" + ИсчисленныйВОСМСТекст + ") КАК ИсчисленныйВОСМС
	|	  ПО ИсчисленныйВОСМС.ФизЛицо = УдержанияПоИЛ.ФизЛицо 
	|			И ИсчисленныйВОСМС.Организация = УдержанияПоИЛ.Организация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НачисленияРаботников.ФизЛицо,
	|			НачисленияРаботников.Организация,
	|			НачисленияРаботников.ДокументОснование,
	|			СУММА(НачисленияРаботников.Результат) КАК Результат
	|		ИЗ
	|			(ВЫБРАТЬ
	|				Удержания.Организация,
	|				Удержания.ФизЛицо,
	|				Удержания.ДокументОснование,
	|			    ВЫБОР 
	|					КОГДА СУММА(ОсновныеНачисления.Результат) ЕСТЬ NULL 
	|						ТОГДА 0 
	|					ИНАЧЕ СУММА(ОсновныеНачисления.Результат) 
	|				КОНЕЦ КАК Результат
	|			ИЗ
	|				(" + УдержанияПоИсполнительнымЛистамТекст + ") КАК Удержания
	|
	|				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.БазовыеВидыРасчета КАК БазовыеВидыРасчета
	|					ПО Удержания.ВидРасчета = БазовыеВидыРасчета.Ссылка 
	|						И БазовыеВидыРасчета.ВидРасчета ССЫЛКА ПланВидовРасчета.ОсновныеНачисленияОрганизаций 
	|						И Удержания.ВидРасчета.СпособРасчета В (&СписокСпособовУдержанийПоИЛ)
	|
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|					ПО Удержания.Организация = ОсновныеНачисления.ОбособленноеПодразделение 
	|						И Удержания.ФизЛицо = ОсновныеНачисления.ФизЛицо 
	|						И ОсновныеНачисления.ПериодРегистрации МЕЖДУ Удержания.БазовыйПериодНачало И Удержания.БазовыйПериодКонец 
	|						И ОсновныеНачисления.ВидРасчета = БазовыеВидыРасчета.ВидРасчета
	|				
	|			СГРУППИРОВАТЬ ПО
	|				Удержания.Организация,
	|				Удержания.ФизЛицо,	
	|				Удержания.ДокументОснование
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|
	|			ВЫБРАТЬ
	|				Удержания.Организация,
	|				Удержания.ФизЛицо,
	|				Удержания.ДокументОснование,
	|		 	   	ВЫБОР 
	|					КОГДА СУММА(ДополнительныеНачисления.Результат) ЕСТЬ NULL 
	|						ТОГДА 0 
	|					ИНАЧЕ СУММА(ДополнительныеНачисления.Результат) 
	|				КОНЕЦ КАК Результат
	|			ИЗ
	|				(" + УдержанияПоИсполнительнымЛистамТекст + ") КАК Удержания
	|
	|				ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.БазовыеВидыРасчета КАК БазовыеВидыРасчета
	|					ПО Удержания.ВидРасчета = БазовыеВидыРасчета.Ссылка 
	|						И БазовыеВидыРасчета.ВидРасчета ССЫЛКА ПланВидовРасчета.ДополнительныеНачисленияОрганизаций 
	|						И Удержания.ВидРасчета.СпособРасчета В (&СписокСпособовУдержанийПоИЛ)
	|
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|					ПО Удержания.Организация = ДополнительныеНачисления.ОбособленноеПодразделение 
	|						И Удержания.ФизЛицо = ДополнительныеНачисления.ФизЛицо 
	|						И ДополнительныеНачисления.ПериодРегистрации МЕЖДУ Удержания.БазовыйПериодНачало И Удержания.БазовыйПериодКонец 
	|						И ДополнительныеНачисления.ВидРасчета = БазовыеВидыРасчета.ВидРасчета
	|
	|			СГРУППИРОВАТЬ ПО
	|				Удержания.Организация,
	|				Удержания.ФизЛицо,
	|				Удержания.ДокументОснование) КАК НачисленияРаботников
	|
	|	СГРУППИРОВАТЬ ПО
	|		НачисленияРаботников.ФизЛицо,
	|		НачисленияРаботников.Организация,
	|		НачисленияРаботников.ДокументОснование) ОблагаемаяБазаИЛ
	|	  ПО ОблагаемаяБазаИЛ.ФизЛицо = УдержанияПоИЛ.ФизЛицо 
	|			И ОблагаемаяБазаИЛ.Организация = УдержанияПоИЛ.Организация
	|			И ОблагаемаяБазаИЛ.ДокументОснование = УдержанияПоИЛ.ДокументОснование
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				РаботникиОрганизации.Сотрудник.ФизЛицо КАК ФизЛицо,
	|				РаботникиОрганизации.Организация,
	|				РаботникиОрганизации.ОбособленноеПодразделение,
	|				МАКСИМУМ(РаботникиОрганизации.Период) КАК Период
	|			ИЗ
	|				РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.УдержанияРаботниковОрганизаций КАК Удержания
	|					ПО РаботникиОрганизации.Организация = Удержания.Организация 
	|						И РаботникиОрганизации.Сотрудник.ФизЛицо = Удержания.ФизЛицо
	|
	|			ГДЕ
	|				РаботникиОрганизации.ПричинаИзмененияСостояния <> &Уволен И
	|				РаботникиОрганизации.Период <= &ДатаКон И
	|				(РаботникиОрганизации.Сотрудник.ВидЗанятости <> &ВнутреннееСовместительство) И
	|				Удержания.ПериодРегистрации МЕЖДУ &ДатаНач И &ДатаКон И
	|				Удержания.ДокументОснование ССЫЛКА Документ.ИсполнительныйЛист
	|
	|			СГРУППИРОВАТЬ ПО
	|				РаботникиОрганизации.Сотрудник.ФизЛицо,
	|				РаботникиОрганизации.Организация,
	|				РаботникиОрганизации.ОбособленноеПодразделение) КАК РаботникиОрганизацииСрез
	|		ПО УдержанияПоИЛ.ФизЛицо = РаботникиОрганизацииСрез.ФизЛицо 
	|			И УдержанияПоИЛ.Организация = РаботникиОрганизацииСрез.ОбособленноеПодразделение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|  		ПО РаботникиОрганизацииСрез.ФизЛицо = РаботникиОрганизации.Сотрудник.ФизЛицо 
	|			И РаботникиОрганизацииСрез.Организация = РаботникиОрганизации.Организация 
	|			И РаботникиОрганизацииСрез.Период = РаботникиОрганизации.Период 
	|			И (РаботникиОрганизации.Сотрудник.ВидЗанятости <> &ВнутреннееСовместительство)
	|//СОЕДИНЕНИЯ
	|
	|
	|ИТОГИ СУММА(СуммаУдержанийПоИЛ), СУММА(СуммаПочтовогоСбора), СУММА(СуммаУдержанийВсего) ПО
	|	ОБЩИЕ
	|
	|{ВЫБРАТЬ
	|	УдержанияПоИЛ.Организация.*,
	|	УдержанияПоИЛ.ФизЛицо.*,
	|	РаботникиОрганизации.Сотрудник.Код КАК ТабельныйНомер,
	|	УдержанияПоИЛ.Получатель.*,
	|	УдержанияПоИЛ.ДокументОснование.*
	|	//СВОЙСТВА
	|}
	|
    |{ГДЕ
	|	УдержанияПоИЛ.Организация.*,
	|	УдержанияПоИЛ.ФизЛицо.*,
	|	УдержанияПоИЛ.Получатель.*,
	|	УдержанияПоИЛ.ДокументОснование.*
	|	//СВОЙСТВА
	|	//КАТЕГОРИИ
	|}
	|
	|{УПОРЯДОЧИТЬ ПО
	|	УдержанияПоИЛ.Организация.*,
	|	УдержанияПоИЛ.ФизЛицо.*,
	|	РаботникиОрганизации.Сотрудник.Код КАК ТабельныйНомер,
	|	УдержанияПоИЛ.Получатель.*,
	|	УдержанияПоИЛ.ДокументОснование.*
	|	//СВОЙСТВА
	|}
	|
	|{ИТОГИ ПО
	|	УдержанияПоИЛ.Организация.*,
	|	УдержанияПоИЛ.ФизЛицо.*,
	|	УдержанияПоИЛ.Получатель.*,
	|	УдержанияПоИЛ.ДокументОснование.*
	|	//СВОЙСТВА
	|
	|}
	|";
	
	//ПостроительОтчета.Текст = ТекстЗапроса;							  

	Если ОбщийОтчет.ИспользоватьСвойстваИКатегории Тогда
		
		// Свойства и категории, назначаемые пользователем:
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДанным");  // описание поля запроса поля, для которого добавляются свойства и категории. Используется в условии соединения с регистром сведений, хранящим значения свойств или категорий
		ТаблицаПолей.Колонки.Добавить("Представление");// представление поля, для которого добавляются свойства и категории. 
		ТаблицаПолей.Колонки.Добавить("Назначение");   // назначение свойств/категорий объектов для данного поля
		ТаблицаПолей.Колонки.Добавить("ТипЗначения");  // тип значения поля, для которого добавляются свойства и категории. Используется, если не установлено назначение
		ТаблицаПолей.Колонки.Добавить("НетКатегорий"); // признак НЕиспользования категорий для объекта
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "УдержанияПоИЛ.Организация";
		НоваяСтрока.Представление = "Организация";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Организации;
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "УдержанияПоИЛ.ФизЛицо";
		НоваяСтрока.Представление = "Сотрудник";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ФизическиеЛица;
		
		// Добавим строки запроса, необходимые для использования свойств и категорий
		ТекстПоляКатегорий = "";
		ТекстПоляСвойств = "";
		
		УправлениеОтчетами.ДобавитьВТекстСвойстваИКатегории(ТаблицаПолей, ТекстЗапроса, СтруктураПредставлениеПолей, 
		ОбщийОтчет.мСоответствиеНазначений, ПостроительОтчета.Параметры
		,, ТекстПоляКатегорий, ТекстПоляСвойств,,,,,,ОбщийОтчет.мСтруктураДляОтбораПоКатегориям);		
		
	КонецЕсли;
	
	ПостроительОтчета.Текст = ТекстЗапроса;
		
	Если ОбщийОтчет.ИспользоватьСвойстваИКатегории Тогда			
		УправлениеОтчетами.УстановитьТипыЗначенийСвойствИКатегорийДляОтбора(ПостроительОтчета, ТекстПоляКатегорий, ТекстПоляСвойств, ОбщийОтчет.мСоответствиеНазначений, СтруктураПредставлениеПолей);
	КонецЕсли;
	
	// Представление полей запроса
	СтруктураПредставлениеПолей.Вставить("ТабельныйНомер", "Табельный номер");
	СтруктураПредставлениеПолей.Вставить("ФизЛицо", "Сотрудник");
	СтруктураПредставлениеПолей.Вставить("ДокументОснование", "Документ основание");
	СтруктураПредставлениеПолей.Вставить("Налог", "ИПН");
	СтруктураПредставлениеПолей.Вставить("Взнос", "ОПВ");
	СтруктураПредставлениеПолей.Вставить("ВОСМС", "ВОСМС");
	СтруктураПредставлениеПолей.Вставить("ОблагаемаяБазаИЛ", "Облагаемая база");
	СтруктураПредставлениеПолей.Вставить("СуммаУдержанийВсего", "Всего удержано");
	СтруктураПредставлениеПолей.Вставить("СуммаУдержанийПоИЛ", "Удержано");
	СтруктураПредставлениеПолей.Вставить("СуммаПочтовогоСбора", "Почтовый сбор");
	
	
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
	// Группировки по умолчанию
	ПостроительОтчета.ИзмеренияСтроки.Добавить("Организация");
	
	// Показатели по умолчанию
	ОбщийОтчет.ЗаполнитьПоказатели("ОблагаемаяБазаИЛ", "Облагаемая база", Истина,  "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("Налог", "ИПН", Истина,  "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("Взнос", "ОПВ", Истина,  "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("ВОСМС", "ВОСМС", Истина,  "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("СуммаУдержанийПоИЛ", "Удержано", Истина,  "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("СуммаПочтовогоСбора", "Почтовый сбор", Истина,  "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("СуммаУдержанийВсего", "Всего удержано", Истина,  "ЧЦ=15; ЧДЦ=2");
	
	// Отборы по умолчанию
	МассивОтбора = Новый Массив;
	МассивОтбора.Добавить("Организация");
	МассивОтбора.Добавить("ФизЛицо");
	МассивОтбора.Добавить("Получатель");
	УправлениеОтчетами.ЗаполнитьОтбор(МассивОтбора, ПостроительОтчета);
	
	// Отбор по организации по умолчанию
	ОрганизацияПоУмолчанию = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глТекущийПользователь, "ОсновнаяОрганизация"); 
	Если ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
		ЭлементОтбора = ПостроительОтчета.Отбор.Организация;
		ЭлементОтбора.ВидСравнения = ВидСравнения.Равно;
		ЭлементОтбора.Значение = ОрганизацияПоУмолчанию;
		ЭлементОтбора.Использование = Истина;
	КонецЕсли;
	
	ПостроительОтчета.ВыбранныеПоля.Очистить();
	ПостроительОтчета.ВыбранныеПоля.Добавить("ФизЛицо");
	ПостроительОтчета.ВыбранныеПоля.Добавить("Получатель");
	
	// Сортировка по умолчанию
	ПостроительОтчета.Порядок.Добавить("ФизЛицо.Наименование");

	// настройки отчета
	ОбщийОтчет.РаскрашиватьИзмерения = Истина;
	ОбщийОтчет.ВыводитьИтогиПоВсемУровням = Истина;
	ОбщийОтчет.ВыводитьПоказателиВСтроку = Истина;
	ОбщийОтчет.мРежимВводаПериода = 0; // Период
	
	УправлениеОтчетами.УпорядочитьПоляПостроителяОтчета(ПостроительОтчета);
	
КонецПроцедуры

// Читает свойство Построитель отчета
//
// Параметры
//	Нет
//
Функция ПолучитьПостроительОтчета() Экспорт

	Возврат ОбщийОтчет.ПолучитьПостроительОтчета();

КонецФункции // ПолучитьПостроительОтчета()

// Настраивает отчет по переданной структуре параметров
//
// Параметры:
//	Нет.
//
Процедура Настроить(Параметры) Экспорт

	ОбщийОтчет.Настроить(Параметры, ЭтотОбъект);

КонецПроцедуры

// Возвращает основную форму отчета, связанную с данным экземпляром отчета
//
// Параметры
//	Нет
//
Функция ПолучитьОсновнуюФорму() Экспорт
	
	ОснФорма = ПолучитьФорму();
	ОснФорма.ОбщийОтчет = ОбщийОтчет;
	ОснФорма.ЭтотОтчет = ЭтотОбъект;
	Возврат ОснФорма;
	
КонецФункции // ПолучитьОсновнуюФорму()

#КонецЕсли