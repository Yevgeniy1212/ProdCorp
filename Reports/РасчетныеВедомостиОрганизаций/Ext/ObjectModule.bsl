////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем ЭлементыНастройки    Экспорт; // Массив элементов структуры СКД         
Перем СохраненнаяНастройка Экспорт;        // Текущий вариант отчета
Перем ТаблицаВариантовОтчета Экспорт;      // Таблица вариантов доступных текущему пользователю
Перем СоответствиеНаборовДанныхИЗапросов;

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ 
// 

// Сохранение настроек схемы компоновки
//
Процедура СохранитьНастройку() Экспорт
	
	СтруктураНастроек = ТиповыеОтчеты.ПолучитьСтруктуруПараметровТиповогоОтчета(ЭтотОбъект);
	СохранениеНастроек.СохранитьНастройкуОбъекта(СохраненнаяНастройка, СтруктураНастроек);
	
КонецПроцедуры

// Инициализация отчета
//
// Параметры:
//  Нет.
//
Процедура ИнициализацияОтчета() Экспорт
	
	ТиповыеОтчеты.ИнициализацияТиповогоОтчета(ЭтотОбъект);
	
КонецПроцедуры //ИнициализацияОтчета()

// Заполнение параметров отчета по элементу справочника из переменной СохраненнаяНастройка.
//
Процедура ПрименитьНастройку() Экспорт
	
	Схема = ТиповыеОтчеты.ПолучитьСхемуКомпоновкиОбъекта(ЭтотОбъект);

	// Считываение структуры настроек отчета
 	Если НЕ СохраненнаяНастройка.Пустая() Тогда
		
		СтруктураНастроек = СохраненнаяНастройка.ХранилищеНастроек.Получить();
		Если Не СтруктураНастроек = Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(СтруктураНастроек.НастройкиКомпоновщика);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураНастроек);
		Иначе
			КомпоновщикНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
		КонецЕсли;
		
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
	КонецЕсли;
	
КонецПроцедуры

// Формирование отчета в табличный документ
// 
Функция СформироватьОтчет(Результат = Неопределено, ДанныеРасшифровки = Неопределено, ВыводВФормуОтчета = Истина, ЭтоФормаОтчета = Ложь) Экспорт
	
	Если ДанныеРасшифровки = Неопределено Тогда
		ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КонецЕсли;
	
	НастрокаПоУмолчанию        = КомпоновщикНастроек.ПолучитьНастройки();
	ТиповыеОтчеты.ПолучитьПримененуюНастройку(ЭтотОбъект);
	
	ЗначениеВыводитьПодписи = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВыводитьПодписи"));
	ВыводитьПодписи = ЗначениеВыводитьПодписи.Значение;

	Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") И ЭлементОтбора.Использование Тогда
			Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
				
				ЗначениеОтбора = ЭлементОтбора.ПравоеЗначение;			
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если НЕ ЭтоФормаОтчета Тогда
		ВыводитьПодписи = Истина;
	ИначеЕсли ЗначениеОтбора = Неопределено Тогда
		ВыводитьПодписи = Ложь;
	ИначеЕсли ТипЗнч(ЗначениеОтбора) = Тип("СписокЗначений") Тогда
		ВыводитьПодписи = ВыводитьПодписи И ЗначениеОтбора.Количество() = 1;	
	ИначеЕсли ТипЗнч(ЗначениеОтбора) = Тип("СправочникСсылка.Организации") Тогда
		ВыводитьПодписи = ВыводитьПодписи И ЗначениеЗаполнено(ЗначениеОтбора);
	КонецЕсли;
	
    УстановитьВыводДополнительнойИнформации(КомпоновщикНастроек.Настройки.Структура, ВыводитьПодписи);

	ТиповыеОтчеты.СформироватьТиповойОтчет(ЭтотОбъект, Результат, ДанныеРасшифровки, ВыводВФормуОтчета);
	КомпоновщикНастроек.ЗагрузитьНастройки(НастрокаПоУмолчанию);
	УправлениеОтчетами.УстановитьЗапросыСКДПоСоответсвию(СхемаКомпоновкиДанных.НаборыДанных, СоответствиеНаборовДанныхИЗапросов);
	
	Возврат Результат;
	
КонецФункции

// Настройка отчета 
//
Процедура Настроить(Отбор, КомпоновщикНастроекОсновногоОтчета = Неопределено) Экспорт
	
	ТиповыеОтчеты.НастроитьТиповойОтчет(ЭтотОбъект, Отбор, КомпоновщикНастроекОсновногоОтчета);
	
КонецПроцедуры

// Запоминание элементов структуры отчета компоновщика настроек
//
Процедура ЗапомнитьНастройку() Экспорт 
	
	Если КомпоновщикНастроек.Настройки.Структура.Количество() <> 0 Тогда
		Если ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) <> Тип("ТаблицаКомпоновкиДанных") Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЭлементыНастройки[0] = КомпоновщикНастроек.Настройки.Структура[0].Колонки[0];
	ЭлементыНастройки[1] = КомпоновщикНастроек.Настройки.Структура[0].Колонки[1];
	ЭлементыНастройки[2] = КомпоновщикНастроек.Настройки.Структура[0].Колонки[2];
	ЭлементыНастройки[3] = КомпоновщикНастроек.Настройки.Структура[0].Колонки[3];
	ЭлементыНастройки[4] = КомпоновщикНастроек.Настройки.Структура[0].Колонки[1].Структура[0];
	ЭлементыНастройки[5] = КомпоновщикНастроек.Настройки.Структура[0].Колонки[3].Структура[0];
 	
	КомпоновщикНастроек.Настройки.Структура[0].Колонки.Удалить(ЭлементыНастройки[0]);
	КомпоновщикНастроек.Настройки.Структура[0].Колонки.Удалить(ЭлементыНастройки[1]);
	КомпоновщикНастроек.Настройки.Структура[0].Колонки.Удалить(ЭлементыНастройки[2]);
	КомпоновщикНастроек.Настройки.Структура[0].Колонки.Удалить(ЭлементыНастройки[3]);
	КомпоновщикНастроек.Настройки.Структура[0].Колонки.Удалить(ЭлементыНастройки[4]);
	КомпоновщикНастроек.Настройки.Структура[0].Колонки.Удалить(ЭлементыНастройки[5]);
	
	Если КомпоновщикНастроек.Настройки.Структура.Количество() = 2 Тогда
		Если ТипЗнч(КомпоновщикНастроек.Настройки.Структура[1]) <> Тип("ГруппировкаКомпоновкиДанных") Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЭлементыНастройки[6] = КомпоновщикНастроек.Настройки.Структура[1];	 	
	КомпоновщикНастроек.Настройки.Структура.Удалить(ЭлементыНастройки[6]);	
	
КонецПроцедуры

// Добавление настройки структуры в колонки таблицы
//
Процедура ВосстановитьНастройку() Экспорт  
	
	Если КомпоновщикНастроек.Настройки.Структура.Количество() <> 0 Тогда
		Если ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) <> Тип("ТаблицаКомпоновкиДанных") Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЭлементГруппировки = КомпоновщикНастроек.Настройки.Структура[0].Колонки.Добавить();
	ТиповыеОтчеты.СкопироватьНастройкиКомпоновкиДанных(ЭлементГруппировки, ЭлементыНастройки[0]);
	
	ЭлементГруппировки = КомпоновщикНастроек.Настройки.Структура[0].Колонки.Добавить();
	ТиповыеОтчеты.СкопироватьНастройкиКомпоновкиДанных(ЭлементГруппировки, ЭлементыНастройки[1]);
	
	ЭлементГруппировки = КомпоновщикНастроек.Настройки.Структура[0].Колонки.Добавить();
	ТиповыеОтчеты.СкопироватьНастройкиКомпоновкиДанных(ЭлементГруппировки, ЭлементыНастройки[2]);
	
	ЭлементГруппировки = КомпоновщикНастроек.Настройки.Структура[0].Колонки.Добавить();
	ТиповыеОтчеты.СкопироватьНастройкиКомпоновкиДанных(ЭлементГруппировки, ЭлементыНастройки[3]);

	//ЭлементГруппировки = КомпоновщикНастроек.Настройки.Структура[0].Колонки.Добавить();
	//ТиповыеОтчеты.СкопироватьНастройкиКомпоновкиДанных(ЭлементГруппировки, ЭлементыНастройки[4]);

	//ЭлементГруппировки = КомпоновщикНастроек.Настройки.Структура[0].Колонки.Добавить();
	//ТиповыеОтчеты.СкопироватьНастройкиКомпоновкиДанных(ЭлементГруппировки, ЭлементыНастройки[5]);

	Если КомпоновщикНастроек.Настройки.Структура.Количество() = 1 Тогда
		Если ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) = Тип("ТаблицаКомпоновкиДанных") Тогда
			ЭлементГруппировки = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
			ТиповыеОтчеты.СкопироватьНастройкиКомпоновкиДанных(ЭлементГруппировки, ЭлементыНастройки[6]);     		
		КонецЕсли;  		
	КонецЕсли;
	
КонецПроцедуры

// Доработка компоновщика отчета перед выводом
//
Процедура ДоработатьКомпоновщикПередВыводом() Экспорт
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ЕстьГруппировкаПоПериодуРегистрации"));
	ЗначениеПараметра.Значение = ПрисутствуетПолеПериодРегистрацииВГруппировке();
	ЗначениеПараметра.Использование = Истина;
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВалютаРеглУчета"));
	ЗначениеПараметра.Значение = Константы.ВалютаРегламентированногоУчета.Получить();
	ЗначениеПараметра.Использование = Истина;
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НачальнаяДата"));
	ЗначениеПараметра.Значение = '00010101';
	ЗначениеПараметра.Использование = Истина;
	
	// Если в выбранные поля добавлены поле нумерации в группе или сквозной нумерации в группе перенесем данные 
	// поля в самую нижнюю группировку строк отчета.
	ГруппировкаДляВремени = Неопределено;
	УдаляемыеПоля = Новый Массив;
	
	Для Каждого ВыбранноеПоле Из КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл
		Если ТипЗнч(ВыбранноеПоле) <> Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			Если ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("СистемныеПоля.НомерПоПорядкуВГруппировке") ИЛИ ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("SystemFields.GroupSerialNumber") Тогда
				ПоследнийЭлементСтруктуры = НайтиПоследнийЭлементСтруктурыСтрок(КомпоновщикНастроек.Настройки.Структура[0].Строки);
				Если ПоследнийЭлементСтруктуры <> Неопределено Тогда
					ЭлементВыбранногоПоля = ПоследнийЭлементСтруктуры.Выбор.Элементы.Вставить(0,Тип("ВыбранноеПолеКомпоновкиДанных"));
					ЭлементВыбранногоПоля.Поле = Новый ПолеКомпоновкиДанных("СистемныеПоля.НомерПоПорядкуВГруппировке");
				КонецЕсли;
				
			ИначеЕсли ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("СистемныеПоля.НомерПоПорядку") ИЛИ ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("SystemFields.SerialNumber") Тогда
				ПоследнийЭлементСтруктуры = НайтиПоследнийЭлементСтруктурыСтрок(КомпоновщикНастроек.Настройки.Структура[0].Строки);
				Если ПоследнийЭлементСтруктуры <> Неопределено Тогда
					ЭлементВыбранногоПоля = ПоследнийЭлементСтруктуры.Выбор.Элементы.Вставить(0,Тип("ВыбранноеПолеКомпоновкиДанных"));
					ЭлементВыбранногоПоля.Поле = Новый ПолеКомпоновкиДанных("СистемныеПоля.НомерПоПорядку");
				КонецЕсли;
			ИначеЕсли ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтработаноДней") 
				ИЛИ ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтработаноЧасов")
				Тогда
				Если КомпоновщикНастроек.Настройки.Структура.Количество() > 0 и ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) = Тип("ТаблицаКомпоновкиДанных") Тогда
					Если КомпоновщикНастроек.Настройки.Структура[0].Колонки.Количество() > 0 и ГруппировкаДляВремени = Неопределено Тогда
						ГруппировкаДляВремени = КомпоновщикНастроек.Настройки.Структура[0].Колонки.Вставить(0);
						ГруппировкаДляВремени.Использование = Истина;
						
						ПолеГруппировки = ГруппировкаДляВремени.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
						ПолеГруппировки.Поле = Новый ПолеКомпоновкиДанных("Группа");
						ПолеГруппировки.Использование = Истина;
						
						ПолеВыбора = ГруппировкаДляВремени.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
						ПолеВыбора.Поле = Новый ПолеКомпоновкиДанных("Группа");
						ПолеВыбора.Использование = Истина;
						
						ТиповыеОтчеты.ДобавитьОтбор(ГруппировкаДляВремени.Отбор, "Группа", "Всего начислено");
						
						УсловноеОформление = ГруппировкаДляВремени.УсловноеОформление.Элементы.Добавить();
						УсловноеОформление.Использование = Истина;
						
						ПолеОформления = УсловноеОформление.Поля.Элементы.Добавить();
						ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("Группа");
						ПолеОформления.Использование = Истина;
						
						ПараметрОформелния = УсловноеОформление.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Text"));
						ПараметрОформелния.Использование = Истина;
						ПараметрОформелния.Значение = "Отработанное время";
						
						ПараметрОформелния = УсловноеОформление.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("BackColor"));
						ПараметрОформелния.Использование = Истина;
						ПараметрОформелния.Значение = ЦветаСтиля.ЦветФонаФормы;
						
						ПараметрОформелния = УсловноеОформление.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Font"));
						ПараметрОформелния.Использование = Истина;
						ПараметрОформелния.Значение =  Новый Шрифт(ПараметрОформелния.Значение, "Verdana", , Истина);
						
					КонецЕсли;
					Если ГруппировкаДляВремени <> Неопределено Тогда
						ПолеВыбора = ГруппировкаДляВремени.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
						ПолеВыбора.Поле = ВыбранноеПоле.Поле;
						УдаляемыеПоля.Добавить(ВыбранноеПоле);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ВыбранноеПоле.Поле <> Новый ПолеКомпоновкиДанных("Результат") Тогда 
				Если ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) = Тип("ТаблицаКомпоновкиДанных") Тогда
					ПоследнийЭлементСтруктуры = НайтиПоследнийЭлементСтруктурыСтрок(КомпоновщикНастроек.Настройки.Структура[0].Строки);
					Если ПоследнийЭлементСтруктуры <> Неопределено Тогда
						ЭлементВыбранногоПоляОтчета = ПоследнийЭлементСтруктуры.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
						ЭлементВыбранногоПоляОтчета.Поле = ВыбранноеПоле.Поле;
						ЭлементВыбранногоПоляОтчета.Использование = Истина;
				
	 					ЭлементВыбранногоПоля = ПоследнийЭлементСтруктуры.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
						ЭлементВыбранногоПоля.Поле = ВыбранноеПоле.Поле;
						ЭлементВыбранногоПоля.Использование = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если ГруппировкаДляВремени <> Неопределено Тогда
		Для Каждого ПолеВыбора Из УдаляемыеПоля Цикл
			КомпоновщикНастроек.Настройки.Выбор.Элементы.Удалить(ПолеВыбора);
		КонецЦикла;
	КонецЕсли;
	
	// Отключим строку итогов, если расшифровывается конечное или начальное сальдо
	//
	МассивОтборов = ТиповыеОтчеты.ПолучитьЭлементыОтбора(КомпоновщикНастроек);
	
	Для Каждого ЭлементОтбора Из МассивОтборов Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		Если ЭлементОтбора.Использование 
		   И ЭлементОтбора.ВидСравнения    = ВидСравненияКомпоновкиДанных.Равно 
		   И (ЭлементОтбора.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ИмяВидаРасчета") ИЛИ ЭлементОтбора.ПравоеЗначение = "Конечное сальдо" ИЛИ ЭлементОтбора.ПравоеЗначение = "Начальное сальдо")
		   И (ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ИмяВидаРасчета") ИЛИ ЭлементОтбора.ЛевоеЗначение = "Конечное сальдо" ИЛИ ЭлементОтбора.ЛевоеЗначение = "Начальное сальдо")
		    тогда
			ТиповыеОтчеты.УстановитьПараметрВывода(КомпоновщикНастроек, "VerticalOverallPlacement", РасположениеИтоговКомпоновкиДанных.Нет);
		КонецЕсли;
	КонецЦикла;
	
	//Отключим строки итогов выше 
	ПараметрНачалоПериода = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НачалоПериода"));
	ПараметрКонецПериода = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("КонецПериода"));
	
	Если ПараметрНачалоПериода = Неопределено ИЛИ ПараметрКонецПериода = Неопределено Тогда
		Возврат;
	Иначе
		НачалоПериода = ?(ПараметрНачалоПериода.Значение <> Неопределено, Дата(ПараметрНачалоПериода.Значение), '00010101');
		КонецПериода  = ?(ПараметрКонецПериода.Значение <> Неопределено, Дата(ПараметрКонецПериода.Значение), '00010101');
		Если НачалоПериода = '00010101'  Тогда
			НачалоПериода = НачалоМесяца(ТекущаяДата());
		КонецЕсли;
		Если КонецПериода = '00010101' Тогда
			КонецПериода = КонецМесяца(ТекущаяДата());
		КонецЕсли;
		ПараметрКонецПериода.Использование = Истина;
		ПараметрНачалоПериода.Использование = Истина;
		
		ПараметрКонецПериода.Значение  = КонецПериода;
		ПараметрНачалоПериода.Значение = НачалоПериода;
	КонецЕсли;
	
	Если НачалоПериода <> Неопределено И КонецПериода <> Неопределено Тогда
		УправлениеОтчетами.ЗаменитьВСКДТекстЗапросКалендаря(СхемаКомпоновкиДанных, НачалоПериода, КонецПериода, СоответствиеНаборовДанныхИЗапросов);
	КонецЕсли;
	
КонецПроцедуры

Функция НайтиПоследнийЭлементСтруктурыСтрок(Строки) 
	
	Если Строки.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		ЭлементСтруктуры = Строки[0];
	КонецЕсли;
	Пока ЭлементСтруктуры.Структура.Количество() <> 0 Цикл
		ЭлементСтруктуры = ЭлементСтруктуры.Структура[0];
	КонецЦикла;
	
	Возврат ЭлементСтруктуры;
	
КонецФункции

// Установим видимость дополнительной информации в расчетных листках.
//
Процедура УстановитьВыводДополнительнойИнформации(Структура, ВыводитьПодписи)
	          
	Для Каждого ЭлементСтруктурыОтчета Из Структура Цикл
		
		Если ТипЗнч(ЭлементСтруктурыОтчета) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			// установим видимость подписей
			ЭлементСтруктурыОтчета.Использование = ВыводитьПодписи;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Возвращает значение истина, если в элементах структур отчета присутствует поле Период регистрации
//
Функция ПрисутствуетПолеПериодРегистрацииВГруппировке()
	
	ЕстьГруппировка = Ложь;
	
	Если КомпоновщикНастроек.Настройки.Структура.Количество() <> 0 Тогда
		
		Если Тип(КомпоновщикНастроек.Настройки.Структура[0]) = Тип("ТаблицаКомпоновкиДанных") Тогда
			
			Если КомпоновщикНастроек.Настройки.Структура[0].Строки.Количество() > 0 Тогда
				ЕстьГруппировка = НайтиПериодРегистрации(КомпоновщикНастроек.Настройки.Структура[0].Строки[0]);
			КонецЕсли;
			
		ИначеЕсли Тип(КомпоновщикНастроек.Настройки.Структура[0]) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			
			ЕстьГруппировка = НайтиПериодРегистрации(КомпоновщикНастроек.Настройки.Структура[0]);
			
		ИначеЕсли Тип(КомпоновщикНастроек.Настройки.Структура[0]) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			
			Если КомпоновщикНастроек.Настройки.Структура[0].Точки.Количество() <> 0 Тогда
				
				ЕстьГруппировка = НайтиПериодРегистрации(КомпоновщикНастроек.Настройки.Структура[0].Точки[0]) ИЛИ НайтиПериодРегистрации(КомпоновщикНастроек.Настройки.Структура[0].Серии[0]);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЕстьГруппировка;
	
КонецФункции //ПрисутствуетПолеПериодРегистрацииВГруппировке()

// Функция возвращает значение истина, если в группировках элементов структуры присутствует поле "Период регистрации"
//
Функция НайтиПериодРегистрации(Структура)
	
	ЕстьПоле = Ложь;
	
	Если ТипЗнч(Структура) <> Тип("ГруппировкаКомпоновкиДанных")
       И ТипЗнч(Структура) <> Тип("ГруппировкаТаблицыКомпоновкиДанных")
	   И ТипЗнч(Структура) <> Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		Возврат ЕстьПоле;
		
	КонецЕсли;
	
	ПолеПериодРегистрации = Новый ПолеКомпоновкиДанных("ПериодРегистрации");
	
	Для Каждого ПолеГруппировки Из Структура.ПоляГруппировки.Элементы Цикл
		
		Если ПолеГруппировки.Использование И ПолеГруппировки.Поле = ПолеПериодРегистрации тогда
			
			ЕстьПоле = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЕстьПоле И Структура.Структура.Количество() <> 0 Тогда
		
		ЕстьПоле = НайтиПериодРегистрации(Структура.Структура[0]);
		
	КонецЕсли;
	
	Возврат ЕстьПоле;
	
КонецФункции //НайтиПериодРегистрации()

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт

	СписокПолейПодстановкиОтборовПоУмолчанию = Новый Соответствие;
	СписокПолейПодстановкиОтборовПоУмолчанию.Вставить("Организация", "ОсновнаяОрганизация");
	
	Возврат Новый Структура("ИспользоватьСобытияПриФормированииОтчета,
	|ПриВыводеЗаголовкаОтчета,
	|ПослеВыводаПанелиПользователя,
	|ПослеВыводаПериода,
	|ПослеВыводаПараметра,
	|ПослеВыводаГруппировки,
	|ПослеВыводаОтбора,
	|ДействияПанелиИзменениеФлажкаДопНастроек,
	|ПриПолучениеНастроекПользователя, 
	|ЗаполнитьОтборыПоУмолчанию, 
	|СписокПолейПодстановкиОтборовПоУмолчанию,
	|СписокДоступныхПредопределенныхНастроек,
	|МинимальныйПериодОтчета", 
	Ложь, Ложь, Ложь, Ложь, Ложь, Ложь, Ложь, Ложь, Ложь, Истина, СписокПолейПодстановкиОтборовПоУмолчанию, , "Месяц");

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

Если СохраненнаяНастройка = Неопределено Тогда
	СохраненнаяНастройка =  Справочники.СохраненныеНастройки.ПустаяСсылка();
КонецЕсли;
ЭлементыНастройки = Новый Массив(7);

#КонецЕсли

