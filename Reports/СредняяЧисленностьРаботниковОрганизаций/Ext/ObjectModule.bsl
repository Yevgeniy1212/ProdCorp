#Если Клиент Тогда

Функция ПолучитьТекстЗапроса()
	
	// Месяцы отчета
	ПериодыТекст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(РеглПроизводственныйКалендарь.ДатаКалендаря, МЕСЯЦ) КАК НачалоМесяца,
	|	КОНЕЦПЕРИОДА(РеглПроизводственныйКалендарь.ДатаКалендаря, МЕСЯЦ) КАК КонецМесяца
	|
	|ПОМЕСТИТЬ ВТ_Периоды
	|
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РеглПроизводственныйКалендарь
	|ГДЕ
	|	РеглПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ДатаНач И &ДатаКон
	|";

	ШтатноеРасписаниеТекст = "
	|ВЫБРАТЬ
	|	ШтатноеРасписание.Должность,
	|	ШтатноеРасписание.ПодразделениеОрганизации,
	|	&ДатаНач КАК Период,
	|	ШтатноеРасписание.ТарифныйРазряд,
	|	ЕСТЬNULL(ШтатноеРасписание.УсловияТруда, ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)) КАК УсловияТруда
	|ПОМЕСТИТЬ ВТШтатноеРасписание
	|ИЗ
	|	РегистрСведений.ШтатноеРасписаниеОрганизаций.СрезПоследних(&ДатаНач, ) КАК ШтатноеРасписание
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ШтатноеРасписание.Должность,
	|	ШтатноеРасписание.ПодразделениеОрганизации,
	|	ШтатноеРасписание.Период,
	|	ШтатноеРасписание.ТарифныйРазряд,
	|	ЕСТЬNULL(ШтатноеРасписание.УсловияТруда, ЗНАЧЕНИЕ(Справочник.УсловияТруда.ПустаяСсылка)) КАК УсловияТруда
	|ИЗ
	|	РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписание
	|ГДЕ
	|	ШтатноеРасписание.Период > &ДатаНач
	|	И ШтатноеРасписание.Период <= &ДатаКон";

	ШтатноеРасписаниеТекст = " 
	|" + ШтатноеРасписаниеТекст + "
	|;
	|
	|ВЫБРАТЬ
	|	ШтатноеРасписание.Должность,
	|	ШтатноеРасписание.ПодразделениеОрганизации,
	|	ШтатноеРасписание.Период КАК Период,
	|	КОНЕЦПЕРИОДА(МИНИМУМ(ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ШтатноеРасписаниеСлед.Период, ДЕНЬ, -1), &ДатаКон)), ДЕНЬ) КАК КонецПериода,
	|	ШтатноеРасписание.ТарифныйРазряд,
	|	ШтатноеРасписание.УсловияТруда
	|ПОМЕСТИТЬ ВТШтатноеРасписаниеПериоды
	|ИЗ
	|	ВТШтатноеРасписание КАК ШтатноеРасписание
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТШтатноеРасписание КАК ШтатноеРасписаниеСлед
	|		ПО ШтатноеРасписание.Должность = ШтатноеРасписаниеСлед.Должность
	|			И ШтатноеРасписание.ПодразделениеОрганизации = ШтатноеРасписаниеСлед.ПодразделениеОрганизации
	|			И ШтатноеРасписание.ТарифныйРазряд = ШтатноеРасписаниеСлед.ТарифныйРазряд
	|			И ШтатноеРасписание.Период < ШтатноеРасписаниеСлед.Период
	|			И (ШтатноеРасписаниеСлед.Период <= &ДатаКон)
	|
	|СГРУППИРОВАТЬ ПО
	|	ШтатноеРасписание.Должность,
	|	ШтатноеРасписание.ПодразделениеОрганизации,
	|	ШтатноеРасписание.Период,
	|	ШтатноеРасписание.ТарифныйРазряд,
	|	ШтатноеРасписание.УсловияТруда";
	
	// Выбираем назначения работников
	РаботникиОрганизацииТекст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ДатаНач КАК Период,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.Сотрудник.ФизЛицо КАК ФизЛицо,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.Сотрудник.ВидЗанятости КАК ВидЗанятости,
	|	РаботникиОрганизации.ПодразделениеОрганизации,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	РаботникиОрганизации.Должность,
	|	РаботникиОрганизации.ТарифныйРазряд,
	|	РаботникиОрганизации.ПричинаИзмененияСостояния
	|
	|ПОМЕСТИТЬ ВТ_РаботникиОрганизации
	|
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|			&ДатаНач,
	|			Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)) КАК РаботникиОрганизации
	|
	|ГДЕ
	|	РаботникиОрганизации.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РаботникиОрганизации.Период,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.Сотрудник.ФизЛицо,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.Сотрудник.ВидЗанятости,
	|	РаботникиОрганизации.ПодразделениеОрганизации,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	РаботникиОрганизации.Должность,
	|	РаботникиОрганизации.ТарифныйРазряд,
	|	РаботникиОрганизации.ПричинаИзмененияСостояния
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|
	|ГДЕ
	|   РаботникиОрганизации.Период > &ДатаНач
	|	И РаботникиОрганизации.Период <= &ДатаКон
	|	И РаботникиОрганизации.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|";
	
	РаботникиОрганизацииТекст = "
	|" + РаботникиОрганизацииТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаботникиОрганизации.Период КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(МИНИМУМ(ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РаботникиОрганизацииСлед.Период, ДЕНЬ, -1), &ДатаКон)), ДЕНЬ) КАК ДатаОкончания,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ФизЛицо КАК ФизЛицо,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.ВидЗанятости КАК ВидЗанятости,
	|	РаботникиОрганизации.ПодразделениеОрганизации,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	РаботникиОрганизации.Должность,
	|	РаботникиОрганизации.ТарифныйРазряд,
	|	РаботникиОрганизации.ПричинаИзмененияСостояния
	|ПОМЕСТИТЬ ВТ_РаботникиОрганизацииПериоды
	|ИЗ
	|	ВТ_РаботникиОрганизации КАК РаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РаботникиОрганизации КАК РаботникиОрганизацииСлед
	|		ПО РаботникиОрганизации.Организация = РаботникиОрганизацииСлед.Организация
	|			И РаботникиОрганизации.Сотрудник = РаботникиОрганизацииСлед.Сотрудник
	|			И РаботникиОрганизации.Период < РаботникиОрганизацииСлед.Период
	|			И (РаботникиОрганизацииСлед.Период <= &ДатаКон)
	|	СГруппировать ПО
	|	РаботникиОрганизации.Период,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ФизЛицо,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.ВидЗанятости,
	|	РаботникиОрганизации.ПодразделениеОрганизации,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	РаботникиОрганизации.Должность,
	|	РаботникиОрганизации.ТарифныйРазряд,
	|	РаботникиОрганизации.ПричинаИзмененияСостояния	
	|";
	
	РаботникиТяжелоеПроизводствоТекст = "
	|" + ШтатноеРасписаниеТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|" + РаботникиОрганизацииТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	Выбор Когда РаботникиОрганизации.ДатаНачала <= ШтатноеРасписание.Период 
	|			Тогда ШтатноеРасписание.Период 
	|		Иначе РаботникиОрганизации.ДатаНачала
	|	Конец КАК Период,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ФизЛицо,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.ВидЗанятости,
	|	РаботникиОрганизации.ПодразделениеОрганизации,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	РаботникиОрганизации.Должность,
	|	РаботникиОрганизации.ТарифныйРазряд,
	|	РаботникиОрганизации.ПричинаИзмененияСостояния,
	|	ШтатноеРасписание.УсловияТруда
	|
	|ПОМЕСТИТЬ ВТРаботникиТяжелоеПроизводство
	|ИЗ
	|	ВТ_РаботникиОрганизацииПериоды КАК РаботникиОрганизации
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТШтатноеРасписаниеПериоды КАК  ШтатноеРасписание
	|	ПО  РаботникиОрганизации.ПодразделениеОрганизации = ШтатноеРасписание.ПодразделениеОрганизации
	|				И РаботникиОрганизации.Должность = ШтатноеРасписание.Должность
	|				И РаботникиОрганизации.ТарифныйРазряд = ШтатноеРасписание.ТарифныйРазряд
	|				И РаботникиОрганизации.ДатаОкончания >= ШтатноеРасписание.Период
	|				И РаботникиОрганизации.ДатаНачала <= ШтатноеРасписание.КонецПериода
	|";

	// Периоды назначений
	ПериодыНазначенийТекст = "
	|" + РаботникиТяжелоеПроизводствоТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаботникиОрганизации.Период КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(МИНИМУМ(ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РаботникиОрганизацииСлед.Период, ДЕНЬ, -1), ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РаботникиОрганизации2.Период, ДЕНЬ, -1), &ДатаКон))), ДЕНЬ) КАК ДатаОкончания,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ФизЛицо,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.ВидЗанятости,
	|	РаботникиОрганизации.ПодразделениеОрганизации,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	РаботникиОрганизации.Должность,
	|	РаботникиОрганизации.ТарифныйРазряд,
	|	РаботникиОрганизации.УсловияТруда
	|
	|ПОМЕСТИТЬ ВТ_ПериодыНазначений
	|
	|ИЗ
	|	ВТРаботникиТяжелоеПроизводство КАК РаботникиОрганизации
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации2
	|		ПО РаботникиОрганизации.Организация = РаботникиОрганизации2.Организация
	|			И РаботникиОрганизации.Сотрудник = РаботникиОрганизации2.Сотрудник
	|			И РаботникиОрганизации.Период < РаботникиОрганизации2.Период
	|			И РаботникиОрганизации2.Период <= &ДатаКон
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТРаботникиТяжелоеПроизводство КАК РаботникиОрганизацииСлед
	|		ПО РаботникиОрганизации.Организация = РаботникиОрганизацииСлед.Организация
	|			И РаботникиОрганизации.Сотрудник = РаботникиОрганизацииСлед.Сотрудник
	|			И РаботникиОрганизации.Период < РаботникиОрганизацииСлед.Период
	|			И РаботникиОрганизацииСлед.Период <= &ДатаКон
	|ГДЕ
	|	РаботникиОрганизации.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.Период,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ФизЛицо,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.ВидЗанятости,
	|	РаботникиОрганизации.ПодразделениеОрганизации,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	РаботникиОрганизации.Должность,
	|	РаботникиОрганизации.ТарифныйРазряд,
	|	РаботникиОрганизации.УсловияТруда
	|";
	
	// Определим состояние работников
	СостояниеРаботниковТекст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаботникиОрганизации.Сотрудник.ФизЛицо КАК ФизЛицо,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.Период,
	|	РаботникиОрганизации.ПричинаИзмененияСостояния,
	|	МАКСИМУМ(СостояниеРаботников.Период) КАК ПериодСостояния
	|
	|ПОМЕСТИТЬ ВТ_РаботникиОрганизации1
	|
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботников
	|		ПО РаботникиОрганизации.Сотрудник = СостояниеРаботников.Сотрудник
	|			И РаботникиОрганизации.Организация = СостояниеРаботников.Организация
	|			И СостояниеРаботников.Период <= РаботникиОрганизации.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.Сотрудник.ФизЛицо,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.Сотрудник,
	|	РаботникиОрганизации.Период,
	|	РаботникиОрганизации.ПричинаИзмененияСостояния
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ДатаНач КАК Период,
	|	СостояниеРаботников.Организация,
	|	СостояниеРаботников.Сотрудник.ФизЛицо КАК ФизЛицо,
	|	СостояниеРаботников.Сотрудник,
	|	ВЫБОР
	|		КОГДА СостояниеРаботников.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				И СостояниеРаботников.ПериодЗавершения <= &ДатаНач
	|			ТОГДА СостояниеРаботников.СостояниеЗавершения
	|		ИНАЧЕ СостояниеРаботников.Состояние
	|	КОНЕЦ КАК Состояние
	|
	|ПОМЕСТИТЬ ВТ_СостояниеРаботников
	|
	|ИЗ
	|	РегистрСведений.СостояниеРаботниковОрганизаций.СрезПоследних(&ДатаНач) КАК СостояниеРаботников
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СостояниеРаботников.Период,
	|	СостояниеРаботников.Организация,
	|	СостояниеРаботников.Сотрудник.ФизЛицо КАК ФизЛицо,
	|	СостояниеРаботников.Сотрудник,
	|	СостояниеРаботников.Состояние
	|ИЗ
	|	РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботников
	|ГДЕ
	|	СостояниеРаботников.Период > &ДатаНач
	|	И СостояниеРаботников.Период <= &ДатаКон
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СостояниеРаботников.ПериодЗавершения КАК Период,
	|	СостояниеРаботников.Организация,
	|	СостояниеРаботников.Сотрудник.ФизЛицо КАК ФизЛицо,
	|	СостояниеРаботников.Сотрудник,
	|	СостояниеРаботников.СостояниеЗавершения КАК Состояние
	|ИЗ
	|	РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботников
	|ГДЕ
	|	СостояниеРаботников.ПериодЗавершения МЕЖДУ &ДатаНач И &ДатаКон
	|	И СостояниеРаботников.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РаботникиОрганизации.Период,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ФизЛицо,
	|	РаботникиОрганизации.Сотрудник,
	|	ВЫБОР
	|		КОГДА СостояниеРаботников.Сотрудник ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.НеРаботает)
	|				  КОНЕЦ
	|		КОГДА СостояниеРаботников.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|					И СостояниеРаботников.ПериодЗавершения <= РаботникиОрганизации.Период
	|				ТОГДА СостояниеРаботников.СостояниеЗавершения
	|		ИНАЧЕ СостояниеРаботников.Состояние
	|	КОНЕЦ КАК Состояние
	|	
	|ИЗ
	|	ВТРаботникиТяжелоеПроизводство КАК РаботникиОрганизации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РаботникиОрганизации1 КАК ДатыПоследнихСостояний
	|		ПО РаботникиОрганизации.Сотрудник = ДатыПоследнихСостояний.Сотрудник
	|			И РаботникиОрганизации.Организация = ДатыПоследнихСостояний.Организация
	|			И РаботникиОрганизации.Период = ДатыПоследнихСостояний.Период
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботников
	|		ПО ДатыПоследнихСостояний.Сотрудник = СостояниеРаботников.Сотрудник
	|			И ДатыПоследнихСостояний.Организация = СостояниеРаботников.Организация
	|			И ДатыПоследнихСостояний.ПериодСостояния = СостояниеРаботников.Период
	|";
	
	// Периоды состояний
	// Все состояния Работает, НеРаботает здесь заменим на NULL. Тогда функция МАКСИМУМ()
	// при наложении на один день нескольких Состояний будет возвращать NULL, только если за день не было ни одного невыхода
	ПериодыСостоянийТекст = "
		|" + СостояниеРаботниковТекст + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ 
		|	ТекущееСостояние.ФизЛицо,
		|	ТекущееСостояние.Сотрудник,
		|	ТекущееСостояние.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ВЫБОР 
		|						КОГДА ТекущееСостояние.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает), ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.НеРаботает)) 
		|							ТОГДА NULL 
		|						ИНАЧЕ ТекущееСостояние.Состояние
		|					   КОНЕЦ) ЕСТЬ NULL 
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает)
		|		ИНАЧЕ МАКСИМУМ(ВЫБОР 
		|						КОГДА ТекущееСостояние.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает), ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.НеРаботает)) 
		|							ТОГДА NULL 
		|						ИНАЧЕ ТекущееСостояние.Состояние
		|					   КОНЕЦ)
		|	КОНЕЦ КАК Состояние,
		|	ТекущееСостояние.Период КАК ДатаНачала,		
		|	МИНИМУМ(ВЫБОР 
		|				КОГДА СледующееСостояние.Период ЕСТЬ NULL ТОГДА &ДатаКон
		|				ИНАЧЕ КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СледующееСостояние.Период, ДЕНЬ, -1))
		|			КОНЕЦ) КАК ДатаОкончания
		|
		|ПОМЕСТИТЬ ВТ_ПериодыСостояний
		|
		|ИЗ
		|	ВТ_СостояниеРаботников КАК ТекущееСостояние
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СостояниеРаботников КАК СледующееСостояние
		|		ПО ТекущееСостояние.Сотрудник = СледующееСостояние.Сотрудник
		|			И ТекущееСостояние.Организация = СледующееСостояние.Организация
		|			И ТекущееСостояние.Период < СледующееСостояние.Период
		|СГРУППИРОВАТЬ ПО
		|	ТекущееСостояние.ФизЛицо,
		|	ТекущееСостояние.Сотрудник,
		|	ТекущееСостояние.Организация,
		|	ТекущееСостояние.Период
		|";

	
	// Соединяем периоды назначений и периоды состояний, чтобы правильно учесть даты приема и увольнения
	ПериодыНазначенийТекст = "
	|" + ПериодыНазначенийТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|" + ПериодыСостоянийТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПериодыНазначений.Организация,
	|	ПериодыНазначений.ФизЛицо,	
	|	ПериодыНазначений.ВидЗанятости,
	|	ПериодыНазначений.ПодразделениеОрганизации,
	|	ПериодыНазначений.ОбособленноеПодразделение,
	|	ПериодыНазначений.Должность,
	|	ПериодыНазначений.ТарифныйРазряд,
	|	ПериодыНазначений.УсловияТруда,
	|	ВЫБОР
	|		КОГДА ПериодыНазначений.ДатаНачала > ПериодыСостояний.ДатаНачала
	|			  ИЛИ ПериодыСостояний.ДатаНачала ЕСТЬ NULL ТОГДА ПериодыНазначений.ДатаНачала
	|		ИНАЧЕ ПериодыСостояний.ДатаНачала
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ПериодыНазначений.ДатаОкончания < ПериодыСостояний.ДатаОкончания 
	|			  ИЛИ ПериодыСостояний.ДатаОкончания ЕСТЬ NULL ТОГДА ПериодыНазначений.ДатаОкончания
	|		ИНАЧЕ ПериодыСостояний.ДатаОкончания
	|	КОНЕЦ КАК ДатаОкончания,
	|	ЕСТЬNULL(ПериодыСостояний.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает)) КАК Состояние
	|
	|ПОМЕСТИТЬ ВТ_ПериодыНазначений1
	|
	|ИЗ
	|	ВТ_ПериодыНазначений КАК ПериодыНазначений
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыСостояний КАК ПериодыСостояний
	|		ПО ПериодыНазначений.Сотрудник = ПериодыСостояний.Сотрудник
	|		   И ПериодыНазначений.Организация = ПериодыСостояний.Организация
	|		   И ПериодыНазначений.ДатаНачала <= ПериодыСостояний.ДатаОкончания 
	|		   И ПериодыНазначений.ДатаОкончания >= ПериодыСостояний.ДатаНачала 
	|";

	// Разобьем данные о назначениях-состояниях на помесячные записи и подсчитаем число дней за каждый период
	ПериодыНазначенийТекст = "
	|" + ПериодыНазначенийТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|" + ПериодыТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ПериодыНазначений.Организация,
	|	ПериодыНазначений.ФизЛицо,	
	|	ПериодыНазначений.ВидЗанятости,
	|	ПериодыНазначений.ПодразделениеОрганизации,
	|	ПериодыНазначений.ОбособленноеПодразделение,
	|	ПериодыНазначений.Должность,
	|	ПериодыНазначений.ТарифныйРазряд,
	|	ПериодыНазначений.УсловияТруда,
	|	ПериодыНазначений.Состояние КАК Состояние,
	|	ДЕНЬ(ВЫБОР // ДатаОкончания
	|			КОГДА ПериодыНазначений.ДатаОкончания < Периоды.КонецМесяца ТОГДА ПериодыНазначений.ДатаОкончания
	|			ИНАЧЕ Периоды.КонецМесяца 
	|		КОНЕЦ) - 
    |	ДЕНЬ(ВЫБОР // ДатаНачала
	|			КОГДА ПериодыНазначений.ДатаНачала > Периоды.НачалоМесяца ТОГДА ПериодыНазначений.ДатаНачала
	|			ИНАЧЕ Периоды.НачалоМесяца
	|		КОНЕЦ) + 1 КАК ДнейРаботыВМесяце,
	|	Периоды.НачалоМесяца,
	|	Периоды.КонецМесяца
	|
	|ПОМЕСТИТЬ ВТ_ПериодыНазначений2
	|
	|ИЗ
	|	ВТ_ПериодыНазначений1 КАК ПериодыНазначений
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Периоды КАК Периоды
	|		ПО ПериодыНазначений.ДатаНачала <= Периоды.КонецМесяца 
	|		   И ПериодыНазначений.ДатаОкончания >= Периоды.НачалоМесяца 
	|";
	
	// Разобьем данные о назначениях-состояниях на помесячные записи и подсчитаем число дней за каждый период
	ЧисленностьПоМесяцамТекст = "
	|" + ПериодыНазначенийТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ПериодыНазначений.Организация,
	|	ПериодыНазначений.ФизЛицо,
	|	ПериодыНазначений.ВидЗанятости,
	|	ПериодыНазначений.Состояние,
	|	ПериодыНазначений.ПодразделениеОрганизации,
	|	ПериодыНазначений.ОбособленноеПодразделение,
	|	ПериодыНазначений.Должность,
	|	ПериодыНазначений.ТарифныйРазряд,
	|	ПериодыНазначений.УсловияТруда,
	|	ПериодыНазначений.НачалоМесяца,
	|	СУММА(ВЫБОР 
	|						КОГДА ПериодыНазначений.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.Совместительство) ТОГДА ПериодыНазначений.ДнейРаботыВМесяце
	|						ИНАЧЕ 0
	|				   КОНЕЦ) / ДЕНЬ(ПериодыНазначений.КонецМесяца) КАК СписочнаяЧисленность,
	|	СУММА(ВЫБОР 
	|						КОГДА ПериодыНазначений.Состояние НЕ В(&парамСписокИсключаемыхСостояний) ТОГДА ПериодыНазначений.ДнейРаботыВМесяце
	|						ИНАЧЕ 0
	|				   КОНЕЦ) / ДЕНЬ(ПериодыНазначений.КонецМесяца) КАК ФактическаяЧисленность
	|
	|ПОМЕСТИТЬ ВТ_ЧисленностьПоМесяцам
	|
	|ИЗ
	|	ВТ_ПериодыНазначений2 КАК ПериодыНазначений
	|СГРУППИРОВАТЬ ПО
	|	ПериодыНазначений.НачалоМесяца, 
	|	ПериодыНазначений.КонецМесяца,
	|	ПериодыНазначений.Организация,
	|	ПериодыНазначений.ФизЛицо,
	|	ПериодыНазначений.ВидЗанятости,
	|	ПериодыНазначений.Состояние,
	|	ПериодыНазначений.ПодразделениеОрганизации,
	|	ПериодыНазначений.ОбособленноеПодразделение,
	|	ПериодыНазначений.Должность,
	|	ПериодыНазначений.ТарифныйРазряд,
	|	ПериодыНазначений.УсловияТруда
	|";

	//|" + ПериодыТекст + "
	//|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|" + ПериодыНазначенийТекст + "
	//|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	//|	ПериодыНазначений.Организация,
	//|	ПериодыНазначений.ФизЛицо,
	//|	ПериодыНазначений.ВидЗанятости,
	//|	ПериодыНазначений.Состояние,
	//|	ПериодыНазначений.ПодразделениеОрганизации,
	//|	ПериодыНазначений.ОбособленноеПодразделение,
	//|	ПериодыНазначений.Должность,
	//|	ПериодыНазначений.ТарифныйРазряд,
	//|	СУММА(ДЕНЬ(ВЫБОР // ДатаОкончания
	//|					КОГДА ПериодыНазначений.ДатаОкончания < Периоды.КонецМесяца ТОГДА ПериодыНазначений.ДатаОкончания
	//|					ИНАЧЕ Периоды.КонецМесяца 
	//|				КОНЕЦ) - ДЕНЬ(ВЫБОР // ДатаНачала
	//|								КОГДА ПериодыНазначений.ДатаНачала > Периоды.НачалоМесяца ТОГДА ПериодыНазначений.ДатаНачала
	//|								ИНАЧЕ Периоды.НачалоМесяца
	//|					 		  КОНЕЦ) + 1) / ДЕНЬ(Периоды.КонецМесяца) КАК СписочнаяЧисленность,
	//|	СУММА(ВЫБОР
	//|			КОГДА НЕ ПериодыНазначений.Состояние В (&парамСписокИсключаемыхСостояний)
	//|				ТОГДА ДЕНЬ(ВЫБОР // ДатаОкончания
	//|								КОГДА ПериодыНазначений.ДатаОкончания < Периоды.КонецМесяца ТОГДА ПериодыНазначений.ДатаОкончания
	//|								ИНАЧЕ Периоды.КонецМесяца 
	//|							КОНЕЦ) - ДЕНЬ(ВЫБОР // ДатаНачала
	//|											КОГДА ПериодыНазначений.ДатаНачала > Периоды.НачалоМесяца ТОГДА ПериодыНазначений.ДатаНачала
	//|											ИНАЧЕ Периоды.НачалоМесяца
	//|					 		  			  КОНЕЦ) + 1
	//|			ИНАЧЕ 0
	//|		  КОНЕЦ) / ДЕНЬ(Периоды.КонецМесяца) КАК ФактическаяЧисленность,
	//|	Периоды.НачалоМесяца
	//|
	//|ПОМЕСТИТЬ ВТ_ЧисленностьПоМесяцам
	//|
	//|ИЗ
	//|	ВТ_ПериодыНазначений1 КАК ПериодыНазначений
	//|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Периоды КАК Периоды
	//|		ПО ПериодыНазначений.ДатаНачала <= Периоды.КонецМесяца 
	//|		   И ПериодыНазначений.ДатаОкончания >= Периоды.НачалоМесяца 
	//|
	//|СГРУППИРОВАТЬ ПО 
	//|	ПериодыНазначений.Организация,
	//|	ПериодыНазначений.ФизЛицо,
	//|	ПериодыНазначений.ВидЗанятости,
	//|	ПериодыНазначений.Состояние,
	//|	ПериодыНазначений.ПодразделениеОрганизации,
	//|	ПериодыНазначений.ОбособленноеПодразделение,
	//|	ПериодыНазначений.Должность,
	//|	ПериодыНазначений.ТарифныйРазряд,
	//|	Периоды.НачалоМесяца,
	//|	Периоды.КонецМесяца
	//|";

	// определим фонд заработной платы для вычисления фактической численности работников по договорам ГПХ
	ФондЗаработнойПлатыТекст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновныеНачисления.Организация КАК Организация,
	|	ОсновныеНачисления.ОбособленноеПодразделение,
	|	ОсновныеНачисления.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА ОсновныеНачисления.ВидРасчета В (&парамВидыОтпуска) 
	|				И ОсновныеНачисления.ПериодДействия >= ОсновныеНачисления.ПериодРегистрации
	|			ТОГДА НАЧАЛОПЕРИОДА(ОсновныеНачисления.ПериодДействия, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ОсновныеНачисления.ПериодРегистрации, МЕСЯЦ)
	|	КОНЕЦ КАК НачалоМесяца,
	|	ОсновныеНачисления.Результат КАК Результат,
	|	ВЫБОР КОГДА ОсновныеНачисления.ВидРасчета В (&парамВидыПоДоговорам) ТОГДА ОсновныеНачисления.Результат ИНАЧЕ 0 КОНЕЦ КАК РезультатПоДоговору
	|
	|ПОМЕСТИТЬ  ВТ_ФЗП
	|
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|ГДЕ
	|	((ОсновныеНачисления.ПериодРегистрации МЕЖДУ &ДатаНач И &ДатаКон И
	|	  (ОсновныеНачисления.ВидРасчета НЕ В (&парамВидыОтпуска) ИЛИ
	|	   ОсновныеНачисления.ПериодДействия < ОсновныеНачисления.ПериодРегистрации)) ИЛИ 
	|	 (ОсновныеНачисления.ПериодДействия МЕЖДУ &ДатаНач И &ДатаКон И
	|	  ОсновныеНачисления.ВидРасчета В (&парамВидыОтпуска) И
	|	  ОсновныеНачисления.ПериодДействия >= ОсновныеНачисления.ПериодРегистрации)) И
	|	ОсновныеНачисления.ВидРасчета В (&парамВидыВходящиеВФЗП)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДополнительныеНачисления.Организация КАК Организация,
	|	ДополнительныеНачисления.ОбособленноеПодразделение,
	|	ДополнительныеНачисления.ФизЛицо КАК ФизЛицо,
	|	НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) КАК НачалоМесяца,
	|	ДополнительныеНачисления.Результат КАК Результат,
	|	ВЫБОР КОГДА ДополнительныеНачисления.ВидРасчета В (&парамВидыПоДоговорам) ТОГДА ДополнительныеНачисления.Результат ИНАЧЕ 0 КОНЕЦ КАК РезультатПоДоговору
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|ГДЕ
	|	ДополнительныеНачисления.ПериодРегистрации МЕЖДУ &ДатаНач И &ДатаКон И
	|	ДополнительныеНачисления.ВидРасчета В (&парамВидыВходящиеВФЗП)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// прочие доходы, зарегистрированные в целях налогообложения
	|ВЫБРАТЬ
	|	СНСведенияОДоходах.Организация.ГоловнаяОрганизация КАК Организация,
	|	СНСведенияОДоходах.Организация КАК ОбособленноеПодразделение,
	|	СНСведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	НАЧАЛОПЕРИОДА(СНСведенияОДоходах.ПериодРегистрации, МЕСЯЦ) КАК НачалоМесяца,
	|	СНСведенияОДоходах.СуммаДохода КАК Результат,
	|	ВЫБОР КОГДА СНСведенияОДоходах.ВидРасчета В (&парамВидыПоДоговорам) ТОГДА СНСведенияОДоходах.СуммаДохода ИНАЧЕ 0 КОНЕЦ КАК РезультатПоДоговору
	|ИЗ
	|	РегистрНакопления.СНСведенияОДоходах КАК СНСведенияОДоходах
	|ГДЕ
	|	СНСведенияОДоходах.ПериодРегистрации МЕЖДУ &ДатаНач И &ДатаКон И
	|	СНСведенияОДоходах.ВидРасчета В (&парамВидыВходящиеВФЗП) И
	|	СНСведенияОДоходах.Регистратор ССЫЛКА Документ.РегистрацияПрочихДоходовВЦеляхНалогообложения
	|";
	
	// вычислим расчетную численность работников по договорам ГПХ
	РасчетнаяЧисленностьГПХТекст = "
	|" + ЧисленностьПоМесяцамТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаботникиОрганизации.НачалоМесяца,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	СУММА(РаботникиОрганизации.ФактическаяЧисленность) КАК ФактическаяЧисленность
	|ПОМЕСТИТЬ ВТ_РаботникиОрганизации2
	|ИЗ
	|	ВТ_ЧисленностьПоМесяцам КАК РаботникиОрганизации
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.НачалоМесяца,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ОбособленноеПодразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|" + ФондЗаработнойПлатыТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаботникиОрганизации.НачалоМесяца,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(РаботникиОрганизации.ФактическаяЧисленность) <> 0
	|				И СУММА(ФондЗаработнойПлаты.Результат - ФондЗаработнойПлаты.РезультатПоДоговору) <> 0
	|			ТОГДА СУММА(ФондЗаработнойПлаты.РезультатПоДоговору) / 
	|							(СУММА(ФондЗаработнойПлаты.Результат - ФондЗаработнойПлаты.РезультатПоДоговору) / 
	|								МАКСИМУМ(РаботникиОрганизации.ФактическаяЧисленность))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ФактическаяЧисленность
	|
	|ПОМЕСТИТЬ ВТ_РасчетнаяЧисленностьГПХ
	|
	|ИЗ
	|	ВТ_РаботникиОрганизации2 КАК РаботникиОрганизации
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ФЗП КАК ФондЗаработнойПлаты
	|		ПО ФондЗаработнойПлаты.Организация = РаботникиОрганизации.Организация
	|			И ФондЗаработнойПлаты.ОбособленноеПодразделение = РаботникиОрганизации.ОбособленноеПодразделение
	|			И РаботникиОрганизации.НачалоМесяца = ФондЗаработнойПлаты.НачалоМесяца
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.НачалоМесяца,
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ОбособленноеПодразделение
	|";

	// Определим назначение договорников на конец каждого периода
	ПоследниеДатыДоговоровТекст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Периоды.НачалоМесяца,
	|	ВЫБОР
	|		КОГДА ДоговорникиОрганизации.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ДоговорникиОрганизации.Организация
	|		ИНАЧЕ ДоговорникиОрганизации.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК Организация,
	|	ДоговорникиОрганизации.Организация КАК ОбособленноеПодразделение,
	|	ДоговорникиОрганизации.Сотрудник,
	|	МАКСИМУМ(ДоговорникиОрганизации.ДатаНачала) КАК ДатаНачала
	|
	|ПОМЕСТИТЬ ВТ_ПоследниеДатыДоговоров
	|
	|ИЗ
	|	ВТ_Периоды КАК Периоды
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоговорникиОрганизаций КАК ДоговорникиОрганизации
	|		ПО ДоговорникиОрганизации.ДатаНачала <= Периоды.КонецМесяца
	|
	|СГРУППИРОВАТЬ ПО
	|	Периоды.НачалоМесяца,
	|	ВЫБОР
	|		КОГДА ДоговорникиОрганизации.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ДоговорникиОрганизации.Организация
	|		ИНАЧЕ ДоговорникиОрганизации.Организация.ГоловнаяОрганизация
	|	КОНЕЦ,
	|	ДоговорникиОрганизации.Организация,
	|	ДоговорникиОрганизации.Сотрудник
	|";
	
	ДоговорникиОрганизацииТекст = "
	|" + ПоследниеДатыДоговоровТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоследниеДатыДоговоров.НачалоМесяца,
	|	ПоследниеДатыДоговоров.Организация,
	|	ПоследниеДатыДоговоров.ОбособленноеПодразделение,
	|	ПоследниеДатыДоговоров.Сотрудник.ФизЛицо КАК ФизЛицо,
	|	МАКСИМУМ(ДоговорникиОрганизации.ПодразделениеОрганизации) КАК ПодразделениеОрганизации,
	|	МАКСИМУМ(ДоговорникиОрганизации.Должность) КАК Должность
	|
	|ПОМЕСТИТЬ ВТ_ДоговорникиОрганизации
	|
	|ИЗ
	|	ВТ_ПоследниеДатыДоговоров КАК ПоследниеДатыДоговоров
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоговорникиОрганизаций КАК ДоговорникиОрганизации
	|		ПО ПоследниеДатыДоговоров.Сотрудник = ДоговорникиОрганизации.Сотрудник
	|			И ПоследниеДатыДоговоров.ОбособленноеПодразделение = ДоговорникиОрганизации.Организация
	|			И ПоследниеДатыДоговоров.ДатаНачала = ДоговорникиОрганизации.ДатаНачала
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоследниеДатыДоговоров.НачалоМесяца,
	|	ПоследниеДатыДоговоров.Организация,
	|	ПоследниеДатыДоговоров.ОбособленноеПодразделение,
	|	ПоследниеДатыДоговоров.Сотрудник.ФизЛицо
	|";

	// объединяем численность штатных работников и по договору ГПХ
	ЧисленностьПоМесяцамТекст = "
	|" + РасчетнаяЧисленностьГПХТекст + "
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|" + ДоговорникиОрганизацииТекст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФондЗаработнойПлаты.Организация,
	|	ФондЗаработнойПлаты.ОбособленноеПодразделение,
	|	ФондЗаработнойПлаты.НачалоМесяца,
	|	СУММА(ФондЗаработнойПлаты.РезультатПоДоговору) КАК РезультатПоДоговору
	|
	|ПОМЕСТИТЬ ВТ_ОбщийФЗП
	|
	|ИЗ
	|	ВТ_ФЗП КАК ФондЗаработнойПлаты
	|СГРУППИРОВАТЬ ПО
	|	ФондЗаработнойПлаты.Организация,
	|	ФондЗаработнойПлаты.ОбособленноеПодразделение,
	|	ФондЗаработнойПлаты.НачалоМесяца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.ФизЛицо,
	|	РаботникиОрганизации.ВидЗанятости,
	|	РаботникиОрганизации.Состояние,
	|	РаботникиОрганизации.ПодразделениеОрганизации,
	|	РаботникиОрганизации.ОбособленноеПодразделение,
	|	РаботникиОрганизации.Должность,
	|	РаботникиОрганизации.ТарифныйРазряд,
	|	РаботникиОрганизации.УсловияТруда,
	|	ЛОЖЬ КАК РаботаетПоДоговоруГПХ,
	|	РаботникиОрганизации.СписочнаяЧисленность,
	|	РаботникиОрганизации.ФактическаяЧисленность,
	|	-РАЗНОСТЬДАТ(&ДатаНач, РаботникиОрганизации.Физлицо.ДатаРождения, ГОД) КАК Возраст,
	|	РаботникиОрганизации.НачалоМесяца
	|
	|ПОМЕСТИТЬ ВТ_ЧисленностьПоМесяцам1
	|
	|ИЗ
	|	ВТ_ЧисленностьПоМесяцам КАК РаботникиОрганизации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорникиОрганизации.Организация,
	|	ДоговорникиОрганизации.ФизЛицо,
	|	""Работает по договору ГПХ"" КАК ВидЗанятости,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает) КАК Состояние,
	|	ДоговорникиОрганизации.ПодразделениеОрганизации,
	|	ДоговорникиОрганизации.ОбособленноеПодразделение,
	|	ДоговорникиОрганизации.Должность,
	|	NULL КАК ТарифныйРазряд,
	|	NULL КАК УсловияТруда,
	|	ИСТИНА КАК РаботаетПоДоговоруГПХ,
	|	0 КАК СписочнаяЧисленность,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ОбщийФондЗаработнойПлаты.РезультатПоДоговору) <> 0 
	|			ТОГДА МАКСИМУМ(РасчетнаяЧисленностьГПХ.ФактическаяЧисленность) * 
	|							СУММА(ФондЗаработнойПлатыФизЛица.РезультатПоДоговору) / 
	|								МАКСИМУМ(ОбщийФондЗаработнойПлаты.РезультатПоДоговору)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ФактическаяЧисленность,
	|	-РАЗНОСТЬДАТ(&ДатаНач, ДоговорникиОрганизации.Физлицо.ДатаРождения, ГОД) КАК Возраст,
	|	ДоговорникиОрганизации.НачалоМесяца
	|ИЗ
	|	ВТ_ДоговорникиОрганизации КАК ДоговорникиОрганизации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетнаяЧисленностьГПХ КАК РасчетнаяЧисленностьГПХ
	|		ПО ДоговорникиОрганизации.Организация = РасчетнаяЧисленностьГПХ.Организация
	|			И ДоговорникиОрганизации.ОбособленноеПодразделение = РасчетнаяЧисленностьГПХ.ОбособленноеПодразделение
	|			И ДоговорникиОрганизации.НачалоМесяца = РасчетнаяЧисленностьГПХ.НачалоМесяца
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбщийФЗП КАК ОбщийФондЗаработнойПлаты
	|   ПО ДоговорникиОрганизации.Организация = ОбщийФондЗаработнойПлаты.Организация
	|  		И ДоговорникиОрганизации.ОбособленноеПодразделение = ОбщийФондЗаработнойПлаты.ОбособленноеПодразделение
	|   	И ДоговорникиОрганизации.НачалоМесяца = ОбщийФондЗаработнойПлаты.НачалоМесяца
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ФЗП КАК ФондЗаработнойПлатыФизЛица
	|		ПО ДоговорникиОрганизации.Организация = ФондЗаработнойПлатыФизЛица.Организация
	|			И ДоговорникиОрганизации.ОбособленноеПодразделение = ФондЗаработнойПлатыФизЛица.ОбособленноеПодразделение
	|			И ДоговорникиОрганизации.ФизЛицо = ФондЗаработнойПлатыФизЛица.ФизЛицо
	|			И ДоговорникиОрганизации.НачалоМесяца = ФондЗаработнойПлатыФизЛица.НачалоМесяца
	|							
	|СГРУППИРОВАТЬ ПО
	|	ДоговорникиОрганизации.Организация,
	|	ДоговорникиОрганизации.ФизЛицо,
	|	ДоговорникиОрганизации.ПодразделениеОрганизации,
	|	ДоговорникиОрганизации.ОбособленноеПодразделение,
	|	ДоговорникиОрганизации.Должность,
	|	ДоговорникиОрганизации.НачалоМесяца
	|";

	Если РазворачиватьЧисленностьПоМесяцам Тогда
		
		ЧисленностьПоМесяцамТекст = "
		|" + ЧисленностьПоМесяцамТекст + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.СписочнаяЧисленность,
		|	Данные.ФактическаяЧисленность
		|	//ДОПОЛНИТЕЛЬНЫЕ ПОЛЯ
		|
		|{ВЫБРАТЬ
		|	Данные.ФизЛицо.*,
		|	Данные.ВидЗанятости,
		|	Данные.Состояние,
		|	Данные.ПодразделениеОрганизации.*,
		|	Данные.ОбособленноеПодразделение.* КАК Организация,
		|	Данные.Должность.*,
		|	ВЫРАЗИТЬ(Данные.Должность.НаименованиеПрофессии КАК СТРОКА(250)) КАК НаименованиеПрофессии,
		|	Данные.ТарифныйРазряд.*,
		|	Данные.УсловияТруда.*,
		|	Данные.РаботаетПоДоговоруГПХ,
		|	Данные.НачалоМесяца
		|	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
		|	//ДОПОЛНИТЕЛЬНЫЕ ПОЛЯ
		|	//СВОЙСТВА
		|}
		|ИЗ
		|	ВТ_ЧисленностьПоМесяцам1 КАК Данные
		|
		|	//ДАННЫЕ О ФИЗЛИЦЕ: СОЕДИНЕНИЯ
		|	//КОНТАКТНАЯ ИНФОРМАЦИЯ: СОЕДИНЕНИЯ
		|	//СОЕДИНЕНИЯ
		|
		|ГДЕ
		|	(Данные.СписочнаяЧисленность <> 0
		|		ИЛИ Данные.ФактическаяЧисленность <> 0)
		|		
		|{ГДЕ
		|	Данные.ФизЛицо.*,
		|	Данные.ВидЗанятости,
		|	Данные.Состояние,
		|	Данные.ПодразделениеОрганизации.*,
		|	Данные.ОбособленноеПодразделение.* КАК Организация,
		|	Данные.Должность.*,
		|	ВЫРАЗИТЬ(Данные.Должность.НаименованиеПрофессии КАК СТРОКА(250)) КАК НаименованиеПрофессии,
		|	Данные.ТарифныйРазряд.*,
		|	Данные.УсловияТруда.*,
		|	Данные.РаботаетПоДоговоруГПХ,
		|	Данные.НачалоМесяца
		| 	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
		|	//СВОЙСТВА
		|	//КАТЕГОРИИ
		|}
		|{УПОРЯДОЧИТЬ ПО
		|	Данные.ФизЛицо.*,
		|	Данные.ВидЗанятости,
		|	Данные.Состояние,
		|	Данные.ПодразделениеОрганизации.*,
		|	Данные.ОбособленноеПодразделение.* КАК Организация,
		|	Данные.Должность.*,
		|	ВЫРАЗИТЬ(Данные.Должность.НаименованиеПрофессии КАК СТРОКА(250)) КАК НаименованиеПрофессии,
		|	Данные.ТарифныйРазряд.*,
		|	Данные.УсловияТруда.*,
		|	Данные.РаботаетПоДоговоруГПХ,
		|	Данные.НачалоМесяца
		| 	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
		|	//КОНТАКТНАЯ ИНФОРМАЦИЯ: ПОЛЯ 
		|	//СВОЙСТВА
		|}
		|ИТОГИ 
		|	СУММА(СписочнаяЧисленность),
		|	СУММА(ФактическаяЧисленность)
		|ПО
		|	ОБЩИЕ
		|
		|{ИТОГИ	ПО	
		|	Данные.ФизЛицо.*,
		|	Данные.ВидЗанятости,
		|	Данные.Состояние,
		|	Данные.ПодразделениеОрганизации.*,
		|	Данные.ОбособленноеПодразделение.* КАК Организация,
		|	Данные.Должность.*,
		|	ВЫРАЗИТЬ(Данные.Должность.НаименованиеПрофессии КАК СТРОКА(250)) КАК НаименованиеПрофессии,
		|	Данные.ТарифныйРазряд.*,
		|	Данные.УсловияТруда.*,
		|	Данные.РаботаетПоДоговоруГПХ,
		|	Данные.НачалоМесяца
		| 	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
		|	//ДОПОЛНИТЕЛЬНЫЕ ПСЕВДОНИМЫ ПОЛЕЙ
		|	//СВОЙСТВА
		|}
		|";
	
		Возврат ЧисленностьПоМесяцамТекст;
		
	Иначе

		// разделим на число месяцев в периоде 
		ТекстЗапроса = "
		|" + ЧисленностьПоМесяцамТекст + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|   СУММА(Данные.СписочнаяЧисленность / РАЗНОСТЬДАТ(&ДатаНач, ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(&ДатаКон, МЕСЯЦ), СЕКУНДА, 1), МЕСЯЦ)) КАК СписочнаяЧисленность,
		|   СУММА(Данные.ФактическаяЧисленность / РАЗНОСТЬДАТ(&ДатаНач, ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(&ДатаКон, МЕСЯЦ), СЕКУНДА, 1), МЕСЯЦ)) КАК ФактическаяЧисленность
		|
		|{ВЫБРАТЬ
		|	Данные.ФизЛицо.*,
		|	Данные.ВидЗанятости.*,
		|	Данные.Состояние.*,
		|	Данные.ПодразделениеОрганизации.*,
		|	Данные.ОбособленноеПодразделение.* КАК Организация,
		|	Данные.Должность.*,
		|	ВЫРАЗИТЬ(Данные.Должность.НаименованиеПрофессии КАК СТРОКА(250)) КАК НаименованиеПрофессии,
		|	Данные.ТарифныйРазряд.*,
		|	Данные.УсловияТруда.*,
		|	Данные.Возраст.*,
		|	Данные.РаботаетПоДоговоруГПХ
		|	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
		|	//ДОПОЛНИТЕЛЬНЫЕ ПОЛЯ
		|	//СВОЙСТВА
		|}
		|ИЗ
		|	 ВТ_ЧисленностьПоМесяцам1 КАК Данные
		|
		|	//ДАННЫЕ О ФИЗЛИЦЕ: СОЕДИНЕНИЯ
		|	//КОНТАКТНАЯ ИНФОРМАЦИЯ: СОЕДИНЕНИЯ
		|	//СОЕДИНЕНИЯ
		|
		|ГДЕ
		|	(Данные.СписочнаяЧисленность <> 0
		|		ИЛИ Данные.ФактическаяЧисленность <> 0)
		|
		|{ГДЕ
		|	Данные.ФизЛицо.*,
		|	Данные.ВидЗанятости,
		|	Данные.Состояние,
		|	Данные.ПодразделениеОрганизации.*,
		|	Данные.ОбособленноеПодразделение.* КАК Организация,
		|	Данные.Должность.*,
		|	ВЫРАЗИТЬ(Данные.Должность.НаименованиеПрофессии КАК СТРОКА(250)) КАК НаименованиеПрофессии,
		|	Данные.ТарифныйРазряд.*,
		|	Данные.УсловияТруда.*,
		|	Данные.Возраст.*,
		|	Данные.РаботаетПоДоговоруГПХ
		| 	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
		|	//СВОЙСТВА
		|	//КАТЕГОРИИ
		|}
		|{УПОРЯДОЧИТЬ ПО
		|	Данные.ФизЛицо.*,
		|	Данные.ВидЗанятости,
		|	Данные.Состояние,
		|	Данные.ПодразделениеОрганизации.*,
		|	Данные.ОбособленноеПодразделение.* КАК Организация,
		|	Данные.Должность.*,
		|	ВЫРАЗИТЬ(Данные.Должность.НаименованиеПрофессии КАК СТРОКА(250)) КАК НаименованиеПрофессии,
		|	Данные.ТарифныйРазряд.*,
		|	Данные.УсловияТруда.*,
		|	Данные.Возраст.*,
		|	Данные.РаботаетПоДоговоруГПХ
		| 	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
		|	//КОНТАКТНАЯ ИНФОРМАЦИЯ: ПОЛЯ 
		|	//СВОЙСТВА
		|}
		|ИТОГИ 
		|	СУММА(СписочнаяЧисленность),
		|	СУММА(ФактическаяЧисленность)
		|ПО
		|	ОБЩИЕ
		|
		|{ИТОГИ	ПО	
		|	Данные.ФизЛицо.*,
		|	Данные.ВидЗанятости,
		|	Данные.Состояние,
		|	Данные.ПодразделениеОрганизации.*,
		|	Данные.ОбособленноеПодразделение.* КАК Организация,
		|	Данные.Должность.*,
		|	ВЫРАЗИТЬ(Данные.Должность.НаименованиеПрофессии КАК СТРОКА(250)) КАК НаименованиеПрофессии,
		|	Данные.ТарифныйРазряд.*,
		|	Данные.УсловияТруда.*,
		|	Данные.Возраст.*,
		|	Данные.РаботаетПоДоговоруГПХ
		| 	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
		|	//ДОПОЛНИТЕЛЬНЫЕ ПСЕВДОНИМЫ ПОЛЕЙ
		|	//СВОЙСТВА
		|}
		|";
		
		Возврат ТекстЗапроса;
		
	КонецЕсли;
	
КонецФункции

Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок = Ложь, ВысотаЗаголовка = 0, ТолькоЗаголовок = Ложь) Экспорт

	Если Не ТолькоЗаголовок Тогда
		
		Если НЕ ЗначениеЗаполнено(ОбщийОтчет.ДатаНач) или НЕ ЗначениеЗаполнено(ОбщийОтчет.ДатаКон) Тогда
			Предупреждение("Не указаны даты, ограничивающие период!");
			Возврат
		КонецЕсли;
		
		Если Не ПроцедурыУправленияПерсоналом.РегламентированныйКалендарьЗаполнен(ОбщийОтчет.ДатаНач, ОбщийОтчет.ДатаКон, Истина) Тогда
			Возврат
		КонецЕсли;
		
	КонецЕсли;

	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДатаКонУвольнений", КонецДня(ОбщийОтчет.ДатаКон) + 1);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДатаНачУвольнений", КонецДня(ОбщийОтчет.ДатаНач) + 1);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("парамСписокИсключаемыхСостояний", ?(ЭтотОбъект.ОбщиеИсключаемыеСостояния = Истина, ПроцедурыУправленияПерсоналом.ПолучитьСписокСостоянийИсключаемыхИзФактическойЧисленности(), ПроцедурыУправленияПерсоналом.ПолучитьСписокСостоянийИсключаемыхИзФактическойЧисленности2ТПРОФ()));
	
	// определим списки видов расчета по договору и по среднему заработку для отпуска
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамПоДоговорам", ПроведениеРасчетов.ПолучитьСписокСпособовРасчетовПоДоговору());
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПВР.Ссылка КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.ДополнительныеНачисленияОрганизаций КАК ПВР
	|ГДЕ
	|	ПВР.СпособРасчета В (&парамПоДоговорам)
	|";
	
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("парамВидыПоДоговорам", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидРасчета"));

	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПВР.Ссылка КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ПВР
	|ГДЕ
	|	ПВР.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуДляОтпуска)
	|";

	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("парамВидыОтпуска", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидРасчета"));

	// виды расчета, входящее в фонд заработной платы
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОсновныеНачисления.Ссылка КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.ФондВыплат.ФондЗаработнойПлаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДополнительныеНачисления.Ссылка КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.ДополнительныеНачисленияОрганизаций КАК ДополнительныеНачисления
	|ГДЕ
	|	ДополнительныеНачисления.ФондВыплат.ФондЗаработнойПлаты
	|";

    ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("парамВидыВходящиеВФЗП", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидРасчета"));
	
	ОбщийОтчет.СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок);

КонецПроцедуры

Процедура ЗаполнитьНачальныеНастройки(ИзмененВидОтчета = Ложь) Экспорт
	
	ОбщийОтчет.ИмяРегистра = "-";
	ОбщийОтчет.мНазваниеОтчета = ?(РазворачиватьЧисленностьПоМесяцам,"Численность по месяцам","Численность работников в среднем за период");
	
	ПостроительОтчета = ОбщийОтчет.ПостроительОтчета;
	
	ТекстЗапроса = ПолучитьТекстЗапроса();
	
	СтруктураПредставлениеПолей = Новый Структура();
	
	// При использовании свойств и категорий в текст запроса добавляются дополнительные поля
	Если ОбщийОтчет.ИспользоватьСвойстваИКатегории Тогда

		// Свойства и категории, назначаемые пользователем:
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДанным");  // описание поля запроса поля, для которого добавляются свойства и категории. Используется в условии соединения с регистром сведений, хранящим значения свойств или категорий
		ТаблицаПолей.Колонки.Добавить("Представление");// представление поля, для которого добавляются свойства и категории. 
		ТаблицаПолей.Колонки.Добавить("Назначение");   // назначение свойств/категорий объектов для данного поля
		ТаблицаПолей.Колонки.Добавить("ТипЗначения");  // тип значения поля, для которого добавляются свойства и категории. Используется, если не установлено назначение
		ТаблицаПолей.Колонки.Добавить("НетКатегорий"); // признак НЕиспользования категорий для объекта

		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "Данные.ФизЛицо";
		НоваяСтрока.Представление = "Сотрудник";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ФизическиеЛица;

		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "Данные.Организация";
		НоваяСтрока.Представление = "Организация";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Организации;

		// Добавим строки запроса, необходимые для использования свойств и категорий
		ТекстПоляКатегорий = "";
		ТекстПоляСвойств = "";

		УправлениеОтчетами.ДобавитьВТекстСвойстваИКатегории(ТаблицаПолей, ТекстЗапроса, СтруктураПредставлениеПолей, 
				ОбщийОтчет.мСоответствиеНазначений, ПостроительОтчета.Параметры
				,, ТекстПоляКатегорий, ТекстПоляСвойств,,,,,,ОбщийОтчет.мСтруктураДляОтбораПоКатегориям);
	
	КонецЕсли;
	              
	ПостроительОтчета.Текст = ТекстЗапроса;
	
	Если ОбщийОтчет.ИспользоватьСвойстваИКатегории = Истина Тогда
		УправлениеОтчетами.УстановитьТипыЗначенийСвойствИКатегорийДляОтбора(ПостроительОтчета, ТекстПоляКатегорий, ТекстПоляСвойств, ОбщийОтчет.мСоответствиеНазначений, СтруктураПредставлениеПолей);
	КонецЕсли;
	
	Если ИзмененВидОтчета Тогда
		ОбщийОтчет.Показатели.Очистить();	
		ОбщийОтчет.мТаблицаПоказатели.Очистить();
	КонецЕсли;
	
	// группировки по умолчанию
	ПостроительОтчета.ИзмеренияСтроки.Добавить("Организация");
	ПостроительОтчета.ИзмеренияСтроки.Добавить("ПодразделениеОрганизации");
	
	Если ПостроительОтчета.ДоступныеПоля.Найти("НачалоМесяца") <> Неопределено Тогда
		ПостроительОтчета.ИзмеренияКолонки.Добавить("НачалоМесяца");
	КонецЕсли;
	
	// список показателей и форматы их предстовления
	ОбщийОтчет.ЗаполнитьПоказатели("СписочнаяЧисленность", "Списочная численность", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("ФактическаяЧисленность", "Фактическая численность", Истина, "ЧЦ=15; ЧДЦ=2");
	
	// представление полей
	СтруктураПредставлениеПолей.Вставить("Физлицо", "Сотрудник");
	СтруктураПредставлениеПолей.Вставить("ПодразделениеОрганизации", "Подразделение");
	СтруктураПредставлениеПолей.Вставить("НаименованиеПрофессии", "Наименование профессии/должности");
	СтруктураПредставлениеПолей.Вставить("ТарифныйРазряд", "Тарифный разряд");
	СтруктураПредставлениеПолей.Вставить("УсловияТруда", "Условия труда");
	СтруктураПредставлениеПолей.Вставить("ВидЗанятости", "Вид занятости");
	СтруктураПредставлениеПолей.Вставить("НачалоМесяца", "Месяц");
	СтруктураПредставлениеПолей.Вставить("Возраст", "Возраст");
	СтруктураПредставлениеПолей.Вставить("РаботаетПоДоговоруГПХ", "Работает по договору ГПХ");
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
	// формат полей
	ОбщийОтчет.СтруктураФорматаПолей.Вставить("НачалоМесяца", "ДФ='MMMM yyyy ""г.""'");
	ОбщийОтчет.СтруктураФорматаПолей.Вставить("РаботаетПоДоговоруГПХ", "БЛ='Работает по трудовому договору'; БИ='Работает по договору ГПХ'");
	
	// очистка автозаполненных полей построителя
	ПостроительОтчета.ВыбранныеПоля.Очистить();
	
	// Выбранные поля
	ПостроительОтчета.ВыбранныеПоля.Добавить("Должность");
	ПостроительОтчета.ВыбранныеПоля.Добавить("НаименованиеПрофессии");
	ПостроительОтчета.ВыбранныеПоля.Добавить("Физлицо");
	
	// Общие итоги для помесячного отчета не выводим, если в колонка есть месяц,
	// т.к. складывать численность за разные месяцы не имеет смысла
	Если РазворачиватьЧисленностьПоМесяцам Тогда
		ОбщийОтчет.мСтруктураСвязиПоказателейИИзмерений.Вставить("СписочнаяЧисленность", Новый Структура("НачалоМесяца"));
		ОбщийОтчет.мСтруктураСвязиПоказателейИИзмерений.Вставить("ФактическаяЧисленность", Новый Структура("НачалоМесяца"));
	Иначе
		ОбщийОтчет.мСтруктураСвязиПоказателейИИзмерений.Очистить();
	КонецЕсли;

	Если Не ИзмененВидОтчета Тогда
		
		// отборы по умолчанию
		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить("Организация");
		МассивОтбора.Добавить("ПодразделениеОрганизации");
		МассивОтбора.Добавить("ВидЗанятости");
		УправлениеОтчетами.ЗаполнитьОтбор(МассивОтбора, ПостроительОтчета);
		
		ОтборВидЗанятости = ПостроительОтчета.Отбор.ВидЗанятости;
		ОтборВидЗанятости.Использование = Истина;
		СписокВидовЗанятости = Новый СписокЗначений;
		СписокВидовЗанятости.Добавить(Перечисления.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы);
		СписокВидовЗанятости.Добавить(Перечисления.ВидыЗанятостиВОрганизации.Совместительство);
		СписокВидовЗанятости.Добавить("Работает по договору ГПХ");
		ОтборВидЗанятости.ВидСравнения = ВидСравнения.ВСписке;
		ОтборВидЗанятости.Значение = СписокВидовЗанятости;
		
		// настройки отчета
		ОбщийОтчет.РаскрашиватьИзмерения = Истина;
		ОбщийОтчет.ВыводитьИтогиПоВсемУровням = Ложь;
		ОбщийОтчет.ВыводитьПоказателиВСтроку = Истина;
		ОбщийОтчет.мРежимВводаПериода = 0; // Период
		ОбщийОтчет.мВыбиратьИспользованиеСвойств =  Ложь;
		
	КонецЕсли;	
	
	//Выводить нумерацию в случае, если вид отчета "Численность работников в среднем за период"

	Если РазворачиватьЧисленностьПоМесяцам = Истина Тогда 
		ОбщийОтчет.ВыводитьНумерацию = Ложь;
	Иначе 
		ОбщийОтчет.ВыводитьНумерацию = Истина;
	КонецЕсли;
	
	УправлениеОтчетами.УпорядочитьПоляПостроителяОтчета(ПостроительОтчета);
	
КонецПроцедуры

// Читает свойство Построитель отчета
//
// Параметры
//	Нет
//
Функция ПолучитьПостроительОтчета() Экспорт

	Возврат ОбщийОтчет.ПолучитьПостроительОтчета();

КонецФункции // ПолучитьПостроительОтчета()

// Настраивает отчет по переданной структуре параметров
//
// Параметры:
//	Нет.
//
Процедура Настроить(Параметры) Экспорт

	РазворачиватьЧисленностьПоМесяцам = Параметры["ЭтотОтчет"].РазворачиватьЧисленностьПоМесяцам;
	ОбщийОтчет.Настроить(Параметры, ЭтотОбъект);

КонецПроцедуры

// Возвращает основную форму отчета, связанную с данным экземпляром отчета
//
// Параметры
//	Нет
//
Функция ПолучитьОсновнуюФорму() Экспорт
	
	ОснФорма = ПолучитьФорму();
	ОснФорма.ОбщийОтчет = ОбщийОтчет;
	ОснФорма.ЭтотОтчет = ЭтотОбъект;
	Возврат ОснФорма;
	
КонецФункции // ПолучитьОсновнуюФорму()

#КонецЕсли