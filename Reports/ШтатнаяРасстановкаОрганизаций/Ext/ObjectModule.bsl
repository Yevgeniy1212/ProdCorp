#Если Клиент Тогда

Перем мВалютаРеглУчета;

Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок = Ложь, ВысотаЗаголовка = 0, ТолькоЗаголовок = Ложь) Экспорт

	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Уволен", Перечисления.ПричиныИзмененияСостояния.Увольнение);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("НачальнаяДата", '00010101');
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДатаАктуальности", ОбщийОтчет.ДатаКон);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДатаПослеАктуальности", ОбщийОтчет.ДатаКон+ 1);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Месячная", Перечисления.ВидыТарифныхСтавок.Месячная);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Дневная", Перечисления.ВидыТарифныхСтавок.Дневная);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Часовая", Перечисления.ВидыТарифныхСтавок.Часовая);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ПоДням", Перечисления.ВидыУчетаВремени.ПоДням);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("Сменный", Перечисления.ВидыРабочихГрафиков.Сменный);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ВалютаРеглУчета", мВалютаРеглУчета);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ПустаяВалюта", Справочники.Валюты.ПустаяСсылка());
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ТипПоказателяТарифныйРазряд", Перечисления.ТипыПоказателейСхемМотивации.ТарифныйРазряд);
	
	МассивМесячныхСпособовРасчета = Новый Массив;
	МассивМесячныхСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоДням);
	МассивМесячныхСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоЧасам);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("МесячныеСпособыРасчета", МассивМесячныхСпособовРасчета);
	
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДневныеСпособыРасчета", Перечисления.СпособыРасчетаОплатыТруда.ПоДневнойТарифнойСтавке);
	
	МассивЧасовыхСпособовРасчета = Новый Массив;
	МассивЧасовыхСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ПоЧасовойТарифнойСтавке);
	МассивЧасовыхСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.СдельныйЗаработок);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ЧасовыеСпособыРасчета", МассивЧасовыхСпособовРасчета);
	
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("СпособыРасчетаОтМЗП", ПроведениеРасчетов.ПолучитьСписокСпособовРасчетовПоМинимальнойЗаработнойПлате());
	
	ОбщийОтчет.СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок);

КонецПроцедуры

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОбщийОтчет.ИмяРегистра = "-";
	ОбщийОтчет.мНазваниеОтчета = "Штатная расстановка организации";
	
	ПостроительОтчета = ОбщийОтчет.ПостроительОтчета;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации.Владелец КАК Организация,
	|	РаботникиОрганизации.Сотрудник.Физлицо КАК ФизЛицо,
	|	РаботникиОрганизации.Сотрудник КАК Сотрудник,
	|	РаботникиОрганизации.Сотрудник.ВидЗанятости КАК ВидЗанятости,
	|	РаботникиОрганизации.Сотрудник.Код КАК ТабельныйНомер,
	|	ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ШтатноеРасписаниеОрганизаций.Должность КАК Должность,
	|	ШтатноеРасписаниеОрганизаций.ТарифныйРазряд КАК ТарифныйРазряд,
	|	ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы КАК ГрафикРаботыШтЕд,
	|	ШтатноеРасписаниеОрганизацийСрезПоследних.УсловияТруда КАК УсловияТруда,
	|	РаботникиОрганизации.ГрафикРаботы КАК ГрафикРаботыРаботника,
	|	ШтатноеРасписаниеОрганизацийСрезПоследних.ВидТарифнойСтавки КАК ВидТарифнойСтавки,
	|	ШтатноеРасписаниеОрганизацийСрезПоследних.ВалютаТарифнойСтавки КАК ВалютаТарифнойСтавки,
	|	ШтатноеРасписаниеОрганизацийСрезПоследних.КоличествоСтавок КАК КоличествоСтавок,
	|	ШтатноеРасписаниеОрганизацийСрезПоследних.МинимальнаяТарифнаяСтавка,
	|	ШтатноеРасписаниеОрганизацийСрезПоследних.МаксимальнаяТарифнаяСтавка,
	|	ШтатноеРасписаниеОрганизацийСрезПоследних.УстановленнаяТарифнаяСтавка,
	|	РаботникиОрганизации.ЗанимаемыхСтавок,
	|	ВЫБОР
	|		КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|				И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|			ТОГДА ПлановыеНачисления.ВидРасчетаЗавершения
	|		ИНАЧЕ ПлановыеНачисления.ВидРасчета
	|	КОНЕЦ КАК ВидРасчетаОкладТариф,
	|	ВЫБОР
	|		КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|				И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|			ТОГДА ВЫБОР 
	|					КОГДА ПлановыеНачисления.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|							ИЛИ ПоказателиЗавершения.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|						ТОГДА РазмерТарифныхСтавок.Размер
	|					ИНАЧЕ ПлановыеНачисления.Показатель1Завершения
	|				  КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПлановыеНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда
	|						ИЛИ Показатели.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|					ТОГДА РазмерТарифныхСтавок.Размер
	|				ИНАЧЕ ПлановыеНачисления.Показатель1
	|			  КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|				КОГДА ВЫБОР
	|						КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|								И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|							ТОГДА ПлановыеНачисления.ВидРасчетаЗавершения.СпособРасчета В (&СпособыРасчетаОтМЗП)
	|						ИНАЧЕ ПлановыеНачисления.ВидРасчета.СпособРасчета В (&СпособыРасчетаОтМЗП)
	|					  КОНЕЦ 
	|					ТОГДА ЕСТЬNULL(РегламентированныеРасчетныеПоказатели.РазмерМЗП, 0)
	|				ИНАЧЕ 1
	|			КОНЕЦ КАК РазмерОкладТариф,
	|	ВЫБОР
	|		КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|				И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|			ТОГДА ВЫБОР 
	|					КОГДА ПлановыеНачисления.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|							ИЛИ ПоказателиЗавершения.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|						ТОГДА РазмерТарифныхСтавок.Валюта
	|					ИНАЧЕ ПлановыеНачисления.Валюта1Завершения
	|				  КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПлановыеНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда
	|						ИЛИ Показатели.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|					ТОГДА РазмерТарифныхСтавок.Валюта
	|				ИНАЧЕ ПлановыеНачисления.Валюта1
	|			  КОНЕЦ
	|	КОНЕЦ КАК ВалютаОкладТариф,
	|	ВЫБОР
	|		КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВидТарифнойСтавки = &Месячная
	|			ТОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.МинимальнаяТарифнаяСтавка
	|		КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВидТарифнойСтавки = &Дневная
	|			ТОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.МинимальнаяТарифнаяСтавка * 
	|						ВЫБОР
	|							КОГДА ЕСТЬNULL(ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы.ИспользуетсяСовместноСПланГрафиком, ЛОЖЬ)
	|								ТОГДА БалансВремениДляШтЕд.НормаДней
	|							ИНАЧЕ НормыВремениДляШтЕд.НормаДней
	|						КОНЕЦ
	|		ИНАЧЕ // часовая
	|			ШтатноеРасписаниеОрганизацийСрезПоследних.МинимальнаяТарифнаяСтавка *
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы.ИспользуетсяСовместноСПланГрафиком, ЛОЖЬ)
	|						ТОГДА БалансВремениДляШтЕд.НормаЧасов
	|					ИНАЧЕ НормыВремениДляШтЕд.НормаЧасов
	|				КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|				КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВалютаТарифнойСтавки = &ПустаяВалюта
	|						ИЛИ ШтатноеРасписаниеОрганизацийСрезПоследних.ВалютаТарифнойСтавки = &ВалютаРеглУчета
	|					ТОГДА 1
	|				ИНАЧЕ ЕСТЬNULL(КурсДляШтЕд.Курс, 0) / ЕСТЬNULL(КурсДляШтЕд.Кратность, 1)
	|			КОНЕЦ КАК МинимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте,
	|
	|	ВЫБОР
	|		КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВидТарифнойСтавки = &Месячная
	|			ТОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.МаксимальнаяТарифнаяСтавка
	|		КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВидТарифнойСтавки = &Дневная
	|			ТОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.МаксимальнаяТарифнаяСтавка * 
	|						ВЫБОР
	|							КОГДА ЕСТЬNULL(ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы.ИспользуетсяСовместноСПланГрафиком, ЛОЖЬ)
	|								ТОГДА БалансВремениДляШтЕд.НормаДней
	|							ИНАЧЕ НормыВремениДляШтЕд.НормаДней
	|						КОНЕЦ
	|		ИНАЧЕ // часовая
	|			ШтатноеРасписаниеОрганизацийСрезПоследних.МаксимальнаяТарифнаяСтавка *
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы.ИспользуетсяСовместноСПланГрафиком, ЛОЖЬ)
	|						ТОГДА БалансВремениДляШтЕд.НормаЧасов
	|					ИНАЧЕ НормыВремениДляШтЕд.НормаЧасов
	|				КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|				КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВалютаТарифнойСтавки = &ПустаяВалюта
	|						ИЛИ ШтатноеРасписаниеОрганизацийСрезПоследних.ВалютаТарифнойСтавки = &ВалютаРеглУчета
	|					ТОГДА 1
	|				ИНАЧЕ ЕСТЬNULL(КурсДляШтЕд.Курс, 0) / ЕСТЬNULL(КурсДляШтЕд.Кратность, 1)
	|			КОНЕЦ КАК МаксимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте,
	|
	|	ВЫБОР
	|		КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВидТарифнойСтавки = &Месячная
	|			ТОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.УстановленнаяТарифнаяСтавка
	|		КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВидТарифнойСтавки = &Дневная
	|			ТОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.УстановленнаяТарифнаяСтавка * 
	|						ВЫБОР
	|							КОГДА ЕСТЬNULL(ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы.ИспользуетсяСовместноСПланГрафиком, ЛОЖЬ)
	|								ТОГДА БалансВремениДляШтЕд.НормаДней
	|							ИНАЧЕ НормыВремениДляШтЕд.НормаДней
	|						КОНЕЦ
	|		ИНАЧЕ // часовая
	|			ШтатноеРасписаниеОрганизацийСрезПоследних.УстановленнаяТарифнаяСтавка *
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы.ИспользуетсяСовместноСПланГрафиком, ЛОЖЬ)
	|						ТОГДА БалансВремениДляШтЕд.НормаЧасов
	|					ИНАЧЕ НормыВремениДляШтЕд.НормаЧасов
	|				КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|				КОГДА ШтатноеРасписаниеОрганизацийСрезПоследних.ВалютаТарифнойСтавки = &ПустаяВалюта
	|						ИЛИ ШтатноеРасписаниеОрганизацийСрезПоследних.ВалютаТарифнойСтавки = &ВалютаРеглУчета
	|					ТОГДА 1
	|				ИНАЧЕ ЕСТЬNULL(КурсДляШтЕд.Курс, 0) / ЕСТЬNULL(КурсДляШтЕд.Кратность, 1)
	|			КОНЕЦ КАК УстановленнаяТарифнаяСтавкаЗаМесяцВРеглВалюте,
	|
	|	ВЫБОР
	|		КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|				И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|			ТОГДА ВЫБОР 
	|					КОГДА ПлановыеНачисления.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|							ИЛИ ПоказателиЗавершения.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|						ТОГДА РазмерТарифныхСтавок.Размер
	|					ИНАЧЕ ПлановыеНачисления.Показатель1Завершения
	|				  КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПлановыеНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда
	|						ИЛИ Показатели.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|					ТОГДА РазмерТарифныхСтавок.Размер
	|				ИНАЧЕ ПлановыеНачисления.Показатель1
	|			  КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|				КОГДА ВЫБОР 
	|						КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|								И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|							ТОГДА ПлановыеНачисления.ВидРасчетаЗавершения.СпособРасчета В (&ДневныеСпособыРасчета)
	|						ИНАЧЕ ПлановыеНачисления.ВидРасчета.СпособРасчета В (&ДневныеСпособыРасчета)
	|					  КОНЕЦ
	|					ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(РаботникиОрганизации.ГрафикРаботы.ИспользуетсяСовместноСПланГрафиком, ЛОЖЬ)
	|									ТОГДА БалансВремениДляРаботника.НормаДней
	|								ИНАЧЕ НормыВремениДляРаботника.НормаДней
	|				  		  КОНЕЦ
	|
	|				КОГДА ВЫБОР 
	|						КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|								И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|							ТОГДА ПлановыеНачисления.ВидРасчетаЗавершения.СпособРасчета В (&ЧасовыеСпособыРасчета)
	|						ИНАЧЕ ПлановыеНачисления.ВидРасчета.СпособРасчета В (&ЧасовыеСпособыРасчета)
	|					  КОНЕЦ 
	|					ТОГДА ВЫБОР
	|							КОГДА ЕСТЬNULL(РаботникиОрганизации.ГрафикРаботы.ИспользуетсяСовместноСПланГрафиком, ЛОЖЬ)
	|								ТОГДА БалансВремениДляРаботника.НормаЧасов
	|							ИНАЧЕ НормыВремениДляРаботника.НормаЧасов
	|			  		 	  КОНЕЦ
	|
	|				КОГДА ВЫБОР
	|						КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|								И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|							ТОГДА ПлановыеНачисления.ВидРасчетаЗавершения.СпособРасчета В (&СпособыРасчетаОтМЗП)
	|						ИНАЧЕ ПлановыеНачисления.ВидРасчета.СпособРасчета В (&СпособыРасчетаОтМЗП)
	|					  КОНЕЦ
	|					ТОГДА ЕСТЬNULL(РегламентированныеРасчетныеПоказатели.РазмерМЗП, 0)
	|
	|				ИНАЧЕ 1
	|			КОНЕЦ * ВЫБОР
	|						КОГДА ВЫБОР
	|									КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|											И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|										ТОГДА ВЫБОР
	|												КОГДА ПлановыеНачисления.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|														ИЛИ ПоказателиЗавершения.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|													ТОГДА РазмерТарифныхСтавок.Валюта
	|												ИНАЧЕ ПлановыеНачисления.Валюта1Завершения
	|								  			  КОНЕЦ
	|										ИНАЧЕ ВЫБОР
	|												КОГДА ПлановыеНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда
	|														ИЛИ Показатели.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|													ТОГДА РазмерТарифныхСтавок.Валюта
	|												ИНАЧЕ ПлановыеНачисления.Валюта1
	|							  				  КОНЕЦ
	|							   КОНЕЦ В (&ВалютаРеглУчета, &ПустаяВалюта)
	|							ТОГДА 1
	|						ИНАЧЕ ЕСТЬNULL(КурсДляРаботника.Курс, 0) / ЕСТЬNULL(КурсДляРаботника.Кратность, 1)
	|					КОНЕЦ КАК РазмерОкладТарифЗаМесяцВРеглВалюте
	|
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|		ШтатноеРасписаниеОрганизаций.Должность КАК Должность,
	|		ШтатноеРасписаниеОрганизаций.ТарифныйРазряд КАК ТарифныйРазряд
	|	ИЗ
	|		РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписаниеОрганизаций
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РаботникиОрганизаций.ПодразделениеОрганизации,
	|		РаботникиОрганизаций.Должность,
	|		РаботникиОрганизаций.ТарифныйРазряд КАК ТарифныйРазряд
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций) КАК ШтатноеРасписаниеОрганизаций
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтатноеРасписаниеОрганизаций.СрезПоследних(&ДатаАктуальности, ) КАК ШтатноеРасписаниеОрганизацийСрезПоследних
	|			ПО ШтатноеРасписаниеОрганизаций.Должность = ШтатноеРасписаниеОрганизацийСрезПоследних.Должность 
	|				И ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации = ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации
	|				И ШтатноеРасписаниеОрганизаций.ТарифныйРазряд = ШтатноеРасписаниеОрганизацийСрезПоследних.ТарифныйРазряд
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаАктуальности, ) КАК РаботникиОрганизации
	|			ПО ШтатноеРасписаниеОрганизаций.Должность = РаботникиОрганизации.Должность 
	|				И ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации = РаботникиОрганизации.ПодразделениеОрганизации 
	|				И ШтатноеРасписаниеОрганизаций.ТарифныйРазряд = РаботникиОрганизации.ТарифныйРазряд
	|				И (РаботникиОрганизации.ПричинаИзмененияСостояния <> &Уволен)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&ДатаАктуальности, ВидРасчетаИзмерение = НЕОПРЕДЕЛЕНО) КАК ПлановыеНачисления
	|			ПО РаботникиОрганизации.Организация = ПлановыеНачисления.Организация
	|				И РаботникиОрганизации.Сотрудник = ПлановыеНачисления.Сотрудник
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Показатели КАК Показатели
	|			ПО ПлановыеНачисления.ВидРасчета = Показатели.ВидРасчета
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Показатели КАК ПоказателиЗавершения
	|			ПО ПлановыеНачисления.ВидРасчетаЗавершения = ПоказателиЗавершения.ВидРасчета
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ДатаАктуальности) КАК РазмерТарифныхСтавок
	|			ПО РаботникиОрганизации.Должность = РазмерТарифныхСтавок.Должность
	|				И ВЫБОР
	|						КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|								И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|							ТОГДА ПлановыеНачисления.ТарифныйРазряд1Завершения
	|						ИНАЧЕ ПлановыеНачисления.ТарифныйРазряд1 
	|				  КОНЕЦ = РазмерТарифныхСтавок.ТарифныйРазряд
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсДляШтЕд
	|			ПО ШтатноеРасписаниеОрганизацийСрезПоследних.ВалютаТарифнойСтавки = КурсДляШтЕд.Валюта
	|				И КурсДляШтЕд.Период = НАЧАЛОПЕРИОДА(&ДатаАктуальности, МЕСЯЦ)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсДляРаботника
	|			ПО ВЫБОР
	|					КОГДА ПлановыеНачисления.ПериодЗавершения <> &НачальнаяДата
	|							И ПлановыеНачисления.ПериодЗавершения < &ДатаАктуальности
	|						ТОГДА ВЫБОР
	|									КОГДА ПлановыеНачисления.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|											ИЛИ ПоказателиЗавершения.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|										ТОГДА РазмерТарифныхСтавок.Валюта
	|									ИНАЧЕ ПлановыеНачисления.Валюта1Завершения
	|							  КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|								КОГДА ПлановыеНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда
	|										ИЛИ Показатели.Показатель1ТипПоказателя = &ТипПоказателяТарифныйРазряд
	|									ТОГДА РазмерТарифныхСтавок.Валюта
	|								ИНАЧЕ ПлановыеНачисления.Валюта1
	|						  КОНЕЦ
	|			   КОНЕЦ = КурсДляРаботника.Валюта
	|				И КурсДляРаботника.Период = НАЧАЛОПЕРИОДА(&ДатаАктуальности, МЕСЯЦ)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БалансРабочегоВремени КАК БалансВремениДляШтЕд
	|			ПО ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы = БалансВремениДляШтЕд.ГрафикРаботы
	|				И БалансВремениДляШтЕд.ДатаМесяца = НАЧАЛОПЕРИОДА(&ДатаАктуальности, МЕСЯЦ)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|								ГрафикиРаботыПоВидамВремени.ГрафикРаботы,
	|								СУММА(ГрафикиРаботыПоВидамВремени.ОсновноеЗначениеНорма) КАК НормаДней,
	|								СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначениеНорма) КАК НормаЧасов
	|							ИЗ
	|								РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|							ГДЕ
	|								ГрафикиРаботыПоВидамВремени.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаАктуальности, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ДатаАктуальности, МЕСЯЦ)
	|								И ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = &ПоДням
	|								И ГрафикиРаботыПоВидамВремени.ГрафикРаботы ССЫЛКА Справочник.ГрафикиРаботы
	|							СГРУППИРОВАТЬ ПО
	|								ГрафикиРаботыПоВидамВремени.ГрафикРаботы) КАК НормыВремениДляШтЕд
	|			ПО ШтатноеРасписаниеОрганизацийСрезПоследних.ГрафикРаботы = НормыВремениДляШтЕд.ГрафикРаботы
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БалансРабочегоВремени КАК БалансВремениДляРаботника
	|			ПО РаботникиОрганизации.ГрафикРаботы = БалансВремениДляРаботника.ГрафикРаботы
	|				И БалансВремениДляРаботника.ДатаМесяца = НАЧАЛОПЕРИОДА(&ДатаАктуальности, МЕСЯЦ)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|								ГрафикиРаботыПоВидамВремени.ГрафикРаботы,
	|								СУММА(ГрафикиРаботыПоВидамВремени.ОсновноеЗначениеНорма) КАК НормаДней,
	|								СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначениеНорма) КАК НормаЧасов
	|							ИЗ
	|								РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|							ГДЕ
	|								ГрафикиРаботыПоВидамВремени.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаАктуальности, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ДатаАктуальности, МЕСЯЦ)
	|								И ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = &ПоДням
	|								И ГрафикиРаботыПоВидамВремени.ГрафикРаботы ССЫЛКА Справочник.ГрафикиРаботы
	|							СГРУППИРОВАТЬ ПО
	|								ГрафикиРаботыПоВидамВремени.ГрафикРаботы) КАК НормыВремениДляРаботника
	|			ПО РаботникиОрганизации.ГрафикРаботы = НормыВремениДляРаботника.ГрафикРаботы
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныеРасчетныеПоказатели.СрезПоследних(&ДатаАктуальности) КАК РегламентированныеРасчетныеПоказатели
	|			ПО ИСТИНА
	|
	|ГДЕ
	|	(НЕ((РаботникиОрганизации.Сотрудник) ЕСТЬ NULL ))
	|";
	
	ОкончательныйТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПоказателиПлановыхНачисленийРаботниковОрганизаций.ВидРасчета,
	|	ПоказателиПлановыхНачисленийРаботниковОрганизаций.Показатель1ТипПоказателя КАК Показатель1ТипПоказателя
	|ПОМЕСТИТЬ ВТ_Показатели
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета КАК ВидРасчета,
	|		Показатели1.Показатель.ТипПоказателя КАК Показатель1ТипПоказателя
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета КАК ВидРасчета
	|		ИЗ
	|			РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|		ГДЕ
	|			ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение = НЕОПРЕДЕЛЕНО
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения
	|		ИЗ
	|			РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|		ГДЕ
	|			ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение = НЕОПРЕДЕЛЕНО
	|			И ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения <> НЕОПРЕДЕЛЕНО) КАК ПлановыеНачисленияРаботниковОрганизаций
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций.Показатели КАК Показатели1
	|			ПО ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета = Показатели1.Ссылка
	|				И (Показатели1.НомерСтроки = 1)) КАК ПоказателиПлановыхНачисленийРаботниковОрганизаций
	|;
	|////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Данные.ЗанимаемыхСтавок,
	|	Данные.МинимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте,
	|	Данные.МаксимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте,
	|	Данные.УстановленнаяТарифнаяСтавкаЗаМесяцВРеглВалюте,
	|	Данные.РазмерОкладТарифЗаМесяцВРеглВалюте
	|
	|{ВЫБРАТЬ
	|	 Данные.Организация.* КАК Организация,
	|	 Данные.Физлицо.* КАК Физлицо,
	|	 Данные.Сотрудник.* КАК Сотрудник,
	|	 Данные.ВидЗанятости КАК ВидЗанятости,
	|	 Данные.ТабельныйНомер КАК ТабельныйНомер,
	|	 Данные.ПодразделениеОрганизации.* КАК ПодразделениеОрганизации,
	|	 Данные.Должность.* КАК Должность,
	|	 Данные.ТарифныйРазряд.* КАК ТарифныйРазряд,
	|	 Данные.ГрафикРаботыШтЕд.* КАК ГрафикРаботыШтЕд,
	|	 Данные.УсловияТруда.* КАК УсловияТруда,
	|	 Данные.ГрафикРаботыРаботника.* КАК ГрафикРаботыРаботника,
	|	 Данные.ВидТарифнойСтавки КАК ВидТарифнойСтавки,
	|	 Данные.ВалютаТарифнойСтавки.* КАК ВалютаТарифнойСтавки,
	|	 Данные.МинимальнаяТарифнаяСтавка КАК МинимальнаяТарифнаяСтавка,
	|	 Данные.МаксимальнаяТарифнаяСтавка КАК МаксимальнаяТарифнаяСтавка,
	|	 Данные.УстановленнаяТарифнаяСтавка КАК УстановленнаяТарифнаяСтавка,
	|	 Данные.ВидРасчетаОкладТариф.* КАК ВидРасчетаОкладТариф,
	|	 Данные.РазмерОкладТариф КАК РазмерОкладТариф,
	|	 Данные.ВалютаОкладТариф.* КАК ВалютаОкладТариф
	|	//СВОЙСТВА	
	|}
	|
	|ИЗ
	|	(" + ТекстЗапроса + ") КАК Данные
	|	//СОЕДИНЕНИЯ
	|
	|ИТОГИ СУММА(ЗанимаемыхСтавок),
	|		СУММА(МинимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте),
	|		СУММА(МаксимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте),
	|		СУММА(УстановленнаяТарифнаяСтавкаЗаМесяцВРеглВалюте),
	|		СУММА(РазмерОкладТарифЗаМесяцВРеглВалюте) ПО
	|	ОБЩИЕ
	|	
	|{ГДЕ
	|	 Данные.Организация.* КАК Организация,
	|	 Данные.Физлицо.* КАК Физлицо,
	|	 Данные.Сотрудник.* КАК Сотрудник,
	|	 Данные.ВидЗанятости КАК ВидЗанятости,
	|	 Данные.ТабельныйНомер КАК ТабельныйНомер,
	|	 Данные.ПодразделениеОрганизации.* КАК ПодразделениеОрганизации,
	|	 Данные.Должность.* КАК Должность,
	|	 Данные.ТарифныйРазряд.* КАК ТарифныйРазряд,
	|	 Данные.ГрафикРаботыШтЕд.* КАК ГрафикРаботыШтЕд,
	|	 Данные.УсловияТруда.* КАК УсловияТруда,
	|	 Данные.ГрафикРаботыРаботника.* КАК ГрафикРаботыРаботника,
	|	 Данные.ВидТарифнойСтавки КАК ВидТарифнойСтавки,
	|	 Данные.ВалютаТарифнойСтавки.* КАК ВалютаТарифнойСтавки,
	|	 Данные.МинимальнаяТарифнаяСтавка КАК МинимальнаяТарифнаяСтавка,
	|	 Данные.МаксимальнаяТарифнаяСтавка КАК МаксимальнаяТарифнаяСтавка,
	|	 Данные.УстановленнаяТарифнаяСтавка КАК УстановленнаяТарифнаяСтавка,
	|	 Данные.ВидРасчетаОкладТариф.* КАК ВидРасчетаОкладТариф,
	|	 Данные.РазмерОкладТариф КАК РазмерОкладТариф,
	|	 Данные.ВалютаОкладТариф.* КАК ВалютаОкладТариф
	|	 //СВОЙСТВА
	|	 //КАТЕГОРИИ
	|}
	|
	|{УПОРЯДОЧИТЬ ПО
	|	 Данные.Организация.* КАК Организация,
	|	 Данные.Физлицо.* КАК Физлицо,
	|	 Данные.Сотрудник.* КАК Сотрудник,
	|	 Данные.ВидЗанятости КАК ВидЗанятости,
	|	 Данные.ТабельныйНомер КАК ТабельныйНомер,
	|	 Данные.ПодразделениеОрганизации.* КАК ПодразделениеОрганизации,
	|	 Данные.Должность.* КАК Должность,
	|	 Данные.ТарифныйРазряд.* КАК ТарифныйРазряд,
	|	 Данные.УсловияТруда.* КАК УсловияТруда,
	|	 Данные.ГрафикРаботыШтЕд.* КАК ГрафикРаботыШтЕд,
	|	 Данные.ГрафикРаботыРаботника.* КАК ГрафикРаботыРаботника,
	|	 Данные.ВидТарифнойСтавки КАК ВидТарифнойСтавки,
	|	 Данные.ВалютаТарифнойСтавки.* КАК ВалютаТарифнойСтавки,
	|	 Данные.МинимальнаяТарифнаяСтавка КАК МинимальнаяТарифнаяСтавка,
	|	 Данные.МаксимальнаяТарифнаяСтавка КАК МаксимальнаяТарифнаяСтавка,
	|	 Данные.УстановленнаяТарифнаяСтавка КАК УстановленнаяТарифнаяСтавка,
	|	 Данные.ВидРасчетаОкладТариф.* КАК ВидРасчетаОкладТариф,
	|	 Данные.РазмерОкладТариф КАК РазмерОкладТариф,
	|	 Данные.ВалютаОкладТариф.* КАК ВалютаОкладТариф
	|	//СВОЙСТВА
	|}
	|
	|{ИТОГИ ПО 
	|	 Данные.Организация.* КАК Организация,
	|	 Данные.Физлицо.* КАК Физлицо,
	|	 Данные.Сотрудник.* КАК Сотрудник,
	|	 Данные.ВидЗанятости КАК ВидЗанятости,
	|	 Данные.ТабельныйНомер КАК ТабельныйНомер,
	|	 Данные.ПодразделениеОрганизации.* КАК ПодразделениеОрганизации,
	|	 Данные.Должность.* КАК Должность,
	|	 Данные.ТарифныйРазряд.* КАК ТарифныйРазряд,
	|	 Данные.ГрафикРаботыШтЕд.* КАК ГрафикРаботыШтЕд,
	|	 Данные.УсловияТруда.* КАК УсловияТруда,
	|	 Данные.ГрафикРаботыРаботника.* КАК ГрафикРаботыРаботника,
	|	 Данные.ВидТарифнойСтавки КАК ВидТарифнойСтавки,
	|	 Данные.ВалютаТарифнойСтавки.* КАК ВалютаТарифнойСтавки,
	|	 Данные.МинимальнаяТарифнаяСтавка КАК МинимальнаяТарифнаяСтавка,
	|	 Данные.МаксимальнаяТарифнаяСтавка КАК МаксимальнаяТарифнаяСтавка,
	|	 Данные.УстановленнаяТарифнаяСтавка КАК УстановленнаяТарифнаяСтавка,
	|	 Данные.ВидРасчетаОкладТариф.* КАК ВидРасчетаОкладТариф,
	|	 Данные.РазмерОкладТариф КАК РазмерОкладТариф,
	|	 Данные.ВалютаОкладТариф.* КАК ВалютаОкладТариф
	|	//СВОЙСТВА
	|}
	|";
	
	СтруктураПредставлениеПолей = Новый Структура();
	
	Если ОбщийОтчет.ИспользоватьСвойстваИКатегории Тогда
		
		// Свойства и категории, назначаемые пользователем:
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДанным");  // описание поля запроса поля, для которого добавляются свойства и категории. Используется в условии соединения с регистром сведений, хранящим значения свойств или категорий
		ТаблицаПолей.Колонки.Добавить("Представление");// представление поля, для которого добавляются свойства и категории. 
		ТаблицаПолей.Колонки.Добавить("Назначение");   // назначение свойств/категорий объектов для данного поля
		ТаблицаПолей.Колонки.Добавить("ТипЗначения");  // тип значения поля, для которого добавляются свойства и категории. Используется, если не установлено назначение
		ТаблицаПолей.Колонки.Добавить("НетКатегорий"); // признак НЕиспользования категорий для объекта
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "Данные.Организация";
		НоваяСтрока.Представление = "Организация";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Организации;
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "Данные.Физлицо";
		НоваяСтрока.Представление = "Сотрудник";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ФизическиеЛица;
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "Данные.ПодразделениеОрганизации";
		НоваяСтрока.Представление = "Подразделение";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ПодразделенияОрганизаций;
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "Данные.Должность";
		НоваяСтрока.Представление = "Должность";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ДолжностиОрганизаций;
		
		// Добавим строки запроса, необходимые для использования свойств и категорий
		ТекстПоляКатегорий = "";
		ТекстПоляСвойств = "";
		
		УправлениеОтчетами.ДобавитьВТекстСвойстваИКатегории(ТаблицаПолей, ОкончательныйТекстЗапроса, СтруктураПредставлениеПолей, 
		ОбщийОтчет.мСоответствиеНазначений, ПостроительОтчета.Параметры
		,, ТекстПоляКатегорий, ТекстПоляСвойств,,,,,,ОбщийОтчет.мСтруктураДляОтбораПоКатегориям);		
		
	КонецЕсли;
	               
	ПостроительОтчета.Текст = ОкончательныйТекстЗапроса;
		
	Если ОбщийОтчет.ИспользоватьСвойстваИКатегории Тогда			
		УправлениеОтчетами.УстановитьТипыЗначенийСвойствИКатегорийДляОтбора(ПостроительОтчета, ТекстПоляКатегорий, ТекстПоляСвойств, ОбщийОтчет.мСоответствиеНазначений, СтруктураПредставлениеПолей);
	КонецЕсли;
	
	// группировки по умолчанию
	ПостроительОтчета.ИзмеренияСтроки.Добавить("Организация");
	ПостроительОтчета.ИзмеренияСтроки.Добавить("ПодразделениеОрганизации");
	
	// список показателей и форматы их предстовления
	НазваниеВалютыРеглУчета = мВалютаРеглУчета.НаименованиеПолное;
	ОбщийОтчет.ЗаполнитьПоказатели("ЗанимаемыхСтавок", "Количество занимаемых ставок", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("МинимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте", "Мин тарифная ставка за месяц (" + НазваниеВалютыРеглУчета + ")", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("МаксимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте", "Макс тарифная ставка за месяц (" + НазваниеВалютыРеглУчета + ")", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("УстановленнаяТарифнаяСтавкаЗаМесяцВРеглВалюте", "Устан тарифная ставка за месяц (" + НазваниеВалютыРеглУчета + ")", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("РазмерОкладТарифЗаМесяцВРеглВалюте", "Размер основного начисления (оклада/тарифа) за месяц (" + НазваниеВалютыРеглУчета + ")", Истина, "ЧЦ=15; ЧДЦ=2");
	
	// представление полей
	СтруктураПредставлениеПолей.Вставить("Физлицо", "Сотрудник");
	СтруктураПредставлениеПолей.Вставить("ВидЗанятости", "Вид занятости");
	СтруктураПредставлениеПолей.Вставить("КоличествоСтавок", "Количество ставок по штатной единице");
	СтруктураПредставлениеПолей.Вставить("ПодразделениеОрганизации", "Подразделение");
	СтруктураПредставлениеПолей.Вставить("ТарифныйРазряд", "Тарифный разряд");
	СтруктураПредставлениеПолей.Вставить("ВидТарифнойСтавки", "Вид тарифной ставки по штатному расписанию");
	СтруктураПредставлениеПолей.Вставить("ГрафикРаботыШтЕд", "График работы по штатному расписанию");
	СтруктураПредставлениеПолей.Вставить("УсловияТруда", "Условия труда");
	СтруктураПредставлениеПолей.Вставить("ВалютаТарифнойСтавки", "Валюта тарифной ставки по штатному расписанию");
	СтруктураПредставлениеПолей.Вставить("МинимальнаяТарифнаяСтавка", "Минимальная тарифная ставка");
	СтруктураПредставлениеПолей.Вставить("МаксимальнаяТарифнаяСтавка", "Максимальная тарифная ставка");
	СтруктураПредставлениеПолей.Вставить("УстановленнаяТарифнаяСтавка", "Установленная тарифная ставка");
	СтруктураПредставлениеПолей.Вставить("ГрафикРаботыРаботника", "График работы работника");
	СтруктураПредставлениеПолей.Вставить("ВидРасчетаОкладТариф", "Вид основного начисления (оклад/тариф)");
	СтруктураПредставлениеПолей.Вставить("РазмерОкладТариф", "Размер основного начисления (оклада/тарифа)");
	СтруктураПредставлениеПолей.Вставить("ВалютаОкладТариф", "Валюта основного начисления (оклада/тарифа)");
	СтруктураПредставлениеПолей.Вставить("ТабельныйНомер", "Табельный номер");
	СтруктураПредставлениеПолей.Вставить("ЗанимаемыхСтавок", "Количество занимаемых ставок");
	СтруктураПредставлениеПолей.Вставить("МинимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте", "Мин тарифная ставка за месяц (" + НазваниеВалютыРеглУчета + ")");
	СтруктураПредставлениеПолей.Вставить("МаксимальнаяТарифнаяСтавкаЗаМесяцВРеглВалюте", "Макс тарифная ставка за месяц (" + НазваниеВалютыРеглУчета + ")");	
	СтруктураПредставлениеПолей.Вставить("УстановленнаяТарифнаяСтавкаЗаМесяцВРеглВалюте", "Устан тарифная ставка за месяц (" + НазваниеВалютыРеглУчета + ")");	
	СтруктураПредставлениеПолей.Вставить("РазмерОкладТарифЗаМесяцВРеглВалюте", "Размер основного начисления (оклада/тарифа) за месяц (" + НазваниеВалютыРеглУчета + ")");		
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
	// очистка автозаполненных полей построителя
	ПостроительОтчета.ВыбранныеПоля.Очистить();
	
	// Выбранные поля
	ПостроительОтчета.ВыбранныеПоля.Добавить("Должность");
	ПостроительОтчета.ВыбранныеПоля.Добавить("Физлицо");
	ПостроительОтчета.ВыбранныеПоля.Добавить("РазмерОкладТариф");
	ПостроительОтчета.ВыбранныеПоля.Добавить("МинимальнаяТарифнаяСтавка");
	ПостроительОтчета.ВыбранныеПоля.Добавить("МаксимальнаяТарифнаяСтавка");
	
	// отборы по умолчанию
	МассивОтбора = Новый Массив;
	МассивОтбора.Добавить("Организация");
	МассивОтбора.Добавить("ПодразделениеОрганизации");
	УправлениеОтчетами.ЗаполнитьОтбор(МассивОтбора, ПостроительОтчета);
	
	// параметры отчета
	ОбщийОтчет.ВыводитьИтогиПоВсемУровням = Истина;
	ОбщийОтчет.ВыводитьПоказателиВСтроку = Истина;
	ОбщийОтчет.РаскрашиватьИзмерения = Истина;
	ОбщийОтчет.мРежимВводаПериода = 1; // Дата

	УправлениеОтчетами.УпорядочитьПоляПостроителяОтчета(ПостроительОтчета);
	
КонецПроцедуры

// Читает свойство Построитель отчета
//
// Параметры
//	Нет
//
Функция ПолучитьПостроительОтчета() Экспорт

	Возврат ОбщийОтчет.ПолучитьПостроительОтчета();

КонецФункции // ПолучитьПостроительОтчета()

// Настраивает отчет по переданной структуре параметров
//
// Параметры:
//	Нет.
//
Процедура Настроить(Параметры) Экспорт

	ОбщийОтчет.Настроить(Параметры, ЭтотОбъект);

КонецПроцедуры

// Возвращает основную форму отчета, связанную с данным экземпляром отчета
//
// Параметры
//	Нет
//
Функция ПолучитьОсновнуюФорму() Экспорт
	
	ОснФорма = ПолучитьФорму();
	ОснФорма.ОбщийОтчет = ОбщийОтчет;
	ОснФорма.ЭтотОтчет = ЭтотОбъект;
	Возврат ОснФорма;
	
КонецФункции // ПолучитьОсновнуюФорму()

мВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();

#КонецЕсли

