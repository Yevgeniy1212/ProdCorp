#Если Клиент Тогда
	
Перем НП Экспорт;

Перем мКэшНаименованиеНоменклатуры;
Перем мКэшНаименованиеСчета;
Перем мКэшНаименованийЗатрат;

// переменные для управления отображения структурного подразделения
Перем мОтображатьСтруктурныеПодразделения Экспорт;

Функция КэшированныйВыводНаименованияВыпуска(Номенклатура)

	Результат = мКэшНаименованиеНоменклатуры[Номенклатура];
	Если Результат = Неопределено Тогда
		Результат = УправлениеПроизводством.ВыводНаименованияВыпуска(Номенклатура);
		мКэшНаименованиеНоменклатуры[Номенклатура] = Результат;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция КэшированныйВыводНаименованияЗатрат(Затрата)

	Результат = мКэшНаименованийЗатрат[Затрата];
	Если Результат = Неопределено Тогда
		Результат = Строка(Затрата);
		мКэшНаименованийЗатрат[Затрата] = Результат;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция СформироватьТекстЗапроса(Учет = "Бух")
	
Текст	= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	ТиповойОборотыДтКт.СубконтоКт1 КАК ПодразделениеВыпуска,
     	  |	ТиповойОборотыДтКт.СубконтоКт2 КАК НоменклатурнаяГруппаВыпуска,
     	  |	ТиповойОборотыДтКт.СубконтоДт1 КАК НоменклатураВыпуска,
     	  |	ТиповойОборотыДтКт.СуммаОборот КАК СуммаВыпуска,
     	  |	ТиповойОборотыДтКт.КоличествоОборотДт КАК КоличествоВыпуска,
     	  |	ТиповойОборотыДтКт.СуммаОборот КАК ОбщаяСуммаВыпуска,
		  |	ТиповойОборотыДтКт.СчетКт КАК Счет
     	  |ПОМЕСТИТЬ ВТ_Выпуск
     	  |ИЗ
     	  |	РегистрБухгалтерии.Типовой.ОборотыДтКт(&НачДата, &КонДата, , , , СчетКт В ИЕРАРХИИ (&СчетПрямЗатрат), , СтруктурноеПодразделениеКт = &СтруктурноеПодразделение И Организация = &Организация " + ?(Учет = "Нал", " И ВидУчетаКт = Значение(Справочник.ВидыУчетаНУ.НУ)", "") + ") КАК ТиповойОборотыДтКт
     	  |
     	  |ИНДЕКСИРОВАТЬ ПО
     	  |	ПодразделениеВыпуска,
     	  |	НоменклатурнаяГруппаВыпуска
     	  |;
     	  |
     	  |////////////////////////////////////////////////////////////////////////////////
     	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	ТиповойОстаткиИОбороты.СуммаНачальныйОстаток КАК НачОст,
     	  |	ТиповойОстаткиИОбороты.СуммаКонечныйОстаток КАК КонОст,
     	  |	ТиповойОстаткиИОбороты.Субконто1 КАК ПодразделениеНЗП,
     	  |	ТиповойОстаткиИОбороты.Субконто2 КАК НоменклатурнаяГруппаНЗП,
	      | ТиповойОстаткиИОбороты.Счет
          |ПОМЕСТИТЬ ВТ_НЗП
     	  |ИЗ
     	  |	РегистрБухгалтерии.Типовой.ОстаткиИОбороты(&НачДата, &КонДата, , , Счет В ИЕРАРХИИ (&СчетПрямЗатратДляНЗП), , СтруктурноеПодразделение = &СтруктурноеПодразделение И Организация = &Организация  " + ?(Учет = "Нал", " И ВидУчета = Значение(Справочник.ВидыУчетаНУ.НУ)", "") + ") КАК ТиповойОстаткиИОбороты
     	  |
     	  |ИНДЕКСИРОВАТЬ ПО
     	  |	ПодразделениеНЗП,
     	  |	НоменклатурнаяГруппаНЗП
     	  |;
     	  |
     	  |////////////////////////////////////////////////////////////////////////////////
     	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	ТиповойОбороты.Субконто1 КАК ПодразделениеКосвенныхЗатрат,
     	  |	ТиповойОбороты.Субконто2 КАК НоменклатурнаяГруппаКосвенныхЗатрат,
     	  |	ТиповойОбороты.Субконто3 КАК ЗатратыКосвенные,
     	  |	ТиповойОбороты.СуммаОборотДт КАК СуммаКосвЗатрат,
		  | ТиповойОбороты.Счет
     	  |ПОМЕСТИТЬ ВТ_КосвенныеЗатраты
     	  |ИЗ
     	  |	РегистрБухгалтерии.Типовой.Обороты(&НачДата, &КонДата, , Счет В ИЕРАРХИИ (&СчетПрямЗатрат), , СтруктурноеПодразделение = &СтруктурноеПодразделение И Организация = &Организация " + ?(Учет = "Нал", " И ВидУчета = Значение(Справочник.ВидыУчетаНУ.НУ)", "") + ", КорСчет В ИЕРАРХИИ (&СчетКосвЗатрат), ) КАК ТиповойОбороты
     	  |ГДЕ
     	  |	ТиповойОбороты.СуммаОборотДт <> 0
     	  |
     	  |ИНДЕКСИРОВАТЬ ПО
     	  |	ПодразделениеКосвенныхЗатрат,
     	  |	НоменклатурнаяГруппаКосвенныхЗатрат
     	  |;
     	  |
     	  |////////////////////////////////////////////////////////////////////////////////
     	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	ТиповойОбороты.Субконто1 КАК ПодразделениеПрямыхЗатрат,
     	  |	ТиповойОбороты.Субконто2 КАК НоменклатурнаяГруппаПрямыхЗатрат,
     	  |	ТиповойОбороты.Субконто3 КАК ЗатратыПрямыхЗатрат,
     	  |	ТиповойОбороты.КорСчет КАК КорСчетПрямыхЗатрат,
     	  |	ТиповойОбороты.КорСубконто1 КАК КорСубконтоПрямыхЗатрат,
     	  |	ТиповойОбороты.СуммаОборотДт КАК СуммаПрямыхЗатрат,
     	  |	ТиповойОбороты.КоличествоКорОборот КАК КоличествоПрямыхЗатрат,
		  | ТиповойОбороты.Счет
     	  |ПОМЕСТИТЬ ВТ_ПрямыеЗатраты
     	  |ИЗ
     	  |	РегистрБухгалтерии.Типовой.Обороты(&НачДата, &КонДата, , Счет В ИЕРАРХИИ (&СчетПрямЗатрат), , СтруктурноеПодразделение = &СтруктурноеПодразделение И Организация = &Организация" + ?(Учет = "Нал", " И ВидУчета = Значение(Справочник.ВидыУчетаНУ.НУ)", "") + ", (НЕ КорСчет В ИЕРАРХИИ (&СчетКосвЗатрат)), ) КАК ТиповойОбороты
     	  |ГДЕ
     	  |	(ТиповойОбороты.СуммаОборотДт <> 0
     	  |			ИЛИ ТиповойОбороты.КоличествоКорОборот <> 0)
     	  |
     	  |ИНДЕКСИРОВАТЬ ПО
     	  |	ПодразделениеПрямыхЗатрат,
     	  |	НоменклатурнаяГруппаПрямыхЗатрат
     	  |;
     	  |
     	  |////////////////////////////////////////////////////////////////////////////////
     	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	Выпуск.ПодразделениеВыпуска КАК Подразделение,
     	  |	Выпуск.НоменклатурнаяГруппаВыпуска КАК НоменклатурнаяГруппа,
     	  |	Выпуск.НоменклатураВыпуска КАК Номенклатура,
     	  |	ПрямыеЗатраты.ЗатратыПрямыхЗатрат КАК ПрямыеЗатраты,
     	  |	ПрямыеЗатраты.КорСчетПрямыхЗатрат КАК КорСчетПрямыхЗатрат,
     	  |	ПрямыеЗатраты.КорСубконтоПрямыхЗатрат КАК НоменклатураПрямыхЗатрат,
     	  |	ОбщеПроизводственныеЗатраты.ЗатратыКосвенные КАК ЗатратыКосвенные,
     	  |	ЕСТЬNULL(Выпуск.КоличествоВыпуска, 0) КАК Количество,
     	  |	ЕСТЬNULL(ПрямыеЗатраты.КоличествоПрямыхЗатрат, 0) КАК КоличествоПрямыхЗатрат,
     	  |	ЕСТЬNULL(Выпуск.ОбщаяСуммаВыпуска, 0) КАК ОбщаяСуммаВыпуска,
     	  |	ЕСТЬNULL(Выпуск.СуммаВыпуска, 0) КАК Сумма,
     	  |	ЕСТЬNULL(ПрямыеЗатраты.СуммаПрямыхЗатрат, 0) КАК СуммаПрямыхЗатрат,
     	  |	ЕСТЬNULL(НЗП.НачОст, 0) КАК НачОстНЗП,
     	  |	ЕСТЬNULL(НЗП.КонОст, 0) КАК КонОстНЗП,
     	  |	ЕСТЬNULL(ОбщеПроизводственныеЗатраты.СуммаКосвЗатрат, 0) КАК СуммаКосвЗатрат
     	  |ИЗ
     	  |	ВТ_Выпуск КАК Выпуск
     	  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрямыеЗатраты КАК ПрямыеЗатраты
     	  |		ПО Выпуск.ПодразделениеВыпуска = ПрямыеЗатраты.ПодразделениеПрямыхЗатрат
     	  |			И Выпуск.НоменклатурнаяГруппаВыпуска = ПрямыеЗатраты.НоменклатурнаяГруппаПрямыхЗатрат
		  |			И Выпуск.Счет = ПрямыеЗатраты.Счет
     	  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КосвенныеЗатраты КАК ОбщеПроизводственныеЗатраты
     	  |		ПО Выпуск.ПодразделениеВыпуска = ОбщеПроизводственныеЗатраты.ПодразделениеКосвенныхЗатрат
     	  |			И Выпуск.НоменклатурнаяГруппаВыпуска = ОбщеПроизводственныеЗатраты.НоменклатурнаяГруппаКосвенныхЗатрат
		  |			И Выпуск.Счет = ОбщеПроизводственныеЗатраты.Счет
		  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НЗП КАК НЗП
     	  |		ПО Выпуск.НоменклатурнаяГруппаВыпуска = НЗП.НоменклатурнаяГруппаНЗП
     	  |			И Выпуск.ПодразделениеВыпуска = НЗП.ПодразделениеНЗП
		  |Упорядочить 
     	  |ПО
     	  |	Подразделение,
     	  |	НоменклатурнаяГруппа,
     	  |	Номенклатура
     	  |ИТОГИ
     	  |	МАКСИМУМ(Количество),
     	  |	МАКСИМУМ(КоличествоПрямыхЗатрат),
     	  |	СУММА(ОбщаяСуммаВыпуска),
     	  |	МАКСИМУМ(Сумма),
     	  |	МАКСИМУМ(СуммаПрямыхЗатрат),
     	  |	МАКСИМУМ(НачОстНЗП),
     	  |	МАКСИМУМ(КонОстНЗП),
     	  |	МАКСИМУМ(СуммаКосвЗатрат)
     	  |ПО
     	  |	Подразделение,
     	  |	НоменклатурнаяГруппа,
     	  |	Номенклатура,
     	  |	ПрямыеЗатраты,
     	  |	КорСчетПрямыхЗатрат,
     	  |	НоменклатураПрямыхЗатрат,
     	  |	ЗатратыКосвенные"; 
Возврат Текст;
	
КонецФункции

Функция СформироватьТекстЗапросаСРазницами()
	
Текст	= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	НалоговыйОборотыДтКт.СубконтоКт1 КАК ПодразделениеВыпуска,
     	  |	НалоговыйОборотыДтКт.СубконтоКт2 КАК НоменклатурнаяГруппаВыпуска,
     	  |	НалоговыйОборотыДтКт.СубконтоДт1 КАК НоменклатураВыпуска,
		  | НалоговыйОборотыДтКт.СчетКт КАК Счет,
		  |	СУММА(НалоговыйОборотыДтКт.СуммаОборот) КАК СуммаВыпуска,
     	  |	СУММА(НалоговыйОборотыДтКт.КоличествоОборотДт) КАК КоличествоВыпуска,
     	  |	СУММА(НалоговыйОборотыДтКт.СуммаОборот) КАК ОбщаяСуммаВыпуска,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОборотыДтКт.ВидУчетаДт.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПР)
     	  |				ТОГДА НалоговыйОборотыДтКт.СуммаОборот
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК СуммаВыпускаПР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОборотыДтКт.ВидУчетаДт.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПР)
     	  |				ТОГДА НалоговыйОборотыДтКт.КоличествоОборотДт
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК КоличествоВыпускаПР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОборотыДтКт.ВидУчетаДт = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ВР)
     	  |				ТОГДА НалоговыйОборотыДтКт.СуммаОборот
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК СуммаВыпускаВР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОборотыДтКт.ВидУчетаДт = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ВР)
     	  |				ТОГДА НалоговыйОборотыДтКт.КоличествоОборотДт
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК КоличествоВыпускаВР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОборотыДтКт.ВидУчетаДт.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПР)
     	  |				ТОГДА НалоговыйОборотыДтКт.СуммаОборот
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК ОбщаяСуммаВыпускаПР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОборотыДтКт.ВидУчетаДт = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ВР)
     	  |				ТОГДА НалоговыйОборотыДтКт.СуммаОборот
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК ОбщаяСуммаВыпускаВР
     	  |ПОМЕСТИТЬ ВТ_Выпуск
     	  |ИЗ
     	  |	РегистрБухгалтерии.Налоговый.ОборотыДтКт(
     	  |			&НачДата,
     	  |			&КонДата,
     	  |			,
     	  |			,
     	  |			,
     	  |			СчетКт В ИЕРАРХИИ (&СчетПрямЗатрат),
     	  |			,
     	  |			СтруктурноеПодразделениеКт = &СтруктурноеПодразделение
     	  |				И Организация = &Организация
     	  |				И ВидУчетаКт В ИЕРАРХИИ (&ВидУчета)) КАК НалоговыйОборотыДтКт
     	  |
     	  |СГРУППИРОВАТЬ ПО
     	  |	НалоговыйОборотыДтКт.СубконтоКт1,
     	  |	НалоговыйОборотыДтКт.СубконтоКт2,
     	  |	НалоговыйОборотыДтКт.СубконтоДт1,
		  | НалоговыйОборотыДтКт.СчетКт
     	  |
     	  |ИНДЕКСИРОВАТЬ ПО
     	  |	ПодразделениеВыпуска,
     	  |	НоменклатурнаяГруппаВыпуска
     	  |;
     	  |
     	  |////////////////////////////////////////////////////////////////////////////////
     	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	СУММА(НалоговыйОстаткиИОбороты.СуммаНачальныйОстаток) КАК НачОст,
     	  |	СУММА(НалоговыйОстаткиИОбороты.СуммаКонечныйОстаток) КАК КонОст,
     	  |	НалоговыйОстаткиИОбороты.Субконто1 КАК ПодразделениеНЗП,
     	  |	НалоговыйОстаткиИОбороты.Субконто2 КАК НоменклатурнаяГруппаНЗП,
		  | НалоговыйОстаткиИОбороты.Счет КАК Счет,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОстаткиИОбороты.ВидУчета.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПР)
     	  |				ТОГДА НалоговыйОстаткиИОбороты.СуммаКонечныйОстаток
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК КонОстПР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОстаткиИОбороты.ВидУчета.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПР)
     	  |				ТОГДА НалоговыйОстаткиИОбороты.СуммаНачальныйОстаток
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК НачОстПР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОстаткиИОбороты.ВидУчета = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ВР)
     	  |				ТОГДА НалоговыйОстаткиИОбороты.СуммаКонечныйОстаток
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК КонОстВР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОстаткиИОбороты.ВидУчета = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ВР)
     	  |				ТОГДА НалоговыйОстаткиИОбороты.СуммаНачальныйОстаток
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК НачОстВР
     	  |ПОМЕСТИТЬ ВТ_НЗП
     	  |ИЗ
     	  |	РегистрБухгалтерии.Налоговый.ОстаткиИОбороты(
     	  |			&НачДата,
     	  |			&КонДата,
     	  |			,
     	  |			,
     	  |			Счет В ИЕРАРХИИ (&СчетПрямЗатратДляНЗП),
     	  |			,
     	  |			СтруктурноеПодразделение = &СтруктурноеПодразделение
     	  |				И Организация = &Организация
     	  |				И ВидУчета В ИЕРАРХИИ (&ВидУчета)) КАК НалоговыйОстаткиИОбороты
     	  |
     	  |СГРУППИРОВАТЬ ПО
     	  |	НалоговыйОстаткиИОбороты.Субконто1,
     	  |	НалоговыйОстаткиИОбороты.Субконто2,
		  | НалоговыйОстаткиИОбороты.Счет
     	  |
     	  |ИНДЕКСИРОВАТЬ ПО
     	  |	ПодразделениеНЗП,
     	  |	НоменклатурнаяГруппаНЗП
     	  |;
     	  |
     	  |////////////////////////////////////////////////////////////////////////////////
     	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	НалоговыйОбороты.Субконто1 КАК ПодразделениеКосвенныхЗатрат,
     	  |	НалоговыйОбороты.Субконто2 КАК НоменклатурнаяГруппаКосвенныхЗатрат,
     	  |	НалоговыйОбороты.Субконто3 КАК ЗатратыКосвенные,
		  | НалоговыйОбороты.Счет КАК Счет, 
     	  |	СУММА(НалоговыйОбороты.СуммаОборотДт) КАК СуммаКосвЗатрат,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОбороты.ВидУчета.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПР)
     	  |				ТОГДА НалоговыйОбороты.СуммаОборотДт
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК СуммаКосвЗатратПР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалоговыйОбороты.ВидУчета = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ВР)
     	  |				ТОГДА НалоговыйОбороты.СуммаОборотДт
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК СуммаКосвЗатратВР
     	  |ПОМЕСТИТЬ ВТ_КосвенныеЗатраты
     	  |ИЗ
     	  |	РегистрБухгалтерии.Налоговый.Обороты(
     	  |			&НачДата,
     	  |			&КонДата,
     	  |			,
     	  |			Счет В ИЕРАРХИИ (&СчетПрямЗатрат),
     	  |			,
     	  |			СтруктурноеПодразделение = &СтруктурноеПодразделение
     	  |				И Организация = &Организация
     	  |				И ВидУчета В ИЕРАРХИИ (&ВидУчета),
     	  |			КорСчет В ИЕРАРХИИ (&СчетКосвЗатрат),
     	  |			) КАК НалоговыйОбороты
     	  |ГДЕ
     	  |	НалоговыйОбороты.СуммаОборотДт <> 0
     	  |
     	  |СГРУППИРОВАТЬ ПО
     	  |	НалоговыйОбороты.Субконто1,
     	  |	НалоговыйОбороты.Субконто2,
     	  |	НалоговыйОбороты.Субконто3,
		  | НалоговыйОбороты.Счет
     	  |
     	  |ИНДЕКСИРОВАТЬ ПО
     	  |	ПодразделениеКосвенныхЗатрат,
     	  |	НоменклатурнаяГруппаКосвенныхЗатрат
     	  |;
     	  |
     	  |////////////////////////////////////////////////////////////////////////////////
     	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	НалговыйОбороты.Субконто1 КАК ПодразделениеПрямыхЗатрат,
     	  |	НалговыйОбороты.Субконто2 КАК НоменклатурнаяГруппаПрямыхЗатрат,
     	  |	НалговыйОбороты.Субконто3 КАК ЗатратыПрямыхЗатрат,
     	  |	НалговыйОбороты.КорСчет КАК КорСчетПрямыхЗатрат,
     	  |	НалговыйОбороты.КорСубконто1 КАК КорСубконтоПрямыхЗатрат,
		  |	НалговыйОбороты.Счет КАК Счет,
     	  |	СУММА(НалговыйОбороты.СуммаОборотДт) КАК СуммаПрямыхЗатрат,
     	  |	СУММА(НалговыйОбороты.КоличествоКорОборот) КАК КоличествоПрямыхЗатрат,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалговыйОбороты.ВидУчета.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПР)
     	  |				ТОГДА НалговыйОбороты.СуммаОборотДт
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК СуммаПрямыхЗатратПР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалговыйОбороты.ВидУчета.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПР)
     	  |				ТОГДА НалговыйОбороты.КоличествоКорОборот
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК КоличествоПрямыхЗатратПР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалговыйОбороты.ВидУчета = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ВР)
     	  |				ТОГДА НалговыйОбороты.СуммаОборотДт
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК СуммаПрямыхЗатратВР,
     	  |	СУММА(ВЫБОР
     	  |			КОГДА НалговыйОбороты.ВидУчета = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ВР)
     	  |				ТОГДА НалговыйОбороты.КоличествоКорОборот
     	  |			ИНАЧЕ 0
     	  |		КОНЕЦ) КАК КоличествоПрямыхЗатратВР
     	  |ПОМЕСТИТЬ ВТ_ПрямыеЗатраты
     	  |ИЗ
     	  |	РегистрБухгалтерии.Налоговый.Обороты(
     	  |			&НачДата,
     	  |			&КонДата,
     	  |			,
     	  |			Счет В ИЕРАРХИИ (&СчетПрямЗатрат),
     	  |			,
     	  |			СтруктурноеПодразделение = &СтруктурноеПодразделение
     	  |				И Организация = &Организация
     	  |				И ВидУчета В ИЕРАРХИИ (&ВидУчета),
     	  |			(НЕ КорСчет В ИЕРАРХИИ (&СчетКосвЗатрат)),
     	  |			) КАК НалговыйОбороты
     	  |ГДЕ
     	  |	(НалговыйОбороты.СуммаОборотДт <> 0
     	  |			ИЛИ НалговыйОбороты.КоличествоКорОборот <> 0)
     	  |
     	  |СГРУППИРОВАТЬ ПО
     	  |	НалговыйОбороты.Субконто1,
     	  |	НалговыйОбороты.Субконто2,
     	  |	НалговыйОбороты.Субконто3,
     	  |	НалговыйОбороты.КорСчет,
     	  |	НалговыйОбороты.КорСубконто1,
		  |	НалговыйОбороты.Счет
     	  |
     	  |ИНДЕКСИРОВАТЬ ПО
     	  |	ПодразделениеПрямыхЗатрат,
     	  |	НоменклатурнаяГруппаПрямыхЗатрат
     	  |;
     	  |
     	  |////////////////////////////////////////////////////////////////////////////////
     	  |ВЫБРАТЬ РАЗРЕШЕННЫЕ
     	  |	Выпуск.ПодразделениеВыпуска КАК Подразделение,
     	  |	Выпуск.НоменклатурнаяГруппаВыпуска КАК НоменклатурнаяГруппа,
     	  |	Выпуск.НоменклатураВыпуска КАК Номенклатура,
     	  |	ПрямыеЗатраты.ЗатратыПрямыхЗатрат КАК ПрямыеЗатраты,
     	  |	ПрямыеЗатраты.КорСчетПрямыхЗатрат КАК КорСчетПрямыхЗатрат,
     	  |	ПрямыеЗатраты.КорСубконтоПрямыхЗатрат КАК НоменклатураПрямыхЗатрат,
     	  |	ОбщеПроизводственныеЗатраты.ЗатратыКосвенные КАК ЗатратыКосвенные,
     	  |	ЕСТЬNULL(Выпуск.КоличествоВыпуска, 0) КАК Количество,
     	  |	ЕСТЬNULL(ПрямыеЗатраты.КоличествоПрямыхЗатрат, 0) КАК КоличествоПрямыхЗатрат,
     	  |	ЕСТЬNULL(Выпуск.СуммаВыпуска, 0) КАК Сумма,
     	  |	ЕСТЬNULL(ПрямыеЗатраты.СуммаПрямыхЗатрат, 0) КАК СуммаПрямыхЗатрат,
     	  |	ЕСТЬNULL(НЗП.НачОст, 0) КАК НачОстНЗП,
     	  |	ЕСТЬNULL(НЗП.КонОст, 0) КАК КонОстНЗП,
     	  |	ЕСТЬNULL(ОбщеПроизводственныеЗатраты.СуммаКосвЗатрат, 0) КАК СуммаКосвЗатрат,
     	  |	ЕСТЬNULL(Выпуск.СуммаВыпускаПР, 0) КАК СуммаПР,
     	  |	ЕСТЬNULL(Выпуск.КоличествоВыпускаПР, 0) КАК КоличествоПР,
     	  |	ЕСТЬNULL(Выпуск.СуммаВыпускаВР, 0) КАК СуммаВР,
     	  |	ЕСТЬNULL(Выпуск.КоличествоВыпускаВР, 0) КАК КоличествоВР,
     	  |	ЕСТЬNULL(НЗП.КонОстПР, 0) КАК КонОстНЗППР,
     	  |	ЕСТЬNULL(НЗП.НачОстПР, 0) КАК НачОстНЗППР,
     	  |	ЕСТЬNULL(НЗП.КонОстВР, 0) КАК КонОстНЗПВР,
     	  |	ЕСТЬNULL(НЗП.НачОстВР, 0) КАК НачОстНЗПВР,
     	  |	ЕСТЬNULL(ОбщеПроизводственныеЗатраты.СуммаКосвЗатратПР, 0) КАК СуммаКосвЗатратПР,
     	  |	ЕСТЬNULL(ОбщеПроизводственныеЗатраты.СуммаКосвЗатратВР, 0) КАК СуммаКосвЗатратВР,
     	  |	ЕСТЬNULL(ПрямыеЗатраты.СуммаПрямыхЗатратПР, 0) КАК СуммаПрямыхЗатратПР,
     	  |	ЕСТЬNULL(ПрямыеЗатраты.КоличествоПрямыхЗатратПР, 0) КАК КоличествоПрямыхЗатратПР,
     	  |	ЕСТЬNULL(ПрямыеЗатраты.СуммаПрямыхЗатратВР, 0) КАК СуммаПрямыхЗатратВР,
     	  |	ЕСТЬNULL(ПрямыеЗатраты.КоличествоПрямыхЗатратВР, 0) КАК КоличествоПрямыхЗатратВР,
     	  |	ЕСТЬNULL(Выпуск.ОбщаяСуммаВыпускаПР, 0) КАК ОбщаяСуммаВыпускаПР,
     	  |	ЕСТЬNULL(Выпуск.ОбщаяСуммаВыпускаВР, 0) КАК ОбщаяСуммаВыпускаВР,
     	  |	ЕСТЬNULL(Выпуск.ОбщаяСуммаВыпуска, 0) КАК ОбщаяСуммаВыпуска
     	  |ИЗ
     	  |	ВТ_Выпуск КАК Выпуск
     	  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрямыеЗатраты КАК ПрямыеЗатраты
     	  |		ПО Выпуск.ПодразделениеВыпуска = ПрямыеЗатраты.ПодразделениеПрямыхЗатрат
     	  |			И Выпуск.НоменклатурнаяГруппаВыпуска = ПрямыеЗатраты.НоменклатурнаяГруппаПрямыхЗатрат
		  |			И Выпуск.Счет = ПрямыеЗатраты.Счет
     	  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КосвенныеЗатраты КАК ОбщеПроизводственныеЗатраты
     	  |		ПО Выпуск.ПодразделениеВыпуска = ОбщеПроизводственныеЗатраты.ПодразделениеКосвенныхЗатрат
     	  |			И Выпуск.НоменклатурнаяГруппаВыпуска = ОбщеПроизводственныеЗатраты.НоменклатурнаяГруппаКосвенныхЗатрат
		  |			И Выпуск.Счет = ОбщеПроизводственныеЗатраты.Счет
		  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НЗП КАК НЗП
     	  |		ПО Выпуск.НоменклатурнаяГруппаВыпуска = НЗП.НоменклатурнаяГруппаНЗП
     	  |			И Выпуск.ПодразделениеВыпуска = НЗП.ПодразделениеНЗП
		  |
     	  |УПОРЯДОЧИТЬ ПО
     	  |	Подразделение,
     	  |	НоменклатурнаяГруппа,
     	  |	Номенклатура
     	  |ИТОГИ
     	  |	МАКСИМУМ(Количество),
     	  |	МАКСИМУМ(КоличествоПрямыхЗатрат),
     	  |	МАКСИМУМ(Сумма),
     	  |	МАКСИМУМ(СуммаПрямыхЗатрат),
     	  |	МАКСИМУМ(НачОстНЗП),
     	  |	МАКСИМУМ(КонОстНЗП),
     	  |	МАКСИМУМ(СуммаКосвЗатрат),
     	  |	МАКСИМУМ(СуммаПР),
     	  |	МАКСИМУМ(КоличествоПР),
     	  |	МАКСИМУМ(СуммаВР),
     	  |	МАКСИМУМ(КоличествоВР),
     	  |	МАКСИМУМ(КонОстНЗППР),
     	  |	МАКСИМУМ(НачОстНЗППР),
     	  |	МАКСИМУМ(КонОстНЗПВР),
     	  |	МАКСИМУМ(НачОстНЗПВР),
     	  |	МАКСИМУМ(СуммаКосвЗатратПР),
     	  |	МАКСИМУМ(СуммаКосвЗатратВР),
     	  |	МАКСИМУМ(СуммаПрямыхЗатратПР),
     	  |	МАКСИМУМ(КоличествоПрямыхЗатратПР),
     	  |	МАКСИМУМ(СуммаПрямыхЗатратВР),
     	  |	МАКСИМУМ(КоличествоПрямыхЗатратВР),
     	  |	СУММА(ОбщаяСуммаВыпускаПР),
     	  |	СУММА(ОбщаяСуммаВыпускаВР),
     	  |	СУММА(ОбщаяСуммаВыпуска)
     	  |ПО
     	  |	Подразделение,
     	  |	НоменклатурнаяГруппа,
     	  |	Номенклатура,
     	  |	ПрямыеЗатраты,
     	  |	КорСчетПрямыхЗатрат,
     	  |	НоменклатураПрямыхЗатрат,
     	  |	ЗатратыКосвенные"; 
Возврат Текст;

	
КонецФункции
  
Процедура ВывестиОбласть(ДокументРезультат, ОбластьОсновная, ОбластьРазницы, ОбластьОкончание, Уровень, ИмяГруппы, Открыта, ИмяПараметра, Выборка, СуммаПР = Неопределено, СуммаВР = Неопределено)
	
	Если Уровень = 0 Тогда
		ОбластьНачало = ДокументРезультат.Вывести(ОбластьОсновная);
	Иначе
		ОбластьНачало = ДокументРезультат.Вывести(ОбластьОсновная, Уровень, ИмяГруппы, Открыта);
	КонецЕсли;
	Если ВариантОтчета = 3 Тогда
		ОбластьРазницы.Параметры[ИмяПараметра + "ПР"] = ?(СуммаПР = Неопределено, Выборка[ИмяПараметра + "ПР"] , СуммаПР);
		ОбластьРазницы.Параметры[ИмяПараметра + "ВР"] = ?(СуммаВР = Неопределено, Выборка[ИмяПараметра + "ВР"], СуммаВР);
		
		Если Уровень = 0 Тогда
			ОбластьРазниц = ДокументРезультат.Присоединить(ОбластьРазницы);
		Иначе
			
			ОбластьРазниц = ДокументРезультат.Присоединить(ОбластьРазницы, Уровень, ИмяГруппы, Открыта);
		КонецЕсли;
	
	КонецЕсли;
	
	Если Уровень = 0 Тогда
		ОбластьКонец = ДокументРезультат.Присоединить(ОбластьОкончание);
	Иначе
		
		ОбластьКонец = ДокументРезультат.Присоединить(ОбластьОкончание, Уровень, ИмяГруппы, Открыта);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь, НачалоПодписи, ПоказыватьПодписи) Экспорт

	ДатаНач = НачалоМесяца(ДатаНач);
	ДатаКон  = КонецМесяца(ДатаКон);
	Если ДатаНач > ДатаКон И ДатаКон <> '00010101000000' Тогда
		Предупреждение("Дата начала периода не может быть больше даты окончания периода");
		Возврат;
	КонецЕсли;
	
	НачалоПериода = НачалоДня(ДатаНач);
	КонецПериода  = КонецДня(ДатаКон);

	ДокументРезультат.Очистить();

	Если ГруппироватьОтчет Тогда 
		ДокументРезультат.НачатьАвтогруппировкуСтрок();
	КонецЕсли;
		
	Макет      = ПолучитьМакет("Отчет");
    МакетОбщий = ПолучитьОбщийМакет("СправкаРасчет");

	ЗаголовокОтчета = МакетОбщий.ПолучитьОбласть("Заголовок");
	
	ЗаголовокОтчета.Параметры.Период              = "" + ПредставлениеПериода(НачалоДня(ДатаНач), КонецДня(ДатаКон), "ФП = Истина");
	ЗаголовокОтчета.Параметры.НазваниеОрганизации = "" + Организация.НаименованиеПолное;
	ЗаголовокОтчета.Параметры.ДатаСоставления     = КонецДня(ДатаКон);
	ЗаголовокОтчета.Параметры.НазваниеОтчета      = "Калькуляция себестоимости продукции (" +?(ВариантОтчета = 1, "бухгалтерский учет", "налоговый учет") + ")";
	ДокументРезультат.Вывести(ЗаголовокОтчета);
	
	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;
	
	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	СписокСчетов = Новый Массив;
	СписокСчетов.Добавить(ПланыСчетов.Типовой.ОсновноеПроизводство);
	СписокСчетов.Добавить(ПланыСчетов.Типовой.ПолуфабрикатыСобственногоПроизводства);
	СписокСчетов.Добавить(ПланыСчетов.Типовой.ВспомогательныеПроизводства);

	СписокСчетовДляНЗП = Новый Массив;
	СписокСчетовДляНЗП.Добавить(ПланыСчетов.Типовой.ОсновноеПроизводство);
	СписокСчетовДляНЗП.Добавить(ПланыСчетов.Типовой.ПолуфабрикатыСобственногоПроизводства);
	СписокСчетовДляНЗП.Добавить(ПланыСчетов.Типовой.ВспомогательныеПроизводства);
    СписокСчетовДляНЗП.Добавить(ПланыСчетов.Типовой.НезавершенноеПроизводство);
		
	СписокСчетовНУ = Новый Массив;
	СписокСчетовНУ.Добавить(ПланыСчетов.Налоговый.ОсновноеПроизводство);
	СписокСчетовНУ.Добавить(ПланыСчетов.Налоговый.ПолуфабрикатыСобственногоПроизводства);
	СписокСчетовНУ.Добавить(ПланыСчетов.Налоговый.ВспомогательныеПроизводства);  

	СписокСчетовНУДляНЗП = Новый Массив;
	СписокСчетовНУДляНЗП.Добавить(ПланыСчетов.Налоговый.ОсновноеПроизводство);
	СписокСчетовНУДляНЗП.Добавить(ПланыСчетов.Налоговый.ПолуфабрикатыСобственногоПроизводства);
	СписокСчетовНУДляНЗП.Добавить(ПланыСчетов.Налоговый.ВспомогательныеПроизводства);  
    СписокСчетовНУДляНЗП.Добавить(ПланыСчетов.Налоговый.НезавершенноеПроизводство);  
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачДата",     			  Новый Граница(НачалоДня(ДатаНач), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонДата",     			  Новый Граница(КонецДня(ДатаКон),  ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", 			  Организация);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("СчетПрямЗатрат",  		  СписокСчетов);
	Запрос.УстановитьПараметр("СчетПрямЗатратДляНЗП",  	  СписокСчетовДляНЗП);
	Запрос.УстановитьПараметр("СчетКосвЗатрат",  		  ПланыСчетов.Типовой.НакладныеРасходы);		    	
	
	ОграниченияПоПостроителюОтчета = Отчеты_БК.ПолучитьТекстОграниченийПоПостроителюОтчета(ПостроительОтчета, Запрос);
	
	Если Не ПустаяСтрока(ОграниченияПоПостроителюОтчета) Тогда
		
		ОграниченияПоПостроителюОтчета = " И " + ОграниченияПоПостроителюОтчета;
		
	КонецЕсли;
	
	Если Не ВариантОтчета = 3 Тогда
		
		Запрос.Текст = СформироватьТекстЗапроса(?(ВариантОтчета = 2, "Нал", "Бух"));
		
		Запрос.Текст  = СтрЗаменить(Запрос.Текст, ") КАК ТиповойОборотыДтКт","" + ОграниченияПоПостроителюОтчета + ") КАК ТиповойОборотыДтКт");
		Если ВариантОтчета = 2 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Типовой", "Налоговый");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Бухгалтерский", "Налоговый");
			
			Запрос.УстановитьПараметр("СчетПрямЗатрат",  	   СписокСчетовНУ);
			Запрос.УстановитьПараметр("СчетПрямЗатратДляНЗП",  СписокСчетовНУДляНЗП);
			Запрос.УстановитьПараметр("СчетКосвЗатрат",  	   ПланыСчетов.Налоговый.НакладныеРасходы);		
		КонецЕсли;
		
	Иначе
		Запрос.Текст = СформироватьТекстЗапросаСРазницами();
		
		Запрос.Текст  = СтрЗаменить(Запрос.Текст, ") КАК НалоговыйОборотыДтКт","" + ОграниченияПоПостроителюОтчета + ") КАК НалоговыйОборотыДтКт"); 
		Массив = Новый Массив;
		Массив.Добавить(Справочники.ВидыУчетаНУ.НУ);
		Массив.Добавить(Справочники.ВидыУчетаНУ.ПР);
		Массив.Добавить(Справочники.ВидыУчетаНУ.ВР);
		Запрос.УстановитьПараметр("СчетПрямЗатрат",  	  СписокСчетовНУ);
		Запрос.УстановитьПараметр("СчетПрямЗатратДляНЗП", СписокСчетовНУДляНЗП);
		Запрос.УстановитьПараметр("СчетКосвЗатрат",  	  ПланыСчетов.Налоговый.НакладныеРасходы);		
		Запрос.УстановитьПараметр("ВидУчета", 			  Массив);
	КонецЕсли;		
	
	   				   
	Результат = Запрос.Выполнить();
	
	СтрОтбор = УправлениеОтчетами.СформироватьСтрокуОтборов(ПостроительОтчета.Отбор);
	Если Не ПустаяСтрока(СтрОтбор) Тогда
		ОбластьОтбор = МакетОбщий.ПолучитьОбласть("СтрокаОтбор");                     
		ОбластьОтбор.Параметры.ТекстПроОтбор = "Отбор: " + СтрОтбор;
		ДокументРезультат.Вывести(ОбластьОтбор);
	КонецЕсли;	
	
	ОбластьШапка              		   = Макет.ПолучитьОбласть("Шапка|Основная");
	ОбластьШапкаСПР              	   = Макет.ПолучитьОбласть("ШапкаРазницы|Основная");
	ОбластьСтрокаЗатрат       		   = Макет.ПолучитьОбласть("СтрокаЗатрат|Основная");
	ОбластьСчетЗатрат         		   = Макет.ПолучитьОбласть("СчетЗатрат|Основная");
	ОбластьНоменклатураЗатрат 		   = Макет.ПолучитьОбласть("СтрокаНоменклатурыЗатрат|Основная");
	ОбластьШапкаПрямые        		   = Макет.ПолучитьОбласть("ШапкаПрямые|Основная");
	ОбластьШапкаКосвенные     		   = Макет.ПолучитьОбласть("ШапкаКосвенные|Основная");
	ОбластьИтогоПрямые        		   = Макет.ПолучитьОбласть("ИтогоПрямые|Основная");
	ОбластьИтогоКосвенные     		   = Макет.ПолучитьОбласть("ИтогоКосвенные|Основная");
	ОбластьНЗПНач             		   = Макет.ПолучитьОбласть("НЗПНач|Основная");
	ОбластьНЗПКон             		   = Макет.ПолучитьОбласть("НЗПКон|Основная");
	ОбластьНЗПИтого           		   = Макет.ПолучитьОбласть("НЗПИтого|Основная");
	ОбластьОкончаниеТаблицы   		   = Макет.ПолучитьОбласть("ОкончаниеТаблицы|Основная");
	ОбластьСтрокаЗатратОкончание       = Макет.ПолучитьОбласть("СтрокаЗатрат|Окончание");
	ОбластьШапкаОкончание              = Макет.ПолучитьОбласть("Шапка|Окончание");
	ОбластьШапкаСПРОкончание           = Макет.ПолучитьОбласть("ШапкаРазницы|Окончание");
	ОбластьСчетЗатратОкончание         = Макет.ПолучитьОбласть("СчетЗатрат|Окончание");
	ОбластьНоменклатураЗатратОкончание = Макет.ПолучитьОбласть("СтрокаНоменклатурыЗатрат|Окончание");
	ОбластьШапкаПрямыеОкончание        = Макет.ПолучитьОбласть("ШапкаПрямые|Окончание");
	ОбластьШапкаКосвенныеОкончание     = Макет.ПолучитьОбласть("ШапкаКосвенные|Окончание");
	ОбластьИтогоПрямыеОкончание        = Макет.ПолучитьОбласть("ИтогоПрямые|Окончание");
	ОбластьИтогоКосвенныеОкончание     = Макет.ПолучитьОбласть("ИтогоКосвенные|Окончание");
	ОбластьНЗПНачОкончание             = Макет.ПолучитьОбласть("НЗПНач|Окончание");
	ОбластьНЗПКонОкончание             = Макет.ПолучитьОбласть("НЗПКон|Окончание");
	ОбластьНЗПИтогоОкончание           = Макет.ПолучитьОбласть("НЗПИтого|Окончание");
	
	ОбластьШапкаРазницы        		 = Макет.ПолучитьОбласть("Шапка|Разницы");
	ОбластьШапкаСПРРазницы           = Макет.ПолучитьОбласть("ШапкаРазницы|Разницы");
	ОбластьСтрокаЗатратРазницы       = Макет.ПолучитьОбласть("СтрокаЗатрат|Разницы");
	ОбластьСчетЗатратРазницы         = Макет.ПолучитьОбласть("СчетЗатрат|Разницы");
	ОбластьНоменклатураЗатратРазницы = Макет.ПолучитьОбласть("СтрокаНоменклатурыЗатрат|Разницы");
	ОбластьШапкаПрямыеРазницы        = Макет.ПолучитьОбласть("ШапкаПрямые|Разницы");
	ОбластьШапкаКосвенныеРазницы     = Макет.ПолучитьОбласть("ШапкаКосвенные|Разницы");
	ОбластьИтогоПрямыеРазницы        = Макет.ПолучитьОбласть("ИтогоПрямые|Разницы");
	ОбластьИтогоКосвенныеРазницы     = Макет.ПолучитьОбласть("ИтогоКосвенные|Разницы");
	ОбластьНЗПНачРазницы             = Макет.ПолучитьОбласть("НЗПНач|Разницы");
	ОбластьНЗПКонРазницы             = Макет.ПолучитьОбласть("НЗПКон|Разницы");
	ОбластьНЗПИтогоРазницы           = Макет.ПолучитьОбласть("НЗПИтого|Разницы");
	ОбластьОкончаниеТаблицыРазницы   = Макет.ПолучитьОбласть("ОкончаниеТаблицы|Разницы");
	ОбластьОкончаниеТаблицыОкончание = Макет.ПолучитьОбласть("ОкончаниеТаблицы|Окончание");

	ОбластьПодвал             = МакетОбщий.ПолучитьОбласть("Подвал");
	
	ПостояннаяРазница = "Постоянные
	                     |разницы";
	ВременнаяРазница = "Временные
	                     |разницы";
	
	ВыборкаПодразделение = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Подразделение");
	
	Пока ВыборкаПодразделение.Следующий() Цикл		
		ВыборкаНоменклатурнаяГруппа = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатурнаяГруппа");
		
		Пока ВыборкаНоменклатурнаяГруппа.Следующий() Цикл
			ВсегоВыпуск   = ВыборкаНоменклатурнаяГруппа.ОбщаяСуммаВыпуска;
			ВсегоВыпускПР = ?(ВариантОтчета = 3, ВыборкаНоменклатурнаяГруппа.ОбщаяСуммаВыпускаПР, 0);
			ВсегоВыпускВР = ?(ВариантОтчета = 3, ВыборкаНоменклатурнаяГруппа.ОбщаяСуммаВыпускаВР, 0);
			Если ВсегоВыпуск = 0 И ВсегоВыпускПР = 0 И ВсегоВыпускВР = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ВыборкаНоменклатураВыпуска = ВыборкаНоменклатурнаяГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Номенклатура");
			
			Пока ВыборкаНоменклатураВыпуска.Следующий() Цикл
				
				Если НЕ ЗначениеЗаполнено(ВыборкаНоменклатураВыпуска.Номенклатура) Тогда
					Продолжить;
				КонецЕсли;
				
				СуммаВыпуска   = ВыборкаНоменклатураВыпуска.ОбщаяСуммаВыпуска;
				СуммаВыпускаПР = ?(ВариантОтчета = 3, ВыборкаНоменклатураВыпуска.ОбщаяСуммаВыпускаПР, 0);
				СуммаВыпускаВР = ?(ВариантОтчета = 3, ВыборкаНоменклатураВыпуска.ОбщаяСуммаВыпускаВР, 0);
				
				Если СуммаВыпуска = 0 И СуммаВыпускаПР = 0 И СуммаВыпускаВР = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Коэффициент   = ?(ВсегоВыпуск = 0, 0, СуммаВыпуска   / ВсегоВыпуск);
				КоэффициентПР = ?(ВсегоВыпускПР = 0, 0, СуммаВыпускаПР / ВсегоВыпускПР);
				КоэффициентВР = ?(ВсегоВыпускВР = 0, 0, СуммаВыпускаВР / ВсегоВыпускВР);
				
				ОбластьШапка.Параметры.НоменклатураВыпуска         = КэшированныйВыводНаименованияВыпуска(ВыборкаНоменклатураВыпуска.Номенклатура);
				ОбластьШапка.Параметры.АналитикаВыпуска            = "" + ВыборкаНоменклатураВыпуска.Подразделение + " / " + ВыборкаНоменклатураВыпуска.НоменклатурнаяГруппа;
				
				ОбластьШапка.Параметры.КоличествоВыпуска           = ВыборкаНоменклатураВыпуска.Количество;
				ОбластьШапка.Параметры.СуммаВыпуска                =  ?(ВыборкаНоменклатураВыпуска.Количество = 0, 0, ВыборкаНоменклатураВыпуска.Сумма);
				ОбластьШапка.Параметры.ЦенаВыпуска                 = ?(ВыборкаНоменклатураВыпуска.Количество = 0, ВыборкаНоменклатураВыпуска.Сумма, ВыборкаНоменклатураВыпуска.Сумма / ВыборкаНоменклатураВыпуска.Количество);
				ОбластьШапка.Параметры.ТекстКоличество             = ?(ВыборкаНоменклатураВыпуска.Количество = 0, "", "Количество выпуска:");
				ОбластьШапка.Параметры.ТекстСумма                  = ?(ВыборкаНоменклатураВыпуска.Количество = 0, "", "Фактическая стоимость выпуска:");
				ОбластьШапка.Параметры.СебестоимостьЕдиницы        = "Себестоимость ед.";
					
				Если ВариантОтчета  = 3 И СуммаВыпускаПР <> 0 Тогда
					ОбластьШапкаСПР.Параметры.КоличествоВыпуска           = ВыборкаНоменклатураВыпуска.КоличествоПР;
					ОбластьШапкаСПР.Параметры.СуммаВыпуска                =  ?(ВыборкаНоменклатураВыпуска.КоличествоПР = 0, 0, ВыборкаНоменклатураВыпуска.СуммаПР);
					ОбластьШапкаСПР.Параметры.ЦенаВыпуска                 = ?(ВыборкаНоменклатураВыпуска.КоличествоПР = 0, ВыборкаНоменклатураВыпуска.СуммаПР, ВыборкаНоменклатураВыпуска.СуммаПР / ВыборкаНоменклатураВыпуска.КоличествоПР);
					ОбластьШапкаСПР.Параметры.ТекстКоличество             = ?(ВыборкаНоменклатураВыпуска.КоличествоПР = 0, "", "Количество выпуска(ПР):");
					ОбластьШапкаСПР.Параметры.ТекстСумма                  = ?(ВыборкаНоменклатураВыпуска.КоличествоПР = 0, "", "Фактическая стоимость выпуска (ПР):");
					ОбластьШапкаСПР.Параметры.СебестоимостьЕдиницы        = "Себестоимость ед.(ПР)";

				КонецЕсли;

                ВывестиОбласть(ДокументРезультат, ОбластьШапка, ОбластьШапкаРазницы, ОбластьШапкаОкончание, 1, "Группа1", Ложь, "Сумма", ВыборкаНоменклатураВыпуска);
				Если ВариантОтчета  = 3 И СуммаВыпускаПР <> 0 Тогда
					ВывестиОбласть(ДокументРезультат, ОбластьШапкаСПР, ОбластьШапкаСПРРазницы, ОбластьШапкаСПРОкончание, 1, "Группа1", Ложь, "Текст", ВыборкаНоменклатураВыпуска, "", "");
				КонецЕсли;
				

                ВывестиОбласть(ДокументРезультат, ОбластьШапкаПрямые, ОбластьШапкаПрямыеРазницы, ОбластьШапкаПрямыеОкончание, 2, "Группа2", Ложь, "Текст", ВыборкаНоменклатураВыпуска, ПостояннаяРазница, ВременнаяРазница);
				ВыборкаПрямыеЗатраты = ВыборкаНоменклатураВыпуска.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПрямыеЗатраты");
				ИтогоСуммаПрямыхЗатрат = 0;
				ИтогоСуммаПрямыхЗатратПР = 0;
				ИтогоСуммаПрямыхЗатратВР = 0;
				Пока ВыборкаПрямыеЗатраты.Следующий() Цикл
					
					Если ВыборкаПрямыеЗатраты.СуммаПрямыхЗатрат = 0 Тогда
						Продолжить;
					КонецЕсли;
					ВыборкаНоменклатураЗатрат = ВыборкаПрямыеЗатраты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатураПрямыхЗатрат");
					СуммаПрямыхЗатрат   = 0;
					СуммаПрямыхЗатратПР = 0;
		        	СуммаПрямыхЗатратВР = 0;
					Если ВыборкаНоменклатураЗатрат.Количество() = 0 Тогда
						СуммаПрямыхЗатрат = ВыборкаПрямыеЗатраты.СуммаПрямыхЗатрат;
						Если ВариантОтчета = 3 Тогда

							СуммаПрямыхЗатратПР = ВыборкаПрямыеЗатраты.СуммаПрямыхЗатратПР ;
							СуммаПрямыхЗатратВР = ВыборкаПрямыеЗатраты.СуммаПрямыхЗатратВР ;
						КонецЕсли;
					Иначе
						
						
						Пока ВыборкаНоменклатураЗатрат.Следующий() Цикл
							
						ВыборкаСчетПрямыхЗатрат = ВыборкаНоменклатураЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "КорСчетПрямыхЗатрат");
							
							Пока ВыборкаСчетПрямыхЗатрат.Следующий() Цикл
								СуммаПрямыхЗатрат = СуммаПрямыхЗатрат + ВыборкаСчетПрямыхЗатрат.СуммаПрямыхЗатрат;
								Если ВариантОтчета = 3 Тогда
									СуммаПрямыхЗатратПР = СуммаПрямыхЗатратПР + ВыборкаСчетПрямыхЗатрат.СуммаПрямыхЗатратПР ;
									СуммаПрямыхЗатратВР = СуммаПрямыхЗатратВР + ВыборкаСчетПрямыхЗатрат.СуммаПрямыхЗатратВР ;
								КонецЕсли;
								
							КонецЦикла;
						КонецЦикла
					КонецЕсли;
					
					ОбластьСтрокаЗатрат.Параметры.НаименованиеЗатрат = ВыборкаПрямыеЗатраты.ПрямыеЗатраты;
					ОбластьСтрокаЗатрат.Параметры.СуммаЗатрат = СуммаПрямыхЗатрат * Коэффициент;
                    ВывестиОбласть(ДокументРезультат, ОбластьСтрокаЗатрат, ОбластьСтрокаЗатратРазницы, ОбластьСтрокаЗатратОкончание, 2, "Группа2", Ложь, "СуммаЗатрат", ВыборкаПрямыеЗатраты, СуммаПрямыхЗатратПР * КоэффициентПР, СуммаПрямыхЗатратВР * КоэффициентВР);
					ВыборкаСчетПрямыхЗатрат = ВыборкаПрямыеЗатраты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "КорСчетПрямыхЗатрат");
					
					Пока ВыборкаСчетПрямыхЗатрат.Следующий() Цикл
						
						Если ВыборкаСчетПрямыхЗатрат.СуммаПрямыхЗатрат = 0 Тогда
							Продолжить;
						КонецЕсли;
						
					ВыборкаНоменклатураЗатрат = ВыборкаСчетПрямыхЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатураПрямыхЗатрат");
					СуммаПрямыхЗатрат = 0;
					СуммаПрямыхЗатратПР = 0;
		        	СуммаПрямыхЗатратВР = 0;
					Пока ВыборкаНоменклатураЗатрат.Следующий() Цикл
						СуммаПрямыхЗатрат = СуммаПрямыхЗатрат + ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатрат;
						Если ВариантОтчета = 3 Тогда
						СуммаПрямыхЗатратПР = СуммаПрямыхЗатратПР + ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатратПР;
						СуммаПрямыхЗатратВР = СуммаПрямыхЗатратВР + ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатратВР;
						КонецЕсли;

					КонецЦикла;
						СуммаПрямыхЗатрат = ?(СуммаПрямыхЗатрат = 0, ВыборкаСчетПрямыхЗатрат.СуммаПрямыхЗатрат, СуммаПрямыхЗатрат);
						ОбластьСчетЗатрат.Параметры.НаименованиеСчетаЗатрат = РазъяснитьЗатраты(ВыборкаСчетПрямыхЗатрат.КорСчетПрямыхЗатрат);
						ОбластьСчетЗатрат.Параметры.СуммаСчетаЗатрат = СуммаПрямыхЗатрат * Коэффициент;
                        ВывестиОбласть(ДокументРезультат, ОбластьСчетЗатрат, ОбластьСчетЗатратРазницы, ОбластьСчетЗатратОкончание, 3, "Группа3", Ложь, "СуммаСчетаЗатрат", ВыборкаСчетПрямыхЗатрат, СуммаПрямыхЗатратПР * КоэффициентПР, СуммаПрямыхЗатратВР * КоэффициентВР);
						
						ВыборкаНоменклатураЗатрат = ВыборкаСчетПрямыхЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатураПрямыхЗатрат");
						Пока ВыборкаНоменклатураЗатрат.Следующий() Цикл
							
							ОбластьНоменклатураЗатрат.Параметры.НаименованиеНоменклатурыЗатрат = УправлениеПроизводством.ВыводНаименованияВыпуска(ВыборкаНоменклатураЗатрат.НоменклатураПрямыхЗатрат);
							ОбластьНоменклатураЗатрат.Параметры.СуммаНоменклатурыЗатрат        = ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатрат * Коэффициент;
							ОбластьНоменклатураЗатрат.Параметры.КоличествоНоменклатурыЗатрат   = ВыборкаНоменклатураЗатрат.КоличествоПрямыхЗатрат * Коэффициент;
							ОбластьНоменклатураЗатрат.Параметры.ЦенаНоменклатурыЗатрат         = ?(ВыборкаНоменклатураЗатрат.КоличествоПрямыхЗатрат = 0, 0, ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатрат / ВыборкаНоменклатураЗатрат.КоличествоПрямыхЗатрат);
							ВывестиОбласть(ДокументРезультат, ОбластьНоменклатураЗатрат, ОбластьНоменклатураЗатратРазницы, ОбластьНоменклатураЗатратОкончание, 4, "Группа4", Ложь, "СуммаНоменклатурыЗатрат", ВыборкаНоменклатураЗатрат, ?(ВариантОтчета = 3, (ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатратПР) * КоэффициентПР, 0), ?(ВариантОтчета = 3, (ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатратВР) * КоэффициентВР, 0));
							
							ИтогоСуммаПрямыхЗатрат = ИтогоСуммаПрямыхЗатрат + ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатрат * Коэффициент;
							Если ВариантОтчета = 3 Тогда
								ИтогоСуммаПрямыхЗатратПР = ИтогоСуммаПрямыхЗатратПР + ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатратПР * КоэффициентПР ;
								ИтогоСуммаПрямыхЗатратВР = ИтогоСуммаПрямыхЗатратВР + ВыборкаНоменклатураЗатрат.СуммаПрямыхЗатратВР  * КоэффициентВР;
							КонецЕсли;
							
						КонецЦикла; // НоменклатураЗатрат
						
						Если ВыборкаНоменклатураЗатрат.Количество() = 0 Тогда
							ИтогоСуммаПрямыхЗатрат = ИтогоСуммаПрямыхЗатрат + ВыборкаСчетПрямыхЗатрат.СуммаПрямыхЗатрат * Коэффициент;
							Если ВариантОтчета = 3 Тогда
								ИтогоСуммаПрямыхЗатратПР = ИтогоСуммаПрямыхЗатратПР + ВыборкаСчетПрямыхЗатрат.СуммаПрямыхЗатратПР * КоэффициентПР ;
								ИтогоСуммаПрямыхЗатратВР = ИтогоСуммаПрямыхЗатратВР + ВыборкаСчетПрямыхЗатрат.СуммаПрямыхЗатратВР  * КоэффициентВР;
							КонецЕсли;
						КонецЕсли;
						
					КонецЦикла; // СчетЗатрат
				КонецЦикла;  // ПрямыеЗатраты
				
				ОбластьИтогоПрямые.Параметры.ИтогоСуммаПрямыхЗатрат = ИтогоСуммаПрямыхЗатрат;
			    ВывестиОбласть(ДокументРезультат, ОбластьИтогоПрямые, ОбластьИтогоПрямыеРазницы, ОбластьИтогоПрямыеОкончание, 2, "Группа2", Ложь, "ИтогоСуммаПрямыхЗатрат", ВыборкаСчетПрямыхЗатрат, ИтогоСуммаПрямыхЗатратПР, ИтогоСуммаПрямыхЗатратВР);
				
				ВсегоКосвенныхЗатрат = ВыборкаНоменклатураВыпуска.СуммаКосвЗатрат;
				ВсегоСуммаЗатрат   = 0;
				ВсегоСуммаЗатратПР = 0;
				ВсегоСуммаЗатратВР = 0;
				Если НЕ ВсегоКосвенныхЗатрат = 0 Тогда
			        ВывестиОбласть(ДокументРезультат, ОбластьШапкаКосвенные, ОбластьШапкаКосвенныеРазницы, ОбластьШапкаКосвенныеОкончание, 2, "Группа2", Ложь, "Текст", ВыборкаНоменклатураВыпуска, "", "");
					ВыборкаКосвенные = ВыборкаНоменклатураВыпуска.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ЗатратыКосвенные");
					Пока ВыборкаКосвенные.Следующий() Цикл
						ОбластьСтрокаЗатрат.Параметры.НаименованиеЗатрат = КэшированныйВыводНаименованияЗатрат(ВыборкаКосвенные.ЗатратыКосвенные);
						ОбластьСтрокаЗатрат.Параметры.СуммаЗатрат        = ВыборкаКосвенные.СуммаКосвЗатрат * Коэффициент;
						ВсегоСуммаЗатрат = ВсегоСуммаЗатрат + ВыборкаКосвенные.СуммаКосвЗатрат * Коэффициент;
						Если ВариантОтчета = 3 Тогда
							СуммаКосвЗатратПР = ВыборкаКосвенные.СуммаКосвЗатратПР * КоэффициентПР;
							СуммаКосвЗатратВР = ВыборкаКосвенные.СуммаКосвЗатратВР * КоэффициентВР;
						Иначе
							СуммаКосвЗатратПР = 0;
							СуммаКосвЗатратВР = 0;
						КонецЕсли;
							ВсегоСуммаЗатратПР = ВсегоСуммаЗатратПР + СуммаКосвЗатратПР;
							ВсегоСуммаЗатратВР = ВсегоСуммаЗатратВР + СуммаКосвЗатратВР;
                        ВывестиОбласть(ДокументРезультат, ОбластьСтрокаЗатрат, ОбластьСтрокаЗатратРазницы, ОбластьСтрокаЗатратОкончание, 0, 0, Ложь, "СуммаЗатрат", ВыборкаКосвенные, СуммаКосвЗатратПР, СуммаКосвЗатратВР);
					КонецЦикла; 					
					ОбластьИтогоКосвенные.Параметры.ИтогоСуммаКосвенныхЗатрат        = ВсегоСуммаЗатрат;
			        ВывестиОбласть(ДокументРезультат, ОбластьИтогоКосвенные, ОбластьИтогоКосвенныеРазницы, ОбластьИтогоКосвенныеОкончание, 2, "Группа2", Ложь, "ИтогоСуммаКосвенныхЗатрат", ВыборкаКосвенные, ВсегоСуммаЗатратПР, ВсегоСуммаЗатратВР);
		   		КонецЕсли;  // косвенные затраты
				               				
				НачОстНЗП   = ВыборкаНоменклатураВыпуска.НачОстНЗП   * Коэффициент;
				КонОстНЗП   = ВыборкаНоменклатураВыпуска.КонОстНЗП   * Коэффициент;
				НачОстНЗППР = 0;
				КонОстНЗППР = 0;
				НачОстНЗПВР = 0;
				КонОстНЗПВР = 0;
				Если ВариантОтчета = 3 Тогда
					НачОстНЗППР = (ВыборкаНоменклатураВыпуска.НачОстНЗППР) * КоэффициентПР;
					КонОстНЗППР = (ВыборкаНоменклатураВыпуска.КонОстНЗППР) * КоэффициентПР;
					НачОстНЗПВР = (ВыборкаНоменклатураВыпуска.НачОстНЗПВР) * КоэффициентВР;
					КонОстНЗПВР = (ВыборкаНоменклатураВыпуска.КонОстНЗПВР) * КоэффициентВР;
				КонецЕсли;
				
				Если     НачОстНЗП   <> 0 Или КонОстНЗП   <> 0
					 Или НачОстНЗППР <> 0 Или КонОстНЗППР <> 0
					 Или НачОстНЗПВР <> 0 Или КонОстНЗПВР <> 0 Тогда
					   
					ОбластьНЗПНач.Параметры.СуммаНЗПнаНачало = НачОстНЗП;
					ОбластьНЗПКон.Параметры.СуммаНЗПнаКонец  = КонОстНЗП;
					ОбластьНЗПИтого.Параметры.ИтогоНЗП         = НачОстНЗП - КонОстНЗП;
					ОбластьНЗПНач.Параметры.НачалоПериода	  = ДатаНач;
					ОбластьНЗПКон.Параметры.КонецПериода	  =	ДатаКон;
			        ВывестиОбласть(ДокументРезультат, ОбластьНЗПНач, ОбластьНЗПНачРазницы, ОбластьНЗПНачОкончание, 2, "Группа2", Ложь, "НачОстНЗП", ВыборкаНоменклатураВыпуска, НачОстНЗППР, НачОстНЗПВР);
			        ВывестиОбласть(ДокументРезультат, ОбластьНЗПКон, ОбластьНЗПКонРазницы, ОбластьНЗПКонОкончание, 2, "Группа2", Ложь, "КонОстНЗП", ВыборкаНоменклатураВыпуска, КонОстНЗППР, КонОстНЗПВР);
			        ВывестиОбласть(ДокументРезультат, ОбластьНЗПИтого, ОбластьНЗПИтогоРазницы, ОбластьНЗПИтогоОкончание, 2, "Группа2", Ложь, "ИтогоНЗП", ВыборкаНоменклатураВыпуска, - КонОстНЗППР + НачОстНЗППР, - КонОстНЗПВР + НачОстНЗПВР);
				КонецЕсли;  // НЗП
				
			    ВывестиОбласть(ДокументРезультат, ОбластьОкончаниеТаблицы, ОбластьОкончаниеТаблицыРазницы, ОбластьОкончаниеТаблицыОкончание, 1, "Группа1", Ложь, "Текст", ВыборкаНоменклатураВыпуска, "", "");
			КонецЦикла;  // НоменклатураВыпуска

		КонецЦикла;   // НоменклатурнаяГруппа
	КонецЦикла;    // Подразделение
	
	ОбластьПодвал.Параметры.ТекстПримечания = ?(ВариантОтчета = 3, "Постоянные разницы: Постоянные разницы в оценке стоимости активов и обязательств.
	                                                               |Временные разницы: Временные разницы в оценке стоимости активов и обязательств.", "");
	ДокументРезультат.Вывести(ОбластьПодвал);
	
	ВысотаПодписи = ДокументРезультат.Области.Подвал.Низ - ДокументРезультат.Области.Подвал.Верх;
	
	ДокументРезультат.Области.Подвал.Видимость = ПоказыватьПодписи;
	
	// Первую колонку не печатаем
	ДокументРезультат.ОбластьПечати = ДокументРезультат.Область(1,2,ДокументРезультат.ВысотаТаблицы,ДокументРезультат.ШиринаТаблицы);
	
	// Присвоим имя для сохранения параметров печати табличного документа
	ДокументРезультат.ИмяПараметровПечати = "Калькуляция себестоимости";

	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ДокументРезультат, ЗаголовокОтчета, Строка(глЗначениеПеременной("глТекущийПользователь")));
	
КонецПроцедуры // СформироватьОтчет

Функция РазъяснитьЗатраты(СчетЗатрат)
	
	Если НЕ ЗначениеЗаполнено(СчетЗатрат) Тогда
		Возврат "";
	Иначе
		Результат = мКэшНаименованиеСчета[СчетЗатрат];
		Если Результат = Неопределено Тогда
			Результат = СчетЗатрат.Наименование;
			мКэшНаименованиеСчета[СчетЗатрат] = Результат;
		КонецЕсли;
		Возврат Результат;
	КонецЕсли;

  
КонецФункции

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	СтарыеНастройки = УправлениеОтчетами.ПолучитьКопиюОтбораВТЗ(ПостроительОтчета.Отбор);
	
	СоотвСубконто = Новый Соответствие;
	
	Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТиповойОбороты.СуммаОборот КАК СуммаОборот
			|{ВЫБРАТЬ
			|	ТиповойОбороты.СубконтоКт1 КАК СубконтоКт1,
			|	ТиповойОбороты.СубконтоКт2 КАК СубконтоКт2}
			|ИЗ
			|	РегистрБухгалтерии.Типовой.ОборотыДтКт(, , Месяц, , , СчетКт В ИЕРАРХИИ (&Счет), , {(СубконтоКт1).*, (СубконтоКт2).*}) КАК ТиповойОбороты
			|ИТОГИ
			|	СУММА(СуммаОборот)
			|ПО
			|	ОБЩИЕ
			|{ИТОГИ ПО
			|	ТиповойОбороты.СубконтоКт1.*,
			|	ТиповойОбороты.СубконтоКт2.*}";

	Массив = Новый Массив;
	Массив.Добавить(ПланыСчетов.Типовой.ОсновноеПроизводство);
	Массив.Добавить(ПланыСчетов.Типовой.ПолуфабрикатыСобственногоПроизводства);
	Массив.Добавить(ПланыСчетов.Типовой.ВспомогательныеПроизводства);

	ПостроительОтчета.Параметры.Вставить("Счет", Массив);
	ПостроительОтчета.Параметры.Вставить("ПустаяОрганизация", Справочники.Организации.ПустаяСсылка());
		
	ПостроительОтчета.Текст = Текст;

	СоотвСубконто = Новый Соответствие;
	СоотвСубконто.Вставить("СубконтоКт1",    ПланыВидовХарактеристик.ВидыСубконтоТиповые.Подразделения);
	СоотвСубконто.Вставить("СубконтоКт2",    ПланыВидовХарактеристик.ВидыСубконтоТиповые.НоменклатурныеГруппы);

			
	Сч = 0;
	Для каждого Элемент Из СоотвСубконто Цикл
		Сч = Сч+1;
		Поле = ПостроительОтчета.ДоступныеПоля.Найти(Элемент.Ключ);
		Поле.ТипЗначения = Элемент.Значение.ТипЗначения;
		Поле.Представление = Элемент.Значение.Наименование;
		
		ПостроительОтчета.Отбор.Добавить(Элемент.Ключ);
	КонецЦикла;

	УправлениеОтчетами.УстановитьОтборИзТаблицы(ПостроительОтчета.Отбор, СтарыеНастройки);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;
мКэшНаименованиеНоменклатуры = Новый Соответствие;
мКэшНаименованиеСчета = Новый Соответствие;
мКэшНаименованийЗатрат = Новый Соответствие;

мОтображатьСтруктурныеПодразделения = ОбщегоНазначения.ПолучитьПризнакОтображенияСтруктурныхПодразделений();

#КонецЕсли