////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Хранит список структурных единиц, по которым стоится отчет
Перем мСписокСтруктурныхЕдиниц Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//	ЕстьОшибки - флаг того, что при формировании произошли ошибки
//
Процедура СформироватьОтчет(ДокументРезультат, ЕстьОшибки) Экспорт

    Если ТипЗнч(мСписокСтруктурныхЕдиниц) <> Тип("СписокЗначений") Тогда 
		ЕстьОшибки = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	  
    СписокОфицерскихЗваний	=	Новый СписокЗначений;
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.МладшийЛейтенант);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.Лейтенант);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.СтаршийЛейтенант);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.Капитан);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.Майор);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.Подполковник);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.Полковник);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.ГенералМайор);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.ГенералЛейтенант);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.ГенералПолковник);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.ГенералАрмии);
	СписокОфицерскихЗваний.Добавить(Справочники.ВоинскиеЗвания.МаршалРеспубликиКазахстан);
	
	СписокЗванийПрапорщиков	=	Новый СписокЗначений;
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.Прапорщик);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.СтаршийПрапорщик);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.МладшийСержант);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.Сержант);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.СтаршийСержант);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.Старшина);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.МастерСержант);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.Сержант1Класса);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.Сержант2Класса);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.Сержант3Класса);
	СписокЗванийПрапорщиков.Добавить(Справочники.ВоинскиеЗвания.ШтабСержант);
	
	СписокСолдатскихЗваний	=	Новый СписокЗначений;
	СписокСолдатскихЗваний.Добавить(Справочники.ВоинскиеЗвания.Рядовой);
	СписокСолдатскихЗваний.Добавить(Справочники.ВоинскиеЗвания.Ефрейтор);
	
	СписокГодныеКСлужбе = Новый СписокЗначений;
	СписокГодныеКСлужбе.Добавить(Перечисления.ГодностьКВоеннойСлужбе.Годен);
	СписокГодныеКСлужбе.Добавить(Перечисления.ГодностьКВоеннойСлужбе.ГоденКНестроевой);
	
	Запрос.УстановитьПараметр("ДатаАктуальности",	ДатаАктуальности);
	Запрос.УстановитьПараметр("Организации",		мСписокСтруктурныхЕдиниц);
	Запрос.УстановитьПараметр("Офицеры",			СписокОфицерскихЗваний);
	Запрос.УстановитьПараметр("Прапорщики",			СписокЗванийПрапорщиков);
	Запрос.УстановитьПараметр("Солдаты",			СписокСолдатскихЗваний);
	Запрос.УстановитьПараметр("Военнообязанный",	Перечисления.ОтношениеКВоинскойОбязанности.Военнообязанный);
	Запрос.УстановитьПараметр("СписокГодныеКСлужбе",СписокГодныеКСлужбе);
	
	Запрос.УстановитьПараметр("Призывник",			Перечисления.ОтношениеКВоинскойОбязанности.Призывник);
	Запрос.УстановитьПараметр("Уволен",				Перечисления.ПричиныИзмененияСостояния.Увольнение);
	Запрос.УстановитьПараметр("ВнутреннееСовместительство", Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(1) КАК ВсегоРаботающих,
	|	СУММА(ВЫБОР
	|			КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВсегоВЗапасе,
	|	СУММА(ВЫБОР
	|			КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|					И ВУ.Годность В (&СписокГодныеКСлужбе)
	|					И ВУ.Звание.ОбщевойсковоеЗвание В (&Офицеры)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОфицеровВЗапасе,
	|	СУММА(ВЫБОР
	|			КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|					И ВУ.Годность В (&СписокГодныеКСлужбе)
	|					И ВУ.Звание.ОбщевойсковоеЗвание В (&Прапорщики)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПрапорщиковВЗапасе,
	|	СУММА(ВЫБОР
	|			КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|					И ВУ.Годность В (&СписокГодныеКСлужбе)
	|					И ВУ.Звание.ОбщевойсковоеЗвание В (&Солдаты)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СолдатВЗапасе,
	|	СУММА(ВЫБОР
	|			КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|					И (НЕ ВУ.Годность В (&СписокГодныеКСлужбе))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НегодныхВМирноеВремя,
	|	СУММА(ВЫБОР
	|			КОГДА ВУ.ЗабронированОрганизацией В (&Организации)
	|					И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВсегоВЗапасеЗабронировано,
	|	СУММА(ВЫБОР
	|			КОГДА ВУ.ЗабронированОрганизацией В (&Организации)
	|					И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|					И ВУ.Звание.ОбщевойсковоеЗвание В (&Офицеры)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОфицеровЗабронировано,
	|	СУММА(ВЫБОР
	|			КОГДА (НЕ ВУ.ЗабронированОрганизацией В (&Организации))
	|					И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|					И (НЕ ВУ.НаличиеМобпредписания)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезабронированноБезМобпредписаний,
	|	СУММА(ВЫБОР
	|			КОГДА (НЕ ВУ.ЗабронированОрганизацией В (&Организации))
	|					И ВУ.ОтношениеКВоинскойОбязанности = &Военнообязанный
	|					И ВУ.НаличиеМобпредписания
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НезабронированноСМобпредписанием,
	|	СУММА(ВЫБОР
	|			КОГДА ВУ.ОтношениеКВоинскойОбязанности = &Призывник
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Призывников
	|ИЗ
	|	(ВЫБРАТЬ
	|		РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо КАК ФизЛицо
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ДатаАктуальности,
	|				Сотрудник.ВидЗанятости <> &ВнутреннееСовместительство) КАК РаботникиОрганизацииСрезПоследних
	|	ГДЕ
	|		РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделение В (&Организации)
	|		И РаботникиОрганизацииСрезПоследних.ПричинаИзмененияСостояния <> &Уволен) КАК РаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВоинскийУчет.СрезПоследних(&ДатаАктуальности, ) КАК ВУ
	|		ПО РаботникиОрганизации.ФизЛицо = ВУ.Физлицо";

	РезультатЗапроса = Запрос.Выполнить();

	ДокументРезультат.Очистить();
	Макет = ПолучитьМакет("Карточка");

	ВыборкаПоКатегориям	=	РезультатЗапроса.Выбрать();
	Если ВыборкаПоКатегориям.Следующий() тогда
		ОбластьКарточка 	= Макет.ПолучитьОбласть("Карточка");
		ОбластьКарточка.Параметры.ВсегоРаботающих								= "ВСЕГО работающих  "	+	ВыборкаПоКатегориям.ВсегоРаботающих;
		ОбластьКарточка.Параметры.ВсегоВЗапасе 									= "12.1. Граждан, пребывающих в запасе (военнообязанных),  "	+	ВыборкаПоКатегориям.ВсегоВЗапасе;
		ОбластьКарточка.Параметры.ОфицеровВЗапасе 								= "а) офицеров и генералов  "	+	ВыборкаПоКатегориям.ОфицеровВЗапасе;
		ОбластьКарточка.Параметры.ПрапорщиковВЗапасе 							= "б) прапорщиков, мичманов, сержантов и старшин  "	+	ВыборкаПоКатегориям.ПрапорщиковВЗапасе;
		ОбластьКарточка.Параметры.СолдатВЗапасе 								= "в) солдат и матросов  "	+	ВыборкаПоКатегориям.СолдатВЗапасе;
		ОбластьКарточка.Параметры.НегодныхВМирноеВремя 	= "г) негодных к военной службе в мирное время  "	+	ВыборкаПоКатегориям.НегодныхВМирноеВремя;
		ОбластьКарточка.Параметры.ВсегоВЗапасеЗабронировано 					= "12.2. Забронировано всего:  " + ВыборкаПоКатегориям.ВсегоВЗапасеЗабронировано	+	", в том числе офицеров и генералов  " + ВыборкаПоКатегориям.ОфицеровЗабронировано;
		ОбластьКарточка.Параметры.НезабронированноБезМобпредписаний 			= "12.3. Незабронировано не имеющих мобпредписаний  "	+	ВыборкаПоКатегориям.НезабронированноБезМобпредписаний;
		ОбластьКарточка.Параметры.НезабронированноСМобпредписанием				= "12.4. Подлежит призыву по мобилизации  "	+	ВыборкаПоКатегориям.НезабронированноСМобпредписанием;
		ОбластьКарточка.Параметры.Призывников									= "12.5. Призывников  "	+	ВыборкаПоКатегориям.Призывников;
	КонецЕсли;
	Если ТипЗнч(ОбластьКарточка)	=	Тип("ТабличныйДокумент")	тогда
		ДокументРезультат.Вывести(ОбластьКарточка);
	КонецЕсли;
	
	//Параметры документа
	ДокументРезультат.Автомасштаб		 = Истина;
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.Защита             = Ложь;
	ДокументРезультат.ТолькоПросмотр     = Ложь;
	
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ДокументРезультат, "Данные для карточки учета организации в военкомате", Строка(глТекущийПользователь));
	
КонецПроцедуры

#КонецЕсли

мСписокСтруктурныхЕдиниц = Новый СписокЗначений;