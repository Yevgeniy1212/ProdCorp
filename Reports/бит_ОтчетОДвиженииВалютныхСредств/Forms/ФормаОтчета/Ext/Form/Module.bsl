
&НаСервере
Процедура СформироватьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта,
		|	КурсыВалютСрезПоследних.Курс
		|ПОМЕСТИТЬ ВтКурсы
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаФормирования, ) КАК КурсыВалютСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта,
		|	КурсыВалютСрезПоследних.Курс КАК КурсUSD
		|ПОМЕСТИТЬ ВтКурсыUSD
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаФормирования, Валюта = &USD) КАК КурсыВалютСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ТиповойОстаткиИОбороты.ВалютнаяСуммаНачальныйОстаток * ВтКурсы.Курс / ВтКурсыUSD.КурсUSD) КАК СуммаВUSDНаНачало,
		|	СУММА(ТиповойОстаткиИОбороты.ВалютнаяСуммаКонечныйОстаток * ВтКурсы.Курс / ВтКурсыUSD.КурсUSD) КАК СуммаВUSDНаКонец
		|ПОМЕСТИТЬ ВТОстатки
		|ИЗ
		|	РегистрБухгалтерии.Типовой.ОстаткиИОбороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			,
		|			,
		|			Счет В ИЕРАРХИИ (&Счет1030),
		|			,
		|			Валюта <> &KZT
		|				И Валюта <> &CNY) КАК ТиповойОстаткиИОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсы КАК ВтКурсы
		|		ПО ТиповойОстаткиИОбороты.Валюта = ВтКурсы.Валюта,
		|	ВтКурсыUSD КАК ВтКурсыUSD
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА ТиповойОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПриобретениеИностраннойВалюты)
		|				ТОГДА ТиповойОбороты.ВалютнаяСуммаОборотДт * ВтКурсы.Курс / ВтКурсыUSD.КурсUSD
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПокупкаВалюты,
		|	СУММА(ВЫБОР
		|			КОГДА ТиповойОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПрочееСписаниеБезналичныхДенежныхСредств)
		|					И ТиповойОбороты.КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Типовой.ДенежныеСредстваВПутиКонвертацияВалюты)
		|				ТОГДА ТиповойОбороты.ВалютнаяСуммаОборотКт * ВтКурсы.Курс / ВтКурсыUSD.КурсUSD
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПродажаВалюты,
		|	СУММА(ВЫБОР
		|			КОГДА ТиповойОбороты.Регистратор.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПриобретениеИностраннойВалюты)
		|				ТОГДА ТиповойОбороты.ВалютнаяСуммаОборотДт * ВтКурсы.Курс / ВтКурсыUSD.КурсUSD
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Приход,
		|	СУММА(ВЫБОР
		|			КОГДА ТиповойОбороты.Регистратор.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПрочееСписаниеБезналичныхДенежныхСредств)
		|					И ТиповойОбороты.КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Типовой.ДенежныеСредстваВПутиКонвертацияВалюты)
		|				ТОГДА 0
		|			ИНАЧЕ ТиповойОбороты.ВалютнаяСуммаОборотКт * ВтКурсы.Курс / ВтКурсыUSD.КурсUSD
		|		КОНЕЦ) КАК Расход
		|ПОМЕСТИТЬ ВТОбороты
		|ИЗ
		|	РегистрБухгалтерии.Типовой.Обороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			Регистратор,
		|			Счет В ИЕРАРХИИ (&Счет1030),
		|			,
		|			Валюта <> &KZT
		|				И Валюта <> &CNY,
		|			,
		|			) КАК ТиповойОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсы КАК ВтКурсы
		|		ПО ТиповойОбороты.Валюта = ВтКурсы.Валюта,
		|	ВтКурсыUSD КАК ВтКурсыUSD
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОстатки.СуммаВUSDНаНачало КАК СуммаВUSDНаНачало,
		|	ВТОстатки.СуммаВUSDНаКонец КАК СуммаВUSDНаКонец,
		|	ВТОбороты.ПокупкаВалюты КАК ПокупкаВалюты,
		|	ВТОбороты.ПродажаВалюты КАК ПродажаВалюты,
		|	ВТОбороты.Приход КАК Приход,
		|	ВТОбороты.Расход КАК Расход,
		|	ВТОбороты.ПродажаВалюты + ВТОбороты.Расход КАК ВсегоРасход,
		|	ВТОбороты.ПокупкаВалюты + ВТОбороты.Приход КАК ВсегоПриход
		|ИЗ
		|	ВТОстатки КАК ВТОстатки,
		|	ВТОбороты КАК ВТОбороты";
	
	ДатаНачала = Отчет.КомпоновщикНастроек.ПолучитьНастройки().ПараметрыДанных.Элементы.Найти("ДатаНачала").Значение.Дата;
	ДатаОкончания = Отчет.КомпоновщикНастроек.ПолучитьНастройки().ПараметрыДанных.Элементы.Найти("ДатаОкончания").Значение.Дата;
	Подписант = Отчет.КомпоновщикНастроек.ПолучитьНастройки().ПараметрыДанных.Элементы.Найти("Подписант").Значение;
	
	Запрос.УстановитьПараметр("KZT", Справочники.Валюты.НайтиПоКоду("398"));
	Запрос.УстановитьПараметр("USD", Справочники.Валюты.НайтиПоКоду("840"));
	Запрос.УстановитьПараметр("CNY", Справочники.Валюты.НайтиПоКоду("156"));

	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("ДатаФормирования", Отчет.КомпоновщикНастроек.ПолучитьНастройки().ПараметрыДанных.Элементы.Найти("ДатаФормирования").Значение.Дата);
	Запрос.УстановитьПараметр("Счет1030", ПланыСчетов.Типовой.ДенежныеСредстваНаТекущихБанковскихСчетах);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Макет = Отчеты.бит_ОтчетОДвиженииВалютныхСредств.ПолучитьМакет("Макет");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	Результат.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	   		
		ОбластьШапка.Параметры.Заполнить(ВыборкаДетальныеЗаписи);

		ОбластьШапка.Параметры.ДатаНачала = Формат(ДатаНачала, "ДЛФ=Д");
		ОбластьШапка.Параметры.ДатаОкончания = Формат(ДатаОкончания, "ДЛФ=Д");
		
		ОбластьШапка.Параметры.Должность = Подписант.ТекущаяДолжностьОрганизации;
		ОбластьШапка.Параметры.ФИО = Подписант.Физлицо.Наименование;

		Результат.Вывести(ОбластьШапка);

	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры
