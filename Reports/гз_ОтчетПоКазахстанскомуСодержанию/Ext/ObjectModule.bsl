#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит таблицу значений - состав показателей отчета.
Перем мТаблицаСоставПоказателей Экспорт;  // (переменная отчета)

// Хранит структуру - состав показателей отчета,
// значение которых автоматически заполняется по учетным данным.
Перем мСтруктураВариантыЗаполнения Экспорт;  // (переменная отчета)

// Хранит имя выбранной формы отчета
Перем мВыбраннаяФорма Экспорт; // (переменная отчета)

// Хранит признак скопированной формы.
Перем мСкопированаФорма Экспорт; // (переменная отчета)

// Хранит ссылку на документ, хранящий данные отчета
Перем мСохраненныйДок Экспорт; // (переменная отчета)

// Следующие переменные хранят границы
// периода построения отчета
Перем мДатаНачалаПериодаОтчета Экспорт; // (переменная отчета)
//хранит дату конца периода
Перем мДатаКонцаПериодаОтчета  Экспорт; // (переменная отчета)

//хранит версию формы
Перем мВерсияФормы Экспорт; // (переменная отчета)
//хранит полное имя файла
Перем мПолноеИмяФайлаВнешнейОбработки Экспорт; // (переменная отчета)
//хранит флаг запрета на запись
Перем мЗаписьЗапрещена Экспорт; // (переменная отчета)
// хранит интервал автосохранения
Перем мИнтервалАвтосохранения Экспорт; // (переменная отчета)
// хранит таблицу страниц на печать
Перем мТаблицаСтраницНаПечать Экспорт; // (переменная отчета)
//хранит количество страниц
Перем ВсегоСтраниц Экспорт; // (переменная отчета)

// Хранит ФИО исполнителя 
Перем мИсполнитель Экспорт; // (переменная отчета)

// Хранит список структурных единиц, по которым строится отчет
Перем мСписокСтруктурныхЕдиниц Экспорт;  // (переменная отчета)

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция получает основные сведения о выбранной организации
// 
Функция ЗаполнитьСведенияОбОрганизации()Экспорт
	
	Если (Организация  = Неопределено) ИЛИ (Организация = гз_ОбщегоНазначения.мПустоеЗначениеТипа("СправочникСсылка.Организации")) Тогда
		Возврат 0;
	КонецЕсли;
	
	// Составляем список данных, необходимых для вывода в отчетную форму
	Сведения = Новый СписокЗначений;
			
	Сведения.Добавить("", "НаимЮЛПол"); // Полное название организации
			
	Сведения.Добавить("", "КодПоОКПО"); // ОКПО
			
	Сведения.Добавить("", "АдрЮР"); // Юридический адрес
	
	Сведения.Добавить("", "ТелОрганизации"); // Телефон организации
	
	Сведения.Добавить("", "ФИОБухКраткое"); // ФИО гл. бухгалтера
	
	Сведения.Добавить("", "ФИОРукКраткое"); // ФИО руководителя
	
	// Теперь получаем данные из глобальной общей функции
	ОргСведения = гз_ОбщегоНазначения.мПолучитьСведенияОбОрганизации(Организация, ТекущаяДата(), Сведения);
	
	Возврат ОргСведения;
	
КонецФункции // ЗаполнитьСведенияОбОрганизации

// Функция формирует запрос
// 
Функция СформироватьЗапрос(мДатаНачалаПериодаОтчета,мДатаКонцаПериодаОтчета,Состояния)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", мСписокСтруктурныхЕдиниц.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("Исполненные", Исполненные);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоКвартала(мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецКвартала(мДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("Год", НачалоГода(мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("Состояние",Состояния);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.СпособЗакупки,
	|	ДоговорыКонтрагентов.ДатаДоговора,
	|	ДоговорыКонтрагентов.НомерДоговора,
	|	гз_РегистрацияДоговораПоГосударственнымЗакупкам.Номер КАК НомерЗаписиВРеестре,
	|	гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДоговорКонтрагента,
	|	Поставщики.Наименование КАК НаименованиеПоставщика,
	|	Поставщики.РНН КАК РННПоставщика,
	|	Субподрядчики.Наименование КАК НаименованиеСубподрядчика,
	|	Субподрядчики.РНН КАК РННСубподрядчика,
	|	Поставщики.ИдентификационныйКодЛичности КАК ИННПоставщика,
	|	Субподрядчики.ИдентификационныйКодЛичности КАК ИННСубподрядчика,
	|	ВЫБОР
	|		КОГДА Поставщики.Ссылка ЕСТЬ NULL 
	|			ТОГДА """"
	|		КОГДА Поставщики.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Казахстан)
	|				ИЛИ Поставщики.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Товаропроизводитель
	|						ТОГДА 1
	|					ИНАЧЕ 2
	|				КОНЕЦ
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК СтатусПоставщика,
	|	ВЫБОР
	|		КОГДА Субподрядчики.Ссылка ЕСТЬ NULL 
	|			ТОГДА """"
	|		КОГДА Субподрядчики.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Казахстан)
	|				ИЛИ Субподрядчики.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК СтатусСубподрядчика,
	|	гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор
	|ПОМЕСТИТЬ ВТ_СведенияПоДоговорам
	|ИЗ
	|	РегистрСведений.гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам КАК гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гз_РегистрацияДоговораПоГосударственнымЗакупкам КАК гз_РегистрацияДоговораПоГосударственнымЗакупкам
	|		ПО гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор = гз_РегистрацияДоговораПоГосударственнымЗакупкам.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Поставщики
	|		ПО гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Контрагент = Поставщики.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Субподрядчики
	|		ПО гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Субподрядчик = Субподрядчики.Ссылка
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ДатаНачала = НАЧАЛОПЕРИОДА(&Год, ГОД)
	|					И ВЫБОР
	|						КОГДА &Исполненные
	|							ТОГДА гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДатаИсполнения
	|						ИНАЧЕ гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор.Дата
	|					КОНЕЦ < &ДатаНачала
	|				ТОГДА ИСТИНА
	|			КОГДА &ДатаОкончания = КОНЕЦПЕРИОДА(&Год, ГОД)
	|					И ВЫБОР
	|						КОГДА &Исполненные
	|							ТОГДА гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДатаИсполнения
	|						ИНАЧЕ гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор.Дата
	|					КОНЕЦ > &ДатаОкончания
	|				ТОГДА ИСТИНА
	|			КОГДА &Исполненные
	|				ТОГДА гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДатаИсполнения >= &ДатаНачала
	|						И гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДатаИсполнения <= &ДатаОкончания
	|			ИНАЧЕ гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор.Дата >= &ДатаНачала
	|					И гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор.Дата <= &ДатаОкончания
	|		КОНЕЦ
	|	И гз_РегистрацияДоговораПоГосударственнымЗакупкам.Состояние В(&Состояние)
	|	И гз_РегистрацияДоговораПоГосударственнымЗакупкам.Организация В(&Организация)
	|	И гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Год = &Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА (НЕ гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга)
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Наименование
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК НаименованиеТовара,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Наименование
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК НаименованиеУслуги,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Наименование
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК НаименованиеРаботы,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.НаименованиеТовара
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК НаименованиеТовараВРаботе,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА NULL
	|		ИНАЧЕ ВЫБОР
	|				КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Количество = 0
	|					ТОГДА 0
	|				ИНАЧЕ гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма / гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Количество
	|			КОНЕЦ
	|	КОНЕЦ КАК ЦенаЗаЕдиницу,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА NULL
	|		ИНАЧЕ гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СуммаУслуги,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СуммаРаботы,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СуммаТовара,
	|	гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма КАК ОбщаяСумма,
	|	гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА (гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров) * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100 - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ТрудСубподрядчика
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100 - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ТрудСубподрядчика
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СтоимостьТрудаПоставщика,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ТрудСубподрядчика
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СтоимостьТрудаСубподрядчика,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СтоимостьОтечественногоТовара,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.КазахстанскоеСодержаниеТоваровПриВыполненииРабот / 100
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ОбщаяСтоимостьОтечественногоТовара,
	|	гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Производитель,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СтоимостьТовараПоИмпорту,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.КазахстанскоеСодержаниеТоваровПриВыполненииРабот / 100
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ОбщаяСтоимостьТовараПоИмпорту
	|ПОМЕСТИТЬ ВТ_СоставДоговоров
	|ИЗ
	|	ВТ_СведенияПоДоговорам КАК ВТ_СведенияПоДоговорам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.гз_НоменклатураПоДоговорамГосударственныхЗакупок.СрезПоследних КАК гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гз_СвойстваНоменклатуры КАК гз_СвойстваНоменклатуры
	|			ПО гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура = гз_СвойстваНоменклатуры.Номенклатура
	|		ПО ВТ_СведенияПоДоговорам.ДоговорКонтрагента = гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СведенияПоДоговорам.СпособЗакупки КАК СпособЗакупки,
	|	ВТ_СведенияПоДоговорам.ДатаДоговора КАК ДатаДоговора,
	|	ВТ_СведенияПоДоговорам.НомерДоговора КАК НомерДоговора,
	|	ВТ_СведенияПоДоговорам.НомерЗаписиВРеестре КАК НомерЗаписиВРеестре,
	|	ВТ_СведенияПоДоговорам.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_СоставДоговоров.НаименованиеТовара,
	|	ВТ_СоставДоговоров.НаименованиеУслуги,
	|	ВТ_СоставДоговоров.НаименованиеРаботы,
	|	ВТ_СоставДоговоров.НаименованиеТовараВРаботе,
	|	ВТ_СоставДоговоров.ЦенаЗаЕдиницу,
	|	ВТ_СоставДоговоров.Количество,
	|	ВТ_СоставДоговоров.СуммаУслуги КАК СуммаУслуги,
	|	ВТ_СоставДоговоров.СуммаРаботы КАК СуммаРаботы,
	|	ВТ_СоставДоговоров.СуммаТовара КАК СуммаТовара,
	|	ВТ_СоставДоговоров.ОбщаяСумма КАК ОбщаяСумма,
	|	ВТ_СоставДоговоров.ДоговорКонтрагента.Организация КАК Заказчик,
	|	гз_КодыСпособовЗакупки.Код КАК Код,
	|	ВТ_СоставДоговоров.СтоимостьТрудаПоставщика КАК СтоимостьТрудаПоставщика,
	|	ВТ_СоставДоговоров.СтоимостьТрудаСубподрядчика КАК СтоимостьТрудаСубподрядчика,
	|	ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаПоставщика, 0) + ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаСубподрядчика, 0) КАК ОбщаяСтоимостьТруда,
	|	ВТ_СоставДоговоров.СтоимостьОтечественногоТовара КАК СтоимостьОтечественногоТовара,
	|	ВТ_СоставДоговоров.ОбщаяСтоимостьОтечественногоТовара КАК ОбщаяСтоимостьОтечественногоТовара,
	|	ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаПоставщика, 0) + ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаСубподрядчика, 0) + ЕСТЬNULL(ВТ_СоставДоговоров.ОбщаяСтоимостьОтечественногоТовара, 0) КАК КазахстанскоеСодержаниеСумма,
	|	ВЫБОР
	|		КОГДА ВТ_СоставДоговоров.ОбщаяСумма = 0
	|			ТОГДА 0
	|		ИНАЧЕ (ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаПоставщика, 0) + ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаСубподрядчика, 0) + ЕСТЬNULL(ВТ_СоставДоговоров.ОбщаяСтоимостьОтечественногоТовара, 0)) / ВТ_СоставДоговоров.ОбщаяСумма * 100
	|	КОНЕЦ КАК КазахстанскоеСодержаниеПроцент,
	|	ВТ_СведенияПоДоговорам.НаименованиеПоставщика КАК НаименованиеПоставщика,
	|	ВТ_СведенияПоДоговорам.РННПоставщика КАК РННПоставщика,
	|	ВТ_СведенияПоДоговорам.НаименованиеСубподрядчика КАК НаименованиеСубподрядчика,
	|	ВТ_СведенияПоДоговорам.РННСубподрядчика КАК РННСубподрядчика,
	|	Производители.Наименование КАК НаименованиеПроизводителя,
	|	Производители.РНН КАК РННПроизводителя,
	|	ВТ_СоставДоговоров.СтоимостьТовараПоИмпорту КАК СтоимостьТовараПоИмпорту,
	|	ВТ_СоставДоговоров.ОбщаяСтоимостьТовараПоИмпорту КАК ОбщаяСтоимостьТовараПоИмпорту,
	|	ВТ_СведенияПоДоговорам.ИННСубподрядчика КАК ИННСубподрядчика,
	|	ВТ_СведенияПоДоговорам.ИННПоставщика КАК ИННПоставщика,
	|	Производители.ИдентификационныйКодЛичности КАК ИННПроизводителя,
	|	Производители.СтранаРезидентства КАК СтранаПроизводителя,
	|	ВЫБОР
	|		КОГДА Производители.СтранаРезидентства ЕСТЬ NULL 
	|			ТОГДА """"
	|		КОГДА Производители.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Казахстан)
	|				ИЛИ Производители.СтранаРезидентства = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК СтатусПроизводителя,
	|	ВТ_СведенияПоДоговорам.СтатусПоставщика КАК СтатусПоставщика,
	|	ВТ_СведенияПоДоговорам.СтатусСубподрядчика КАК СтатусСубподрядчика,
	|	ВТ_СведенияПоДоговорам.Регистратор КАК Регистратор
	|ИЗ
	|	ВТ_СведенияПоДоговорам КАК ВТ_СведенияПоДоговорам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СоставДоговоров КАК ВТ_СоставДоговоров
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Производители
	|			ПО ВТ_СоставДоговоров.Производитель = Производители.Ссылка
	|		ПО ВТ_СведенияПоДоговорам.ДоговорКонтрагента = ВТ_СоставДоговоров.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гз_КодыСпособовЗакупки КАК гз_КодыСпособовЗакупки
	|		ПО ВТ_СведенияПоДоговорам.СпособЗакупки = гз_КодыСпособовЗакупки.СпособЗакупки
	|ИТОГИ
	|	МАКСИМУМ(ДатаДоговора),
	|	МАКСИМУМ(НомерДоговора),
	|	МАКСИМУМ(НомерЗаписиВРеестре),
	|	СУММА(СуммаУслуги),
	|	СУММА(СуммаРаботы),
	|	СУММА(СуммаТовара),
	|	СУММА(ОбщаяСумма),
	|	МАКСИМУМ(Код),
	|	СУММА(СтоимостьТрудаПоставщика),
	|	СУММА(СтоимостьТрудаСубподрядчика),
	|	СУММА(ОбщаяСтоимостьТруда),
	|	СУММА(СтоимостьОтечественногоТовара),
	|	СУММА(ОбщаяСтоимостьОтечественногоТовара),
	|	СУММА(КазахстанскоеСодержаниеСумма),
	|	ВЫБОР
	|		КОГДА СУММА(ОбщаяСумма) = 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(КазахстанскоеСодержаниеСумма) / СУММА(ОбщаяСумма) * 100
	|	КОНЕЦ КАК КазахстанскоеСодержаниеПроцент,
	|	МАКСИМУМ(НаименованиеПоставщика),
	|	МАКСИМУМ(РННПоставщика),
	|	МАКСИМУМ(НаименованиеСубподрядчика),
	|	МАКСИМУМ(РННСубподрядчика),
	|	СУММА(СтоимостьТовараПоИмпорту),
	|	СУММА(ОбщаяСтоимостьТовараПоИмпорту),
	|	МАКСИМУМ(ИННСубподрядчика),
	|	МАКСИМУМ(ИННПоставщика),
	|	МАКСИМУМ(СтатусПоставщика),
	|	МАКСИМУМ(СтатусСубподрядчика),
	|	МАКСИМУМ(Регистратор)
	|ПО
	|	ОБЩИЕ,
	|	Заказчик,
	|	СпособЗакупки,
	|	ДоговорКонтрагента";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // ЗаполнитьСведенияОбОрганизации

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ 


мСписокСтруктурныхЕдиниц = Новый СписокЗначений;

ОписаниеТиповСтрока15  = гз_ОбщегоНазначения.мПолучитьОписаниеТиповСтроки(15);
ОписаниеТиповСтрока50  = гз_ОбщегоНазначения.мПолучитьОписаниеТиповСтроки(50);

// Таблица значений хранит состав показателей отчета.
// В колонках таблицы хранятся следующие данные:
//    - имя поля табличного документа;
//    - код показатели по составу показателей;
//    - код показателя по форме (имя области табличного документа);
//    - признак многострочности;
//    - тип данных показателя.
//
мТаблицаСоставПоказателей    = Новый ТаблицаЗначений;
мТаблицаСоставПоказателей.Колонки.Добавить("ИмяПоляТаблДокумента",    ОписаниеТиповСтрока50);
мТаблицаСоставПоказателей.Колонки.Добавить("КодПоказателяПоСоставу",  ОписаниеТиповСтрока50);
мТаблицаСоставПоказателей.Колонки.Добавить("КодПоказателяПоФорме",    ОписаниеТиповСтрока50);
мТаблицаСоставПоказателей.Колонки.Добавить("ПризнакМногострочности",    ОписаниеТиповСтрока15);
мТаблицаСоставПоказателей.Колонки.Добавить("ТипДанныхПоказателя",     ОписаниеТиповСтрока15);
мТаблицаСоставПоказателей.Колонки.Добавить("КодПоказателяПоСтруктуре",ОписаниеТиповСтрока50);

мСтруктураВариантыЗаполнения = Новый Структура;

мТаблицаСтраницНаПечать = Новый ТаблицаЗначений;
мТаблицаСтраницНаПечать.Колонки.Добавить("ПолеТабличногоДокумента");
мТаблицаСтраницНаПечать.Колонки.Добавить("ИмяЛиста");
мТаблицаСтраницНаПечать.Колонки.Добавить("ИмяЛистаДляЗаписи");

Если Не гз_ОбщегоНазначения.АвтономныйРежимРаботы() Тогда 
	// определим ФИО исполнителя
	Запрос =  Новый Запрос;
	Запрос.УстановитьПараметр("Исполнитель", глТекущийПользователь.ФизЛицо);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ФИОФИзЛиц.Фамилия,
	|	ФИОФИзЛиц.Имя,
	|	ФИОФИзЛиц.Отчество
	|ИЗ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних(, ФизЛицо = &Исполнитель) КАК ФИОФизЛиц
	|";

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		мИсполнитель = Выборка.Фамилия + ?(Выборка.Имя <> "", " " + Лев(Выборка.Имя, 1) + ".", "") + ?(Выборка.Отчество <> "", " " + Лев(Выборка.Отчество, 1) + ".", "");
	Иначе
		Если глТекущийПользователь.ФизЛицо.Пустая() Тогда
			мИсполнитель = глТекущийПользователь.Наименование;
		Иначе
			мИсполнитель = глТекущийПользователь.ФизЛицо.Наименование;
		КонецЕсли;
	КонецЕсли;
КонецЕсли;

#КонецЕсли
