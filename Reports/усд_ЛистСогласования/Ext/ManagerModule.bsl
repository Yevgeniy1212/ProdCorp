////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС


// Получить параметры исполнения отчета
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Возврат Новый Структура("ИспользоватьПередКомпоновкойМакета,
							|ИспользоватьПослеКомпоновкиМакета,
							|ИспользоватьПослеВыводаРезультата,
							|ИспользоватьДанныеРасшифровки",
							Истина, Ложь, Истина, Ложь);
							
КонецФункции

//получить текст заголовка
//
Функция ПолучитьТекстЗаголовка(ПараметрыОтчета, ОрганизацияВНачале = Истина) Экспорт
	
	ЗаголовокОтчета = Метаданные.Отчеты.усд_ЛистСогласования.Синоним;
	Возврат общ_ОтчетыВызовСервера.ПолучитьТекстЗаголовка(ПараметрыОтчета, ОрганизацияВНачале, ЗаголовокОтчета);
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	Если Метаданные.Справочники.Пользователи.Реквизиты.Найти("ФизЛицо")=Неопределено Тогда
		Схема.НаборыДанных[0].Запрос = СтрЗаменить(Схема.НаборыДанных[0].Запрос,"Пользователи.ФизЛицо","Пользователи.ФизическоеЛицо");
	КонецЕсли;
	 общ_ОтчетыВызовСервера.ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек);
	 
КонецПроцедуры

// После вывода результата
//
Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	Макет 			= Отчеты.усд_ЛистСогласования.ПолучитьМакет("Макет");
	Область 		= Макет.ПолучитьОбласть("Заголовок");
	ВысотаОбласти 	= Макет.ВысотаТаблицы+1;
	ШиринаОбласти 	= Макет.ШиринаТаблицы;

	ЗаполнитьПараметрыОбласти(Область,ПараметрыОтчета.СтруктураПараметровОтчетаДляФормирования.Документ);
	НовыйТД = Новый ТабличныйДокумент;
	НовыйТД.Вывести(Область);
	
	ОбластьИсточник = НовыйТД.Область(1,,ВысотаОбласти);
	ОбластьПриемник = Результат.Область(1,,ВысотаОбласти);

	Результат.ВставитьОбласть(ОбластьИсточник,ОбластьПриемник,ТипСмещенияТабличногоДокумента.ПоВертикали);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыОбласти(Область,Документ) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Период,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Документ,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.ОтветственноеЛицо,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Этап,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Маршрут,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.ВидДокументов,
		|	NULL КАК ВозвращенНаЭтап,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Статус,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Приоритет,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.ПричинаВозврата
		|ИЗ
		|	РегистрСведений.усд_АктивныеЭтапыРассмотренияДокументов.СрезПоследних(, Документ = &Документ) КАК усд_ДвижениеДокументовПоМаршрутамСрезПоследних
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Период,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Документ,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.ОтветственноеЛицо,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Этап,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Маршрут,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.ВидДокументов,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.ВозвращенНаЭтап,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Статус,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.Приоритет,
		|	усд_ДвижениеДокументовПоМаршрутамСрезПоследних.ПричинаВозврата
		|ИЗ
		|	РегистрСведений.усд_ДвижениеДокументовПоМаршрутам.СрезПоследних(, Документ = &Документ) КАК усд_ДвижениеДокументовПоМаршрутамСрезПоследних";

	Запрос.УстановитьПараметр("Документ", Документ);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Область.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
	Иначе
		Область.Параметры.Заполнить(Новый Структура("Документ,Маршрут,Статус",Документ,"",""));
	КонецЕсли;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
