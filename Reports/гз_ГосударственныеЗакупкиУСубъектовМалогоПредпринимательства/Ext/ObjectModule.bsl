#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит таблицу значений - состав показателей отчета.
Перем мТаблицаСоставПоказателей Экспорт; // (отчет)

// Хранит структуру - состав показателей отчета,
// значение которых автоматически заполняется по учетным данным.
Перем мСтруктураВариантыЗаполнения Экспорт; // (отчет)

// Хранит имя выбранной формы отчета
Перем мВыбраннаяФорма Экспорт; // (отчет)

// Хранит признак скопированной формы.
Перем мСкопированаФорма Экспорт; // (отчет)

// Хранит ссылку на документ, хранящий данные отчета
Перем мСохраненныйДок Экспорт;  // документ

// Следующие переменные хранят границы
// периода построения отчета
Перем мДатаНачалаПериодаОтчета Экспорт; // (отчет)
//хранит дату конца периода
Перем мДатаКонцаПериодаОтчета  Экспорт; // (отчет)

//хранит версию формы
Перем мВерсияФормы Экспорт; // (отчет)
// хранит полное имя файла внешней обработки
Перем мПолноеИмяФайлаВнешнейОбработки Экспорт; // (отчет)
// хранит флаг запрета записи
Перем мЗаписьЗапрещена Экспорт; // (отчет)
// хранит интервал автосохранения
Перем мИнтервалАвтосохранения Экспорт; // (отчет)
//хранит таблицу страниц на печать
Перем мТаблицаСтраницНаПечать Экспорт; // (отчет)
//хранит количество страниц
Перем ВсегоСтраниц Экспорт; // (отчет)

// Хранит ФИО исполнителя 
Перем мИсполнитель Экспорт; // (отчет)

// Хранит список структурных единиц, по которым строится отчет
Перем мСписокСтруктурныхЕдиниц Экспорт;  // (отчет)

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция получает основные сведения о выбранной организации
// 
Функция ЗаполнитьСведенияОбОрганизации()Экспорт
	
	Если (Организация  = Неопределено) ИЛИ (Организация = гз_ОбщегоНазначения.мПустоеЗначениеТипа("СправочникСсылка.Организации")) Тогда
		Возврат 0;
	КонецЕсли;
	
	// Составляем список данных, необходимых для вывода в отчетную форму
	Сведения = Новый СписокЗначений;
			
	Сведения.Добавить("", "НаимЮЛПол"); // Полное название организации
			
	Сведения.Добавить("", "КодПоОКПО"); // ОКПО
			
	Сведения.Добавить("", "АдрЮР"); // Юридический адрес
	
	Сведения.Добавить("", "ТелОрганизации"); // Телефон организации
	
	Сведения.Добавить("", "ФИОБухКраткое"); // ФИО гл. бухгалтера
	
	Сведения.Добавить("", "ФИОРукКраткое"); // ФИО руководителя
	
	// Теперь получаем данные из глобальной общей функции
	ОргСведения = гз_ОбщегоНазначения.мПолучитьСведенияОбОрганизации(Организация, ТекущаяДата(), Сведения);
	
	Возврат ОргСведения;
	
КонецФункции // ЗаполнитьСведенияОбОрганизации

// Функция формирует запрос
// 
Функция СформироватьЗапрос(мДатаНачалаПериодаОтчета,мДатаКонцаПериодаОтчета,Состояния)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", мСписокСтруктурныхЕдиниц.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("Исполненные", Исполненные);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецКвартала(мДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("Год", НачалоГода(мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("Состояние",Состояния);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДоговорКонтрагента,
	|	гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор,
	|	гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.СубъектМалогоПредпринимательства
	|ПОМЕСТИТЬ ВТ_СведенияПоДоговорам
	|ИЗ
	|	РегистрСведений.гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам КАК гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гз_РегистрацияДоговораПоГосударственнымЗакупкам КАК гз_РегистрацияДоговораПоГосударственнымЗакупкам
	|		ПО гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор = гз_РегистрацияДоговораПоГосударственнымЗакупкам.Ссылка
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ДатаОкончания = КОНЕЦПЕРИОДА(&Год, ГОД)
	|				ТОГДА ИСТИНА
	|			КОГДА &Исполненные
	|				ТОГДА гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДатаИсполнения <= &ДатаОкончания
	|			ИНАЧЕ гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Регистратор.Дата <= &ДатаОкончания
	|		КОНЕЦ
	|	И гз_РегистрацияДоговораПоГосударственнымЗакупкам.Состояние В(&Состояние)
	|	И гз_РегистрацияДоговораПоГосударственнымЗакупкам.Организация В(&Организация)
	|	И гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Год = &Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СуммаУслуги,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СуммаРаботы,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СуммаТовара,
	|	гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма КАК ОбщаяСумма,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА (гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров) * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100 - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ТрудСубподрядчика
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100 - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ТрудСубподрядчика
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СтоимостьТрудаПоставщика,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ТрудСубподрядчика
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СтоимостьТрудаСубподрядчика,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СтоимостьОтечественногоТовара,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.КазахстанскоеСодержаниеТоваровПриВыполненииРабот / 100
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ОбщаяСтоимостьОтечественногоТовара,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга = ЛОЖЬ
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Сумма * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ПроцентКазахстанскогоСодержания / 100
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров - гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.СуммаТоваров * гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.КазахстанскоеСодержаниеТоваровПриВыполненииРабот / 100
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ОбщаяСтоимостьТовараПоИмпорту,
	|	ВЫБОР
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ""Работа""
	|		КОГДА гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура.Услуга
	|				И ЕСТЬNULL(гз_СвойстваНоменклатуры.Работа, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ""Услуга""
	|		ИНАЧЕ ""Товар""
	|	КОНЕЦ КАК Вид,
	|	гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ДоговорКонтрагента
	|ПОМЕСТИТЬ ВТ_СоставДоговоров
	|ИЗ
	|	ВТ_СведенияПоДоговорам КАК ВТ_СведенияПоДоговорам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.гз_НоменклатураПоДоговорамГосударственныхЗакупок.СрезПоследних КАК гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гз_СвойстваНоменклатуры КАК гз_СвойстваНоменклатуры
	|			ПО гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.Номенклатура = гз_СвойстваНоменклатуры.Номенклатура
	|		ПО ВТ_СведенияПоДоговорам.ДоговорКонтрагента = гз_НоменклатураПоДоговорамГосударственныхЗакупокСрезПоследних.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СоставДоговоров.СуммаУслуги КАК Услуги,
	|	ВТ_СоставДоговоров.СуммаРаботы КАК Работы,
	|	ВТ_СоставДоговоров.СуммаТовара КАК Товары,
	|	ВТ_СоставДоговоров.ОбщаяСумма КАК Всего,
	|	ВТ_СоставДоговоров.СтоимостьТрудаПоставщика КАК СтоимостьТрудаПоставщика,
	|	ВТ_СоставДоговоров.СтоимостьТрудаСубподрядчика КАК СтоимостьТрудаСубподрядчика,
	|	ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаПоставщика, 0) + ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаСубподрядчика, 0) КАК ОбщаяСтоимостьТруда,
	|	ВТ_СоставДоговоров.ОбщаяСтоимостьТовараПоИмпорту КАК ТоварыИмпорт,
	|	ВТ_СведенияПоДоговорам.Регистратор КАК Регистратор,
	|	ВТ_СоставДоговоров.Вид,
	|	ВЫБОР
	|		КОГДА ВТ_СоставДоговоров.Вид = ""Работа""
	|			ТОГДА ВТ_СоставДоговоров.СуммаРаботы - ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаПоставщика, 0) + ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаСубподрядчика, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РаботыИмпорт,
	|	ВЫБОР
	|		КОГДА ВТ_СоставДоговоров.Вид = ""Услуга""
	|			ТОГДА ВТ_СоставДоговоров.СуммаУслуги - ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаПоставщика, 0) + ЕСТЬNULL(ВТ_СоставДоговоров.СтоимостьТрудаСубподрядчика, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК УслугиИмпорт,
	|	ВТ_СведенияПоДоговорам.СубъектМалогоПредпринимательства
	|ПОМЕСТИТЬ ВТ_ПредварительныеДанные
	|ИЗ
	|	ВТ_СведенияПоДоговорам КАК ВТ_СведенияПоДоговорам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СоставДоговоров КАК ВТ_СоставДоговоров
	|		ПО ВТ_СведенияПоДоговорам.ДоговорКонтрагента = ВТ_СоставДоговоров.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПредварительныеДанные.Услуги КАК Услуги,
	|	ВТ_ПредварительныеДанные.Работы КАК Работы,
	|	ВТ_ПредварительныеДанные.Товары КАК Товары,
	|	ВТ_ПредварительныеДанные.Всего КАК Всего,
	|	ВТ_ПредварительныеДанные.ТоварыИмпорт КАК ТоварыИмпорт,
	|	ВТ_ПредварительныеДанные.РаботыИмпорт КАК РаботыИмпорт,
	|	ВТ_ПредварительныеДанные.УслугиИмпорт КАК УслугиИмпорт,
	|	ВТ_ПредварительныеДанные.Регистратор КАК Регистратор,
	|	ВТ_ПредварительныеДанные.СубъектМалогоПредпринимательства КАК МП
	|ИЗ
	|	ВТ_ПредварительныеДанные КАК ВТ_ПредварительныеДанные
	|ИТОГИ
	|	СУММА(Услуги),
	|	СУММА(Работы),
	|	СУММА(Товары),
	|	СУММА(Всего),
	|	СУММА(ТоварыИмпорт),
	|	СУММА(РаботыИмпорт),
	|	СУММА(УслугиИмпорт),
	|	МАКСИМУМ(МП)
	|ПО
	|	ОБЩИЕ,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПредварительныеДанные.Услуги КАК Услуги,
	|	ВТ_ПредварительныеДанные.Работы КАК Работы,
	|	ВТ_ПредварительныеДанные.Товары КАК Товары,
	|	ВТ_ПредварительныеДанные.Всего КАК Всего,
	|	ВТ_ПредварительныеДанные.ТоварыИмпорт КАК ТоварыИмпорт,
	|	ВТ_ПредварительныеДанные.РаботыИмпорт КАК РаботыИмпорт,
	|	ВТ_ПредварительныеДанные.УслугиИмпорт КАК УслугиИмпорт,
	|	ВТ_ПредварительныеДанные.Регистратор КАК Регистратор,
	|	ВТ_ПредварительныеДанные.СубъектМалогоПредпринимательства КАК МП
	|ИЗ
	|	ВТ_ПредварительныеДанные КАК ВТ_ПредварительныеДанные
	|ГДЕ
	|	ВТ_ПредварительныеДанные.СубъектМалогоПредпринимательства
	|ИТОГИ
	|	СУММА(Услуги),
	|	СУММА(Работы),
	|	СУММА(Товары),
	|	СУММА(Всего),
	|	СУММА(ТоварыИмпорт),
	|	СУММА(РаботыИмпорт),
	|	СУММА(УслугиИмпорт),
	|	МАКСИМУМ(МП)
	|ПО
	|	ОБЩИЕ,
	|	Регистратор";
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции // ЗаполнитьСведенияОбОрганизации

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ 


мСписокСтруктурныхЕдиниц = Новый СписокЗначений;

ОписаниеТиповСтрока15  = гз_ОбщегоНазначения.мПолучитьОписаниеТиповСтроки(15);
ОписаниеТиповСтрока50  = гз_ОбщегоНазначения.мПолучитьОписаниеТиповСтроки(50);

// Таблица значений хранит состав показателей отчета.
// В колонках таблицы хранятся следующие данные:
//    - имя поля табличного документа;
//    - код показатели по составу показателей;
//    - код показателя по форме (имя области табличного документа);
//    - признак многострочности;
//    - тип данных показателя.
//
мТаблицаСоставПоказателей    = Новый ТаблицаЗначений;
мТаблицаСоставПоказателей.Колонки.Добавить("ИмяПоляТаблДокумента",    ОписаниеТиповСтрока50);
мТаблицаСоставПоказателей.Колонки.Добавить("КодПоказателяПоСоставу",  ОписаниеТиповСтрока50);
мТаблицаСоставПоказателей.Колонки.Добавить("КодПоказателяПоФорме",    ОписаниеТиповСтрока50);
мТаблицаСоставПоказателей.Колонки.Добавить("ПризнакМногострочности",    ОписаниеТиповСтрока15);
мТаблицаСоставПоказателей.Колонки.Добавить("ТипДанныхПоказателя",     ОписаниеТиповСтрока15);
мТаблицаСоставПоказателей.Колонки.Добавить("КодПоказателяПоСтруктуре",ОписаниеТиповСтрока50);

мСтруктураВариантыЗаполнения = Новый Структура;

мТаблицаСтраницНаПечать = Новый ТаблицаЗначений;
мТаблицаСтраницНаПечать.Колонки.Добавить("ПолеТабличногоДокумента");
мТаблицаСтраницНаПечать.Колонки.Добавить("ИмяЛиста");
мТаблицаСтраницНаПечать.Колонки.Добавить("ИмяЛистаДляЗаписи");

Если Не гз_ОбщегоНазначения.АвтономныйРежимРаботы() Тогда 
	// определим ФИО исполнителя
	Запрос =  Новый Запрос;
	Запрос.УстановитьПараметр("Исполнитель", глТекущийПользователь.ФизЛицо);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ФИОФИзЛиц.Фамилия,
	|	ФИОФИзЛиц.Имя,
	|	ФИОФИзЛиц.Отчество
	|Из
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних(, ФизЛицо = &Исполнитель) КАК ФИОФизЛиц
	|";

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		мИсполнитель = Выборка.Фамилия + ?(Выборка.Имя <> "", " " + Лев(Выборка.Имя, 1) + ".", "") + ?(Выборка.Отчество <> "", " " + Лев(Выборка.Отчество, 1) + ".", "");
	Иначе
		Если глТекущийПользователь.ФизЛицо.Пустая() Тогда
			мИсполнитель = глТекущийПользователь.Наименование;
		Иначе
			мИсполнитель = глТекущийПользователь.ФизЛицо.Наименование;
		КонецЕсли;
	КонецЕсли;
	
КонецЕсли;

#КонецЕсли
