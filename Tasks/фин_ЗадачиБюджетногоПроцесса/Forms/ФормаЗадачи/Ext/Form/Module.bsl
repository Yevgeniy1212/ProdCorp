&НаКлиенте
Перем мЧас;
&НаКлиенте
Перем мМинута;

// Процедура сохраняет значения переменных мЧас и мМинута
//
&НаКлиенте
Процедура ПрисвоитьЧасМинутуИзЭлемента(Элемент)
	
	мЧас    = Час(Объект[Элемент.Имя]);
	мМинута = Минута(Объект[Элемент.Имя]);
	
КонецПроцедуры

// Процедура присваивает дату с учетом сохраненных значений мЧас и мМинута
//
&НаКлиенте
Процедура ПередатьДатуВЭлемент(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	Объект[Элемент.Имя] = НачалоДня(ВыбранноеЗначение) + мЧас*60*60 + мМинута*60;
	мЧас             = 0;
	мМинута          = 0;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	Элементы.Наименование.ТолькоПросмотр = Объект.Выполнена ИЛИ ((НЕ Объект.Выполнена) И (Объект.Инициатор <> ПараметрыСеанса.ТекущийПользователь));
	Элементы.Оповещение.Доступность      = НЕ Объект.Выполнена;
	Элементы.СрокОповещения.Доступность  = Объект.Оповещение;

КонецПроцедуры

&НаКлиенте
Процедура ВыполненаПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура СрокИсполненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПрисвоитьЧасМинутуИзЭлемента(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СрокИсполненияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОЖЬ;
	
	СписокВремен = Новый СписокЗначений;
	
	Если НЕ ЗначениеЗаполнено(Объект[Элемент.Имя]) Тогда
		Объект[Элемент.Имя] = НачалоМинуты(ТекущаяДата());
	КонецЕсли;
	
	Если ДеньГода(Объект[Элемент.Имя]) = ДеньГода(ТекущаяДата()) Тогда
		
		Если Минута(ТекущаяДата()) > 30 Тогда
			ВремяСписка = НачалоМинуты(КонецЧаса(ТекущаяДата()) + 60);
		Иначе
			ВремяСписка = НачалоМинуты(КонецЧаса(ТекущаяДата()) - 29*60);
		КонецЕсли;
		
	Иначе
		ВремяСписка = НачалоДня(Объект[Элемент.Имя]);
	КонецЕсли;
		
	Пока НачалоЧаса(ВремяСписка) < КонецДня(Объект[Элемент.Имя]) Цикл
		
		СписокВремен.Добавить(ВремяСписка, Формат(ВремяСписка,"ДФ='дд.ММ.гггг ЧЧ:мм'"));
		ВремяСписка = ВремяСписка + 30*60;
		
	КонецЦикла;
	ОписаниеВыбораИзСписка = Новый ОписаниеОповещения("СрокИсполненияОбработкаВыбораИзСписка",ЭтотОбъект,Элемент.Имя);
	ПоказатьВыборИзСписка(ОписаниеВыбораИзСписка,СписокВремен, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокИсполненияОбработкаВыбораИзСписка(ВыбранноеВремя, ЭлементИмя) Экспорт
	Если ВыбранноеВремя <> Неопределено Тогда
		Объект[ЭлементИмя] = ВыбранноеВремя.Значение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СрокИсполненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПередатьДатуВЭлемент(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	ОбновитьОтображениеДанных();
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		Если НЕ ЗначениеЗаполнено(Объект.Объект) Тогда
			Объект.Объект = Документы.фин_Бюджет.ПустаяСсылка();
		КонецЕсли;
		Объект.Инициатор   = ПараметрыСеанса.ТекущийПользователь;
		Объект.Исполнитель = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	мВыполнена = Объект.Выполнена;
	НадписьЗадача = "Задача";
	НадписьКлассификацияЗадачи = "Классификация задачи";
	НадписьОписание = "Описание";
	УстановитьВидимость();
	Элементы.Выполнена.Видимость = Объект.Выполнена;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если мВыполнена<>ТекущийОбъект.Выполнена И ТекущийОбъект.Выполнена И ТекущийОбъект.Исполнитель.Пустая() Тогда
		ТекущийОбъект.Исполнитель = фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ТекущийПользователь");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если мВыполнена<>ТекущийОбъект.Выполнена И ТекущийОбъект.Выполнена И ТекущийОбъект.Исполнитель.Пустая() Тогда
		ТекущийОбъект.Исполнитель = фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ТекущийПользователь");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ((мВыполнена<>Объект.Выполнена И Объект.Выполнена) ИЛИ ПараметрыЗаписи.Свойство("ВыполнитьЗадачу")) И Объект.Исполнитель.Пустая() Тогда
		Объект.Исполнитель = фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ТекущийПользователь");
	КонецЕсли;
КонецПроцедуры


