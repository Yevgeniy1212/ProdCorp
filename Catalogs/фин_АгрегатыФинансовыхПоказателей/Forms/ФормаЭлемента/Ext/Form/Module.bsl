
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НадписьВходящиеПоказатели = "Входящие показатели";
	НадписьПризнакиУчета = "Признаки учета";
	фин_РаботаСДополнительнымиРазрезамиБюджетирования.ЗаполнитьСписокРазрезовУчета(СписокВыбораРазрезыУчета);
	фин_РаботаСДополнительнымиРазрезамиБюджетирования.НастроитьПредставлениеРазрезов(ЭтотОбъект);
	УправлениеДоступностьюРеквизитов();
КонецПроцедуры

&НаКлиенте
Процедура РазрезыУчетаИзмерениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = СписокВыбораРазрезыУчета;
КонецПроцедуры

&НаКлиенте
Процедура СоставФинансовыйПоказательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Объект.УчетПоКоличеству ИЛИ Объект.УчетПоСумме Тогда
		СтандартнаяОбработка = Ложь;
		СтруктураОтбор = Новый Структура;
		Если Объект.УчетПоКоличеству Тогда
			СтруктураОтбор.Вставить("УчетПоКоличеству",Истина);
		КонецЕсли;
		Если Объект.УчетПоСумме Тогда
			СтруктураОтбор.Вставить("УчетПоСумме",Истина);
		КонецЕсли;
		ОткрытьФорму("Справочник.фин_ФинансовыеПоказатели.ФормаВыбора",Новый Структура("Отбор",СтруктураОтбор),Элемент,УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоставФинансовыйПоказательПриИзменении(Элемент)
	Если НЕ ПроверитьПоказатель(Элементы.Состав.ТекущиеДанные.ФинансовыйПоказатель) Тогда
		Элементы.Состав.ТекущиеДанные.ФинансовыйПоказатель = ПредопределенноеЗначение("Справочник.фин_ФинансовыеПоказатели.ПустаяСсылка");
		ПоказатьПредупреждение(,"Выбранный финансовый показатель не может быть использован в текущем агрегате!");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПроверитьПоказатель(ФинансовыйПоказатель,Оповещать = Истина)
	Если Объект.УчетПоКоличеству И НЕ ФинансовыйПоказатель.УчетПоКоличеству Тогда
		Если Оповещать Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("У финансового показателя "+Строка(ФинансовыйПоказатель)+" не включен учет по количеству!");
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	Если Объект.УчетПоСумме И НЕ ФинансовыйПоказатель.УчетПоСумме Тогда
		Если Оповещать Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("У финансового показателя "+Строка(ФинансовыйПоказатель)+" не включен учет по сумме!");
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	Для Каждого СтрокаРазрез Из Объект.РазрезыУчета Цикл
		Если ФинансовыйПоказатель.РазрезыУчета.НайтиСтроки(Новый Структура("Измерение",СтрокаРазрез.Измерение)).Количество()=0 Тогда
			Если Оповещать Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("У финансового показателя "+Строка(ФинансовыйПоказатель)+" не включен разрез "+СписокВыбораРазрезыУчета.НайтиПоЗначению(СтрокаРазрез.Измерение).Представление+"!");
			КонецЕсли;
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура СоставОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение)=Тип("СправочникСсылка.фин_ФинансовыеПоказатели") Тогда
		ОбработатьВыбор(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыбор(Значение)
	Если Значение.ЭтоГруппа Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	фин_ФинансовыеПоказатели.Ссылка
			|ИЗ
			|	Справочник.фин_ФинансовыеПоказатели КАК фин_ФинансовыеПоказатели
			|ГДЕ
			|	НЕ фин_ФинансовыеПоказатели.ЭтоГруппа
			|	И НЕ фин_ФинансовыеПоказатели.ПометкаУдаления
			|	И фин_ФинансовыеПоказатели.Ссылка В ИЕРАРХИИ(&Группа)";

		Запрос.УстановитьПараметр("Группа", Значение);

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ПроверитьПоказатель(ВыборкаДетальныеЗаписи.Ссылка,Ложь) И Объект.Состав.НайтиСтроки(Новый Структура("ФинансовыйПоказатель",ВыборкаДетальныеЗаписи.Ссылка)).Количество()=0 Тогда
				НС = Объект.Состав.Добавить();
				НС.ФинансовыйПоказатель = ВыборкаДетальныеЗаписи.Ссылка;
			КонецЕсли;
		КонецЦикла;

	ИначеЕсли ПроверитьПоказатель(Значение) И Объект.Состав.НайтиСтроки(Новый Структура("ФинансовыйПоказатель",Значение)).Количество()=0 Тогда
		НС = Объект.Состав.Добавить();
		НС.ФинансовыйПоказатель = Значение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	Если Объект.УчетПоКоличеству ИЛИ Объект.УчетПоСумме Тогда
		СтандартнаяОбработка = Ложь;
		СтруктураОтбор = Новый Структура;
		Если Объект.УчетПоКоличеству Тогда
			СтруктураОтбор.Вставить("УчетПоКоличеству",Истина);
		КонецЕсли;
		Если Объект.УчетПоСумме Тогда
			СтруктураОтбор.Вставить("УчетПоСумме",Истина);
		КонецЕсли;
	Иначе
		СтруктураОтбор = Неопределено;
	КонецЕсли;
	ОткрытьФорму("Справочник.фин_ФинансовыеПоказатели.ФормаВыбора",Новый Структура("Отбор,ЗакрыватьПриВыборе,ЗакрыватьПриЗакрытииВладельца,ВыборГруппИЭлементов",СтруктураОтбор,Ложь,Истина,ИспользованиеГруппИЭлементов.ГруппыИЭлементы),Элементы.Состав,УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура УчетПоСуммеПриИзменении(Элемент)
	ПересмотретьПоказатели();
КонецПроцедуры

&НаКлиенте
Процедура РазрезыУчетаИзмерениеПриИзменении(Элемент)
	ПересмотретьПоказатели();
КонецПроцедуры

&НаСервере
Процедура ПересмотретьПоказатели()
	МассивУдалить = Новый Массив;
	Для Каждого СтрокаТЧ Из Объект.Состав Цикл
		Если НЕ ПроверитьПоказатель(СтрокаТЧ.ФинансовыйПоказатель) Тогда
			МассивУдалить.Добавить(СтрокаТЧ);
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаУдалить Из МассивУдалить Цикл
		Объект.Состав.Удалить(СтрокаУдалить);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	Объект.Состав.Очистить();
КонецПроцедуры
 
//Процедура УправлениеДоступностьюРеквизитов
//
&НаСервере
Процедура УправлениеДоступностьюРеквизитов()
	Элементы.ИспользуемыйНоменклатурныйПеречень.Видимость = (Объект.РазрезыУчета.НайтиСтроки(Новый Структура("Измерение",ПредопределенноеЗначение("Перечисление.фин_ФактическиеПоказателиБюджетирования.Номенклатура"))).Количество()<>0);
КонецПроцедуры // УправлениеДоступностьюРеквизитов()

&НаКлиенте
Процедура РазрезыУчетаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	УправлениеДоступностьюРеквизитов();
КонецПроцедуры
