////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУДЛЯ

Перем СтруктураРеквизитов Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ОБЪЕКТА

Процедура ПередЗаписью(Отказ)
	
	#Если Клиент Тогда
	Сообщение = "";
	
	ПроверитьОбязательныеРеквизиты(ЭтотОбъект, Отказ, Сообщение);
	
	Если Отказ Тогда
		Сообщить("Настройка """ + Наименование + """ не может быть записана:" + Сообщение, СтатусСообщения.Важное);
	КонецЕсли;
	
	// Если все же вариант отчета удаляем, нужно удалить все настройки пользователей панели пользователя
	Если ПометкаУдаления И Не Ссылка.ПометкаУдаления Тогда
		Выборка = ПолучитьВыборкуПодчиненныхСохраненныхНастроек();
		Пока Выборка.Следующий() Цикл
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.УстановитьПометкуУдаления(Истина);
			Объект.Записать();
		КонецЦикла;
	КонецЕсли;
		
	#КонецЕсли

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ОБЪЕКТА

// Функция определяет набор реквизитов, которые должны быть заполнены обязательно.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Структура, в качестве ключей элементов содержит имена реквизитов.
//
Функция ОбязательныеРеквизиты() Экспорт

	СписокИмен = "Наименование, ";
	
	Если НЕ ЭтоГруппа Тогда
		СписокИмен = СписокИмен + "НастраиваемыйОбъект, "
	КонецЕсли;

	Возврат Новый Структура(СписокИмен);

КонецФункции

#Если Клиент Тогда
// Проверяет обязательные реквизиты объекта.
// У объекта должен быть определен экспортируемый метод ОбязательныеРеквизиты().
// У объекта должена быть определена экспортируемая переменная СтруктураРеквизитов.
//
// Параметры:
//  Объект    - объект, реквизиты которого проверяются.
//  Отказ     - Истина, если проверка выявила проблемы. В противном случае не меняется.
//              В параметр удобно передавать параметр Отказ обработчика ПередЗаписью.
//  Сообщение - строка, в которую будут дописаны "замечания", выявленные при проверке.
//              Сформированная таким образом строка может использоваться в вызывающей
//              процедуре в качестве сообщения или предупреждения.
//
Процедура ПроверитьОбязательныеРеквизиты(Объект, Отказ, Сообщение = "", ВлияющиеРеквизиты = "") Экспорт

	Для каждого ЭлементСтруктуры Из Объект.ОбязательныеРеквизиты() Цикл
		ИмяРеквизита = ЭлементСтруктуры.Ключ;
			Если ПустоеЗначениеЗначения(Объект[ИмяРеквизита]) Тогда
				ОтказПриИзмененииРеквизитов(Объект, ИмяРеквизита + ", " + ВлияющиеРеквизиты, Отказ);
				Сообщение = Сообщение + Символы.ПС + "- Не указано значение реквизита """ + Объект.СтруктураРеквизитов[ИмяРеквизита] + """.";
			КонецЕсли;
	КонецЦикла;

КонецПроцедуры
#КонецЕсли

// Функция проверяет, переданный параметр, пустой он, или нет.
//
// Параметры:
//  Параметр - параметр, значение которого проверяется.
//
// Возвращаемое значение:
//  Истина - параметр пустой;
//  Ложь   - параметр не пустой.
//
Функция ПустоеЗначениеЗначения(Значение) Экспорт

	МассивТипов    = Новый Массив(1);
	МассивТипов[0] = ТипЗнч(Значение);

	ОписаниеТипов  = Новый ОписаниеТипов(МассивТипов,,,);

	Если Значение = ОписаниеТипов.ПривестиЗначение() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции


Процедура ПередУдалением(Отказ)
	
	#Если Клиент Тогда	
		
	Если ЭтотОбъект.ТипНастройки <> Перечисления.ТипыНастроек.НастройкиОтчета Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.Метаданные().Реквизиты.Найти("ОтрицательноеКрасным") <> Неопределено тогда
		
		// Проверим возможность удаления варианта пользователем
		СписокДоступныхВариантов = Справочники.общ_СохраненныеНастройки.ПолучитьСписокДоступныхВариантов(ЭтотОбъект.НастраиваемыйОбъект,, Истина);
		ДоступныйВариант = СписокДоступныхВариантов.НайтиПоЗначению(ЭтотОбъект.Ссылка);
		Если ДоступныйВариант = Неопределено ИЛИ Не ДоступныйВариант.Пометка Тогда
			Отказ = Истина;
			куфиб_ОбщегоНазначения.СообщитьОбОшибке("Текущему пользователю запрещено удалять вариант отчета: " + ЭтотОбъект);
		КонецЕсли;
		
		// Если все же вариант отчета удаляем, нужно удалить все настройки пользователей панели пользователя
		Выборка = ПолучитьВыборкуПодчиненныхСохраненныхНастроек();
		Пока Выборка.Следующий() Цикл
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.Удалить();
		КонецЦикла;
	КонецЕсли;
	#КонецЕсли

КонецПроцедуры
// Проверяет, были ли изменены указанные реквизиты объекта.
// Предназначена для установки Отказ в проверках перед записью объекта.
//
// Параметры:
//  Объект    - проверяемый объект.
//  Реквизиты - строка - список проверяемых реквизитов через запятую.
//  Отказ     - устанавливается в Истина, если реквизит был изменен.
//
Процедура ОтказПриИзмененииРеквизитов(Объект, Знач Реквизиты, Отказ) Экспорт
	
	Если ПустаяСтрока(Реквизиты) Тогда
		Возврат;
		
	ИначеЕсли Объект.Ссылка.Пустая() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура(Реквизиты);
	
	Для каждого ЭлементСтруктуры Из СтруктураРеквизитов Цикл
		ИмяРеквизита = ЭлементСтруктуры.Ключ;
		
		Если Объект[ИмяРеквизита] <> Объект.Ссылка[ИмяРеквизита] Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьВыборкуПодчиненныхСохраненныхНастроек()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СохраненныеНастройки.Ссылка
	|ИЗ
	|	Справочник.общ_СохраненныеНастройки КАК СохраненныеНастройки
	|ГДЕ
	|	СохраненныеНастройки.ТипНастройки = ЗНАЧЕНИЕ(Перечисление.ТипыНастроек.НастройкиПользователяНастройкиОтчета)
	|	И СохраненныеНастройки.НастраиваемыйОбъект = &НастраиваемыйОбъект";
	
	Запрос.УстановитьПараметр("НастраиваемыйОбъект", ЭтотОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;
	
КонецФункции

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.Пользователи.Очистить();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// РАЗДЕЛ ОСНОВНОЙ ПРОГРАММЫ


СтруктураРеквизитов = Новый Структура();

СтруктураРеквизитов.Вставить("ПометкаУдаления", "Пометка удаления");
СтруктураРеквизитов.Вставить("Родитель",        "Группа настроек");
СтруктураРеквизитов.Вставить("Наименование",    "Наименование настройки");

Для каждого Реквизит Из Метаданные.Справочники.общ_СохраненныеНастройки.Реквизиты Цикл
	СтруктураРеквизитов.Вставить(Реквизит.Имя, Реквизит.Представление());
КонецЦикла;
