Процедура ЗаписатьСписокЗначенийВХранилище(НомерCтроки,Список) Экспорт
	ИмяПараметра = ПараметрыШаблона[НомерCтроки-1].Параметр;
	Строки = СписочныеЗначенияПараметров.НайтиСтроки(Новый Структура("Параметр",ИмяПараметра));
	Для Каждого Строка Из Строки Цикл
		СписочныеЗначенияПараметров.Удалить(Строка);
	КонецЦикла;
	Для Каждого ЗначениеСписка Из Список Цикл
		НоваяСтрока = СписочныеЗначенияПараметров.Добавить();
		НоваяСтрока.Параметр = ИмяПараметра;
		НоваяСтрока.Значение = ЗначениеСписка.Значение;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьСписокИзХранилища(НомерСтроки) Экспорт
	ИмяПараметра = ПараметрыШаблона[НомерСтроки-1].Параметр;
	Список = Новый СписокЗначений;
	Строки = СписочныеЗначенияПараметров.НайтиСтроки(Новый Структура("Параметр",ИмяПараметра));
	Для Каждого Строка Из Строки Цикл
		Список.Добавить(Строка.Значение);
	КонецЦикла;
	Возврат Список;
КонецФункции

Процедура ПередЗаписью(Отказ)
	МассивСтрокиУдалить = Новый Массив;
	Для Каждого СтрокаСЗ Из СписочныеЗначенияПараметров Цикл
		Если ПараметрыШаблона.НайтиСтроки(Новый Структура("Параметр",СтрокаСЗ.Параметр)).Количество()=0 Тогда
			МассивСтрокиУдалить.Добавить(СтрокаСЗ);
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаУдалить Из МассивСтрокиУдалить Цикл
		СписочныеЗначенияПараметров.Удалить(СтрокаУдалить);
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// проверка корректности параметров
	Для Каждого СтрокаПараметр Из ПараметрыШаблона Цикл
		Если СокрЛП(СтрокаПараметр.Параметр)="" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указано имя параметра "+Строка(СтрокаПараметр.НомерСтроки)+"!",
						ЭтотОбъект,"ПараметрыШаблона["+Строка(ПараметрыШаблона.Индекс(СтрокаПараметр))+"].Параметр",,Отказ);
		Иначе				
			Если ПараметрыШаблона.НайтиСтроки(Новый Структура("Параметр",СокрЛП(СтрокаПараметр.Параметр))).Количество()>1 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Параметр "+СтрокаПараметр.Параметр+" объявлен более одного раза!",
							ЭтотОбъект,"ПараметрыШаблона["+Строка(ПараметрыШаблона.Индекс(СтрокаПараметр))+"].Параметр",,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	
КонецПроцедуры
