
&НаКлиенте
Процедура ФинансовыйПоказательПриИзменении(Элемент)
	//Если Объект.Наименование="" Тогда
	//	Объект.Наименование = Строка(Объект.ФинансовыйПоказатель);
	//	ОбновитьПредставлениеРасчетов();
	//КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеРасчетов()
	ВидСтрокиБюджета = Справочники.фин_ФинансовыеПоказатели.ВидРасчетнойСтроки(Объект.ФинансовыйПоказатель,Объект.Владелец);
	Элементы.ДекорацияРасчетыПредставление.Заголовок = фин_ОбщегоНазначенияКлиентСервер.ПредставлениеНастроекРасчетов(Объект.ФинансовыйПоказатель,Истина,ВидСтрокиБюджета)
			+?(ЗначениеЗаполнено(Объект.ЧастотаВыполненияРасчетовПоФормулам),
			Символы.ПС + "Расчет производится: "+Строка(Объект.ЧастотаВыполненияРасчетовПоФормулам)+?(Объект.ЧастотаВыполненияРасчетовПоФормулам=ПредопределенноеЗначение("Перечисление.фин_ЧастотаВыполненияРасчетов.СУказаннойПериодичностью"),": "+Строка(Объект.ПериодичностьВыполненияРасчетов),""),
			"");
	Элементы.ДекорацияИтогиПредставление.Заголовок = "Расчет итогов: "+фин_ОбщегоНазначенияКлиентСервер.ПредставлениеНастроекИтогов(Объект,Истина);
	Элементы.ИспользоватьРасчетыТолькоДляПервоначальногоЗаполнения.Видимость = НЕ (ВидСтрокиБюджета = Перечисления.фин_ВидыСтрокБюджета.РассчитываемаяПоМоделиВПредшествующихБюджетах ИЛИ ВидСтрокиБюджета = Перечисления.фин_ВидыСтрокБюджета.Наследуемая ИЛИ ВидСтрокиБюджета = Перечисления.фин_ВидыСтрокБюджета.РассчитываемаяПоЗависимости ИЛИ ВидСтрокиБюджета = Перечисления.фин_ВидыСтрокБюджета.РассчитываемаяПоНесколькимИсточникамРасчета);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НадписьПараметрыСтатьиБюджета = "Параметры статьи бюджета";
	ОбновитьПредставлениеРасчетов();
	Элементы.ДекорацияИтогиПредставление.Видимость = Справочники.фин_Бюджеты.РазличныеНастройкиИтоговВСтатьяхБюджета(Объект.Владелец);
	Элементы.Версия.Видимость = Справочники.фин_Бюджеты.ВерсионированиеБюджетаИспользуется(Объект.Владелец);
КонецПроцедуры

&НаКлиенте
Процедура РасчетыПредставлениеНажатие(Элемент)
	ПараметрыНастройкаРасчетов = Новый Структура("УчетПоСумме,УчетПоКоличеству,ЭтоГруппа,Формула,ФормулаКоличество,ТолькоДляПлановыхДанных,ПоРасценкам,
												|ОтносительныйПоказатель,ПризнакПеретекающегоОстатка,ВидПоказателя,ЧастотаВыполненияРасчетовПоФормулам,
												|ПериодичностьВыполненияРасчетов,ИспользоватьРасчетыТолькоДляПервоначальногоЗаполнения");
	ЗаполнитьЗначенияСвойств(ПараметрыНастройкаРасчетов,Объект);
	ЗаполнитьЗначенияСвойств(ПараметрыНастройкаРасчетов,ХарактеристикиФинансовогоПоказателя(Объект.ФинансовыйПоказатель));
	//ПараметрыНастройкаРасчетов.Вставить("ВидПоказателя",ПолеОбъекта(Объект.ФинансовыйПоказатель,"ВидПоказателя"));
	//ПараметрыНастройкаРасчетов.Вставить("ПризнакПеретекающегоОстатка",ПолеОбъекта(Объект.ФинансовыйПоказатель,"ПризнакПеретекающегоОстатка"));
	ПараметрыНастройкаРасчетов.Вставить("ТолькоПросмотр",Истина);
	ПараметрыНастройкаРасчетов.Вставить("ВидСтрокиБюджета",ВидСтрокиБюджета);
	ОткрытьФорму("Обработка.фин_УправлениеСтруктуройБюджета.Форма.ФормаНастройкиРасчетов",ПараметрыНастройкаРасчетов,ЭтотОбъект,УникальныйИдентификатор,?(фин_ОбщегоНазначенияКлиентПовтИсп.РежимОтдельногоОткрытияОкон(),ВариантОткрытияОкна.ОтдельноеОкно,Окно),,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ХарактеристикиФинансовогоПоказателя(ФинансовыйПоказатель)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	фин_ФинансовыеПоказатели.УчетПоСумме,
		|	фин_ФинансовыеПоказатели.УчетПоКоличеству,
		|	фин_ФинансовыеПоказатели.Формула,
		|	фин_ФинансовыеПоказатели.ФормулаКоличество,
		|	фин_ФинансовыеПоказатели.ТолькоДляПлановыхДанных,
		|	фин_ФинансовыеПоказатели.ПоРасценкам,
		|	фин_ФинансовыеПоказатели.ОтносительныйПоказатель,
		|	фин_ФинансовыеПоказатели.ПризнакПеретекающегоОстатка,
		|	фин_ФинансовыеПоказатели.ВидПоказателя
		|ИЗ
		|	Справочник.фин_ФинансовыеПоказатели КАК фин_ФинансовыеПоказатели
		|ГДЕ
		|	фин_ФинансовыеПоказатели.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ФинансовыйПоказатель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Таблица = РезультатЗапроса.Выгрузить();
	Структура = Новый Структура;
	Для Каждого Колонка Из Таблица.Колонки Цикл
		Структура.Вставить(Колонка.Имя);
	КонецЦикла;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Структура,ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат Структура;

КонецФункции

&НаСервереБезКонтекста
Функция ПолеОбъекта(Показатель,Поле)
	 возврат Показатель[Поле];
КонецФункции

&НаКлиенте
Процедура ДекорацияИтогиПредставлениеНажатие(Элемент)
	ОписаниеОповещенияНастройкаИтогов = Новый ОписаниеОповещения("ОбработатьНастройкуИтогов",ЭтотОбъект);
	ПараметрыНастройкаИтогов = Новый Структура("ИтоговаяФункция,ИсключитьИзИтогов,ПризнакВнутригрупповогоОборота,РучнаяНастройкаИтоговПоГруппам");
	ЗаполнитьЗначенияСвойств(ПараметрыНастройкаИтогов,Объект);
	ОткрытьФорму("Обработка.фин_УправлениеСтруктуройБюджета.Форма.ФормаНастройкиИтогов",ПараметрыНастройкаИтогов,ЭтотОбъект,УникальныйИдентификатор,?(фин_ОбщегоНазначенияКлиентПовтИсп.РежимОтдельногоОткрытияОкон(),ВариантОткрытияОкна.ОтдельноеОкно,Окно),,ОписаниеОповещенияНастройкаИтогов,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНастройкуИтогов(РезультатВыбора,ДополнительныеПараметры) Экспорт 
	Если РезультатВыбора<>Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект,РезультатВыбора);
		ОбновитьПредставлениеРасчетов();
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры
