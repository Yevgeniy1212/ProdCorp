
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	фин_ОбщегоНазначенияСервер.НастроитьПредставлениеПолейНадписей(ЭтаФорма);
	Если ЗначениеЗаполнено(Объект.Ссылка) ИЛИ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	общ_СоставПрофилейДоступа.Пользователь
			|ИЗ
			|	РегистрСведений.общ_СоставПрофилейДоступа КАК общ_СоставПрофилейДоступа
			|ГДЕ
			|	общ_СоставПрофилейДоступа.Профиль = &Профиль";

		Запрос.УстановитьПараметр("Профиль", ?(ЗначениеЗаполнено(Объект.Ссылка),Объект.Ссылка,Параметры.ЗначениеКопирования));

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СоставПользователей.Добавить(ВыборкаДетальныеЗаписи.Пользователь);
		КонецЦикла;

	КонецЕсли;
	Если Объект.ВидПрофиляДоступа=Перечисления.общ_ВидыПрофилейДоступа.РолиИнформационнойБазы Тогда
		ЗаполнитьСоставРолей();
	ИначеЕсли ЗначениеЗаполнено(Объект.Ссылка) ИЛИ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	общ_ОбъектыДоступаПоПрофилям.ОбъектДоступа,
			|	общ_ОбъектыДоступаПоПрофилям.Запись
			|ИЗ
			|	РегистрСведений.общ_ОбъектыДоступаПоПрофилям КАК общ_ОбъектыДоступаПоПрофилям
			|ГДЕ
			|	общ_ОбъектыДоступаПоПрофилям.Профиль = &Профиль";

		Запрос.УстановитьПараметр("Профиль", ?(ЗначениеЗаполнено(Объект.Ссылка),Объект.Ссылка,Параметры.ЗначениеКопирования));

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОбъектыДоступа.Добавить(ВыборкаДетальныеЗаписи.ОбъектДоступа,,ВыборкаДетальныеЗаписи.Запись);
		КонецЦикла;

	КонецЕсли;
	УстановитьВидимость();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоставРолей()
	НазначенныеРоли.Очистить();
	Для Каждого РольИБ Из Метаданные.Роли Цикл
		НазначенныеРоли.Добавить(РольИБ.Имя,РольИБ.Синоним,Объект.НазначенныеРоли.НайтиСтроки(Новый Структура("Роль",РольИБ.Имя)).Количество()>0,БиблиотекаКартинок.Пользователь);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	Элементы.ВариантОграничения.Видимость=Объект.ВидПрофиляДоступа<>Перечисления.общ_ВидыПрофилейДоступа.РолиИнформационнойБазы И ЗначениеЗаполнено(Объект.ВидПрофиляДоступа); 	
	Элементы.ВариантОграничения.ОграничениеТипа=?(Объект.ВидПрофиляДоступа=Перечисления.общ_ВидыПрофилейДоступа.КазначейскиеДокументы,Новый ОписаниеТипов("ПеречислениеСсылка.ден_ВариантыОграниченияДоступаКЗаявкамНаРасходИПлановымПоступлениям"),Новый ОписаниеТипов("ПеречислениеСсылка.фин_ВариантыОграниченияДоступаКДаннымБюджетов")); 	
	Элементы.ГруппаРоли.Видимость = Объект.ВидПрофиляДоступа=Перечисления.общ_ВидыПрофилейДоступа.РолиИнформационнойБазы И ЗначениеЗаполнено(Объект.ВидПрофиляДоступа);
	Элементы.ГруппаОбъекты.Видимость = (Объект.ВидПрофиляДоступа=Перечисления.общ_ВидыПрофилейДоступа.Бюджеты  И ЗначениеЗаполнено(Объект.ВариантОграничения) И Объект.ВариантОграничения<>Перечисления.фин_ВариантыОграниченияДоступаКДаннымБюджетов.ДокументыАвтора) ИЛИ (Объект.ВидПрофиляДоступа=Перечисления.общ_ВидыПрофилейДоступа.КазначейскиеДокументы И ЗначениеЗаполнено(Объект.ВариантОграничения) И Объект.ВариантОграничения<>Перечисления.ден_ВариантыОграниченияДоступаКЗаявкамНаРасходИПлановымПоступлениям.ДокументыАвтора);
	Если Элементы.ГруппаОбъекты.Видимость Тогда
		Если Объект.ВидПрофиляДоступа=Перечисления.общ_ВидыПрофилейДоступа.КазначейскиеДокументы Тогда
			Если Объект.ВариантОграничения=Перечисления.ден_ВариантыОграниченияДоступаКЗаявкамНаРасходИПлановымПоступлениям.ДокументыУказанныхПользователей Тогда
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
			ИначеЕсли Объект.ВариантОграничения=Перечисления.ден_ВариантыОграниченияДоступаКЗаявкамНаРасходИПлановымПоступлениям.ДокументыСУказаннымиВидамиОпераций Тогда
				МассивТипов = Новый Массив;
				МассивТипов.Добавить(Тип("ПеречислениеСсылка.ден_ВидыОперацийЗаявкиНаРасходование"));
				МассивТипов.Добавить(Тип("ПеречислениеСсылка.ден_ВидыОперацийПланируемоеПоступлениеДС"));
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов(МассивТипов);
			ИначеЕсли Объект.ВариантОграничения=Перечисления.ден_ВариантыОграниченияДоступаКЗаявкамНаРасходИПлановымПоступлениям.ДокументыУказанныхПодразделений Тогда
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"Подразделения");
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"Подразделения");
			КонецЕсли;
		ИначеЕсли Объект.ВидПрофиляДоступа=Перечисления.общ_ВидыПрофилейДоступа.Бюджеты Тогда
			Если Объект.ВариантОграничения=Перечисления.фин_ВариантыОграниченияДоступаКДаннымБюджетов.ДокументыУказанныхПользователей Тогда
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
			ИначеЕсли Объект.ВариантОграничения=Перечисления.фин_ВариантыОграниченияДоступаКДаннымБюджетов.ПоСценариям Тогда
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"СценарииПланирования");
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"СценарииПланирования");
			ИначеЕсли Объект.ВариантОграничения=Перечисления.фин_ВариантыОграниченияДоступаКДаннымБюджетов.ПоПроектам Тогда
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"Проекты");
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"Проекты");
			ИначеЕсли Объект.ВариантОграничения=Перечисления.фин_ВариантыОграниченияДоступаКДаннымБюджетов.ПоПодразделениям Тогда
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"Подразделения");
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"Подразделения");
			ИначеЕсли Объект.ВариантОграничения=Перечисления.фин_ВариантыОграниченияДоступаКДаннымБюджетов.ПоВидамБюджетов Тогда
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.фин_Бюджеты");
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.фин_Бюджеты");
			ИначеЕсли Объект.ВариантОграничения=Перечисления.фин_ВариантыОграниченияДоступаКДаннымБюджетов.ПоВидамБюджетовИПоПодразделениям Тогда
				МассивТипов = Новый Массив;
				МассивТипов.Добавить(Тип("СправочникСсылка.фин_Бюджеты"));
				МассивТипов.Добавить(Тип("СправочникСсылка."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"Подразделения"));
				Элементы.ОбъектыДоступаЗначение.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
				ОбъектыДоступа.ТипЗначения = Новый ОписаниеТипов(МассивТипов);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ Отказ Тогда
		НаборЗаписей = РегистрыСведений.общ_СоставПрофилейДоступа.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Профиль.Установить(ТекущийОбъект.Ссылка);
		Для Каждого ПользовательСостава Из СоставПользователей Цикл
			НЗ = НаборЗаписей.Добавить();
			НЗ.Профиль = ТекущийОбъект.Ссылка;
			НЗ.Пользователь = ПользовательСостава.Значение;
			Если ТекущийОбъект.ВидПрофиляДоступа = Перечисления.общ_ВидыПрофилейДоступа.РолиИнформационнойБазы Тогда
				общ_УправлениеПрофилямиДоступа.ПрименитьПараметрыПрофиляДоступаКИнформационнойБазе(ПользовательСостава.Значение,ТекущийОбъект.Ссылка);
			КонецЕсли;
		КонецЦикла;
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.общ_ОбъектыДоступаПоПрофилям.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Профиль.Установить(ТекущийОбъект.Ссылка);
		Если ТекущийОбъект.ВидПрофиляДоступа = Перечисления.общ_ВидыПрофилейДоступа.РолиИнформационнойБазы Тогда
			НаборЗаписей.Очистить();
		Иначе
			Для Каждого СтрокаОбъекты Из ОбъектыДоступа Цикл
				НЗ = НаборЗаписей.Добавить();
				НЗ.Профиль = ТекущийОбъект.Ссылка;
				НЗ.ОбъектДоступа = СтрокаОбъекты.Значение;
				НЗ.Запись = СтрокаОбъекты.Пометка;
			КонецЦикла;
		КонецЕсли;
		НаборЗаписей.Записать();

	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Для Каждого ПользовательСостава Из СоставПользователей Цикл
		Если НЕ ЗначениеЗаполнено(ПользовательСостава.Значение) Тогда
			куфиб_ОбщегоНазначения.СообщитьПользователю("В списке пользователей в строке № "+Строка(СоставПользователей.Индекс(ПользовательСостава)+1)+" не указан пользователь!");
			Отказ = Истина;
			Продолжить;
		КонецЕсли;
		Для Каждого мПользовательСостава Из СоставПользователей Цикл
			Если СоставПользователей.Индекс(ПользовательСостава)<=СоставПользователей.Индекс(мПользовательСостава) Тогда
				Продолжить;
			КонецЕсли;
			Если мПользовательСостава<>ПользовательСостава И мПользовательСостава.Значение=ПользовательСостава.Значение Тогда
				куфиб_ОбщегоНазначения.СообщитьПользователю("В списке пользователей в строке № "+Строка(СоставПользователей.Индекс(мПользовательСостава)+1)+" повторяется пользователь, указанный в строке № "+Строка(СоставПользователей.Индекс(ПользовательСостава)+1)+"!");
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Для Каждого ОбъектДоступа Из ОбъектыДоступа Цикл
		Для Каждого мОбъектДоступа Из ОбъектыДоступа Цикл
			Если ОбъектыДоступа.Индекс(ОбъектДоступа)<=ОбъектыДоступа.Индекс(мОбъектДоступа) Тогда
				Продолжить;
			КонецЕсли;
			Если мОбъектДоступа<>ОбъектДоступа И мОбъектДоступа.Значение=ОбъектДоступа.Значение Тогда
				куфиб_ОбщегоНазначения.СообщитьПользователю("В списке объектов доступа в строке № "+Строка(ОбъектыДоступа.Индекс(мОбъектДоступа)+1)+" повторяется объект, указанный в строке № "+Строка(ОбъектыДоступа.Индекс(ОбъектДоступа)+1)+"!");
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ Отказ И ТекущийОбъект.ВидПрофиляДоступа = Перечисления.общ_ВидыПрофилейДоступа.РолиИнформационнойБазы Тогда
		СписокПользователей = СоставПользователей.ВыгрузитьЗначения();

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	общ_СоставПрофилейДоступа.Профиль,
			|	общ_СоставПрофилейДоступа.Пользователь
			|ИЗ
			|	РегистрСведений.общ_СоставПрофилейДоступа КАК общ_СоставПрофилейДоступа
			|ГДЕ
			|	общ_СоставПрофилейДоступа.Профиль <> &Профиль
			|	И общ_СоставПрофилейДоступа.Пользователь В(&Пользователи)
			|	И общ_СоставПрофилейДоступа.Профиль.ПометкаУдаления = ЛОЖЬ
			|	И общ_СоставПрофилейДоступа.Профиль.ВидПрофиляДоступа = &ВидПрофиляДоступа";

		Запрос.УстановитьПараметр("ВидПрофиляДоступа", Перечисления.общ_ВидыПрофилейДоступа.РолиИнформационнойБазы);
		Запрос.УстановитьПараметр("Пользователи", СписокПользователей);
		Запрос.УстановитьПараметр("Профиль", ТекущийОбъект.Ссылка);

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Отказ = Истина;
			куфиб_ОбщегоНазначения.СообщитьПользователю("Пользователь "+ВыборкаДетальныеЗаписи.Пользователь+" уже включен в состав профиля "+ВыборкаДетальныеЗаписи.Профиль);
		КонецЦикла;

		ТекущийОбъект.НазначенныеРоли.Очистить();
		Для Каждого НазначениеРоли Из НазначенныеРоли Цикл
			Если НазначениеРоли.Пометка Тогда
				НС = ТекущийОбъект.НазначенныеРоли.Добавить();
				НС.Роль = НазначениеРоли.Значение;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидПрофиляДоступаПриИзменении(Элемент)
	Если Объект.ВидПрофиляДоступа=ПредопределенноеЗначение("Перечисление.общ_ВидыПрофилейДоступа.РолиИнформационнойБазы") Тогда
		ЗаполнитьСоставРолей();
	КонецЕсли;
	ОписаниеТипов = ?(Объект.ВидПрофиляДоступа=ПредопределенноеЗначение("Перечисление.общ_ВидыПрофилейДоступа.КазначейскиеДокументы"),Новый ОписаниеТипов("ПеречислениеСсылка.ден_ВариантыОграниченияДоступаКЗаявкамНаРасходИПлановымПоступлениям"),Новый ОписаниеТипов("ПеречислениеСсылка.фин_ВариантыОграниченияДоступаКДаннымБюджетов"));
	Объект.ВариантОграничения = ОписаниеТипов.ПривестиЗначение(Объект.ВариантОграничения);
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ВариантОграниченияПриИзменении(Элемент)
	ОбъектыДоступа.Очистить();
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыДоступаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элементы.ОбъектыДоступа.ТекущиеДанные.Пометка = Истина;
	КонецЕсли;
КонецПроцедуры
