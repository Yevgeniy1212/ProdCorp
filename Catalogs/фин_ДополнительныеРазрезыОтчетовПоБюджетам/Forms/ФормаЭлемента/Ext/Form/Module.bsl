
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДоступныеГруппировки.Добавить(Перечисления.фин_ДополнительныеРазрезыОтчетов.СтатьяБюджета,"Статья бюджета");
	ДоступныеГруппировки.Добавить(Перечисления.фин_ФактическиеПоказателиБюджетирования.ФинансовыйПоказатель,"Финансовый показатель");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	фин_НастройкаДополнительныхРазрезовБюджетирования.Измерение
		|ИЗ
		|	РегистрСведений.фин_НастройкаДополнительныхРазрезовБюджетирования КАК фин_НастройкаДополнительныхРазрезовБюджетирования";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РазрезыУчета = РезультатЗапроса.Выгрузить();

	Для Каждого СтрокаРазрез Из РазрезыУчета Цикл
		ДоступныеГруппировки.Добавить(СтрокаРазрез.Измерение,фин_РаботаСДополнительнымиРазрезамиБюджетирования.ПредставлениеРазреза(СтрокаРазрез.Измерение));
	КонецЦикла;
	ДоступныеГруппировки.Добавить(Перечисления.фин_ФактическиеПоказателиБюджетирования.Валюта,"Валюта операции");
	ДоступныеГруппировки.Добавить(Перечисления.фин_ФактическиеПоказателиБюджетирования.Организация,"Организация");
	фин_УправлениеОтчетамиБюджетирование.НастроитьРасширенноеПредставлениеРазрезов(ЭтотОбъект,"Измерение","",Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИзмерениеПриИзменении(Элемент)
	Объект.Путь = "";
КонецПроцедуры

&НаКлиенте
Процедура ИзмерениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка 	= Ложь;
	ДанныеВыбора 			= ДоступныеГруппировки;
КонецПроцедуры


&НаКлиенте
Процедура ОбработчикВыбораПоля(РезультатВыбора,ДополнительныеПараметры) Экспорт
	Если РезультатВыбора<>Неопределено Тогда
		Объект.Путь = РезультатВыбора;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикВыбораПоля",ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.фин_ДеревоДополнительныхПолей",Новый Структура("Измерение,БазовыйПуть",Объект.Измерение,фин_ОбщегоНазначенияСервер.ПолучитьИмяЭлементаПеречисленияПоЗначению(Объект.Измерение)),Элемент,УникальныйИдентификатор,?(фин_ОбщегоНазначенияКлиентПовтИсп.РежимОтдельногоОткрытияОкон(),ВариантОткрытияОкна.ОтдельноеОкно,Окно),,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры
