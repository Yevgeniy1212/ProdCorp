////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.

	ТабличныйДокумент = ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКодБанка     		= ТабличныйДокумент.Области.КодБанка;
	ОбластьКодБанкаДоРеформы 	= ТабличныйДокумент.Области.КодБанкаДоРеформы;
	ОбластьНаименование 		= ТабличныйДокумент.Области.Наименование;
	ОбластьГород        		= ТабличныйДокумент.Области.Город;

	КодБанка     			= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодБанка.Лево, ТекущаяОбласть.Низ, ОбластьКодБанка.Право).Текст;
	КодБанкаДоРеформы     	= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодБанкаДоРеформы.Лево, ТекущаяОбласть.Низ, ОбластьКодБанкаДоРеформы.Право).Текст;
	
	Наименование = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст;
	Город        = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьГород.Лево, ТекущаяОбласть.Низ, ОбластьГород.Право).Текст;
	
	// Проверка наличия выбранного банка.
    // сначала по новому БИК. Есл ине найден - по старому
	КодДляПоиска = КодБанка;
	Ссылка = БанкиНайтиПоРеквизиту("БИК", КодДляПоиска);
	Если Ссылка.Пустая() И НЕ ПустаяСтрока(КодБанкаДоРеформы) Тогда
		КодДляПоиска = КодБанкаДоРеформы;
		Ссылка = БанкиНайтиПоРеквизиту("БИКДоРеформыБанковскихСчетов", КодДляПоиска);
	КонецЕсли;	

	Если НЕ Ссылка.Пустая() Тогда
		Вопрос = "В справочнике ""Банки"" уже существует элемент с БИК """ + КодДляПоиска + """! Открыть существующий?";
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );

		Если Ответ = КодВозвратаДиалога.Да Тогда
			//Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
			ОткрытьЗначение(Ссылка);
			Возврат;

		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	// Создание нового банка.

	//ФормаНовогоЭлемента = Справочники.Банки.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("БИК", КодБанка);
	ПараметрыФормы.Вставить("БИКДоРеформыБанковскихСчетов",КодБанкаДоРеформы);
	ПараметрыФормы.Вставить("Наименование",СтрПолучитьСтроку(Наименование, 1));
	ПараметрыФормы.Вставить("Город",СтрПолучитьСтроку(Город, 1));
	ПараметрыФормы.Вставить("КодВПлатежнойСистеме",УправлениеДенежнымиСредствами.ПолучитьКодБанкаВПлатежнойСистеме(КодБанка));
	ПараметрыФормы.Вставить("КодВПлатежнойСистемеДоРеформыБанковскихСчетов",УправлениеДенежнымиСредствами.ПолучитьКодБанкаВПлатежнойСистеме(КодБанкаДоРеформы));
	ОткрытьФорму("Справочник.Банки.ФормаОбъекта",ПараметрыФормы,ВладелецФормы);

КонецПроцедуры
&НаСервере
Функция БанкиНайтиПоРеквизиту(БИК, КодДляПоиска)
	Возврат Справочники.Банки.НайтиПоРеквизиту(БИК, КодДляПоиска)
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Макет = Справочники.Банки.ПолучитьМакет("КлассификаторБанков");
	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка
	ТабличныйДокумент = ПолеТабличногоДокумента;
 	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);
	ТабличныйДокумент.ФиксацияСверху      = ТабличныйДокумент.Области.ОбластьРасшифровки.Верх - 1;
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;
КонецПроцедуры
