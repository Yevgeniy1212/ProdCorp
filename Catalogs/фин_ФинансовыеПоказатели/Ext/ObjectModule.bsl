#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ)
	Если НЕ ЭтоГруппа И НЕ ПометкаУдаления Тогда
		Если ВидПоказателя.Пустая() Тогда
			ВидПоказателя = Перечисления.фин_ВидыФинансовыхПоказателей.Оборотный;
		КонецЕсли;
		Обработанные = Новый Массив;
		Для Каждого СтрокаРазрез Из РазрезыУчета Цикл
			Если Обработанные.Найти(СтрокаРазрез.Измерение)=Неопределено Тогда
				Если РазрезыУчета.НайтиСтроки(Новый Структура("Измерение",СтрокаРазрез.Измерение)).Количество()>1 Тогда
					Обработанные.Добавить(СтрокаРазрез.Измерение);
					Отказ = Истина;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Финансовый показатель """+Наименование+""" не может быть записан: Разрез "+фин_РаботаСДополнительнымиРазрезамиБюджетирования.ПредставлениеРазреза(СтрокаРазрез.Измерение)+" назначен фин. показателю более одного раза!");
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(СтрокаРазрез.Измерение) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Финансовый показатель """+Наименование+""" не может быть записан: в строке №"+Строка(СтрокаРазрез.НомерСтроки)+" разрезов находится пустое значение!");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если НЕ фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ВестиУчетПоПлановойНоменклатуре") Тогда
			ИспользуемыйНоменклатурныйПеречень = Перечисления.узп_ИспользуемыеНоменклатурныеПеречни.РегламентированнаяНоменклатура;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	Если (НЕ (УчетПоКоличеству И УчетПоСумме)) ИЛИ (ВидПоказателя=Перечисления.фин_ВидыФинансовыхПоказателей.Рассчитываемый И ПоРасценкам) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СпособЦенообразования");
		МассивНепроверяемыхРеквизитов.Добавить("ИспользуемыйТипЦен");
		МассивНепроверяемыхРеквизитов.Добавить("ИсточникДанныхДляЦены");
		МассивНепроверяемыхРеквизитов.Добавить("ПоказательИсточникаДанныхДляЦены");
	КонецЕсли;
	Если СпособЦенообразования=Перечисления.фин_СпособыЦенообразованияПоФинансовымПоказателям.ПоДаннымИсточникаРасчетов
		ИЛИ СпособЦенообразования=Перечисления.фин_СпособыЦенообразованияПоФинансовымПоказателям.Фиксированная
		ИЛИ СпособЦенообразования=Перечисления.фин_СпособыЦенообразованияПоФинансовымПоказателям.НеРассчитывать Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИспользуемыйТипЦен");
	КонецЕсли;
	Если НЕ СпособЦенообразования=Перечисления.фин_СпособыЦенообразованияПоФинансовымПоказателям.ПоДаннымИсточникаРасчетов Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИсточникДанныхДляЦены");
		МассивНепроверяемыхРеквизитов.Добавить("ПоказательИсточникаДанныхДляЦены");
	КонецЕсли;
	Если НЕ ВидПоказателя=Перечисления.фин_ВидыФинансовыхПоказателей.Рассчитываемый Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Формула");
		МассивНепроверяемыхРеквизитов.Добавить("ФормулаКоличество");
	Иначе
		//Если НЕ УчетПоКоличеству Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ФормулаКоличество");
		//КонецЕсли;
		Если (НЕ УчетПоСумме) ИЛИ ПоРасценкам Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Формула");
		КонецЕсли;
	КонецЕсли;
	фин_ЗаполнениеДокументов.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

#КонецЕсли