#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	

Функция ВидРасчетнойСтроки(ФинансовыйПоказатель,Бюджет = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если НЕ ЗначениеЗаполнено(ФинансовыйПоказатель) Тогда
		Возврат Перечисления.фин_ВидыСтрокБюджета.Первичная;	
	КонецЕсли;
	Если ЗначениеЗаполнено(Бюджет) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	фин_РегламентФормированияБюджетов.Бюджет
			|ПОМЕСТИТЬ ВТ_ПредшествующиеБюджеты
			|ИЗ
			|	РегистрСведений.фин_РегламентФормированияБюджетов.СрезПоследних(, Бюджет = &Бюджет) КАК фин_РегламентФормированияБюджетовСрезПоследних
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.фин_РегламентФормированияБюджетов КАК фин_РегламентФормированияБюджетов
			|		ПО фин_РегламентФормированияБюджетовСрезПоследних.Регистратор = фин_РегламентФормированияБюджетов.Регламент
			|			И фин_РегламентФормированияБюджетовСрезПоследних.Ранг > фин_РегламентФормированияБюджетов.Ранг
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	фин_СтатьиБюджета.ФинансовыйПоказатель
			|ПОМЕСТИТЬ ВТ_ВключениеВТекущийБюджет
			|ИЗ
			|	Справочник.фин_СтатьиБюджета КАК фин_СтатьиБюджета
			|ГДЕ
			|	фин_СтатьиБюджета.Владелец = &Бюджет
			|	И НЕ фин_СтатьиБюджета.ЭтоГруппа
			|	И НЕ фин_СтатьиБюджета.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	фин_СтатьиБюджета.ФинансовыйПоказатель
			|ПОМЕСТИТЬ ВТ_Наследуемые
			|ИЗ
			|	ВТ_ПредшествующиеБюджеты КАК ВТ_ПредшествующиеБюджеты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.фин_СтатьиБюджета КАК фин_СтатьиБюджета
			|		ПО ВТ_ПредшествующиеБюджеты.Бюджет = фин_СтатьиБюджета.Владелец
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВключениеВТекущийБюджет КАК ВТ_ВключениеВТекущийБюджет
			|		ПО (фин_СтатьиБюджета.ФинансовыйПоказатель = ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель)
			|ГДЕ
			|	НЕ фин_СтатьиБюджета.ПометкаУдаления
			|	И фин_СтатьиБюджета.ФинансовыйПоказатель = &ФинансовыйПоказатель
			|	И НЕ ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель ЕСТЬ NULL 
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	фин_ЗависимостиФинансовыхПоказателей.ЗависимаяФинансовыйПоказатель,
			|	фин_ЗависимостиФинансовыхПоказателей.ИсхСценарий КАК Сценарий
			|ПОМЕСТИТЬ ВТ_Зависимости
			|ИЗ
			|	РегистрСведений.фин_ЗависимостиФинансовыхПоказателей.СрезПоследних(, ) КАК фин_ЗависимостиФинансовыхПоказателей
			|ГДЕ
			|	фин_ЗависимостиФинансовыхПоказателей.ЗависимаяФинансовыйПоказатель = &ФинансовыйПоказатель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	фин_РегламентныеРасчетыПоМоделям.ФинансовыйПоказатель,
			|	0 КАК ИзВнешнихБюджетов
			|ПОМЕСТИТЬ ВТ_ВсеРасчетыПоМоделям
			|ИЗ
			|	РегистрСведений.фин_РегламентныеРасчетыПоМоделям КАК фин_РегламентныеРасчетыПоМоделям
			|ГДЕ
			|	фин_РегламентныеРасчетыПоМоделям.ФинансовыйПоказатель = &ФинансовыйПоказатель
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	МоделиРаспределения.ФинансовыйПоказатель,
			|	ВЫБОР
			|		КОГДА ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель ЕСТЬ NULL 
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ
			|ИЗ
			|	РегистрСведений.фин_РасчетМоделейРаспределения.СрезПоследних(, ) КАК фин_РасчетМоделейРаспределения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.фин_МоделиРаспределенияБюджетныхДанных.ОтражениеРезультатов КАК МоделиРаспределения
			|		ПО фин_РасчетМоделейРаспределения.Модель = МоделиРаспределения.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВключениеВТекущийБюджет КАК ВТ_ВключениеВТекущийБюджет
			|		ПО фин_РасчетМоделейРаспределения.РаспределяемыйПоказатель = ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель
			|ГДЕ
			|	фин_РасчетМоделейРаспределения.ПрименятьМодель
			|	И МоделиРаспределения.ФинансовыйПоказатель = &ФинансовыйПоказатель
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ФиксированноеЗначение)
			|			ТОГДА ПроизводственныеМодели.ЗначениеФинансовыйПоказатель
			|		КОГДА ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПоПроизводимойПозиции)
			|			ТОГДА фин_РасчетПроизводственныхМоделей.ПоказательПланированияПроизводстваПродукции
			|		КОГДА ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПараметрыПланированияПроизводства)
			|			ТОГДА фин_ПараметрыПланированияПроизводства.ФинансовыйПоказатель
			|		ИНАЧЕ NULL
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель ЕСТЬ NULL 
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ
			|ИЗ
			|	РегистрСведений.фин_РасчетПроизводственныхМоделей.СрезПоследних(, ) КАК фин_РасчетПроизводственныхМоделей
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.фин_ПроизводственныеМодели.ОтражениеРезультатов КАК ПроизводственныеМодели
			|		ПО фин_РасчетПроизводственныхМоделей.Модель = ПроизводственныеМодели.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.фин_ПараметрыПланированияПроизводства.СрезПоследних(, ) КАК фин_ПараметрыПланированияПроизводства
			|		ПО (ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПараметрыПланированияПроизводства))
			|			И (фин_ПараметрыПланированияПроизводства.ФинансовыйПоказатель <> ЗНАЧЕНИЕ(Справочник.фин_ФинансовыеПоказатели.ПустаяСсылка))
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВключениеВТекущийБюджет КАК ВТ_ВключениеВТекущийБюджет
			|		ПО фин_РасчетПроизводственныхМоделей.ПоказательПланированияПроизводстваПродукции = ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель
			|ГДЕ
			|	фин_РасчетПроизводственныхМоделей.ПрименятьМодель
			|	И ВЫБОР
			|			КОГДА ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ФиксированноеЗначение)
			|				ТОГДА ПроизводственныеМодели.ЗначениеФинансовыйПоказатель
			|			КОГДА ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПоПроизводимойПозиции)
			|				ТОГДА фин_РасчетПроизводственныхМоделей.ПоказательПланированияПроизводстваПродукции
			|			КОГДА ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПараметрыПланированияПроизводства)
			|				ТОГДА фин_ПараметрыПланированияПроизводства.ФинансовыйПоказатель
			|			ИНАЧЕ NULL
			|		КОНЕЦ = &ФинансовыйПоказатель
			|	И (ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ФиксированноеЗначение)
			|			ИЛИ ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПараметрыПланированияПроизводства)
			|			ИЛИ ПроизводственныеМодели.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПоПроизводимойПозиции))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	фин_ПроизводственныеМоделиДополнительныеЗатратыНаВыпуск.ФинансовыйПоказатель,
			|	ВЫБОР
			|		КОГДА ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель ЕСТЬ NULL 
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ
			|ИЗ
			|	РегистрСведений.фин_РасчетПроизводственныхМоделей.СрезПоследних(, ) КАК фин_РасчетПроизводственныхМоделей
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.фин_ПроизводственныеМодели.ДополнительныеЗатратыНаВыпуск КАК фин_ПроизводственныеМоделиДополнительныеЗатратыНаВыпуск
			|		ПО фин_РасчетПроизводственныхМоделей.Модель = фин_ПроизводственныеМоделиДополнительныеЗатратыНаВыпуск.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВключениеВТекущийБюджет КАК ВТ_ВключениеВТекущийБюджет
			|		ПО фин_РасчетПроизводственныхМоделей.ПоказательПланированияПроизводстваПродукции = ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель
			|ГДЕ
			|	фин_РасчетПроизводственныхМоделей.ПрименятьМодель
			|	И фин_ПроизводственныеМоделиДополнительныеЗатратыНаВыпуск.ФинансовыйПоказатель = &ФинансовыйПоказатель
			|	И (фин_ПроизводственныеМоделиДополнительныеЗатратыНаВыпуск.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ФиксированноеЗначение)
			|			ИЛИ фин_ПроизводственныеМоделиДополнительныеЗатратыНаВыпуск.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПараметрыПланированияПроизводства)
			|			ИЛИ фин_ПроизводственныеМоделиДополнительныеЗатратыНаВыпуск.ФинансовыйПоказатель = ЗНАЧЕНИЕ(Перечисление.фин_ИсточникиЗаполненияПолейПроизводственнойМодели.ПоПроизводимойПозиции))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	фин_ПроизводственныеМодели.ФинансовыйПоказательДляОтраженияТрудовыхЗатрат,
			|	ВЫБОР
			|		КОГДА ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель ЕСТЬ NULL 
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ
			|ИЗ
			|	РегистрСведений.фин_РасчетПроизводственныхМоделей.СрезПоследних(, ) КАК фин_РасчетПроизводственныхМоделей
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.фин_ПроизводственныеМодели КАК фин_ПроизводственныеМодели
			|		ПО фин_РасчетПроизводственныхМоделей.Модель = фин_ПроизводственныеМодели.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВключениеВТекущийБюджет КАК ВТ_ВключениеВТекущийБюджет
			|		ПО фин_РасчетПроизводственныхМоделей.ПоказательПланированияПроизводстваПродукции = ВТ_ВключениеВТекущийБюджет.ФинансовыйПоказатель
			|ГДЕ
			|	фин_РасчетПроизводственныхМоделей.ПрименятьМодель
			|	И фин_ПроизводственныеМодели.ФинансовыйПоказательДляОтраженияТрудовыхЗатрат = &ФинансовыйПоказатель
			|	И фин_ПроизводственныеМодели.РассчитыватьПрямыеТрудовыеЗатраты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ_ВсеРасчетыПоМоделям.ФинансовыйПоказатель,
			|	СУММА(ВТ_ВсеРасчетыПоМоделям.ИзВнешнихБюджетов) КАК ИзВнешнихБюджетов
			|ПОМЕСТИТЬ ВТ_ИтогоРасчетыПоМоделям
			|ИЗ
			|	ВТ_ВсеРасчетыПоМоделям КАК ВТ_ВсеРасчетыПоМоделям
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТ_ВсеРасчетыПоМоделям.ФинансовыйПоказатель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	фин_СтатьиБюджета.Ссылка КАК Статья,
			|	ВЫБОР
			|		КОГДА НЕ ВТ_Наследуемые.ФинансовыйПоказатель ЕСТЬ NULL 
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.фин_ВидыСтрокБюджета.Наследуемая)
			|		КОГДА фин_ФинансовыеПоказатели.ВидПоказателя = ЗНАЧЕНИЕ(Перечисление.фин_ВидыФинансовыхПоказателей.Рассчитываемый)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.фин_ВидыСтрокБюджета.РассчитываемаяПоФормуле)
			|		КОГДА НЕ ВТ_Зависимости.ЗависимаяФинансовыйПоказатель ЕСТЬ NULL 
			|				И НЕ ВТ_ИтогоРасчетыПоМоделям.ФинансовыйПоказатель ЕСТЬ NULL 
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.фин_ВидыСтрокБюджета.РассчитываемаяПоНесколькимИсточникамРасчета)
			|		КОГДА НЕ ВТ_Зависимости.ЗависимаяФинансовыйПоказатель ЕСТЬ NULL 
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.фин_ВидыСтрокБюджета.РассчитываемаяПоЗависимости)
			|		КОГДА НЕ ВТ_ИтогоРасчетыПоМоделям.ФинансовыйПоказатель ЕСТЬ NULL 
			|			ТОГДА ВЫБОР
			|					КОГДА ВТ_ИтогоРасчетыПоМоделям.ИзВнешнихБюджетов > 0
			|						ТОГДА ЗНАЧЕНИЕ(Перечисление.фин_ВидыСтрокБюджета.РассчитываемаяПоМоделиВПредшествующихБюджетах)
			|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.фин_ВидыСтрокБюджета.РассчитываемаяПоМодели)
			|				КОНЕЦ
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.фин_ВидыСтрокБюджета.Первичная)
			|	КОНЕЦ КАК ВидСтрокиБюджета
			|ИЗ
			|	Справочник.фин_СтатьиБюджета КАК фин_СтатьиБюджета
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.фин_ФинансовыеПоказатели КАК фин_ФинансовыеПоказатели
			|		ПО фин_СтатьиБюджета.ФинансовыйПоказатель = фин_ФинансовыеПоказатели.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Зависимости КАК ВТ_Зависимости
			|		ПО фин_СтатьиБюджета.ФинансовыйПоказатель = ВТ_Зависимости.ЗависимаяФинансовыйПоказатель
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтогоРасчетыПоМоделям КАК ВТ_ИтогоРасчетыПоМоделям
			|		ПО фин_СтатьиБюджета.ФинансовыйПоказатель = ВТ_ИтогоРасчетыПоМоделям.ФинансовыйПоказатель
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Наследуемые КАК ВТ_Наследуемые
			|		ПО фин_СтатьиБюджета.ФинансовыйПоказатель = ВТ_Наследуемые.ФинансовыйПоказатель
			|ГДЕ
			|	фин_СтатьиБюджета.Владелец = &Бюджет
			|	И НЕ фин_СтатьиБюджета.ЭтоГруппа
			|	И НЕ фин_СтатьиБюджета.ПометкаУдаления
			|	И фин_СтатьиБюджета.ФинансовыйПоказатель = &ФинансовыйПоказатель";
		
		Запрос.УстановитьПараметр("Бюджет", Бюджет);
		Запрос.УстановитьПараметр("ФинансовыйПоказатель", ФинансовыйПоказатель);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Возврат ВыборкаДетальныеЗаписи.ВидСтрокиБюджета;
		КонецЕсли;
	КонецЕсли;
	Если ФинансовыйПоказатель.ВидПоказателя = Перечисления.фин_ВидыФинансовыхПоказателей.Рассчитываемый Тогда
		Возврат Перечисления.фин_ВидыСтрокБюджета.РассчитываемаяПоФормуле;
	КонецЕсли;
	
	

	
	ТаблицаВлияний = фин_ПроцедурыМеханизмовБюджетирования.ПолучитьВлияющие(ФинансовыйПоказатель);
	Если ТаблицаВлияний.Количество()=0 Тогда
		Возврат Перечисления.фин_ВидыСтрокБюджета.Первичная;
	Иначе
		РассчитываемаяПоМодели 			= Ложь;
		РассчитываемаяПоЗависимости 	= Ложь;
		Для Каждого СтрокаВлияний Из ТаблицаВлияний Цикл
			Если РассчитываемаяПоЗависимости И РассчитываемаяПоМодели Тогда
				Прервать;
			КонецЕсли;
			Если ТипЗнч(СтрокаВлияний.Основание) = Тип("СправочникСсылка.фин_ФормулыРасчетаФинансовыхПоказателей") Тогда
				Продолжить;
			ИначеЕсли ТипЗнч(СтрокаВлияний.Основание) = Тип("ДокументСсылка.фин_НастройкаЗависимостейМеждуФинансовымиПоказателями") Тогда
				РассчитываемаяПоЗависимости = Истина;
			Иначе
				РассчитываемаяПоМодели = Истина;
			КонецЕсли;	
		КонецЦикла;
		Если РассчитываемаяПоМодели И РассчитываемаяПоЗависимости Тогда
			Возврат Перечисления.фин_ВидыСтрокБюджета.РассчитываемаяПоНесколькимИсточникамРасчета;
		ИначеЕсли РассчитываемаяПоЗависимости Тогда
			Возврат Перечисления.фин_ВидыСтрокБюджета.РассчитываемаяПоЗависимости;
		ИначеЕсли РассчитываемаяПоМодели Тогда
			Возврат Перечисления.фин_ВидыСтрокБюджета.РассчитываемаяПоМодели;
		КонецЕсли;
	КонецЕсли;
	Возврат Перечисления.фин_ВидыСтрокБюджета.Первичная;
КонецФункции

	
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецЕсли