
&НаКлиенте
Процедура ГруппыПравилПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.ГруппыПравил.ТекущиеДанные;
	Если ТекущиеДанные<>Неопределено Тогда
		ПорядокПрименения = фин_ОбщегоНазначенияКлиентСервер.ПредставлениеСпособаИспользованияПравила(ТекущиеДанные);
		ТаблицаДанных = фин_ОбщегоНазначенияКлиентСервер.ПредставлениеТаблицыДанныхПравила(ТекущиеДанные);
	Иначе
		ПорядокПрименения = "";
		ТаблицаДанных = "";
	КонецЕсли;
	УстановитьОтбор();
КонецПроцедуры
	
&НаКлиенте
Процедура УстановитьОтбор()
	ТекущиеДанные = Элементы.ГруппыПравил.ТекущиеДанные;
	Если Отображать=2 Тогда
		ОтборПоВладельцу = ИмеющиесяГруппы;
	Иначе
		ОтборПоВладельцу = Объект.Ссылка;
		Если ТекущиеДанные<>Неопределено И Отображать=1 Тогда
			ОтборПоВладельцу = ТекущиеДанные.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Правила.КомпоновщикНастроек.Настройки.Отбор, "Владелец", ОтборПоВладельцу,?(ТипЗнч(ОтборПоВладельцу)=Тип("СписокЗначений"),ВидСравненияКомпоновкиДанных.ВСписке,ВидСравненияКомпоновкиДанных.Равно) , , Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьОписаниеУсловий(Команда)
	Элементы.ФормаОтображатьОписаниеУсловий.Пометка = НЕ Элементы.ФормаОтображатьОписаниеУсловий.Пометка;
	Элементы.ГруппыПравилУсловиеОписаниеУсловия.Видимость = Элементы.ФормаОтображатьОписаниеУсловий.Пометка;
	Элементы.ПравилаУсловиеОписаниеУсловия.Видимость = Элементы.ФормаОтображатьОписаниеУсловий.Пометка;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ГруппыПравил.КомпоновщикНастроек.Настройки.Отбор, "Владелец", Объект.Ссылка, , , Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Правила.КомпоновщикНастроек.Настройки.Отбор, "Владелец", Объект.Ссылка, , , Истина);
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ИмеющиесяГруппы.Добавить(Объект.Ссылка);
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	фин_ГруппыПравилОтраженияФактическихДанных.Ссылка
			|ИЗ
			|	Справочник.фин_ГруппыПравилОтраженияФактическихДанных КАК фин_ГруппыПравилОтраженияФактическихДанных
			|ГДЕ
			|	фин_ГруппыПравилОтраженияФактическихДанных.Владелец = &Владелец
			|	И НЕ фин_ГруппыПравилОтраженияФактическихДанных.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	фин_ПравилаОтраженияФактическихДанных.Ссылка
			|ИЗ
			|	Справочник.фин_ПравилаОтраженияФактическихДанных КАК фин_ПравилаОтраженияФактическихДанных
			|ГДЕ
			|	фин_ПравилаОтраженияФактическихДанных.Владелец = &Владелец
			|	И НЕ фин_ПравилаОтраженияФактическихДанных.ПометкаУдаления";
		
		Запрос.УстановитьПараметр("Владелец", Объект.Ссылка);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ТаблицаГруппы = РезультатЗапроса[0].Выгрузить();
		ТаблицаПравила = РезультатЗапроса[1].Выгрузить();
		Если ТаблицаПравила.Количество()=0 И ТаблицаГруппы.Количество()<>0 Тогда
			Отображать = 1;
		КонецЕсли;
		
		Элементы.ФормаИспользоватьГруппыПравил.Пометка 		= ТаблицаГруппы.Количество()<>0;
		Элементы.ФормаИспользоватьГруппыПравил.Доступность 	= ТаблицаГруппы.Количество()=0;
		
		Для Каждого СтрокаГруппы Из ТаблицаГруппы Цикл
			ИмеющиесяГруппы.Добавить(СтрокаГруппы.Ссылка);
		КонецЦикла;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Форма.Элементы.ГруппыПравил.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Форма.Объект.Ссылка);
	Форма.Элементы.Правила.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Форма.Объект.Ссылка);
	Форма.Элементы.ГруппаГруппыПравил.Видимость = Форма.Элементы.ФормаИспользоватьГруппыПравил.Пометка;
	Форма.Элементы.Отображать.Видимость = Форма.Элементы.ФормаИспользоватьГруппыПравил.Пометка;
	Форма.Элементы.ПравилаПредставлениеИсточника.Видимость = ТипЗнч(Форма.Объект.Владелец)=Тип("СправочникСсылка.фин_ВидыДокументов");
	Форма.Элементы.ГруппыПравилПредставлениеИсточника.Видимость = ТипЗнч(Форма.Объект.Владелец)=Тип("СправочникСсылка.фин_ВидыДокументов");
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ГруппыПравил.КомпоновщикНастроек.Настройки.Отбор, "Владелец", Объект.Ссылка, , , Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Правила.КомпоновщикНастроек.Настройки.Отбор, "Владелец", Объект.Ссылка, , , Истина);
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПравилОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	ИмеющиесяГруппы.Добавить(НовыйОбъект);
	Элементы.ФормаИспользоватьГруппыПравил.Доступность 	= Ложь;
	Если Отображать = 2 Тогда
		УстановитьОтбор();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьПриИзменении(Элемент)
	УстановитьОтбор();	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьГруппыПравил(Команда)
	Элементы.ФормаИспользоватьГруппыПравил.Пометка 		= НЕ Элементы.ФормаИспользоватьГруппыПравил.Пометка;
	Отображать = 0;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПравилПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверитьЗаписатьОбъект(Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПравилПередНачаломИзменения(Элемент, Отказ)
	ПроверитьЗаписатьОбъект(Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ПравилаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ПроверитьЗаписатьОбъект(Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ПравилаПередНачаломИзменения(Элемент, Отказ)
	ПроверитьЗаписатьОбъект(Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаписатьОбъект(Отказ)
	Если Модифицированность Тогда
		Попытка
			Записать();
		Исключение
			Отказ = Истина;
			ПоказатьПредупреждение(,"Для редактирования правил необходимо записать элемент! 
			|Не удалось автоматически записать по причине: 
			|	"+ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписьСхемыОтражения",Новый Структура("Объект,Наименование,Владелец",Объект.Ссылка,Объект.Наименование,Объект.Владелец));
КонецПроцедуры
