
//Процедура-обработчик события ПередЗаписью объекта
//
Процедура ПередЗаписью(Отказ)
	Текст = "";
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	Если СокрЛП(ПрограммныйИдентификатор) = "" Тогда
		 Текст=Символы.ПС+Символы.Таб+"- не указан программный идентификатор документа;";
		 Отказ=Истина;
	ИначеЕсли НЕ (СокрЛП(ПрограммныйИдентификатор) = "усд_ВходящийДокумент") Тогда
		 Запрос = Новый Запрос;
		 Запрос.Текст="ВЫБРАТЬ
		              |	ВидыДокументов.Ссылка
		              |ИЗ
		              |	Справочник.фин_ВидыДокументов КАК ВидыДокументов
		              |ГДЕ
		              |	ВидыДокументов.ПрограммныйИдентификатор = &ПрограммныйИдентификатор
		              |	И ВидыДокументов.Ссылка <> &Ссылка";
		 Запрос.УстановитьПараметр("ПрограммныйИдентификатор",	ПрограммныйИдентификатор);
		 Запрос.УстановитьПараметр("Ссылка",					Ссылка);
		 Если Не Запрос.Выполнить().Пустой() Тогда
			 Текст=Символы.ПС+Символы.Таб+"- уже имеется элемент справочника для указанного вида документов;";
			 Отказ=Истина;
		 КонецЕсли;
	КонецЕсли;
	Если СокрЛП(Наименование) = "" Тогда
		 Текст=Текст+Символы.ПС+Символы.Таб+"- не указано представление (наименование) документа;";
		 Отказ=Истина;
	КонецЕсли;
	Если ИспользоватьКонтрольРеквизитаПриПрохожденииЭтапов И СокрЛП(КонтролируемыйРеквизит) = "" Тогда
		 Текст=Текст+Символы.ПС+Символы.Таб+"- не указан контролируемый реквизит;";
		 Отказ=Истина;
	КонецЕсли;
	Если Отказ Тогда
		Сообщить("Элемент справочника Виды документов """+Наименование+""" не может быть записан:"+Текст);
	Иначе
		Если ИспользоватьКонтрольРеквизитаПриПрохожденииЭтапов Тогда
			Попытка
				КонтролируемыйРеквизитПредставление = Метаданные.Документы.Найти(ПрограммныйИдентификатор).Реквизиты.Найти(КонтролируемыйРеквизит).Синоним;
			Исключение
				Сообщить("Элемент справочника Виды документов """+Наименование+""". Ошибка при записи:"+ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //ПередЗаписью
