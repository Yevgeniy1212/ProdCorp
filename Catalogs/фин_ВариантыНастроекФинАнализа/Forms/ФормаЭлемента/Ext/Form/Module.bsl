 
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.СтатьиОтчетаОПрибыляхИУбытках.Доступность    = ЗначениеЗаполнено(Объект.Ссылка);
	Элементы.СтатьиАналитическогоБаланса.Доступность      = ЗначениеЗаполнено(Объект.Ссылка);
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УправлениеФормой(ЭтотОбъект);
	ПризнакСоздания = Объект.Ссылка.Пустая();
	
	ОбновитьОтборыДинамическихСписков(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Если ПризнакСоздания Тогда
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	СтатьиАналитическогоБаланса.Ссылка
		|ИЗ
		|	Справочник.фин_СтатьиАналитическогоБаланса КАК СтатьиАналитическогоБаланса
		|
		|ГДЕ
		|	СтатьиАналитическогоБаланса.Владелец = &Владелец";
		
		Запрос.УстановитьПараметр("Владелец",ТекущийОбъект.Ссылка); 
		
		Если Запрос.Выполнить().Пустой() Тогда
			фин_ПроцедурыМеханизмовБюджетирования.ЗаполнитьСтатьиБаланса(ТекущийОбъект.Ссылка);
			Элементы.СтатьиАналитическогоБаланса.Обновить();;
		КонецЕсли;
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	СтатьиОтчетаОПрибыляхИУбытках.Ссылка
		|ИЗ
		|	Справочник.фин_СтатьиОтчетаОПрибыляхИУбытках КАК СтатьиОтчетаОПрибыляхИУбытках
		|
		|ГДЕ
		|	СтатьиОтчетаОПрибыляхИУбытках.Владелец = &Владелец";
		
		Запрос.УстановитьПараметр("Владелец",ТекущийОбъект.Ссылка); 
		
		Если Запрос.Выполнить().Пустой() Тогда
			фин_ПроцедурыМеханизмовБюджетирования.ЗаполнитьСтатьиОтчетаОПрибыляхИУбытках(ТекущийОбъект.Ссылка);
			Элементы.СтатьиОтчетаОприбыляхИУбытках.Обновить();
		КонецЕсли;
		
		ПризнакСоздания = Ложь;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
	ОбновитьОтборыДинамическихСписков(ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьОтборыДинамическихСписков(Форма)
	
	фин_ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.ГруппыСтатейБаланса, 
		"Владелец", 
		Форма.Объект.Ссылка, 
		ВидСравненияКомпоновкиДанных.Равно,
		НСтр("ru = 'Отбор по владельцу'"),
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	фин_ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СтатьиАналитическогоБаланса, 
		"Владелец", 
		Форма.Объект.Ссылка, 
		ВидСравненияКомпоновкиДанных.Равно,
		НСтр("ru = 'Отбор по владельцу'"),
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	фин_ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СтатьиОтчетаОприбыляхИУбытках, 
		"Владелец", 
		Форма.Объект.Ссылка, 
		ВидСравненияКомпоновкиДанных.Равно,
		НСтр("ru = 'Отбор по владельцу'"),
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыСтатейБалансаПриАктивизацииСтроки(Элемент)
	Если Элементы.ГруппыСтатейБаланса.ТекущиеДанные = Неопределено Тогда
		мСтруктураКлассификатораТекущаяСсылка = Неопределено;
	Иначе 
		Если мСтруктураКлассификатораТекущаяСсылка <> Элементы.ГруппыСтатейБаланса.ТекущиеДанные.Ссылка Тогда
			Элементы.СтатьиАналитическогоБаланса.ТекущийРодитель 	= Элементы.ГруппыСтатейБаланса.ТекущиеДанные.Ссылка;
			Элементы.СтатьиАналитическогоБаланса.ТекущаяСтрока 		= Элементы.ГруппыСтатейБаланса.ТекущиеДанные.Ссылка;
			Элементы.СтатьиАналитическогоБаланса.Развернуть(Элементы.СтатьиАналитическогоБаланса.ТекущийРодитель);
			
			мСтруктураКлассификатораТекущаяСсылка = Элементы.ГруппыСтатейБаланса.ТекущиеДанные.Ссылка;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
