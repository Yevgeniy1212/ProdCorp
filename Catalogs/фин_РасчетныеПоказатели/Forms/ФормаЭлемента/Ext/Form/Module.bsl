
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		ЗаполнитьДанныеОбъекта();
	Иначе
		Для Каждого СтрокаОбъект Из Объект.РазрезыУчета Цикл
			СтрокаОбъект.Представление = фин_РаботаСДополнительнымиРазрезамиБюджетирования.ПредставлениеРазреза(СтрокаОбъект.Измерение);
		КонецЦикла;

		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВводЗначенийРасчетныхПоказателей.Ссылка
			|ИЗ
			|	Документ.фин_ВводЗначенийРасчетныхПоказателей КАК ВводЗначенийРасчетныхПоказателей
			|ГДЕ
			|	ВводЗначенийРасчетныхПоказателей.Проведен
			|	И ВводЗначенийРасчетныхПоказателей.РасчетныйПоказатель = &РасчетныйПоказатель";

		Запрос.УстановитьПараметр("РасчетныйПоказатель", Объект.Ссылка);

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Элементы.СпособВвода.Вид=ВидПоляФормы.ПолеНадписи;
			Для Каждого Разрез Из Объект.РазрезыУчета Цикл
				Если Разрез.Использование Тогда
					НеизменяемыеРазрезы.Добавить(Разрез.Измерение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

	КонецЕсли;
	НадписьРезультирующиеЗначенияРазрезов 	= "Разрезы учета";
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОбъекта()
	мЭтотОбъект = РеквизитФормыВЗначение("Объект");
	МассивИзмерений = фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ПолучитьПолныйСписокРазрезов");
	СписокТабличныхЧастей = Новый Массив;
	СписокТабличныхЧастей.Добавить("РазрезыУчета");
	Для Каждого ТЧ Из СписокТабличныхЧастей Цикл
		Для Каждого Измерение Из МассивИзмерений Цикл
			Если мЭтотОбъект[ТЧ].НайтиСтроки(Новый Структура("Измерение",Измерение.Значение)).Количество()=0 Тогда
				НоваяСтрока = мЭтотОбъект[ТЧ].Добавить();
				НоваяСтрока.Измерение = Измерение.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	ЗначениеВРеквизитФормы(мЭтотОбъект,"Объект");
	Для Каждого СтрокаОбъект Из Объект.РазрезыУчета Цикл
		СтрокаОбъект.Представление = фин_РаботаСДополнительнымиРазрезамиБюджетирования.ПредставлениеРазреза(СтрокаОбъект.Измерение);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеРазрезов(Команда)
	ЗаполнитьДанныеОбъекта();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Для Каждого СтрокаОбъект Из Объект.РазрезыУчета Цикл
		СтрокаОбъект.Представление = фин_РаботаСДополнительнымиРазрезамиБюджетирования.ПредставлениеРазреза(СтрокаОбъект.Измерение);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РазрезыУчетаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НеизменяемыеРазрезы.НайтиПоЗначению(Элементы.РазрезыУчета.ТекущиеДанные.Измерение)<>Неопределено Тогда
		Элементы.РазрезыУчета.ТекущиеДанные.Использование = Истина;
		ПоказатьПредупреждение(,"Данный разрез не может быть отключен, т.к. используется в документах!");
	КонецЕсли;
КонецПроцедуры
