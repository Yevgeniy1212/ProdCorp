
&НаКлиенте
Процедура ИсходныеКомплектующиеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущиеДанные.Количество = 1;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ФормаЗаполнитьПоБухгалтерскойСпецификации.Видимость = Метаданные.Справочники.Найти("СпецификацииНоменклатуры")<>Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоБухгалтерскойСпецификации(Команда)
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		РезультатВыбора = ОткрытьФормуМодально("Справочник.СпецификацииНоменклатуры.ФормаВыбора",,ЭтотОбъект);
		Если РезультатВыбора<>Неопределено Тогда
			ОбработатьВыборБухгалтерскойСпецификации(РезультатВыбора,Неопределено);
		КонецЕсли;
	#Иначе
		ОписаниеОбработатьВыборБухгалтерскойСпецификации = Новый ОписаниеОповещения("ОбработатьВыборБухгалтерскойСпецификации",ЭтотОбъект);
		ОткрытьФорму("Справочник.СпецификацииНоменклатуры.ФормаВыбора",,ЭтотОбъект,,,,ОписаниеОбработатьВыборБухгалтерскойСпецификации,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборБухгалтерскойСпецификации(РезультатВыбора,ДополнительныеПараметры) Экспорт
	Если РезультатВыбора<>Неопределено Тогда
		ЗаполнитьПоСпецификации(РезультатВыбора);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоСпецификации(РезультатВыбора)
	Объект.ИсходныеКомплектующие.Очистить();
	Объект.ИсходныеКомплектующие.Загрузить(РезультатВыбора.ИсходныеКомплектующие.Выгрузить());
	Объект.Количество = РезультатВыбора.Количество;
	Если НЕ ЗначениеЗаполнено(Объект.Владелец) Тогда
		Объект.Владелец = РезультатВыбора.Владелец;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = РезультатВыбора.Наименование;
	КонецЕсли;
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАБОТЫ С ПОДБОРОМ НОМЕНКЛАТУРЫ


&НаКлиенте
Процедура ПодборТовары(Команда)
	НачатьПодбор(Элементы.ИсходныеКомплектующие,Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПодборТоварыПлановая(Команда)
	НачатьПодбор(Элементы.ИсходныеКомплектующие,Истина);
КонецПроцедуры

&НаКлиенте
Процедура НачатьПодбор(Элемент,ПлановаяНоменклатура)
	мПараметры = Новый Структура;
	мПараметры.Вставить("ЗакрыватьПриВыборе",Ложь);
	ФормаНоменклатура = ПолучитьФорму("Справочник."+?(ПлановаяНоменклатура=Истина,"фин_Плановая","")+"Номенклатура.ФормаВыбора",мПараметры,Элемент,УникальныйИдентификатор);	
	ФормаНоменклатура.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура ИсходныеКомплектующиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если Объект.ИсходныеКомплектующие.НайтиСтроки(Новый Структура("Номенклатура",ВыбранноеЗначение)).Количество()=0 Тогда
		НС = Объект.ИсходныеКомплектующие.Добавить();
		НС.Номенклатура = ВыбранноеЗначение;
	КонецЕсли;
КонецПроцедуры


