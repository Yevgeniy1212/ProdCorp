
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ОбъектИБ") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,Параметры,"ОбъектИБ,ВидДанных,ИмяИсточника,ПредставлениеИсточника");
	КонецЕсли;
	ЗаполнитьСписокИсточников();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Форма.Элементы.ИмяИсточника.Видимость = Форма.ВидДанных<>ПредопределенноеЗначение("Перечисление.фин_ВидыОтраженийПервичныхДокументовВБюджетировании.ПоШапкеДокумента");
	Форма.Элементы.ПредставлениеИсточника.Видимость = Форма.ВидДанных<>ПредопределенноеЗначение("Перечисление.фин_ВидыОтраженийПервичныхДокументовВБюджетировании.ПоШапкеДокумента");
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(Новый Структура("ВидДанных,ИмяИсточника,ПредставлениеИсточника",ВидДанных,ИмяИсточника,ПредставлениеИсточника));
КонецПроцедуры

&НаКлиенте
Процедура ВидДанныхПриИзменении(Элемент)
	ИмяИсточника = "";
	ПредставлениеИсточника = "";
	ЗаполнитьСписокИсточников();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоРегистр(ОбъектИБ)
	Возврат ТипЗнч(ОбъектИБ)=Тип("СправочникСсылка.фин_КлассификаторРегистров");	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокИсточников()
	СписокИсточников.Очистить();
	Если Не ЭтоРегистр(ОбъектИБ) Тогда
		Если ЗначениеЗаполнено(ОбъектИБ) Тогда
			Если ВидДанных = ПредопределенноеЗначение("Перечисление.фин_ВидыОтраженийПервичныхДокументовВБюджетировании.ПоТабличнойЧасти") Тогда
				ДополнитьТабличнымиЧастямиДокумента(СписокИсточников);
			Иначе
				ДополнитьДвижениямиДокумента(СписокИсточников);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДополнитьТабличнымиЧастямиДокумента(СписокВыбораИсточников)
	ИмяДокумента = ПрограммныйИдентификатор();
	Справочники.фин_ГруппыПравилОтраженияФактическихДанных.ДополнитьТабличнымиЧастямиДокумента(СписокВыбораИсточников,ИмяДокумента);
КонецПроцедуры

&НаСервере
Процедура ДополнитьДвижениямиДокумента(СписокВыбораИсточников)
	ИмяДокумента = ПрограммныйИдентификатор();
	Справочники.фин_ГруппыПравилОтраженияФактическихДанных.ДополнитьДвижениямиДокумента(СписокВыбораИсточников,ИмяДокумента);
КонецПроцедуры

&НаСервере
Функция ПрограммныйИдентификатор()
	Возврат ОбъектИБ.ПрограммныйИдентификатор;	
КонецФункции

&НаКлиенте
Процедура ИмяИсточникаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = СписокИсточников;
КонецПроцедуры


&НаКлиенте
Процедура ИмяИсточникаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИмяИсточника = ВыбранноеЗначение;
	Если СписокИсточников.НайтиПоЗначению(ВыбранноеЗначение)<>Неопределено Тогда
		ПредставлениеИсточника = СписокИсточников.НайтиПоЗначению(ВыбранноеЗначение).Представление;
	КонецЕсли;
КонецПроцедуры
