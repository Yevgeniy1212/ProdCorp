
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	фин_ГруппыПравилОтраженияФактическихДанных.Ссылка
		|ИЗ
		|	Справочник.фин_ГруппыПравилОтраженияФактическихДанных КАК фин_ГруппыПравилОтраженияФактическихДанных
		|ГДЕ
		|	фин_ГруппыПравилОтраженияФактическихДанных.Ссылка <> &Ссылка
		|	И фин_ГруппыПравилОтраженияФактическихДанных.ПометкаУдаления = ЛОЖЬ
		|	И фин_ГруппыПравилОтраженияФактическихДанных.Владелец = &Владелец
		|	И фин_ГруппыПравилОтраженияФактическихДанных.Порядок = &Порядок";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Порядок", Порядок);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Порядок применения группы правил уже используется в группе "+ВыборкаДетальныеЗаписи.Ссылка);
		// Вставить обработку выборки ВыборкаДетальныеЗаписи
	КонецЦикла;
	

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.Свойство("ОбновлятьПравила") Тогда 
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	фин_ПравилаОтраженияФактическихДанных.Ссылка,
			|	фин_ПравилаОтраженияФактическихДанных.Условие,
			|	фин_ПравилаОтраженияФактическихДанных.Условие.ВидДанных КАК Условие_ВидДанных,
			|	фин_ПравилаОтраженияФактическихДанных.Условие.ИмяИсточника КАК Условие_ИмяИсточника,
			|	фин_ПравилаОтраженияФактическихДанных.Условие.Владелец КАК Условие_Владелец
			|ИЗ
			|	Справочник.фин_ПравилаОтраженияФактическихДанных КАК фин_ПравилаОтраженияФактическихДанных
			|ГДЕ
			|	фин_ПравилаОтраженияФактическихДанных.Владелец = &Ссылка
			|	И (фин_ПравилаОтраженияФактическихДанных.ВидДанных <> &ВидДанных
			|	ИЛИ фин_ПравилаОтраженияФактическихДанных.ИмяИсточника <> &ИмяИсточника
			|	ИЛИ (фин_ПравилаОтраженияФактическихДанных.Условие <> ЗНАЧЕНИЕ(Справочник.усд_УсловияВыполненияОперацийПоСтрокамДокумента.ПустаяСсылка)
			|			И фин_ПравилаОтраженияФактическихДанных.Условие.Владелец <> &ОбъектИБ))";
			
		ОбъектИБ = Владелец.Владелец;	
			
		Запрос.УстановитьПараметр("ВидДанных", ВидДанных);
		Запрос.УстановитьПараметр("ИмяИсточника", ИмяИсточника);
		Запрос.УстановитьПараметр("ОбъектИБ", ОбъектИБ);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОбъектПравил = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			ОбъектПравил.ВидДанных 				= ВидДанных;
			ОбъектПравил.ИмяИсточника 			= ИмяИсточника;
			ОбъектПравил.ПредставлениеИсточника = ПредставлениеИсточника;
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Условие) Тогда
				Если ВыборкаДетальныеЗаписи.Условие_Владелец<>ОбъектИБ ИЛИ ВыборкаДетальныеЗаписи.Условие_ВидДанных<>ВидДанных ИЛИ ВыборкаДетальныеЗаписи.Условие_ИмяИсточника<>ИмяИсточника Тогда
					ОбъектПравил.Условие = Справочники.усд_УсловияВыполненияОперацийПоСтрокамДокумента.ПустаяСсылка();
				КонецЕсли;
			КонецЕсли;
			Справочники.фин_ПравилаОтраженияФактическихДанных.КорректироватьЗаполнениеПравил(ОбъектПравил);
			Попытка
				ОбъектПравил.Записать();
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Необходимо пересмотреть настройки правила "+ВыборкаДетальныеЗаписи.Ссылка,ВыборкаДетальныеЗаписи.Ссылка);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось обновить данные правила "+ВыборкаДетальныеЗаписи.Ссылка+"
				|	"+ОписаниеОшибки());
			КонецПопытки;
		КонецЦикла;
		ДополнительныеСвойства.Удалить("ОбновлятьПравила");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка=Истина ИЛИ ДополнительныеСвойства.Свойство("ВнешняяОбработка") Тогда
		Возврат;
	КонецЕсли;
	Если ЗначениеЗаполнено(Ссылка) И (Ссылка.Владелец<>Владелец ИЛИ Ссылка.ВидДанных<>ВидДанных ИЛИ Ссылка.ИмяИсточника<>ИмяИсточника) Тогда
		ДополнительныеСвойства.Вставить("ОбновлятьПравила");
	КонецЕсли;
КонецПроцедуры
