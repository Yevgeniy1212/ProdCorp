#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если Не СтруктурноеОтклонение Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидСтруктурногоОтклонения");
	КонецЕсли;
		
	фин_ЗаполнениеДокументов.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка=Истина Тогда
		Возврат;
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("ВнешняяОбработка") Тогда
		Возврат;
	КонецЕсли;
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	Если Ссылка.СтруктурноеОтклонение = СтруктурноеОтклонение И Ссылка.ВидСтруктурногоОтклонения = ВидСтруктурногоОтклонения Тогда
		Возврат;
	КонецЕсли;
	РазрешеноИзменение = Справочники.фин_ПричиныОтклоненийФактическихДанных.РазрешеноИзменятьСтруктурныеХарактеристикиОтклонения(ЭтотОбъект);
	Если НЕ РазрешеноИзменение Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Элемент справочника Причины отклонений фактических данных """+Наименование+""" не записан, т.к. уже существуют документы рассмотрения отклонений.
		|	Невозможно изменение реквизитов ""Структурное отклонение"" и ""Вид структурного отклонения""");
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры
#КонецЕсли
