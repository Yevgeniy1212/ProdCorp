
// Процедура - обработчик события ПриЗаписи 
//
Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 

	Запрос = Новый Запрос;
	
	СписокПоЗаработку = Новый СписокЗначений();
	СписокПоЗаработку.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоЗаработку);
	СписокПоЗаработку.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуБезИндексации);
	
	СписокПоЗаработкуБЛ = Новый СписокЗначений();
	СписокПоЗаработкуБЛ.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуБЛ);
	СписокПоЗаработкуБЛ.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуБЛБезИндексации);
	
	СписокПоЗаработкуОтпуск = Новый СписокЗначений();
	СписокПоЗаработкуОтпуск.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуОтпуск);
	СписокПоЗаработкуОтпуск.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуОтпускБезИндексации);

	СписокПоПремиям = Новый СписокЗначений();
	СписокПоПремиям.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоПремиям);
	СписокПоПремиям.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоПремиямБезИндексации);
	СписокПоПремиям.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоФиксПремиям);
	СписокПоПремиям.Добавить(ПланыВидовРасчета.СреднийЗаработок.ПоФиксПремиямБезИндексации);
	
	Запрос.УстановитьПараметр("МассивПоЗаработку", СписокПоЗаработку);
	Запрос.УстановитьПараметр("МассивПоЗаработкуБЛ", СписокПоЗаработкуБЛ);
	Запрос.УстановитьПараметр("МассивПоЗаработкуОтпуск", СписокПоЗаработкуОтпуск);
	Запрос.УстановитьПараметр("МассивПоПремиям", СписокПоПремиям);
		
	Запрос.УстановитьПараметр("ТекущийВР", Ссылка);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	""Основные начисления организаций"" КАК ИмяПВР,
	|	ПВР.Ссылка КАК ВидРасчета,
	|	ПВР.Наименование КАК ВидРасчетаНаименование,
	|	База.Ссылка КАК ВидРасчетаСреднегоЗаработка,
	|	База.Ссылка.Наименование КАК ВидРасчетаСреднегоЗаработкаНаименование,
	|	0 КАК КоличествоПремий,
	|	0 КАК КоличествоЗаработок,
	|	0 КАК КоличествоБЛ,
	|	0 КАК КоличествоОтпуск
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ПВР
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.СреднийЗаработок.БазовыеВидыРасчета КАК База
	|		ПО ПВР.Ссылка = База.ВидРасчета
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.СреднийЗаработок.БазовыеВидыРасчета КАК ТекущаяБаза
	|		ПО ПВР.Ссылка = ТекущаяБаза.ВидРасчета
	|			И ТекущаяБаза.Ссылка = &ТекущийВР
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Дополнительные начисления организаций"" КАК ИмяПВР,
	|	ПВР.Ссылка КАК ВидРасчета,
	|	ПВР.Наименование КАК ВидРасчетаНаименование,
	|	База.Ссылка КАК ВидРасчетаСреднегоЗаработка,
	|	База.Ссылка.Наименование КАК ВидРасчетаСреднегоЗаработкаНаименование,
	|	0 КАК КоличествоПремий,
	|	0 КАК КоличествоЗаработок,
	|	0 КАК КоличествоБЛ,
	|	0 КАК КоличествоОтпуск
	|ИЗ
	|	ПланВидовРасчета.ДополнительныеНачисленияОрганизаций КАК ПВР
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.СреднийЗаработок.БазовыеВидыРасчета КАК База
	|		ПО ПВР.Ссылка = База.ВидРасчета
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.СреднийЗаработок.БазовыеВидыРасчета КАК ТекущаяБаза
	|		ПО ПВР.Ссылка = ТекущаяБаза.ВидРасчета
	|			И ТекущаяБаза.Ссылка = &ТекущийВР
	|
	|ИТОГИ 
	|	СУММА(ВЫБОР КОГДА ВидРасчетаСреднегоЗаработка В (&МассивПоПремиям) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоПремий,
	|	СУММА(ВЫБОР КОГДА ВидРасчетаСреднегоЗаработка В (&МассивПоЗаработку) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоЗаработок,
	|	СУММА(ВЫБОР КОГДА ВидРасчетаСреднегоЗаработка В (&МассивПоЗаработкуБЛ) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоБЛ,
	|	СУММА(ВЫБОР КОГДА ВидРасчетаСреднегоЗаработка В (&МассивПоЗаработкуОтпуск) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоОтпуск,
	|	МАКСИМУМ(ИмяПВР) КАК ИмяПВР,
	|	МАКСИМУМ(ВидРасчетаНаименование) КАК ВидРасчетаНаименование
	|ПО 
	|	ВидРасчета
	|";	
	
	ВыборкаВР = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаВР.Следующий() Цикл
	
		СтрокаНачалаСообщенияОбОшибке = """"+ СокрЛП(ВыборкаВР.ВидРасчетаНаименование) + """ из """ + 
											СокрЛП(ВыборкаВР.ИмяПВР) + """: ";
		ВыводитьДетали = Ложь;
		ТолькоПремии = Ложь;
		ТолькоЗаработок = Ложь;
		ТолькоЗаработокБЛ = Ложь;
		ТолькоЗаработокОтпуск = Ложь;
	
		Если ВыборкаВР.КоличествоПремий <> 0
				И (ВыборкаВР.КоличествоЗаработок <> 0 Или ВыборкаВР.КоличествоБЛ <> 0 Или ВыборкаВР.КоличествоОтпуск <> 0) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + " может быть указан в базовых видов расчета либо для премий либо обычных видов среднего заработка:", Отказ);
			ВыводитьДетали = Истина;
		Иначе
		
			Если ВыборкаВР.КоличествоПремий > 1 Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + " должен быть указан в базовых видов расчета только одного вида премий:", Отказ);
				ВыводитьДетали = Истина;
				ТолькоПремии = Истина;
			КонецЕсли;

			Если ВыборкаВР.КоличествоЗаработок > 1 Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + " должен быть указан в базовых видов расчета только одного вида заработка:", Отказ);
				ВыводитьДетали = Истина;
				ТолькоЗаработок = Истина;
			КонецЕсли;

			Если ВыборкаВР.КоличествоБЛ > 1 Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + " должен быть указан в базовых видов расчета только одного вида заработка для больничного листа:", Отказ);
				ВыводитьДетали = Истина;
				ТолькоЗаработокБЛ = Истина;
			КонецЕсли;

			Если ВыборкаВР.КоличествоОтпуск > 1 Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + " должен быть указан в базовых видов расчета только одного вида заработка для отпуска:", Отказ);
				ВыводитьДетали = Истина;
				ТолькоЗаработокОтпуск = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ВыводитьДетали Тогда
			ВыборкаДетали = ВыборкаВР.Выбрать();
			Пока ВыборкаДетали.Следующий() Цикл
				МожноВыводить = Ложь;
				
				Если ТолькоПремии Тогда
					МожноВыводить = МожноВыводить Или (СписокПоПремиям.НайтиПоЗначению(ВыборкаДетали.ВидРасчетаСреднегоЗаработка) <> Неопределено);
				КонецЕсли;
				
				Если ТолькоЗаработок Тогда
					МожноВыводить = МожноВыводить Или (СписокПоЗаработку.НайтиПоЗначению(ВыборкаДетали.ВидРасчетаСреднегоЗаработка) <> Неопределено);
				КонецЕсли;

				Если ТолькоЗаработокБЛ Тогда
					МожноВыводить = МожноВыводить Или (СписокПоЗаработкуБЛ.НайтиПоЗначению(ВыборкаДетали.ВидРасчетаСреднегоЗаработка) <> Неопределено);
				КонецЕсли;

				Если ТолькоЗаработокОтпуск Тогда
					МожноВыводить = МожноВыводить Или (СписокПоЗаработкуОтпуск.НайтиПоЗначению(ВыборкаДетали.ВидРасчетаСреднегоЗаработка) <> Неопределено);
				КонецЕсли;
				
				Если МожноВыводить Тогда
					ОбщегоНазначения.СообщитьОбОшибке("   " + ВыборкаДетали.ВидРасчетаСреднегоЗаработкаНаименование);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
