
// Запись перерасчетов
// Процедура предназначена для записи наборов записей рег-ов ЗаполнениеПлановыхНачислений для тех документов, 
// которые затронуты данным набором записей регистра
// Если набор записей ПлановыеНачисленияРаботниковОрганизаций записывается с датами, после которых проводились 
// начисления зарплаты (по тем же физлицам, по которым записываем начисления), то нужно переначислить 
// зарплату (т.е. перезаполнить соответсвующие документы Начисление зарплаты)
// 
// Параметры:
//	нет
// Возвращаемое значение:
//	нет
//
Процедура ЗаписьПерерасчетов()
	Запрос = Новый Запрос;
	
	// выберем расчеты по среднему заработку за период, в котором произошло изменение коэффициента индексации
	ЗапросТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Расчеты.ИмяРегистра,
	|	Расчеты.Регистратор КАК ОбъектПерерасчета,
	|	Расчеты.ФизЛицо,
	|	Расчеты.Сотрудник,
	|	Расчеты.Организация
	|ИЗ
	|	(ВЫБРАТЬ
	|		""ОсновныеНачисленияРаботниковОрганизаций"" КАК ИмяРегистра,
	|		Расчеты.Регистратор,
	|		Расчеты.ФизЛицо,
	|		Расчеты.Сотрудник,
	|		Расчеты.Организация
	|	ИЗ
	|		РегистрСведений.КоэффициентыИндексацииЗаработка КАК КоэффициентыИндексацииЗаработка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК Расчеты
	|			ПО (КоэффициентыИндексацииЗаработка.Период МЕЖДУ Расчеты.ПериодРасчетаСреднегоЗаработкаНачало И Расчеты.ПериодРасчетаСреднегоЗаработкаОкончание // повышение в предыдущие 12 месяцев
	|					ИЛИ КоэффициентыИндексацииЗаработка.Период МЕЖДУ НАЧАЛОПЕРИОДА(Расчеты.ПериодДействияНачало, МЕСЯЦ) И Расчеты.ПериодДействияКонец) // или повышение в период оплаты по среднему заработку или до начала события
	|				И КоэффициентыИндексацииЗаработка.Сотрудник = Расчеты.Сотрудник 	
	|				И КоэффициентыИндексацииЗаработка.Организация = Расчеты.Организация
	|
	|	ГДЕ
	|		КоэффициентыИндексацииЗаработка.Регистратор = &Регистратор
	|		И Расчеты.ПериодРасчетаСреднегоЗаработкаНачало <> ДАТАВРЕМЯ(1, 1, 1) // расчет связан со средним заработком
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		""ДополнительныеНачисленияРаботниковОрганизаций"" КАК ИмяРегистра,
	|		Расчеты.Регистратор,
	|		Расчеты.ФизЛицо,
	|		Расчеты.Сотрудник,
	|		Расчеты.Организация
	|	ИЗ
	|		РегистрСведений.КоэффициентыИндексацииЗаработка КАК КоэффициентыИндексацииЗаработка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК Расчеты
	|			ПО (КоэффициентыИндексацииЗаработка.Период МЕЖДУ Расчеты.ПериодРасчетаСреднегоЗаработкаНачало И Расчеты.ПериодРасчетаСреднегоЗаработкаОкончание
	|					ИЛИ КоэффициентыИндексацииЗаработка.Период МЕЖДУ НАЧАЛОПЕРИОДА(Расчеты.ДатаНачалаСобытия, МЕСЯЦ) И Расчеты.ДатаНачалаСобытия)
	|				И КоэффициентыИндексацииЗаработка.Сотрудник = Расчеты.Сотрудник 	
	|				И КоэффициентыИндексацииЗаработка.Организация = Расчеты.Организация
	|
	|	ГДЕ
	|		КоэффициентыИндексацииЗаработка.Регистратор = &Регистратор
	|		И Расчеты.ПериодРасчетаСреднегоЗаработкаНачало <> ДАТАВРЕМЯ(1, 1, 1)) КАК Расчеты
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчеты1
	|		ПО Перерасчеты1.ОбъектПерерасчета = Расчеты.Регистратор
	|			И Перерасчеты1.Сотрудник = Расчеты.Сотрудник
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК Перерасчеты2
	|		ПО Перерасчеты2.ОбъектПерерасчета = Расчеты.Регистратор
	|			И Перерасчеты2.Сотрудник = Расчеты.Сотрудник
	|
	|ГДЕ
	|	Перерасчеты1.ОбъектПерерасчета ЕСТЬ NULL 
	|	И Перерасчеты2.ОбъектПерерасчета ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	Расчеты.Регистратор,
	|	Расчеты.ИмяРегистра";
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Текст = ЗапросТекст;
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборОсновные = РегистрыРасчета.ОсновныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетОсновныхНачислений.СоздатьНаборЗаписей();
	НаборОсновные.Отбор.ОбъектПерерасчета.Использование = Истина;
	
	НаборДополнительные = РегистрыРасчета.ДополнительныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетДополнительныхНачислений.СоздатьНаборЗаписей();
	НаборДополнительные.Отбор.ОбъектПерерасчета.Использование = Истина;
	
	ЕстьДанные = Выборка.Следующий();
	Пока ЕстьДанные Цикл
	
		ТекущийОбъектПерерасчета = Выборка.ОбъектПерерасчета;
		ТекущееИмяРегистра = Выборка.ИмяРегистра;
		
		НаборОсновные.Очистить();
		НаборДополнительные.Очистить();

		Пока ЕстьДанные
				И ТекущийОбъектПерерасчета = Выборка.ОбъектПерерасчета
				И ТекущееИмяРегистра = Выборка.ИмяРегистра Цикл
		
			Если Выборка.ИмяРегистра = "ОсновныеНачисленияРаботниковОрганизаций" Тогда
				НаборОсновные.Отбор.ОбъектПерерасчета.Значение = ТекущийОбъектПерерасчета;
				ЗаписьНабора = НаборОсновные.Добавить();
			Иначе
				НаборДополнительные.Отбор.ОбъектПерерасчета.Значение = ТекущийОбъектПерерасчета;
				ЗаписьНабора = НаборДополнительные.Добавить();
			КонецЕсли;
			
			ЗаписьНабора.ОбъектПерерасчета 	= ТекущийОбъектПерерасчета;
			ЗаписьНабора.ФизЛицо			= Выборка.ФизЛицо;
			ЗаписьНабора.Сотрудник			= Выборка.Сотрудник;
			ЗаписьНабора.Организация		= Выборка.Организация;
			
			ЕстьДанные = Выборка.Следующий();
				
		КонецЦикла;
	
		Если НаборОсновные.Количество() > 0 Тогда
			// дописываем набор записей по данному объекту перерасчета
			НаборОсновные.Записать(Ложь);
		КонецЕсли;
		
		Если НаборДополнительные.Количество() > 0 Тогда
			НаборДополнительные.Записать(Ложь);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Замещение Тогда
		// запишем перерасчеты по тем записям, которые сейчас будут замещены
		ЗаписьПерерасчетов();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// запишем перерасчеты по новым записям
	ЗаписьПерерасчетов();
	
КонецПроцедуры