
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	фин_НастройкаДополнительныхРазрезовБюджетирования.Измерение
		|ИЗ
		|	РегистрСведений.фин_НастройкаДополнительныхРазрезовБюджетирования КАК фин_НастройкаДополнительныхРазрезовБюджетирования";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ИспользованныеРазрезы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Измерение");
	Для Инд = 1 По 10 Цикл
		ЭлементПеречисления = Перечисления.фин_ФактическиеПоказателиБюджетирования["Разрез"+Строка(Инд)];
		Если ИспользованныеРазрезы.Найти(ЭлементПеречисления) = Неопределено Тогда
			СписокИзмерений.Добавить(ЭлементПеречисления);
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ФинансовыеПоказателиРазрезыУчета.Ссылка
			|ИЗ
			|	Справочник.фин_ФинансовыеПоказатели.РазрезыУчета КАК ФинансовыеПоказателиРазрезыУчета
			|ГДЕ
			|	ФинансовыеПоказателиРазрезыУчета.Измерение = &Измерение
			|
			|УПОРЯДОЧИТЬ ПО
			|	ФинансовыеПоказателиРазрезыУчета.Ссылка.Наименование";

		Запрос.УстановитьПараметр("Измерение", Запись.Измерение);

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		
		Если НЕ Результат.Пустой() Тогда
			ТолькоПросмотр = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Невозможно изменить разрез, используемый в финансовых показателях!");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Разрез "+Запись.Измерение+"/"+Запись.Разрез+" используется в финансовых показателях:");
		КонецЕсли;

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("	- "+ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
	ИначеЕсли СписокИзмерений.Количество()>0 Тогда
		Запись.Измерение = СписокИзмерений[0].Значение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазрезНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Список = Новый СписокЗначений;
	Список.Добавить(ПредопределенноеЗначение("ПланВидовХарактеристик.фин_ВидыСубконтоБюджетирования.Контрагенты"));
	Список.Добавить(ПредопределенноеЗначение("ПланВидовХарактеристик.фин_ВидыСубконтоБюджетирования.Номенклатура"));
	Список.Добавить(ПредопределенноеЗначение("ПланВидовХарактеристик.фин_ВидыСубконтоБюджетирования.Проекты"));
	Список.Добавить(ПредопределенноеЗначение("ПланВидовХарактеристик.фин_ВидыСубконтоБюджетирования.СтатьиОборотов"));
	Список.Добавить(ПредопределенноеЗначение("ПланВидовХарактеристик.фин_ВидыСубконтоБюджетирования.ЦФО"));
	ОткрытьФорму("ПланВидовХарактеристик.фин_ВидыСубконтоБюджетирования.ФормаВыбора",Новый Структура("Исключения",Список),Элемент,УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура РазрезПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Запись.Разрез) Тогда
		РазрезПриИзмененииНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РазрезПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(Запись.Разрез) Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(фин_ОбщегоНазначенияВызовСервераПовтИсп.ТипСправочникУправленческихПодразделений());
		МассивТипов.Добавить(фин_ОбщегоНазначенияВызовСервераПовтИсп.ТипСправочникаПроектов());
		МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
		МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура"));
		Для Каждого Тип ИЗ МассивТипов Цикл
			Если Запись.Разрез.ТипЗначения.СодержитТип(Тип) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выбранный аналитический разрез использует нежелательный для дополнительных разрезов тип """+Строка(тип)+""", т.к. этот тип используется в постоянных разрезах бюджетирования!");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзмерениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = СписокИзмерений;
КонецПроцедуры
