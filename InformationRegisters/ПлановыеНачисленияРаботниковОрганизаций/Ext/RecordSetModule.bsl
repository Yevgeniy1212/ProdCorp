// Запись перерасчетов
// Процедура предназначена для записи наборов записей рег-ов ЗаполнениеПлановыхНачислений для тех документов, 
// которые затронуты данным набором записей регистра
// Если набор записей ПлановыеНачисленияРаботниковОрганизаций записывается с датами, после которых проводились 
// начисления зарплаты (по тем же физлицам, по которым записываем начисления), то нужно переначислить 
// зарплату (т.е. перезаполнить соответсвующие документы Начисление зарплаты)
// 
// Параметры:
//	нет
// Возвращаемое значение:
//	нет
//
Процедура ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхНачислений()
	
	Запрос = Новый Запрос;

	ЗапросТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК ОбъектЗаполнения,
	|	Расчеты.Сотрудник,
	|	Расчеты.ПериодРегистрации,
	|	Расчеты.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	Перерасчеты.ОбъектЗаполнения КАК ОбъектЗаполнения1
	|ИЗ
	|	(ВЫБРАТЬ
	|		Расчеты.Регистратор КАК Регистратор,
	|		Расчеты.Сотрудник КАК Сотрудник,
	|		Расчеты.ПериодРегистрации КАК ПериодРегистрации,
	|		Расчеты.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|	ИЗ
	|		РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисления
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК Расчеты
	|			ПО (Расчеты.ПериодДействияКонец >= ПлановыеНачисления.Период)
	|				И ПлановыеНачисления.Сотрудник = Расчеты.Сотрудник
	|				И ПлановыеНачисления.Организация = Расчеты.Организация
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеЗарплатыРаботникамОрганизаций КАК ДокНачисление
	|			ПО (Расчеты.Регистратор = ДокНачисление.Ссылка)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ДокументыИсправления
	|			ПО (Расчеты.Регистратор = ДокументыИсправления.СторнируемыйДокумент)
	|				И ПлановыеНачисления.Сотрудник = ДокументыИсправления.Сотрудник
	|				И ПлановыеНачисления.Организация = ДокументыИсправления.Организация
	|	ГДЕ
	|		ПлановыеНачисления.Регистратор = &Регистратор
	|		И (НЕ ДокНачисление.Ссылка ЕСТЬ NULL )
	|		И ДокументыИсправления.СторнируемыйДокумент ЕСТЬ NULL 
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Расчеты.Регистратор,
	|		Расчеты.Сотрудник,
	|		НАЧАЛОПЕРИОДА(Расчеты.ПериодРегистрации, МЕСЯЦ),
	|		Расчеты.ОбособленноеПодразделение
	|	ИЗ
	|		РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисления
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК Расчеты
	|			ПО (КОНЕЦПЕРИОДА(Расчеты.ПериодРегистрации, МЕСЯЦ) >= ПлановыеНачисления.Период)
	|				И ПлановыеНачисления.Сотрудник = Расчеты.Сотрудник
	|				И ПлановыеНачисления.Организация = Расчеты.Организация
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеЗарплатыРаботникамОрганизаций КАК ДокНачисление
	|			ПО (Расчеты.Регистратор = ДокНачисление.Ссылка)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДокументыИсправления
	|			ПО (Расчеты.Регистратор = ДокументыИсправления.СторнируемыйДокумент)
	|	ГДЕ
	|		ПлановыеНачисления.Регистратор = &Регистратор
	|		И (НЕ ДокНачисление.Ссылка ЕСТЬ NULL )
	|		И ДокументыИсправления.СторнируемыйДокумент ЕСТЬ NULL ) КАК Расчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаполнениеПлановыхНачислений КАК Перерасчеты
	|		ПО (Перерасчеты.ОбъектЗаполнения = Расчеты.Регистратор)
	|			И (Перерасчеты.Сотрудник = Расчеты.Сотрудник)
	|ГДЕ
	|	Перерасчеты.ОбъектЗаполнения ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбъектЗаполнения";
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
				
	Запрос.Текст = ЗапросТекст;
	Выборка = Запрос.Выполнить().Выбрать();
	ПроведениеРасчетов.ДописатьПерерасчетыВЗаполнениеПлановыхНачислений(Выборка);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Замещение Тогда
		// запишем перерасчеты по тем записям, которые сейчас будут замещены
		ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхНачислений();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// запишем перерасчеты по новым записям
	ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхНачислений();
	
КонецПроцедуры
