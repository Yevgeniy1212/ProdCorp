
// Обработчик события "ПередЗаписью" набора записей
//
Процедура ПередЗаписью(Отказ, Замещение)

	// отметим как Неопределено значение глобальной переменной
	глЗначениеПеременнойУстановить("глИсчислениеНалоговСтруктурныхЕдиниц", Неопределено, Истина);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		// подразделение, указываемое в регистре должно иметь признак Структурного подразделения
		Если ТипЗнч(ЗаписьНабора.СтруктурнаяЕдиница) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			Если ЗаписьНабора.СтруктурнаяЕдиница.ЯвляетсяСтруктурнымПодразделением = Ложь Тогда
				ОбщегоНазначения.СообщитьОбОшибке("" + ЗаписьНабора.СтруктурнаяЕдиница + " не отмечено в справочнике подразделений организаций как структурное!", Отказ);
			КонецЕсли;
		КонецЕсли;
					
		// Для НДС и КПН структурным подразделением не может являться подразделение
		Если ЗаписьНабора.РазделНалоговогоУчета = Перечисления.РазделыНалоговогоУчета.НДС
				Или ЗаписьНабора.РазделНалоговогоУчета = Перечисления.РазделыНалоговогоУчета.КПН Тогда
				
			Если ТипЗнч(ЗаписьНабора.СтруктурнаяЕдиница) <> Тип("СправочникСсылка.Организации") Тогда
				ОбщегоНазначения.СообщитьОбОшибке("" + ЗаписьНабора.СтруктурнаяЕдиница + " - " + ЗаписьНабора.РазделНалоговогоУчета + ": в качестве структурной единицы может выступать только организация!", Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		// проверяем заполнен ли ресурс "Налогоплательщик" - он должен быть обязательно заполнен
		Если НЕ ЗначениеЗаполнено(ЗаписьНабора.Налогоплательщик) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Запись не верна! Значение поля ""Налогоплательщик"" должно быть обязательно заполнено!", Отказ);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // ПередЗаписью()
