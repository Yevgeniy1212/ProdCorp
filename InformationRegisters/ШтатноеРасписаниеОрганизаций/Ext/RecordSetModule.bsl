////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура предназначена для записи наборов записей перерасчета для тех документов, которые затронуты
// данным набором записей регистра
// Если набор записей ШтатноеРасписаниеОрганизаций записывается с датами, после которых проводились 
// начисления зарплаты (по тем же физлицам, по которым записываем начисления), то нужно переначислить 
// зарплату (т.е. перезаполнить соответсвующие документы Начисление зарплаты)
// 
// Параметры:
//	нет
// Возвращаемое значение:
//	нет
//
Процедура ЗаписьПерерасчетов()
	
	Запрос = Новый Запрос;
	
	УсловиеТекст = "";
	Для Каждого ЭлементОтбора Из ЭтотОбъект.Отбор Цикл
		Если ЭлементОтбора.Использование Тогда
			Если НЕ ПустаяСтрока(УсловиеТекст) Тогда
				УсловиеТекст = УсловиеТекст + " И ";
			КонецЕсли;
			УсловиеТекст = УсловиеТекст + "ШтатноеРасписание." + ЭлементОтбора.Имя + " = &" + ЭлементОтбора.Имя;
			Запрос.УстановитьПараметр(ЭлементОтбора.Имя, ЭлементОтбора.Значение);
		КонецЕсли;

	КонецЦикла;
	Если НЕ ПустаяСтрока(УсловиеТекст) Тогда
		УсловиеТекст = "ГДЕ " + УсловиеТекст;
	КонецЕсли;
	
	ЗапросТекст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВыбранныеЗаписи.Регистратор КАК Регистратор,
		|	ВыбранныеЗаписи.Сотрудник,
		|	ВыбранныеЗаписи.ФизЛицо,
		|	ВыбранныеЗаписи.Организация
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Расчеты.Регистратор КАК Регистратор,
		|		Расчеты.Сотрудник КАК Сотрудник,
		|		Расчеты.ФизЛицо КАК ФизЛицо,
		|		Расчеты.Организация КАК Организация
		|	ИЗ
		|		РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписание
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
		|			ПО РаботникиОрганизации.ПодразделениеОрганизации = ШтатноеРасписание.ПодразделениеОрганизации 
		|				И РаботникиОрганизации.Должность = ШтатноеРасписание.Должность
		|				И РаботникиОрганизации.ТарифныйРазряд = ШтатноеРасписание.ТарифныйРазряд
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК Расчеты
		|			ПО Расчеты.Сотрудник = РаботникиОрганизации.Сотрудник 
		|				И Расчеты.Организация = РаботникиОрганизации.Организация 
		|				И Расчеты.ПериодДействияНачало >= ШтатноеРасписание.Период
		|	
		|	" + УсловиеТекст + ") КАК ВыбранныеЗаписи
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчеты
		|		ПО Перерасчеты.ОбъектПерерасчета = ВыбранныеЗаписи.Регистратор 
		|			И Перерасчеты.Сотрудник = ВыбранныеЗаписи.Сотрудник 
		|			И (Перерасчеты.ВидРасчета = &парамПустойВидРасчета)
		|
		|ГДЕ
		|	((Перерасчеты.ОбъектПерерасчета) ЕСТЬ NULL )
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
	
	Запрос.УстановитьПараметр("парамПустойВидРасчета", ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка());
	Запрос.Текст = ЗапросТекст;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПроведениеРасчетов.ДописатьПерерасчетыОсновныхНачислений(Выборка);
	
КонецПроцедуры

Процедура ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхНачислений()
	
	Запрос = Новый Запрос;

	УсловиеТекст = "";
	Для Каждого ЭлементОтбора Из ЭтотОбъект.Отбор Цикл
		Если ЭлементОтбора.Использование Тогда
			УсловиеТекст = УсловиеТекст + " И ШтатноеРасписание." + ЭлементОтбора.Имя + " = &" + ЭлементОтбора.Имя;
			Запрос.УстановитьПараметр(ЭлементОтбора.Имя, ЭлементОтбора.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗапросТекст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор КАК ОбъектЗаполнения,
		|	Расчеты.Сотрудник,
		|	Расчеты.ОбособленноеПодразделение,
		|	Расчеты.ПериодРегистрации
		|ИЗ
		|	РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписание
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
		|		ПО ШтатноеРасписание.ПодразделениеОрганизации = РаботникиОрганизации.ПодразделениеОрганизации
		|			И ШтатноеРасписание.Должность = РаботникиОрганизации.Должность
		|			И ШтатноеРасписание.ТарифныйРазряд = РаботникиОрганизации.ТарифныйРазряд
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК Расчеты
		|		ПО Расчеты.Сотрудник = РаботникиОрганизации.Сотрудник
		|			И Расчеты.Организация = РаботникиОрганизации.Организация
		|			И Расчеты.ПериодДействияНачало >= ШтатноеРасписание.Период
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаполнениеПлановыхНачислений КАК Перерасчеты
		|		ПО Перерасчеты.ОбъектЗаполнения = Расчеты.Регистратор
		|			И Перерасчеты.Сотрудник = Расчеты.Сотрудник
		|ГДЕ
		|	Расчеты.Регистратор ССЫЛКА Документ.НачислениеЗарплатыРаботникамОрганизаций
		|	И Перерасчеты.ОбъектЗаполнения ЕСТЬ NULL " + УсловиеТекст + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОбъектЗаполнения";

	Запрос.Текст = ЗапросТекст;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПроведениеРасчетов.ДописатьПерерасчетыВЗаполнениеПлановыхНачислений(Выборка);
		
КонецПроцедуры // ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхНачислений()

// Обработчик события ПередЗаписью набора записей регистра сведений
Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Замещение Тогда
		// запишем перерасчеты по тем записям, которые сейчас будут замещены
		ЗаписьПерерасчетов();
		ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхНачислений();
	КонецЕсли;
	
КонецПроцедуры          

// Обработчик события ПриЗаписи набора записей регистра сведений
Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	// запишем перерасчеты по новым записям
	ЗаписьПерерасчетов();
	ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхНачислений();
	
КонецПроцедуры