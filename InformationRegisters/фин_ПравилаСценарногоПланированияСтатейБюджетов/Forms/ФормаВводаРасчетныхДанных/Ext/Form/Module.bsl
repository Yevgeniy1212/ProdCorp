
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Адрес") Тогда
		ТаблицаОтбора = ПолучитьИзВременногоХранилища(Параметры.Адрес);
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	фин_СтатьиБюджета.Ссылка КАК Статья
			|ПОМЕСТИТЬ ВТ_Статьи
			|ИЗ
			|	Справочник.фин_СтатьиБюджета КАК фин_СтатьиБюджета
			|ГДЕ
			|	фин_СтатьиБюджета.Владелец = &Бюджет
			|	И фин_СтатьиБюджета.ЭтоГруппа = ЛОЖЬ
			|	И фин_СтатьиБюджета.ПометкаУдаления = ЛОЖЬ
			|	И ВЫБОР
			|			КОГДА &ИерархияСтатей
			|				ТОГДА фин_СтатьиБюджета.Ссылка В ИЕРАРХИИ (&Статьи)
			|			ИНАЧЕ фин_СтатьиБюджета.Ссылка В (&Статьи)
			|		КОНЕЦ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СценарииПланирования.Ссылка КАК Сценарий
			|ПОМЕСТИТЬ ВТ_Сценарии
			|ИЗ
			|	Справочник."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"СценарииПланирования КАК СценарииПланирования
			|ГДЕ
			|	ВЫБОР
			|			КОГДА &ИерархияСценарий
			|				ТОГДА СценарииПланирования.Ссылка В ИЕРАРХИИ (&Сценарии)
			|			ИНАЧЕ СценарииПланирования.Ссылка В (&Сценарии)
			|		КОНЕЦ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ_Статьи.Статья,
			|	ВТ_Сценарии.Сценарий
			|ПОМЕСТИТЬ ВТ_Кортеж
			|ИЗ
			|	ВТ_Статьи КАК ВТ_Статьи
			|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Сценарии КАК ВТ_Сценарии
			|		ПО (ИСТИНА)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.ИсходныйСценарий,
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.ПоправочныйКоэффициентКСумме,
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.ПоправочныйКоэффициентККоличеству,
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.Лаг,
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.СпособЗаполнения,
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.Формула,
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.ФормулаКоличество,
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.СуммаПоРасценкам
			|ИЗ
			|	ВТ_Кортеж КАК ВТ_Кортеж
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.фин_ПравилаСценарногоПланированияСтатейБюджетов КАК фин_ПравилаСценарногоПланированияСтатейБюджетов
			|		ПО ВТ_Кортеж.Статья = фин_ПравилаСценарногоПланированияСтатейБюджетов.СтатьяБюджета
			|			И ВТ_Кортеж.Сценарий = фин_ПравилаСценарногоПланированияСтатейБюджетов.ФормируемыйСценарий
			|ГДЕ
			|	фин_ПравилаСценарногоПланированияСтатейБюджетов.Бюджет = &Бюджет";
		ИерархияСценарий = Ложь;
		ИерархияСтатей = Ложь;
		СценарииОтбора = Новый СписокЗначений;
		СтатьиОтбора = Новый СписокЗначений;
		
		СтрокиСтатья = ТаблицаОтбора.НайтиСтроки(Новый Структура("Поле","Статья"));
		Если СтрокиСтатья.Количество()>0 Тогда
			СтрокаТаблицы = СтрокиСтатья[0];
			ИерархияСтатей = СтрокаТаблицы.ВидСравнения = "ВИерархии";
			СтатьиОтбора.Добавить(СтрокаТаблицы.Значение);
		КонецЕсли;
		
		СтрокиПодразделение = ТаблицаОтбора.НайтиСтроки(Новый Структура("Поле","Сценарий"));
		Если СтрокиПодразделение.Количество()>0 Тогда
			СтрокаТаблицы = СтрокиПодразделение[0];
			ИерархияСценарий = СтрокаТаблицы.ВидСравнения = "ВИерархии";
			СценарииОтбора.Добавить(СтрокаТаблицы.Значение);
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Значение) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не определен сценарий!");
				Возврат;
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не определен сценарий!");
			Возврат;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Бюджет", Параметры.Бюджет);
		Запрос.УстановитьПараметр("ИерархияСценарий", ИерархияСценарий);
		Запрос.УстановитьПараметр("ИерархияСтатей", ИерархияСтатей);
		Запрос.УстановитьПараметр("Сценарии", СценарииОтбора);
		Запрос.УстановитьПараметр("Статьи", СтатьиОтбора);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект,ВыборкаДетальныеЗаписи);
		КонецЕсли;
	КонецЕсли;
	Если ПоправочныйКоэффициентККоличеству =0 И ПоправочныйКоэффициентКСумме=0 Тогда
		ПоправочныйКоэффициентККоличеству = 1;
		ПоправочныйКоэффициентКСумме = 1;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Форма.Элементы.ГруппаКоэффициенты.Видимость = Форма.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.СПрименениемКоэффициента");
	Форма.Элементы.ГруппаФормулы.Видимость 		= Форма.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.ПоФормуле");
	Форма.Элементы.ИсходныйСценарий.Видимость 	= Форма.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.ПоФормуле") ИЛИ Форма.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.СПрименениемКоэффициента");
	Форма.Элементы.Формула.Видимость 			= НЕ Форма.СуммаПоРасценкам;
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияПриИзменении(Элемент)
	Если СпособЗаполнения <> ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.СПрименениемКоэффициента") Тогда
		ПоправочныйКоэффициентККоличеству 	= 1;
		ПоправочныйКоэффициентКСумме 		= 1;
		Лаг 									= 0;
	КонецЕсли;
	Если СпособЗаполнения <> ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.ПоФормуле") Тогда
		Формула 				= ПредопределенноеЗначение("Справочник.фин_ФормулыРасчетаФинансовыхПоказателей.ПустаяСсылка");
		ФормулаКоличество 	= ПредопределенноеЗначение("Справочник.фин_ФормулыРасчетаФинансовыхПоказателей.ПустаяСсылка");
		СуммаПоРасценкам 	= Ложь;
	КонецЕсли;
	Если СпособЗаполнения <> ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.СПрименениемКоэффициента") И СпособЗаполнения <> ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.ПоФормуле") Тогда
		ИсходныйСценарий = фин_ОбщегоНазначенияКлиентПовтИсп.ПустаяСсылкаСценарий();	
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СуммаПоРасценкамПриИзменении(Элемент)
	Если СуммаПоРасценкам Тогда
		Формула 				= ПредопределенноеЗначение("Справочник.фин_ФормулыРасчетаФинансовыхПоказателей.ПустаяСсылка");
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(Новый Структура("СпособЗаполнения,ИсходныйСценарий,ПоправочныйКоэффициентКСумме,ПоправочныйКоэффициентККоличеству,Лаг,Формула,ФормулаКоличество,СуммаПоРасценкам",СпособЗаполнения,ИсходныйСценарий,ПоправочныйКоэффициентКСумме,ПоправочныйКоэффициентККоличеству,Лаг,Формула,ФормулаКоличество,СуммаПоРасценкам));
КонецПроцедуры
