
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Запись.ИсходныйКлючЗаписи.Пустой() И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Запись.ПоправочныйКоэффициентККоличеству = 1;
		Запись.ПоправочныйКоэффициентКСумме = 1;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Форма.Элементы.ГруппаКоэффициенты.Видимость = Форма.Запись.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.СПрименениемКоэффициента");
	Форма.Элементы.ГруппаФормулы.Видимость 		= Форма.Запись.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.ПоФормуле");
	Форма.Элементы.ИсходныйСценарий.Видимость 	= Форма.Запись.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.ПоФормуле") ИЛИ Форма.Запись.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.СПрименениемКоэффициента");
	Форма.Элементы.Формула.Видимость 			= НЕ Форма.Запись.СуммаПоРасценкам;
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияПриИзменении(Элемент)
	Если Запись.СпособЗаполнения <> ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.СПрименениемКоэффициента") Тогда
		Запись.ПоправочныйКоэффициентККоличеству 	= 1;
		Запись.ПоправочныйКоэффициентКСумме 		= 1;
		Запись.Лаг 									= 0;
	КонецЕсли;
	Если Запись.СпособЗаполнения <> ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.ПоФормуле") Тогда
		Запись.Формула 				= ПредопределенноеЗначение("Справочник.фин_ФормулыРасчетаФинансовыхПоказателей.ПустаяСсылка");
		Запись.ФормулаКоличество 	= ПредопределенноеЗначение("Справочник.фин_ФормулыРасчетаФинансовыхПоказателей.ПустаяСсылка");
		Запись.СуммаПоРасценкам 	= Ложь;
	КонецЕсли;
	Если Запись.СпособЗаполнения <> ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.СПрименениемКоэффициента") И Запись.СпособЗаполнения <> ПредопределенноеЗначение("Перечисление.фин_СпособыЗаполненияСтатейПриСценарныхРасчетах.ПоФормуле") Тогда
		Запись.ИсходныйСценарий = фин_ОбщегоНазначенияКлиентПовтИсп.ПустаяСсылкаСценарий();	
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СуммаПоРасценкамПриИзменении(Элемент)
	Если Запись.СуммаПоРасценкам Тогда
		Запись.Формула 				= ПредопределенноеЗначение("Справочник.фин_ФормулыРасчетаФинансовыхПоказателей.ПустаяСсылка");
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры
