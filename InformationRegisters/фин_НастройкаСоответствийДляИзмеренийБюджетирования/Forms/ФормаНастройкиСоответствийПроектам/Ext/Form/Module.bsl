
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	фин_БюджетированиеОбщегоНазначения.НастроитьОформлениеТабличногоПоля(Элементы.Список);
	Если Параметры.Свойство("Проект",Проект) Тогда
		Элементы.СписокПроект.Видимость=Ложь;	
	КонецЕсли;
	Если Параметры.Свойство("ИсходноеЗначение",ИсходноеЗначение) Тогда
		Элементы.СписокИсходноеЗначение.Видимость=Ложь;	
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	фин_НастройкаСоответствийДляИзмеренийБюджетирования.Период КАК Период,
		|	фин_НастройкаСоответствийДляИзмеренийБюджетирования.ИсходноеЗначение,
		|	фин_НастройкаСоответствийДляИзмеренийБюджетирования.Соответствие КАК Проект
		|ИЗ
		|	РегистрСведений.фин_НастройкаСоответствийДляИзмеренийБюджетирования КАК фин_НастройкаСоответствийДляИзмеренийБюджетирования
		|ГДЕ
		|	фин_НастройкаСоответствийДляИзмеренийБюджетирования.Разрез = ЗНАЧЕНИЕ(Перечисление.фин_ФактическиеПоказателиБюджетирования.Проект)
		|	И ВЫБОР
		|			КОГДА &ОтборПоЗначению
		|				ТОГДА фин_НастройкаСоответствийДляИзмеренийБюджетирования.ИсходноеЗначение = &ИсходноеЗначение
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &ОтборПоПроекту
		|				ТОГДА фин_НастройкаСоответствийДляИзмеренийБюджетирования.Соответствие = &Соответствие
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Запрос.УстановитьПараметр("ИсходноеЗначение", 	ИсходноеЗначение);
	Запрос.УстановитьПараметр("ОтборПоЗначению", 	ЗначениеЗаполнено(ИсходноеЗначение));
	Запрос.УстановитьПараметр("ОтборПоПроекту", 	ЗначениеЗаполнено(Проект));
	Запрос.УстановитьПараметр("Соответствие", 		Проект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = СписокСоответствий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ВыборкаДетальныеЗаписи);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СписокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И НЕ Копирование Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		ТекущиеДанные.Период = ТекущаяДата();
		ТекущиеДанные.Проект = Проект;
		ТекущиеДанные.ИсходноеЗначение = ИсходноеЗначение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если СписокСоответствий.НайтиСтроки(Новый Структура("Период,ИсходноеЗначение",ТекущиеДанные.Период,ТекущиеДанные.ИсходноеЗначение)).Количество()>1 Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	Если ЗначениеЗаполнено(Проект) ИЛИ ЗначениеЗаполнено(ИсходноеЗначение) Тогда
		НаборЗаписей = РегистрыСведений.фин_НастройкаСоответствийДляИзмеренийБюджетирования.СоздатьНаборЗаписей();
		Если ЗначениеЗаполнено(Проект) Тогда
			НаборЗаписей.Отбор.Соответствие.Установить(Проект);
		КонецЕсли;
		Если ЗначениеЗаполнено(ИсходноеЗначение) Тогда
			НаборЗаписей.Отбор.ИсходноеЗначение.Установить(ИсходноеЗначение);
		КонецЕсли;
		Для Каждого СтрокаТЧ Из СписокСоответствий Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись,СтрокаТЧ);
			НоваяЗапись.Соответствие = СтрокаТЧ.Проект;
			НоваяЗапись.Разрез = Перечисления.фин_ФактическиеПоказателиБюджетирования.Проект;
			Если ЗначениеЗаполнено(Проект) Тогда
				НоваяЗапись.Соответствие=Проект;
			КонецЕсли;
			Если ЗначениеЗаполнено(ИсходноеЗначение) Тогда
				НоваяЗапись.ИсходноеЗначение=ИсходноеЗначение;
			КонецЕсли;
		КонецЦикла;
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	Если ЗаполнениеПроверено() Тогда
		ЗаписатьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если ЗаполнениеПроверено() Тогда
		ЗаписатьНаСервере();
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ЗаполнениеПроверено()
	ЕстьОшибки = Ложь;
	Для Каждого СтрокаТЧ Из СписокСоответствий Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Период) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В строке № "+СтрокаТЧ.НомерСтроки+" не указан период!");
			ЕстьОшибки=Истина;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Проект) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В строке № "+СтрокаТЧ.НомерСтроки+" не указан проект!");
			ЕстьОшибки=Истина;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ИсходноеЗначение) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В строке № "+СтрокаТЧ.НомерСтроки+" не указан аналитический разрез!");
			ЕстьОшибки=Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат НЕ ЕстьОшибки;
КонецФункции
