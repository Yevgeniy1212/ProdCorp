////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ГруппаАвторегистрация.Видимость=Объект.АвтоматическаяРегистрация;
	ВыборкаВидов = Справочники.фин_ВидыДокументов.Выбрать();
	Пока ВыборкаВидов.Следующий() Цикл
		Если ВыборкаВидов.ЭтоГруппа ИЛИ ВыборкаВидов.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		Если Метаданные.ПодпискиНаСобытия.дог_АвтоматическаяРегистрацияСобытийПоДоговорам.Источник.СодержитТип(Тип("ДокументОбъект."+ВыборкаВидов.ПрограммныйИдентификатор)) Тогда
			СписокВидовДокументов.Добавить(ВыборкаВидов.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура АвтоматическаяРегистрацияПриИзменении(Элемент)
	Элементы.ГруппаАвторегистрация.Видимость=Объект.АвтоматическаяРегистрация;
	Если НЕ Объект.АвтоматическаяРегистрация Тогда
		Объект.РеквизитВалютыСобытия = "";
		Объект.РеквизитРасчетаСуммыСобытия = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РегистрирующийДокументПриИзменении(Элемент)
	Объект.РеквизитВалютыСобытия = "";
	Объект.РеквизитРасчетаСуммыСобытия = "";
КонецПроцедуры

&НаКлиенте
Процедура РеквизитРасчетаСуммыСобытияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.РегистрирующийДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВыбораРеквизитаРасчетаСуммыСобытия", ЭтаФорма);
	ОткрытьФорму("Справочник.усд_УсловияВыполненияОперацийПоДокументам.Форма.ФормаВыбораРеквизита",Новый Структура("ТолькоДочерние,ВидДокументов,ВидДанных,ИмяИсточника,ТребуетсяАвтовыборПоСубконто,ТребуемыйТип",Истина,Объект.РегистрирующийДокумент,ПредопределенноеЗначение("Перечисление.фин_ВидыОтраженийПервичныхДокументовВБюджетировании.ПоШапкеДокумента"),"",Ложь,Тип("Число")), , , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура РеквизитВалютыСобытияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.РегистрирующийДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВыбораРеквизитаВалютыСобытия", ЭтаФорма);
	ОткрытьФорму("Справочник.усд_УсловияВыполненияОперацийПоДокументам.Форма.ФормаВыбораРеквизита",Новый Структура("ТолькоДочерние,ВидДокументов,ВидДанных,ИмяИсточника,ТребуетсяАвтовыборПоСубконто,ТребуемыйТип",Истина,Объект.РегистрирующийДокумент,ПредопределенноеЗначение("Перечисление.фин_ВидыОтраженийПервичныхДокументовВБюджетировании.ПоШапкеДокумента"),"",Ложь,Тип("СправочникСсылка.Валюты")), , , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура РегистрирующийДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = СписокВидовДокументов;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПослеЗакрытияВыбораРеквизитаРасчетаСуммыСобытия(Результат, ПараметрыЗакрытия) Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.РеквизитРасчетаСуммыСобытия = Результат.Путь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВыбораРеквизитаВалютыСобытия(Результат, ПараметрыЗакрытия) Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.РеквизитВалютыСобытия = Результат.Путь;
	КонецЕсли;
	
КонецПроцедуры