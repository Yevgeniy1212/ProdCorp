Перем ЗаписьИзФормыНабораЗаписей Экспорт;

// предопределенная процедура проверяет логическую 
// связанность введенных данных
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если (ОбменДанными.Загрузка) Или (Не ЗаписьИзФормыНабораЗаписей) Тогда
		Возврат
	КонецЕсли;

	Ошибки = Новый Массив;
	ПредыдущийПредел = 0;
	ПредыдущаяСтрока = Неопределено;	
	
	// проверяем набор записей
	Для Каждого Строка Из ЭтотОбъект Цикл
		
		Индекс = Индекс(Строка);
		
		Строка.НомерСтрокиСтавок = Индекс+1;
		
		Если Индекс = 0 И Строка.СуммаДоходаС <> 0 Тогда
			Ошибки.Добавить("Нижний предел по первой строке должен быть нулевым");
		КонецЕсли;
		Если ПредыдущийПредел <> Строка.СуммаДоходаС Тогда
			Ошибки.Добавить("В строке "+Строка.НомерСтрокиСтавок+" нижний предел не совпадает с верхним пределом предыдущей строки");
		КонецЕсли;
		Если Строка.СуммаДоходаС >= Строка.СуммаДоходаПо Тогда
			Ошибки.Добавить("В строке "+Строка.НомерСтрокиСтавок+" верхний предел должен быть больше нижнего");
		КонецЕсли;
		
		// Расчет суммы фиксированной ставки в тенге от процентов предыдущего предела
		Если Индекс <> 0 Тогда
			
			Если НЕ ЗначениеЗаполнено(Строка.СуммаНалогаПредыдущегоПредела) Тогда
				Строка.СуммаНалогаПредыдущегоПредела = Окр(ПредыдущаяСтрока.СуммаНалогаПредыдущегоПредела + (ПредыдущаяСтрока.СуммаДоходаПо - ПредыдущаяСтрока.СуммаДоходаС) * ПредыдущаяСтрока.Ставка / 100, 0);				
			КонецЕсли;
			
		КонецЕсли; 
		
		// запомним для следующей итерации
		ПредыдущийПредел = Строка.СуммаДоходаПо;
		ПредыдущаяСтрока = Строка;
		
	КонецЦикла;
	
	Если Ошибки.Количество() > 0 Тогда
		Сообщить("В сведениях о ставках ИПН и СН обнаружены ошибки. Сведения не записаны!");
		Для Каждого Строка Из Ошибки Цикл
			Сообщить(Строка);
		КонецЦикла;
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

ЗаписьИзФормыНабораЗаписей = Ложь;