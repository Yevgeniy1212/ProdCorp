
// Формирует текст запроса и исполняет его
//
// Параметры
//  ДатаНачала		– параметр запроса 
//  ДатаОкончания	– параметр запроса 
//
// Возвращаемое значение:
//  Результат выполнения запроса
//
Функция СформироватьЗапрос(ДатаНачала,ДатаОкончания)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачала",						ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",					ДатаОкончания);
	Запрос.УстановитьПараметр("ПланируемоеУвольнение",			Перечисления.НамеченныеСобытияПоПерсоналу.Увольнение);
	Запрос.УстановитьПараметр("ПланируемоеУвольнениеИспСрок",	Перечисления.НамеченныеСобытияПоПерсоналу.УвольнениеПослеИспытательногоСрока);
	Запрос.УстановитьПараметр("ПланируемоеПеремещение",			Перечисления.НамеченныеСобытияПоПерсоналу.Перемещение);
	
	// Описание текста запроса:
	// 1. Выборка "НамеченныеИзменения": 
	//      Представляет собой выборку НамеченныеИзмененияСДаннымиДляПроверок,
	//		расширенную."срезом последних" по регистру Работники.
	// 2. Выборка "НамеченныеИзмененияСДаннымиДляПроверок": 
	//      Представляет собой следующий вложенный запрос.
	//       1. Выборка "СписокИзменений":
	//      		Движения в регистре НамеченныеСобытияПоПерсоналу за указанный период.  
	//      		Для каждого движения в регистре НамеченныеСобытияПоПерсоналу выполняем 
    //      		"срез последних" по регистру СостояниеРаботников.
	//       2. Выборка "СостояниеРаботников": 
	//      		На дату "среза" из регистра СостояниеРаботников выбираем запись для 
    //      		проверки состояния работника.
	// 3. Выборка "Работники": 
	//		На дату "среза" из регистра Работники выбираем запись для определения
    //		должности и подразделения.
	//
	СписокСотрудниковТекст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(РаботникиОсновноеМесто.Сотрудник) КАК Сотрудник,
		|	РаботникиОсновноеМесто.Сотрудник.Физлицо КАК Физлицо,
		|	1 КАК Приоритет
		|ПОМЕСТИТЬ ВТСписокСотрудников
		|ИЗ
		|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаОкончания, Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы)) КАК РаботникиОсновноеМесто
		|ГДЕ
		|	РаботникиОсновноеМесто.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|
		|СГРУППИРОВАТЬ ПО
		|	РаботникиОсновноеМесто.Сотрудник.Физлицо
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(РаботникиСовместительство.Сотрудник),
		|	РаботникиСовместительство.Сотрудник.Физлицо,
		|	2
		|ИЗ
		|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаОкончания, Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.Совместительство)) КАК РаботникиСовместительство
		|ГДЕ
		|	РаботникиСовместительство.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|
		|СГРУППИРОВАТЬ ПО
		|	РаботникиСовместительство.Сотрудник.Физлицо
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(СотрудникиДУ.Ссылка),
		|	СотрудникиДУ.Физлицо,
		|	ВЫБОР
		|		КОГДА СотрудникиДУ.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ДоговорУправленческий)
		|			ТОГДА 3
		|		КОГДА СотрудникиДУ.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
		|			ТОГДА 4
		|		ИНАЧЕ 5
		|	КОНЕЦ
		|ИЗ
		|	Справочник.СотрудникиОрганизаций КАК СотрудникиДУ
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиДУ.Физлицо,
		|	ВЫБОР
		|		КОГДА СотрудникиДУ.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ДоговорУправленческий)
		|			ТОГДА 3
		|		КОГДА СотрудникиДУ.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
		|			ТОГДА 4
		|		ИНАЧЕ 5
		|	КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Физлицо,
		|	Приоритет";
	
	Запрос.Текст = СписокСотрудниковТекст;
	Запрос.Выполнить();
	
	СписокФизическихЛицТекст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СписокФизическихЛиц.Физлицо КАК Физлицо,
		|	МИНИМУМ(СписокФизическихЛиц.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВТСписокФизическихЛиц
		|ИЗ
		|	(ВЫБРАТЬ
		|		РаботникиОсновноеМесто.Сотрудник.Физлицо КАК Физлицо,
		|		1 КАК Приоритет
		|	ИЗ
		|		РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаОкончания, Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы)) КАК РаботникиОсновноеМесто
		|	ГДЕ
		|		РаботникиОсновноеМесто.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		РаботникиСовместительство.Сотрудник.Физлицо,
		|		2
		|	ИЗ
		|		РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаОкончания, Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.Совместительство)) КАК РаботникиСовместительство
		|	ГДЕ
		|		РаботникиСовместительство.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		СотрудникиДУ.Физлицо,
		|		ВЫБОР
		|			КОГДА СотрудникиДУ.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ДоговорУправленческий)
		|				ТОГДА 3
		|			КОГДА СотрудникиДУ.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
		|				ТОГДА 4
		|			ИНАЧЕ 5
		|		КОНЕЦ
		|	ИЗ
		|		Справочник.СотрудникиОрганизаций КАК СотрудникиДУ) КАК СписокФизическихЛиц
		|
		|СГРУППИРОВАТЬ ПО
		|	СписокФизическихЛиц.Физлицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Физлицо,
		|	Приоритет";
	Запрос.Текст = СписокФизическихЛицТекст;
	Запрос.Выполнить();
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	НамеченныеИзменения.ДатаИзменения,
	               |	НамеченныеИзменения.ПланируемоеСобытие,
	               |	НамеченныеИзменения.Подразделение КАК ПланируемоеПодразделение,
	               |	НамеченныеИзменения.Должность КАК ПланируемаяДолжность,
	               |	НамеченныеИзменения.ЗанимаемыхСтавок КАК ПланируемыхСтавок,
	               |	НамеченныеИзменения.ЗаниматьСтавку,
	               |	НамеченныеИзменения.Состояние,
	               |	Работники.Подразделение КАК Подразделение,
	               |	Работники.Должность КАК Должность,
				   |	Работники.ГрафикРаботы КАК ГрафикРаботы,
	               |	Работники.ЗанимаемыхСтавок,
				   |	Сотрудники.Сотрудник
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ДатаИзменения КАК ДатаИзменения,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ФизЛицо КАК ФизЛицо,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ПланируемоеСобытие КАК ПланируемоеСобытие,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.Подразделение КАК Подразделение,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.Должность КАК Должность,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ЗаниматьСтавку КАК ЗаниматьСтавку,
	               |		МАКСИМУМ(Работники.Период) КАК ДатаВРаботники,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.Состояние КАК Состояние,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения
	               |	ИЗ
	               |		(ВЫБРАТЬ
	               |			СписокИзменений.ДатаИзменения КАК ДатаИзменения,
	               |			СписокИзменений.ФизЛицо КАК ФизЛицо,
	               |			СписокИзменений.ПланируемоеСобытие КАК ПланируемоеСобытие,
	               |			СписокИзменений.Подразделение КАК Подразделение,
	               |			СписокИзменений.Должность КАК Должность,
	               |			СписокИзменений.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	               |			СписокИзменений.ЗаниматьСтавку КАК ЗаниматьСтавку,
	               |			СостояниеРаботников.Состояние КАК Состояние,
	               |			СписокИзменений.ДатаЗначения КАК ДатаПоследнегоИзменения
	               |		ИЗ
	               |			(ВЫБРАТЬ
	               |				НамеченныеСобытияПоПерсоналу.ДатаИзменения КАК ДатаИзменения,
	               |				НамеченныеСобытияПоПерсоналу.ФизЛицо КАК ФизЛицо,
	               |				НамеченныеСобытияПоПерсоналу.ПланируемоеСобытие КАК ПланируемоеСобытие,
	               |				НамеченныеСобытияПоПерсоналу.Подразделение КАК Подразделение,
	               |				НамеченныеСобытияПоПерсоналу.Должность КАК Должность,
	               |				НамеченныеСобытияПоПерсоналу.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	               |				НамеченныеСобытияПоПерсоналу.ЗаниматьСтавку КАК ЗаниматьСтавку,
	               |				МАКСИМУМ(СостояниеВнутри.Период) КАК ДатаЗначения
	               |			ИЗ
	               |				РегистрСведений.НамеченныеСобытияПоПерсоналу КАК НамеченныеСобытияПоПерсоналу
	               |					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботников КАК СостояниеВнутри
	               |					ПО СостояниеВнутри.ФизЛицо = НамеченныеСобытияПоПерсоналу.ФизЛицо И (ВЫБОР КОГДА НамеченныеСобытияПоПерсоналу.ПланируемоеСобытие = &ПланируемоеУвольнение ИЛИ НамеченныеСобытияПоПерсоналу.ПланируемоеСобытие = &ПланируемоеУвольнениеИспСрок ТОГДА ДОБАВИТЬКДАТЕ(НамеченныеСобытияПоПерсоналу.ДатаИзменения, ДЕНЬ, 1) ИНАЧЕ НамеченныеСобытияПоПерсоналу.ДатаИзменения КОНЕЦ >= СостояниеВнутри.Период)
	               |			
	               |			ГДЕ
	               |				НамеченныеСобытияПоПерсоналу.ДатаИзменения >= &ДатаНачала И
	               |				НамеченныеСобытияПоПерсоналу.ДатаИзменения <= &ДатаОкончания
	               |			
	               |			СГРУППИРОВАТЬ ПО
	               |				НамеченныеСобытияПоПерсоналу.ДатаИзменения,
	               |				НамеченныеСобытияПоПерсоналу.ФизЛицо,
	               |				НамеченныеСобытияПоПерсоналу.ПланируемоеСобытие,
	               |				НамеченныеСобытияПоПерсоналу.Подразделение,
	               |				НамеченныеСобытияПоПерсоналу.Должность,
	               |				НамеченныеСобытияПоПерсоналу.ЗанимаемыхСтавок,
	               |				НамеченныеСобытияПоПерсоналу.ЗаниматьСтавку) КАК СписокИзменений
	               |				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботников КАК СостояниеРаботников
	               |				ПО СписокИзменений.ФизЛицо = СостояниеРаботников.ФизЛицо И СписокИзменений.ДатаЗначения = СостояниеРаботников.Период) КАК НамеченныеИзмененияСДаннымиДляПроверок
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	               |			ПО НамеченныеИзмененияСДаннымиДляПроверок.ФизЛицо = Работники.ФизЛицо И НамеченныеИзмененияСДаннымиДляПроверок.ДатаИзменения >= Работники.Период
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ДатаИзменения,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ФизЛицо,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ПланируемоеСобытие,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.Подразделение,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.Должность,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ЗанимаемыхСтавок,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ЗаниматьСтавку,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.Состояние,
	               |		НамеченныеИзмененияСДаннымиДляПроверок.ДатаПоследнегоИзменения) КАК НамеченныеИзменения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	               |		ПО НамеченныеИзменения.ФизЛицо = Работники.ФизЛицо И НамеченныеИзменения.ДатаВРаботники = Работники.Период
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				   |			СписокСотрудников.Сотрудник КАК Сотрудник,
				   |			СписокСотрудников.Физлицо КАК Физлицо
				   |		ИЗ
				   |			ВТСписокСотрудников КАК СписокСотрудников
				   |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизическихЛиц КАК СписокФизическихЛиц
				   |			ПО СписокСотрудников.Физлицо = СписокФизическихЛиц.Физлицо
				   |			И СписокСотрудников.Приоритет = СписокФизическихЛиц.Приоритет) КАК Сотрудники
				   |		ПО НамеченныеИзменения.Физлицо = Сотрудники.Физлицо";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапрос()	

// Получаем актуальные намеченные кадровые изменения
//
// Параметры
//  ДатаНачала		– дата, начало периода, в котором 
//                 надо регистрировать изменения
//  ДатаОкончания	– дата, окончание этого периода
//
// Возвращаемое значение:
//  Таблица значений – список подходящих изменений, по 
// структуре соответствует табличной части обработки
//
Функция ВыбратьНамеченныеИзменения(ДатаНачала,ДатаОкончания) Экспорт

	РезультатЗапроса = СформироватьЗапрос(ДатаНачала,ДатаОкончания);
	ВыборкаЗапроса   = РезультатЗапроса.Выбрать();
	
	ТаблицаРезультатов = Новый ТаблицаЗначений;
	ТаблицаРезультатов.Колонки.Добавить("ФормироватьДокумент");
	ТаблицаРезультатов.Колонки.Добавить("ДатаИзменения");
	ТаблицаРезультатов.Колонки.Добавить("Сотрудник");
	ТаблицаРезультатов.Колонки.Добавить("ПланируемоеСобытие");
	ТаблицаРезультатов.Колонки.Добавить("Подразделение");
	ТаблицаРезультатов.Колонки.Добавить("ГрафикРаботы");
	ТаблицаРезультатов.Колонки.Добавить("Должность");
	ТаблицаРезультатов.Колонки.Добавить("ЗанимаемыхСтавок");
	ТаблицаРезультатов.Колонки.Добавить("ЗаниматьСтавку");
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		ПланируемоеСобытие = ВыборкаЗапроса.ПланируемоеСобытие;
		ТекущееСостояние = ВыборкаЗапроса.Состояние;
		
		Отказ = Ложь;
		Если ТекущееСостояние = Перечисления.СостоянияРаботника.Заболевание Тогда
			Если ПланируемоеСобытие <> Перечисления.НамеченныеСобытияПоПерсоналу.Заболевание Тогда
				Отказ = Истина
			КонецЕсли;
		ИначеЕсли ТекущееСостояние = Перечисления.СостоянияРаботника.Командировка Тогда
			Если ПланируемоеСобытие <> Перечисления.НамеченныеСобытияПоПерсоналу.Командировка Тогда
				Отказ = Истина
			КонецЕсли;
		ИначеЕсли ТекущееСостояние = Перечисления.СостоянияРаботника.ОтпускЕжегодный Тогда
			Если ПланируемоеСобытие <> Перечисления.НамеченныеСобытияПоПерсоналу.ОтпускЕжегодный 
				И ПланируемоеСобытие <> Перечисления.НамеченныеСобытияПоПерсоналу.ОтгулВСчетЕжегодногоОтпуска Тогда
				Отказ = Истина
			КонецЕсли;
		ИначеЕсли ТекущееСостояние = Перечисления.СостоянияРаботника.ОтпускПрочий Тогда
			Если ПланируемоеСобытие <> Перечисления.НамеченныеСобытияПоПерсоналу.ОтпускПрочий Тогда
				Отказ = Истина
			КонецЕсли;
		ИначеЕсли ТекущееСостояние = Перечисления.СостоянияРаботника.Работает Тогда
			Если ПланируемоеСобытие <> Перечисления.НамеченныеСобытияПоПерсоналу.УвольнениеПослеИспытательногоСрока
				И ПланируемоеСобытие <> Перечисления.НамеченныеСобытияПоПерсоналу.Увольнение
				И ПланируемоеСобытие <> Перечисления.НамеченныеСобытияПоПерсоналу.Перемещение Тогда
				
				Отказ = Истина
				
			ИначеЕсли ПланируемоеСобытие = Перечисления.НамеченныеСобытияПоПерсоналу.Перемещение Тогда
				
				Если ВыборкаЗапроса.ПланируемоеПодразделение = ВыборкаЗапроса.Подразделение  
					И ВыборкаЗапроса.ПланируемыхСтавок = ВыборкаЗапроса.ЗанимаемыхСтавок 
					И ВыборкаЗапроса.ПланируемаяДолжность = ВыборкаЗапроса.Должность Тогда
				
					Отказ = Истина
				
				КонецЕсли;
			КонецЕсли;
				
		ИначеЕсли ТекущееСостояние = Перечисления.СостоянияРаботника.НеРаботает Тогда
			Отказ = Истина;
		ИначеЕсли ТекущееСостояние = NULL Тогда 
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			
			НоваяСтрока 				   = ТаблицаРезультатов.Добавить();
			НоваяСтрока.ДатаИзменения 	   = ВыборкаЗапроса.ДатаИзменения;
			НоваяСтрока.Сотрудник 		   = ВыборкаЗапроса.Сотрудник;
			НоваяСтрока.ПланируемоеСобытие = ВыборкаЗапроса.ПланируемоеСобытие;
			НоваяСтрока.Подразделение 	   = ВыборкаЗапроса.ПланируемоеПодразделение;
			НоваяСтрока.ГрафикРаботы 	   = ВыборкаЗапроса.ГрафикРаботы;
			НоваяСтрока.Должность 		   = ВыборкаЗапроса.ПланируемаяДолжность;
			НоваяСтрока.ЗанимаемыхСтавок   = ВыборкаЗапроса.ПланируемыхСтавок;
			НоваяСтрока.ЗаниматьСтавку     = ВыборкаЗапроса.ЗаниматьСтавку;
			
		КонецЕсли;
		
	КонецЦикла;	

	Возврат ТаблицаРезультатов
	
КонецФункции // ВыбратьНамеченныеИзменения()

