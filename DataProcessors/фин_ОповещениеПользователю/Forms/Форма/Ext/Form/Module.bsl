
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НадписьЗатрагиваемыеОбъектыИнформационнойБазы = "Затрагиваемые объекты информационной базы";
	НадписьПредупрежденияОВыполняемыхДействияхПользователя = "Предупреждения о выполняемых действиях пользователя";
	Если Параметры.Свойство("ОбъектПроверки") Тогда
		Объект.ОбъектПроверки = Параметры.ОбъектПроверки;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ОбъектПроверки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Форма не предназначена для работы в самостоятельном режиме");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ОбъектПроверки = Объект.ОбъектПроверки;
	Если (НЕ ЗначениеЗаполнено(ОбъектПроверки)) ИЛИ (НЕ ЗначениеЗаполнено(ОбъектПроверки.Ссылка)) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Дерево = Неопределено;
	фин_РегистрацияФактическихДанныхПоБюджетированию.ПроверкаСсылокОбъекта(ОбъектПроверки,Объект.ПредупреждениеПользователю,Дерево);
	Если ТипЗнч(Дерево)=Тип("ХранилищеЗначения") Тогда
		Дерево = Дерево.Получить();
	КонецЕсли;
	Если Дерево = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если Дерево.Строки.Количество()=0 Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Использования в критичных для изменения настройках не выявлено");
		Возврат;
	КонецЕсли;
	// обход дерева
	УровеньОбхода = 1;
	ДеревоДанных = ДанныеФормыВЗначение(ДеревоРезультат,Тип("ДеревоЗначений"));
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		НоваяСтрока = ДеревоДанных.Строки.Добавить();
		НоваяСтрока.Объект = СтрокаДерева["Объект"+Строка(УровеньОбхода)];
		НоваяСтрока.Картинка = ?(Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(НоваяСтрока.Объект)),0,1);
		ОбходСтроки(СтрокаДерева,НоваяСтрока,УровеньОбхода);
	КонецЦикла;
	ЗначениеВДанныеФормы(ДеревоДанных,ДеревоРезультат);
	Если Параметры.Свойство("ЗаголовокКнопкиПродолжить") Тогда
		Элементы.Продолжить.Заголовок = Параметры.ЗаголовокКнопкиПродолжить;
	КонецЕсли;

КонецПроцедуры

//Процедура ОбходСтроки
//
&НаСервере
Процедура ОбходСтроки(ТекущаяСтрока,ОсновнаяСтрока,УровеньОбхода) 
	ТекущийУровень = УровеньОбхода + 1;
	Для Каждого СтрокаДерева Из ТекущаяСтрока.Строки Цикл
		НоваяСтрока = ОсновнаяСтрока.Строки.Добавить();
		НоваяСтрока.Объект = СтрокаДерева["Объект"+Строка(ТекущийУровень)];
		НоваяСтрока.Картинка = ?(Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(НоваяСтрока.Объект)),0,1);
		ОбходСтроки(СтрокаДерева,НоваяСтрока,ТекущийУровень);
	КонецЦикла
КонецПроцедуры //ОбходСтроки


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Для Каждого ЭлементДерева Из ДеревоРезультат.ПолучитьЭлементы() Цикл
		Элементы.Дерево.Развернуть(ЭлементДерева.ПолучитьИдентификатор(),Истина);
	КонецЦикла;
КонецПроцедуры
