
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьЗначенияСвойств(Объект,Параметры);
	Заголовок = "Свойства: " + Объект.ОбъектОтбораКатегорий;
	ЗаполнитьКатегории();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКатегории()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ПрочитатьЗаполнитьКатегорииОбъекта();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если Модифицированность Тогда
		Вопрос = "Измененные категории объекта не были записаны! Сохранить изменения?";
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена);

		Если      Ответ = КодВозвратаДиалога.Да Тогда
			ЗаписатьКатегорииОбъекта();

		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьКатегорииОбъекта()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаписатьКатегорииОбъекта();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры


&НаКлиенте
Процедура ЗаписатьЗначения(Команда)
	ВыполнитьЗапись();
КонецПроцедуры


&НаСервере
Процедура ВыполнитьЗапись()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Если ОбработкаОбъект.ЗаписатьКатегорииОбъекта() Тогда
		Модифицированность = Ложь;
	КонецЕсли;
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры


&НаКлиенте
Процедура Перечитать(Команда)
	Если Модифицированность Тогда
		Вопрос = "Измененные категории объекта не были записаны! Продолжить?";
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ОкОтмена);

		Если Ответ <> КодВозвратаДиалога.ОК Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ЗаполнитьКатегории();

	Модифицированность = Ложь;
КонецПроцедуры


&НаКлиенте
Процедура КатегорииОбъектаПринадлежностьПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

