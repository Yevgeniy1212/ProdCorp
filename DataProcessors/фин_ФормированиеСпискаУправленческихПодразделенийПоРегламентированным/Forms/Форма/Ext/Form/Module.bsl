
&НаКлиенте
Процедура ОтметитьВсе(Команда)
	УстановитьФлажки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	УстановитьФлажки(Ложь);
КонецПроцедуры

&НаСервере
Процедура УстановитьФлажки(Пометка)
	Дерево = РеквизитФормыВЗначение("СписокПодразделений");	
	ПроставитьМетки(Дерево.Строки,Пометка);
	ЗначениеВРеквизитФормы(Дерево,"СписокПодразделений");
КонецПроцедуры

&НаСервере
Процедура ПроставитьМетки(Строки,Пометка)
	Для Каждого Строка Из Строки Цикл
		Строка.Создать = Пометка;
		ПроставитьМетки(Строка.Строки,Пометка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьДерево(Объект.Организация);
	НеСоздаватьПодразделениеПриНаличииОдноименных = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДерево(Организация)
	Дерево = РеквизитФормыВЗначение("СписокПодразделений");	
	Дерево.Строки.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение,
		|	СоответствиеПодразделенийИПодразделенийОрганизаций.Организация,
		|	СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации
		|ИЗ
		|	РегистрСведений."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций";

	Результат = Запрос.Выполнить();

	Соответствия = Результат.Выгрузить();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Владелец КАК Организация,
		|	ПодразделенияОрганизаций.Ссылка КАК Подразделение
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	(ПодразделенияОрганизаций.Владелец = &Организация
		|			ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
		|ИТОГИ
		|ПО
		|	Организация,
		|	Подразделение ИЕРАРХИЯ";

	Результат = Запрос.Выполнить();

	ВыборкаОрганизация = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаОрганизация.Следующий() Цикл
		СтрокаДерева = Дерево.Строки.Добавить();
		СтрокаДерева.ПодразделениеРегламентированное = ВыборкаОрганизация.Организация;
		
		ВыборкаПодразделение = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаПодразделение.Следующий() Цикл
			Если ВыборкаПодразделение.ТипЗаписи()=ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
				Продолжить;
			КонецЕсли;
			НужныеСтроки = СтрокаДерева.Строки.НайтиСтроки(Новый Структура("ПодразделениеРегламентированное",ВыборкаПодразделение.Подразделение),Истина);
			Если НужныеСтроки.Количество()>0 Тогда
				Продолжить;
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(ВыборкаПодразделение.Подразделение.Родитель) Тогда
				ДочерняяСтрокаДерева = СтрокаДерева.Строки.Добавить();
				ДочерняяСтрокаДерева.ПодразделениеРегламентированное = ВыборкаПодразделение.Подразделение;
			Иначе
				НужныеСтроки = СтрокаДерева.Строки.НайтиСтроки(Новый Структура("ПодразделениеРегламентированное",ВыборкаПодразделение.Подразделение.Родитель),Истина);
				ДочерняяСтрокаДерева = НужныеСтроки[0].Строки.Добавить();
				ДочерняяСтрокаДерева.ПодразделениеРегламентированное = ВыборкаПодразделение.Подразделение;
			КонецЕсли;
			СтрокиПоиска = Соответствия.НайтиСтроки(Новый Структура("ПодразделениеОрганизации",ВыборкаПодразделение.Подразделение));
			Если СтрокиПоиска.Количество()>0 Тогда
				ДочерняяСтрокаДерева.ПодразделениеУправленческое = СтрокиПоиска[0].Подразделение;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;

	ЗначениеВРеквизитФормы(Дерево,"СписокПодразделений");
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЗаполнитьДерево(Объект.Организация);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДляВсех(Команда)
	Создать(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДляНеИмеющихСоответствия(Команда)
	Создать(Ложь);
КонецПроцедуры

&НаСервере
Процедура Создать(ДляВсех)
	Дерево = РеквизитФормыВЗначение("СписокПодразделений");	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		СоздатьПоСтрокам(СтрокаДерева,ДляВсех,Неопределено);
	КонецЦикла;
	ЗначениеВРеквизитФормы(Дерево,"СписокПодразделений");
КонецПроцедуры

&НаСервере
Процедура СоздатьПоСтрокам(СтрокаДерева,ДляВсех,Подразделение)
	мПодразделение = Неопределено;
	Если ТипЗнч(СтрокаДерева.ПодразделениеРегламентированное)<>Тип("СправочникСсылка.Организации") Тогда
		Если ЗначениеЗаполнено(СтрокаДерева.ПодразделениеУправленческое) И НЕ ДляВсех Тогда
			мПодразделение = СтрокаДерева.ПодразделениеУправленческое;
		ИначеЕсли СтрокаДерева.Создать Тогда
			Если НеСоздаватьПодразделениеПриНаличииОдноименных Тогда

				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ
					|	Подразделения.Ссылка
					|ИЗ
					|	Справочник."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ИмяСправочникаПодразделений()+" КАК Подразделения
					|ГДЕ
					|	Подразделения.Наименование = &Наименование
					|	И Подразделения.ПометкаУдаления = ЛОЖЬ";

				Запрос.УстановитьПараметр("Наименование", СтрокаДерева.ПодразделениеРегламентированное.Наименование);

				Результат = Запрос.Выполнить();

				ВыборкаДетальныеЗаписи = Результат.Выбрать();

				Если ВыборкаДетальныеЗаписи.Следующий() Тогда
					мПодразделение = ВыборкаДетальныеЗаписи.Ссылка;
				КонецЕсли;

			КонецЕсли;
			Если мПодразделение = Неопределено Тогда
				НовоеПодразделение = Справочники[фин_ОбщегоНазначенияВызовСервераПовтИсп.ИмяСправочникаПодразделений()].СоздатьЭлемент();
				НовоеПодразделение.Наименование = СтрокаДерева.ПодразделениеРегламентированное.Наименование;
				НовоеПодразделение.Родитель = Подразделение;
				НовоеПодразделение.Записать();
				мПодразделение = НовоеПодразделение.Ссылка;
			КонецЕсли;
			НаборЗаписей = РегистрыСведений[фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"СоответствиеПодразделенийИПодразделенийОрганизаций"].СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ПодразделениеОрганизации.Значение = СтрокаДерева.ПодразделениеРегламентированное;
			НаборЗаписей.Отбор.ПодразделениеОрганизации.Использование = Истина;
			Запись = НаборЗаписей.Добавить();
			Запись.Подразделение = мПодразделение;
			Запись.Организация = СтрокаДерева.ПодразделениеРегламентированное.Владелец;
			Запись.ПодразделениеОрганизации = СтрокаДерева.ПодразделениеРегламентированное;
			НаборЗаписей.Записать();
			СтрокаДерева.ПодразделениеУправленческое = мПодразделение;
		Иначе
			мПодразделение = СтрокаДерева.ПодразделениеУправленческое;
		КонецЕсли;
	КонецЕсли;	
	Для Каждого ДочерняяСтрокаДерева Из СтрокаДерева.Строки Цикл
		СоздатьПоСтрокам(ДочерняяСтрокаДерева,ДляВсех,мПодразделение);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СписокПодразделенийПодразделениеУправленческоеПриИзменении(Элемент)
	УстановитьСоответствие(Элементы.СписокПодразделений.ТекущиеДанные.ПодразделениеРегламентированное,Элементы.СписокПодразделений.ТекущиеДанные.ПодразделениеУправленческое);
КонецПроцедуры

&НаСервере
Процедура УстановитьСоответствие(ПодразделениеРегл,ПодразделениеУпр)
	НаборЗаписей = РегистрыСведений[фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"СоответствиеПодразделенийИПодразделенийОрганизаций"].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПодразделениеОрганизации.Значение = ПодразделениеРегл;
	НаборЗаписей.Отбор.ПодразделениеОрганизации.Использование = Истина;
	Если ЗначениеЗаполнено(ПодразделениеУпр) Тогда
		Запись = НаборЗаписей.Добавить();
		Запись.Подразделение = ПодразделениеУпр;
		Запись.Организация = ПодразделениеРегл.Владелец;
		Запись.ПодразделениеОрганизации = ПодразделениеРегл;
	КонецЕсли;
	НаборЗаписей.Записать();
КонецПроцедуры
