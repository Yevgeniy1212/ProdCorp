&НаСервере
Процедура ЗаполнитьДерево()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Зависимости.ИсхФинансовыйПоказатель,
		|	Зависимости.ЗависимаяФинансовыйПоказатель,
		|	Зависимости.ИсходныйНаборРазрезов,
		|	Зависимости.ЗависимыйНаборРазрезов,
		|	Зависимости.ИспользованиеЗависимости,
		|	Зависимости.ДатаЗавершения,
		|	Зависимости.ЗначениеИзмененияПоПериодам,
		|	Зависимости.Регистратор
		|ПОМЕСТИТЬ ВТ_Зависимости
		|ИЗ
		|	РегистрСведений.фин_ЗависимостиФинансовыхПоказателей.СрезПоследних(
		|			&Дата,
		|			ВЫБОР
		|					КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Организация = &Организация
		|				КОНЕЦ
		|				И ВЫБОР
		|					КОГДА &Сценарий = ЗНАЧЕНИЕ(Справочник."+фин_ОбщегоНазначенияВызовСервераПовтИсп.ПрефиксОбщихОбъектов()+"СценарииПланирования.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ИсхСценарий = &Сценарий
		|				КОНЕЦ) КАК Зависимости
		|ГДЕ
		|	ВЫБОР
		|			КОГДА Зависимости.ДатаЗавершения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Зависимости.ДатаЗавершения >= &Дата
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_Зависимости.ИсхФинансовыйПоказатель
		|ПОМЕСТИТЬ ВТ_Независимые
		|ИЗ
		|	ВТ_Зависимости КАК ВТ_Зависимости
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Зависимости КАК ВТ_Зависимости1
		|		ПО ВТ_Зависимости.ИсхФинансовыйПоказатель = ВТ_Зависимости1.ЗависимаяФинансовыйПоказатель
		|ГДЕ
		|	ВТ_Зависимости1.ЗависимаяФинансовыйПоказатель ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Зависимости.ИсхФинансовыйПоказатель КАК ИсхФинансовыйПоказатель,
		|	ВТ_Зависимости.ЗависимаяФинансовыйПоказатель,
		|	ВТ_Зависимости.ИсходныйНаборРазрезов КАК ИсходныйНаборРазрезов,
		|	ВТ_Зависимости.ЗависимыйНаборРазрезов,
		|	ВТ_Зависимости.ИспользованиеЗависимости,
		|	ВТ_Зависимости.ДатаЗавершения,
		|	ВТ_Зависимости.ЗначениеИзмененияПоПериодам,
		|	ВТ_Зависимости.Регистратор
		|ИЗ
		|	ВТ_Зависимости КАК ВТ_Зависимости
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Независимые КАК ВТ_Независимые
		|		ПО ВТ_Зависимости.ИсхФинансовыйПоказатель = ВТ_Независимые.ИсхФинансовыйПоказатель
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_Зависимости.ИсхФинансовыйПоказатель.Наименование
		|ИТОГИ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИсходныйНаборРазрезов)
		|ПО
		|	ИсхФинансовыйПоказатель";

	Запрос.УстановитьПараметр("Дата", 			?(Объект.Дата='00010101',ТекущаяДата(),Объект.Дата));
	Запрос.УстановитьПараметр("Организация", 	Объект.Организация);
	Запрос.УстановитьПараметр("Сценарий", 		Объект.Сценарий);

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Дерево = РеквизитФормыВЗначение("ДеревоЗависимостей");
	Дерево.Строки.Очистить();
	ПустоеДерево = Дерево.Скопировать();
	
	СтруктураРазрезов = фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ПолучитьПолныйСписокРазрезов");
	Для Каждого ЭлементРазрез Из СтруктураРазрезов Цикл
		ИмяРазреза = фин_РаботаСДополнительнымиРазрезамиБюджетирования.ИмяРазреза(ЭлементРазрез.Значение);
		МассивРазрезов.Добавить(ИмяРазреза);
	КонецЦикла;

	Пока Выборка.Следующий() Цикл
		СтрокаДерева = Дерево.Строки.Добавить();
		СтрокаДерева.ФинансовыйПоказатель 	= Выборка.ИсхФинансовыйПоказатель;
		СтрокаДерева.Служебный 				= Выборка.ИсхФинансовыйПоказатель;
		ВыборкаВложенная = Выборка.Выбрать();
		Пока ВыборкаВложенная.Следующий() Цикл
			Если ВыборкаВложенная.Количество()=1 Тогда
				ЗаполняемаяСтрока = СтрокаДерева;
			Иначе
				ЗаполняемаяСтрока = СтрокаДерева.Строки.Добавить();
				ЗаполняемаяСтрока.Служебный = СтрокаДерева.ФинансовыйПоказатель;
				ЗаполняемаяСтрока.ФинансовыйПоказатель 	= ВыборкаВложенная.ИсхФинансовыйПоказатель;
			КонецЕсли;
			ЗаполняемаяСтрока.СрокДействия 			= ВыборкаВложенная.ДатаЗавершения;
			ЗаполняемаяСтрока.ИзменениеПоПериодам 	= ВыборкаВложенная.ЗначениеИзмененияПоПериодам;
			ЗаполняемаяСтрока.Факт 					= (ВыборкаВложенная.ИспользованиеЗависимости = Перечисления.фин_ИспользованиеЗависимостейСтатейОборотов.ПриПланированииИИсполнении);
			Для Каждого Разрез Из МассивРазрезов Цикл
				ИмяРазреза = Разрез.Значение;
				ЗаполняемаяСтрока[ИмяРазреза]		= ?(ВыборкаВложенная.ИсходныйНаборРазрезов["Отбор"+ИмяРазреза],ВыборкаВложенная.ИсходныйНаборРазрезов[ИмяРазреза],Неопределено);
			КонецЦикла;
			ЗаполняемаяСтрока.Регистратор			= ВыборкаВложенная.Регистратор;
			// подчиненная ей строка
			ФормируемаяСтрока 						= ЗаполняемаяСтрока.Строки.Добавить();
			ФормируемаяСтрока.ФинансовыйПоказатель 	= ВыборкаВложенная.ЗависимаяФинансовыйПоказатель;
			ФормируемаяСтрока.Служебный 			= ВыборкаВложенная.ЗависимаяФинансовыйПоказатель;
			ФормируемаяСтрока.СрокДействия 			= ВыборкаВложенная.ДатаЗавершения;
			ФормируемаяСтрока.ИзменениеПоПериодам 	= ВыборкаВложенная.ЗначениеИзмененияПоПериодам;
			ФормируемаяСтрока.Факт 					= (ВыборкаВложенная.ИспользованиеЗависимости = Перечисления.фин_ИспользованиеЗависимостейСтатейОборотов.ПриПланированииИИсполнении);
			ФормируемаяСтрока.Регистратор			= ВыборкаВложенная.Регистратор;
			// измерения формируемой строки
			Для Каждого Разрез Из МассивРазрезов Цикл
				ИмяРазреза = Разрез.Значение;
				ФормируемаяСтрока[ИмяРазреза]		= ?(ЗначениеЗаполнено(ВыборкаВложенная.ЗависимыйНаборРазрезов[ИмяРазреза]),ВыборкаВложенная.ЗависимыйНаборРазрезов[ИмяРазреза],?(ВыборкаВложенная.ИсходныйНаборРазрезов["Отбор"+ИмяРазреза],ВыборкаВложенная.ИсходныйНаборРазрезов[ИмяРазреза],Неопределено));
			КонецЦикла;
			// зависимости подчиненной рекурсивно
			ДобавитьЗависимые(ФормируемаяСтрока,ПустоеДерево);
		КонецЦикла;
	КонецЦикла;

	ЗначениеВРеквизитФормы(Дерево,"ДеревоЗависимостей");
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗависимые(СтрокаДерева,Дерево)
	Таблица = Новый ТаблицаЗначений;
	Для Каждого Колонка Из Дерево.Колонки Цикл
		Таблица.Колонки.Добавить(Колонка.Имя,Колонка.ТипЗначения);
	КонецЦикла;
	Таблица.Колонки.Добавить("НомерСтроки",Новый ОписаниеТипов("Число"));
	НоваяСтрокаДерева = Таблица.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрокаДерева,СтрокаДерева);
	
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ОборотыПоСтатьям.Сценарий,
	             |	ОборотыПоСтатьям.УправленческоеПодразделение,
	             |	ОборотыПоСтатьям.Проект,
	             |	ОборотыПоСтатьям.Контрагент,
	             |	ОборотыПоСтатьям.Номенклатура,
	             |	ОборотыПоСтатьям.Разрез1,
	             |	ОборотыПоСтатьям.Разрез2,
	             |	ОборотыПоСтатьям.Разрез3,
	             |	ОборотыПоСтатьям.Разрез4,
	             |	ОборотыПоСтатьям.Разрез5,
	             |	ОборотыПоСтатьям.Разрез6,
	             |	ОборотыПоСтатьям.Разрез7,
	             |	ОборотыПоСтатьям.Разрез8,
	             |	ОборотыПоСтатьям.Разрез9,
	             |	ОборотыПоСтатьям.Разрез10,
	             |	ОборотыПоСтатьям.ФинансовыйПоказатель,
	             |	ОборотыПоСтатьям.НомерСтроки
	             |ПОМЕСТИТЬ ВТ_Обороты
	             |ИЗ
	             |	&Обороты КАК ОборотыПоСтатьям
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ВТ_Обороты.Сценарий,
	             |	ВТ_Обороты.УправленческоеПодразделение,
	             |	ВТ_Обороты.Проект,
	             |	ВТ_Обороты.Контрагент,
	             |	ВТ_Обороты.Номенклатура,
	             |	ВТ_Обороты.Разрез1,
	             |	ВТ_Обороты.Разрез2,
	             |	ВТ_Обороты.Разрез3,
	             |	ВТ_Обороты.Разрез4,
	             |	ВТ_Обороты.Разрез5,
	             |	ВТ_Обороты.Разрез6,
	             |	ВТ_Обороты.Разрез7,
	             |	ВТ_Обороты.Разрез8,
	             |	ВТ_Обороты.Разрез9,
	             |	ВТ_Обороты.Разрез10,
	             |	ВТ_Обороты.ФинансовыйПоказатель,
	             |	ВТ_Обороты.НомерСтроки
	             |ИЗ
	             |	ВТ_Обороты КАК ВТ_Обороты";
	Запрос.УстановитьПараметр("Обороты",Таблица);
				  
	ТаблицаЗависимостей = фин_УправлениеЗависимостямиСтатейБюджетов.ПолучитьТаблицуЗависимостей(Запрос.Выполнить().Выгрузить(),?(Объект.Сценарий.Пустая(),СтрокаДерева.Сценарий,Объект.Сценарий),Объект.Организация,фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("БюджетированиеПоОрганизациям"),Объект.Дата,Ложь,МассивРазрезов);			  
	
	Для Каждого СтрокаЗависимостей Из ТаблицаЗависимостей Цикл
			ФормируемаяСтрока 						= СтрокаДерева.Строки.Добавить();
			ФормируемаяСтрока.ФинансовыйПоказатель 	= СтрокаЗависимостей.ЗависимаяФинансовыйПоказатель;
			ФормируемаяСтрока.Служебный 			= СтрокаЗависимостей.ЗависимаяФинансовыйПоказатель;
			ФормируемаяСтрока.СрокДействия 			= СтрокаЗависимостей.ДатаЗавершения;
			ФормируемаяСтрока.ИзменениеПоПериодам 	= СтрокаЗависимостей.ЗначениеИзмененияПоПериодам;
			ФормируемаяСтрока.Факт 					= (СтрокаЗависимостей.ИспользованиеЗависимости = Перечисления.фин_ИспользованиеЗависимостейСтатейОборотов.ПриПланированииИИсполнении);
			ФормируемаяСтрока.Регистратор			= СтрокаЗависимостей.Регистратор;
			// измерения формируемой строки
			Для Каждого Разрез Из МассивРазрезов Цикл
				ИмяРазреза = Разрез.Значение;
				ФормируемаяСтрока[ИмяРазреза]		= ?(ЗначениеЗаполнено(СтрокаЗависимостей.ЗависимыйНаборРазрезов[ИмяРазреза]),СтрокаЗависимостей.ЗависимыйНаборРазрезов[ИмяРазреза],?(СтрокаЗависимостей.ИсходныйНаборРазрезов["Отбор"+ИмяРазреза],СтрокаЗависимостей.ИсходныйНаборРазрезов[ИмяРазреза],СтрокаДерева[ИмяРазреза]));
			КонецЦикла;
			// зависимости подчиненной рекурсивно
			ДобавитьЗависимые(ФормируемаяСтрока,Дерево);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьДерево();
	Обработанные = Новый Массив;
	Для Каждого Измерение Из МассивРазрезов Цикл
		ИмяИзмерения = Измерение.Значение;
		Обработанные.Добавить(ИмяИзмерения);
		Элементы.ДеревоЗависимостей.ПодчиненныеЭлементы["ДеревоЗависимостей"+ИмяИзмерения].Видимость	= Истина;
		Элементы.ДеревоЗависимостей.ПодчиненныеЭлементы["ДеревоЗависимостей"+ИмяИзмерения].Заголовок 	= фин_РаботаСДополнительнымиРазрезамиБюджетирования.ПредставлениеРазреза(Перечисления.фин_ФактическиеПоказателиБюджетирования[ИмяИзмерения]);
	КонецЦикла;
	СписокИзмерений = Новый Массив;
	СписокИзмерений.Добавить("Проект");
	СписокИзмерений.Добавить("Контрагент");
	СписокИзмерений.Добавить("Номенклатура");
	СписокИзмерений.Добавить("Разрез1");
	СписокИзмерений.Добавить("Разрез2");
	СписокИзмерений.Добавить("Разрез3");
	СписокИзмерений.Добавить("Разрез4");
	СписокИзмерений.Добавить("Разрез5");
	СписокИзмерений.Добавить("Разрез6");
	СписокИзмерений.Добавить("Разрез7");
	СписокИзмерений.Добавить("Разрез8");
	СписокИзмерений.Добавить("Разрез9");
	СписокИзмерений.Добавить("Разрез10");
	Для Каждого Измерение Из СписокИзмерений Цикл
		Если Обработанные.Найти(Измерение)=Неопределено Тогда
			Элементы.ДеревоЗависимостей.ПодчиненныеЭлементы["ДеревоЗависимостей"+Измерение].Видимость	= Ложь;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ЗаполнитьДерево();
КонецПроцедуры
