////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит значение признака ведения учета по спецификам 
Перем ВедениеУчетаПоСпецификам Экспорт; // признак учета по спецификам
// Хранит значение признака ведения учета закупок в разрезе складов 
Перем ВедениеУчетаЗакупокВРазрезеСкладов Экспорт; // признак учета закупок в разрезе складов


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Функция получает выборку документов регистраций договоров
//
// Возвращаемое значение:
//  Выборка из результата запроса
//  
Функция ПолучитьВыборкуРегистраций() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДоговорКонтрагента",Договор);
	Запрос.Текст = "ВЫБРАТЬ
	               |	гз_РегистрацияДоговораПоГосударственнымЗакупкам.Ссылка КАК Документ,
	               |	ЕСТЬNULL(гз_КорректировкиДоговоровГосударственныхЗакупокСрезПоследних.Сумма, гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.Сумма) КАК Сумма
	               |ИЗ
	               |	Документ.гз_РегистрацияДоговораПоГосударственнымЗакупкам КАК гз_РегистрацияДоговораПоГосударственнымЗакупкам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам КАК гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам
	               |		ПО гз_ДоговорыКонтрагентовПоГосударственнымЗакупкам.ДоговорКонтрагента = гз_РегистрацияДоговораПоГосударственнымЗакупкам.ДоговорКонтрагента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гз_КорректировкиДоговоровГосударственныхЗакупок.СрезПоследних(, ДоговорКонтрагента = &ДоговорКонтрагента) КАК гз_КорректировкиДоговоровГосударственныхЗакупокСрезПоследних
	               |		ПО гз_КорректировкиДоговоровГосударственныхЗакупокСрезПоследних.ДоговорКонтрагента = гз_РегистрацияДоговораПоГосударственнымЗакупкам.ДоговорКонтрагента
	               |ГДЕ
	               |	гз_РегистрацияДоговораПоГосударственнымЗакупкам.Проведен
	               |	И гз_РегистрацияДоговораПоГосударственнымЗакупкам.ДоговорКонтрагента = &ДоговорКонтрагента
	               |ИТОГИ
	               |	МАКСИМУМ(Сумма)
	               |ПО
	               |	ОБЩИЕ";
				   
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции //ПолучитьВыборкуРегистраций()

// Функция получает выборку документов регистраций поступлений по договорам
//
// Возвращаемое значение:
//  Выборка из результата запроса
//  
Функция ПолучитьВыборкуРегистрацийПоступлений() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДоговорКонтрагента",Договор);
	Запрос.Текст = "ВЫБРАТЬ
	               |	гз_ПоступлениеТоваровРаботУслугПоДоговорамГосударственныхЗакупок.Ссылка КАК Документ,
	               |	гз_ПоступлениеТоваровРаботУслугПоДоговорамГосударственныхЗакупок.СуммаДокумента * ВЫБОР
	               |		КОГДА гз_ПоступлениеТоваровРаботУслугПоДоговорамГосударственныхЗакупок.ВозвратПоставщику
	               |			ТОГДА -1
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК Сумма
	               |ИЗ
	               |	Документ.гз_ПоступлениеТоваровРаботУслугПоДоговорамГосударственныхЗакупок КАК гз_ПоступлениеТоваровРаботУслугПоДоговорамГосударственныхЗакупок
	               |ГДЕ
	               |	гз_ПоступлениеТоваровРаботУслугПоДоговорамГосударственныхЗакупок.Проведен
	               |	И гз_ПоступлениеТоваровРаботУслугПоДоговорамГосударственныхЗакупок.ДоговорКонтрагента = &ДоговорКонтрагента
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	ОБЩИЕ";
				   
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции //ПолучитьВыборкуРегистрацийПоступлений()

// Функция получает выборку документов регистраций оплат по договорам
//
// Возвращаемое значение:
//  Выборка из результата запроса
//  
Функция ПолучитьВыборкуРегистрацийОплат() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДоговорКонтрагента",Договор);
	Запрос.Текст = "ВЫБРАТЬ
	               |	гз_ОплатаПоДоговорамГосударственныхЗакупок.Ссылка КАК Документ,
	               |	гз_ОплатаПоДоговорамГосударственныхЗакупок.СуммаДокумента КАК Сумма
	               |ИЗ
	               |	Документ.гз_ОплатаПоДоговорамГосударственныхЗакупок КАК гз_ОплатаПоДоговорамГосударственныхЗакупок
	               |ГДЕ
	               |	гз_ОплатаПоДоговорамГосударственныхЗакупок.Проведен
	               |	И гз_ОплатаПоДоговорамГосударственныхЗакупок.ДоговорКонтрагента = &ДоговорКонтрагента
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	ОБЩИЕ";
				   
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции //ПолучитьВыборкуРегистрацийОплат()

// Функция получает выборку документов поступлений по бухгалтерскому учету
//
// Возвращаемое значение:
//  Выборка из результата запроса
//  
Функция ПолучитьВыборкуПоступлений(РежимРаботы) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДоговорКонтрагента",Договор);
	Если РежимРаботы = "БГУ" Тогда 
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПоступлениеТоваровУслуг.Ссылка КАК Документ,
		               |	ПоступлениеТоваровУслуг.СуммаДокумента КАК Сумма
		               |ИЗ
		               |	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		               |ГДЕ
		               |	ПоступлениеТоваровУслуг.Проведен
		               |	И ПоступлениеТоваровУслуг.ДоговорКонтрагента = &ДоговорКонтрагента
		               |ИТОГИ
		               |	СУММА(Сумма)
		               |ПО
		               |	ОБЩИЕ";
    ИначеЕсли РежимРаботы = "БГП" ИЛИ РежимРаботы = "БК" ИЛИ РежимРаботы = "БУГП" Тогда 
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПоступлениеТоваровУслуг.Ссылка КАК Документ,
		               |	ПоступлениеТоваровУслуг.СуммаДокумента КАК Сумма
		               |ИЗ
		               |	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		               |ГДЕ
		               |	ПоступлениеТоваровУслуг.Проведен
		               |	И ПоступлениеТоваровУслуг.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПоступлениеНМА.Ссылка,
		               |	ПоступлениеНМА.СуммаДокумента
		               |ИЗ
		               |	Документ.ПоступлениеНМА КАК ПоступлениеНМА
		               |ГДЕ
		               |	ПоступлениеНМА.Проведен
		               |	И ПоступлениеНМА.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ВозвратТоваровПоставщику.Ссылка,
		               |	-ВозвратТоваровПоставщику.СуммаДокумента
		               |ИЗ
		               |	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		               |ГДЕ
		               |	ВозвратТоваровПоставщику.Проведен
		               |	И ВозвратТоваровПоставщику.ДоговорКонтрагента = &ДоговорКонтрагента
		               |ИТОГИ
		               |	СУММА(Сумма)
		               |ПО
		               |	ОБЩИЕ";
    КонецЕсли;
				   
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции //ПолучитьВыборкуПоступлений()

// Функция получает выборку документов оплат по бухгалтерскому учету
//
// Возвращаемое значение:
//  Выборка из результата запроса
//  
Функция ПолучитьВыборкуОплат(РежимРаботы) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДоговорКонтрагента",Договор);
	Если РежимРаботы = "БГУ" Тогда 
		Запрос.Текст = "ВЫБРАТЬ
		               |	Заявка.Ссылка КАК Документ,
		               |	СУММА(0) КАК Сумма
		               |ИЗ
		               |	Документ.Заявка КАК Заявка
		               |ГДЕ
		               |	Заявка.Проведен
		               |	И Заявка.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Заявка.Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	СчетКОплате.Ссылка,
		               |	СУММА(СчетКОплате.СуммаДокумента)
		               |ИЗ
		               |	Документ.СчетКОплате КАК СчетКОплате
		               |ГДЕ
		               |	СчетКОплате.Проведен
		               |	И СчетКОплате.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	СчетКОплате.Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.Ссылка,
		               |	СУММА(-ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.СуммаПлатежа)
		               |ИЗ
		               |	Документ.ПлатежныйОрдерПоступлениеДенежныхСредств.РасшифровкаПлатежа КАК ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа
		               |ГДЕ
		               |	ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.Ссылка.Проведен
		               |	И ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.Ссылка,
		               |	СУММА(ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.СуммаПлатежа)
		               |ИЗ
		               |	Документ.ПлатежныйОрдерСписаниеДенежныхСредств.РасшифровкаПлатежа КАК ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа
		               |ГДЕ
		               |	ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.Ссылка.Проведен
		               |	И ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.Ссылка
		               |ИТОГИ
		               |	СУММА(Сумма)
		               |ПО
		               |	ОБЩИЕ";
    ИначеЕсли РежимРаботы = "БГП" ИЛИ РежимРаботы = "БК" ИЛИ РежимРаботы = "БУГП" Тогда 
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.Ссылка КАК Документ,
		               |	СУММА(ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.СуммаПлатежа) КАК Сумма
		               |ИЗ
		               |	Документ.ПлатежноеПоручениеИсходящее.РасшифровкаПлатежа КАК ПлатежноеПоручениеИсходящееРасшифровкаПлатежа
		               |ГДЕ
		               |	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.Ссылка.Проведен
		               |	И ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
		               |	СУММА(РасходныйКассовыйОрдерРасшифровкаПлатежа.СуммаПлатежа)
		               |ИЗ
		               |	Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйКассовыйОрдерРасшифровкаПлатежа
		               |ГДЕ
		               |	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Проведен
		               |	И РасходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
		               |	СУММА(-ПриходныйКассовыйОрдерРасшифровкаПлатежа.СуммаПлатежа)
		               |ИЗ
		               |	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
		               |ГДЕ
		               |	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Проведен
		               |	И ПриходныйКассовыйОрдерРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.Ссылка,
		               |	СУММА(-ПлатежноеПоручениеВходящееРасшифровкаПлатежа.СуммаПлатежа)
		               |ИЗ
		               |	Документ.ПлатежноеПоручениеВходящее.РасшифровкаПлатежа КАК ПлатежноеПоручениеВходящееРасшифровкаПлатежа
		               |ГДЕ
		               |	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.Ссылка.Проведен
		               |	И ПлатежноеПоручениеВходящееРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПлатежноеПоручениеВходящееРасшифровкаПлатежа.Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.Ссылка,
		               |	СУММА(-ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.СуммаПлатежа)
		               |ИЗ
		               |	Документ.ПлатежныйОрдерПоступлениеДенежныхСредств.РасшифровкаПлатежа КАК ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа
		               |ГДЕ
		               |	ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.Ссылка.Проведен
		               |	И ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПлатежныйОрдерПоступлениеДенежныхСредствРасшифровкаПлатежа.Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.Ссылка,
		               |	СУММА(ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.СуммаПлатежа)
		               |ИЗ
		               |	Документ.ПлатежныйОрдерСписаниеДенежныхСредств.РасшифровкаПлатежа КАК ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа
		               |ГДЕ
		               |	ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.Ссылка.Проведен
		               |	И ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.ДоговорКонтрагента = &ДоговорКонтрагента
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПлатежныйОрдерСписаниеДенежныхСредствРасшифровкаПлатежа.Ссылка
		               |ИТОГИ
		               |	СУММА(Сумма)
		               |ПО
		               |	ОБЩИЕ";
    КонецЕсли;
				   
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции //ПолучитьВыборкуПоступлений()

#Если Клиент Тогда

// Функция формирует бухгалтерские документы
//
Функция ОформитьПоступлениеБУ(Ссылка,Вид=Неопределено,Склад = Неопределено) Экспорт
	Возврат Ссылка.ПолучитьОбъект().ОформитьПоступлениеБУ(Ссылка);
КонецФункции //ОформитьПоступлениеБУ()

// Функция формирует бухгалтерские документы
//
Функция ОформитьПоступлениеОплатБУ(Ссылка,Вид=Неопределено) Экспорт
	Возврат Ссылка.ПолучитьОбъект().ОформитьПоступлениеБУ(Ссылка);    	
КонецФункции //ОформитьПоступлениеБУ()

// Процедура заполнения документа по документу-основанию
//
Функция  ЗаполнитьПоДокументуПоступленияБУ(Основание)  Экспорт
	мДокумент = Документы.гз_ПоступлениеТоваровРаботУслугПоДоговорамГосударственныхЗакупок.СоздатьДокумент();
	мДокумент.ЗаполнитьПоДокументуОснования(Основание);
	Возврат мДокумент;
КонецФункции //ЗаполнитьПоДокументуПоступленияБУ()

// Процедура заполнения документа по документу-основанию
//
Функция  ЗаполнитьПоДокументуОплатыБУ(Основание)  Экспорт
	мДокумент = Документы.гз_ОплатаПоДоговорамГосударственныхЗакупок.СоздатьДокумент();
	мДокумент.ЗаполнитьПоДокументуОснования(мДокумент,Основание);
	Возврат мДокумент;
КонецФункции //ЗаполнитьПоДокументуОснования()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ВедениеУчетаПоСпецификам  = Константы.гз_ВестиУчетПоСпецификам.Получить();
ВедениеУчетаЗакупокВРазрезеСкладов  = Константы.гз_ВестиУчетЗакупокВРазрезеМестПоставки.Получить();


