
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Бюджет")=Ложь Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Бюджет 				= Параметры.Бюджет;
	
	ВыбранныеВарианты	= Параметры.ВыбранныеВарианты;
	ВыбранныеВариантыКБ.ЗагрузитьЗначения(ВыбранныеВарианты);
	
	Параметры.Свойство("АдресНастроек",АдресНастроек);
	
	УчетПоКоличеству 	= Параметры.УчетПоКоличеству;
	УчетПоСумме 		= Параметры.УчетПоСумме;
	РазрезыУчета.ЗагрузитьЗначения(Параметры.РазрезыУчета);
	
	Если АдресНастроек = "" Тогда
		СписокСтатей = фин_УправлениеБюджетнойМоделью.ПолучитьТаблицуПостатейногоСоставаБюджетов(ВыбранныеВарианты,Бюджет);
	Иначе
		ДанныеХранилища = ПолучитьИзВременногоХранилища(АдресНастроек);
		СписокСтатей = ДанныеХранилища.СтатьиБюджетов.Скопировать(Новый Структура("Бюджет",Бюджет));
	КонецЕсли;
	
	Дерево = ДанныеФормыВЗначение(ДеревоСтатей,Тип("ДеревоЗначений"));
	
	Для Каждого СтрокаСтатей Из СписокСтатей Цикл
		Если ЗначениеЗаполнено(СтрокаСтатей.Родитель) Тогда
			Найдено = Дерево.Строки.НайтиСтроки(Новый Структура("Статья",СтрокаСтатей.Родитель),Истина);
			НоваяСтрока = Найдено[0].Строки.Добавить();
		Иначе
			НоваяСтрока = Дерево.Строки.Добавить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаСтатей,,"Родитель");
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.НаименованиеСтатьи) Тогда
			НоваяСтрока.НаименованиеСтатьи = СтрЗаменить(Строка(НоваяСтрока.Статья),"(группа)","");
		КонецЕсли;
		РазрезыСтатьи = Новый Структура(СтрокаСтатей.Разрезы);
		Для Каждого ЭлементРазрез Из РазрезыСтатьи Цикл
			Если РазрезыУчета.НайтиПоЗначению(Перечисления.фин_КлассификаторРазрезовБюджетов[ЭлементРазрез.Ключ])= Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока.СписокРазрезов = НоваяСтрока.СписокРазрезов + ?(НоваяСтрока.СписокРазрезов="","",", ")+Перечисления.фин_КлассификаторРазрезовБюджетов[ЭлементРазрез.Ключ];
		КонецЦикла;
	КонецЦикла;
	ЗначениеВДанныеФормы(Дерево,ДеревоСтатей);
	фин_БюджетированиеОбщегоНазначения.НастроитьОформлениеТабличногоПоля(Элементы.ДеревоСтатей);
КонецПроцедуры

&НаКлиенте
Процедура ПравилаРасчетаПоСтатье(Команда)
	Если Элементы.ДеревоСтатей.ТекущиеДанные<>Неопределено Тогда
		СтруктураПараметров = Новый Структура("СтатьяБюджета,ВыбранныеВарианты,АдресНастроек",Элементы.ДеревоСтатей.ТекущиеДанные.Статья,ВыбранныеВариантыКБ.ВыгрузитьЗначения(),АдресНастроек);
		ОткрытьФорму("Обработка.фин_НастройкаКомплектаБюджетов.Форма.ФормаСведенийОСтатье",СтруктураПараметров,ЭтотОбъект,Элементы.ДеревоСтатей.ТекущиеДанные.Статья,ВариантОткрытияОкна.ОтдельноеОкно);	
	КонецЕсли;
КонецПроцедуры
