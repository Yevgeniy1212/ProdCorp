
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПрефиксПродукта",ПрефиксПродукта);
	Если ПрефиксПродукта = "" Тогда
		ПрефиксПродукта = "общ_";
	КонецЕсли;
	
	ОбщийМодуль = Вычислить(ПрефиксПродукта+"ЗащитаКлиентСервер");
	Объект.НомерОсновногоКлюча = Константы[ПрефиксПродукта+"НомерОсновногоКлюча"].Получить();
	Объект.НомерРегистрационнойАнкеты = Константы[ПрефиксПродукта+"НомерРегистрационнойАнкеты"].Получить();
	КатегорияПлатнойПоддержкиОтраслевогоРешения = ОбщийМодуль.КатегорияПоддержки();
	
	СписокКлючей = ОбщийМодуль.СписокНомеровКлючей();
	
	Для Каждого Ключ Из СписокКлючей Цикл
		Элементы.НомерОсновногоКлюча.СписокВыбора.Добавить(Ключ);	
	КонецЦикла;
	
	ОбновитьИнформациюОПодписке();
	            
	ПолеHTML = Обработки[ПрефиксПродукта+"СведенияОЛицензииНаПоддержку"].ПолучитьМакет("МакетИнформацияОбАктивации").ПолучитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ВладелецФормы<>Неопределено Тогда
		Попытка
			ВладелецФормы.ОбновитьИнформациюОПодпискеКлиент();
		Исключение
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	// В связи с некорректным поведением платформы данное событие срабатывает при программном изменении кнопок навигации 
	// параллельно с установкой текущей страницы. При этом в качестве текущей страницы устанавливается СтраницаИнструкция. 
	// Ошибка возникает при переходе на страницу с результатом активации. Поэтому для установки текущей страницы 
	// используется следующий код (видимость страниц восстанавливается позже при нажатии на кнопку Назад):
	Если ТекущаяСтраница = Элементы.СтраницаИнструкция И Элементы.Готово.Видимость = Истина Тогда
		Для каждого ТекСтраница Из Элементы.Страницы.ПодчиненныеЭлементы Цикл
			Если ТекСтраница.Имя <> "СтраницаРезультатАктивации" Тогда
				ТекСтраница.Видимость = Ложь;		
			КонецЕсли;	
		КонецЦикла;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатАктивации;
	КонецЕсли; 		
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	Элементы.Назад.Видимость = Истина;
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаИнструкция Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборСпособаАктивации;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборСпособаАктивации Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводДанных;
		Если ((НЕ ЗначениеЗаполнено(Объект.НомерОсновногоКлюча)) И Элементы.НомерОсновногоКлюча.СписокВыбора.Количество()>0) ИЛИ Элементы.НомерОсновногоКлюча.СписокВыбора.НайтиПоЗначению(Объект.НомерОсновногоКлюча)=Неопределено Тогда
			Объект.НомерОсновногоКлюча = Элементы.НомерОсновногоКлюча.СписокВыбора[0].Значение;
		КонецЕсли;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводДанных Тогда
		Если НЕ ПроверитьДанные() Тогда
			Возврат;
		КонецЕсли;
		Если СпособАктивацииПоддержки = 0 Тогда
			Элементы.Далее.Видимость = Ложь;
			Элементы.Готово.Видимость = Истина;
			Элементы.Готово.КнопкаПоУмолчанию = Истина;
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатАктивации;
			Состояние("Производится попытка соединения с центром лицензирования для активации поддержки");
			ПопыткаАктивации();
			Состояние("Готово");
			Если ВладелецФормы<>Неопределено Тогда
				Попытка
					ВладелецФормы.ОбновитьИнформациюОПодпискеКлиент();
				Исключение
				КонецПопытки;
			КонецЕсли;
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатАктивации;
		ИначеЕсли СпособАктивацииПоддержки = 1 Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУдаленнаяАктивация;
			ЗаполнитьСлужебнуюИнформацию();
			Элементы.ГруппаИнформацияДляПередачиВЦентрЛицензирования.Видимость = Истина;
			Элементы.Далее.Видимость = Истина;
			Элементы.Далее.КнопкаПоУмолчанию = Истина;
			Элементы.Готово.Видимость = Ложь;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУдаленнаяАктивация;
			Элементы.ГруппаИнформацияДляПередачиВЦентрЛицензирования.Видимость = Ложь;
			Элементы.Далее.Видимость = Истина;
			Элементы.Далее.КнопкаПоУмолчанию = Истина;
			Элементы.Готово.Видимость = Ложь;
		КонецЕсли;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУдаленнаяАктивация Тогда
		СохранитьСведенияОбАктивацииНаСервере();
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатАктивации;
		Элементы.Далее.Видимость = Ложь;
		Элементы.Готово.Видимость = Истина;
		Элементы.Готово.КнопкаПоУмолчанию = Истина;
		Если ВладелецФормы<>Неопределено Тогда
			Попытка
				ВладелецФормы.ОбновитьИнформациюОПодпискеКлиент();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	Элементы.Далее.Видимость = Истина;
	Элементы.Далее.КнопкаПоУмолчанию = Истина;
	Элементы.Готово.Видимость = Ложь;
	
	// Восстанавливаем видимость страниц (в случае некорретного поведения платформы при установке текущей страницы)
	Для каждого ТекСтраница Из Элементы.Страницы.ПодчиненныеЭлементы Цикл
		Если НЕ ТекСтраница.Видимость Тогда
			ТекСтраница.Видимость = Истина;
		КонецЕсли;	
	КонецЦикла;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборСпособаАктивации Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаИнструкция;
		Элементы.Назад.Видимость = Ложь;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводДанных Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборСпособаАктивации;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУдаленнаяАктивация Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводДанных;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатАктивации Тогда
		Если СпособАктивацииПоддержки = 0 Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводДанных;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУдаленнаяАктивация;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДомашняяСтраница(Команда)
	ДомашняяСтраницаНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДомашняяСтраницаНаСервере()
	ПолеHTML = Обработки[ПрефиксПродукта+"СведенияОЛицензииНаПоддержку"].ПолучитьМакет("МакетИнформацияОбАктивации").ПолучитьТекст();
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюОПодписке()
	Общиймодуль = Вычислить(ПрефиксПродукта+"ЗащитаКлиентСервер");
	ИнформацияОПодписке = ОбщийМодуль.ПолучитьСведенияОПоддержке();
	Внимание = "";
	Ошибки = "";
	Если НЕ ЗначениеЗаполнено(ИнформацияОПодписке) Тогда
		СостояниеПодписки = "Лицензия не активирована";
		Внимание = "ВНИМАНИЕ!!! Необходимо активировать Лицензию";
		Элементы.ДекорацияУспешно.Видимость = Ложь;
		Элементы.ДанныеДляАктивацииВДругихИБЛокальнойСети.Видимость = Ложь;
	Иначе
		СостояниеПодписки = "Лицензия действительна до: "+Формат(ИнформацияОПодписке.СрокОкончанияПоддержки,"ДФ=dd.MM.yyyy")+", оформлена на ключ защиты с серийным номером: "+ ИнформацияОПодписке.НомерКлюча;
		Элементы.ДекорацияУспешно.Видимость = Истина;
		Элементы.ДанныеДляАктивацииВДругихИБЛокальнойСети.Видимость = Истина;
		Если ИнформацияОПодписке.СрокОкончанияПоддержки < ТекущаяДата() Тогда
			Внимание = "ВНИМАНИЕ!!! Срок действующей Лицензии истек! Необходимо активировать Лицензию.";
			Элементы.ДекорацияУспешно.Видимость = Ложь;
			Элементы.ДанныеДляАктивацииВДругихИБЛокальнойСети.Видимость = Ложь;
		ИначеЕсли ИнформацияОПодписке.СрокОкончанияПоддержки < ДобавитьМесяц(ТекущаяДата(),1) Тогда
			Внимание = "До окончания срока действия Лицензии осталось менее месяца! Не забудьте продлить Лицензию!";
		ИначеЕсли ИнформацияОПодписке.Свойство("КатегорияПоддержки") И Строка(ИнформацияОПодписке.КатегорияПоддержки) <> КатегорияПлатнойПоддержкиОтраслевогоРешения Тогда
			Элементы.ДекорацияУспешно.Видимость = Ложь;
			Элементы.ДанныеДляАктивацииВДругихИБЛокальнойСети.Видимость = Ложь;
			Внимание = "Активированная категория поддержки не соответствует категории поддержки данного программного продукта!";
		КонецЕсли;
		Если Элементы.НомерОсновногоКлюча.СписокВыбора.НайтиПоЗначению(ИнформацияОПодписке.НомерКлюча)=Неопределено Тогда
			Элементы.ДекорацияУспешно.Видимость = Ложь;
			Внимание = Внимание + ?(Внимание="",""," ")+"Отсутствует ключ защиты, на который оформлена Лицензия!";
		КонецЕсли;
	КонецЕсли;
	Элементы.Ошибки.Видимость = Ошибки<>"";
	Элементы.Внимание.Видимость = Внимание<>"";
КонецПроцедуры

&НаСервере
Функция ОпределитьЭтаИнформационнаяБазаФайловая() Экспорт
			
	СтрокаСоединенияСБД = СтрокаСоединенияИнформационнойБазы();
	
	// в зависимости от того файловый это вариант БД или нет немного по-разному путь в БД формируется
	ПозицияПоиска = Найти(Врег(СтрокаСоединенияСБД), "FILE=");
	
	Возврат ПозицияПоиска = 1;	
	
КонецФункции

&НаКлиенте
Процедура ДанныеДляАктивацииВДругихИБЛокальнойСети(Команда)
	ОткрытьФорму(ИмяФормыОбработки()+".Форма.ДанныеАктивации",,ЭтаФорма,УникальныйИдентификатор,ВариантОткрытияОкна.ОтдельноеОкно);
КонецПроцедуры

&НаСервере
Функция ПопыткаАктивации()
	ОписаниеОшибки = "";
	ОбщийМодуль = Вычислить(ПрефиксПродукта+"ЗащитаКлиентСервер");
	РезультатПопытки = ОбщийМодуль.АктивироватьПоддержку(Объект, ОписаниеОшибки);
	Если РезультатПопытки Тогда
		Элементы.Ошибки.Видимость = Ложь;
		Элементы.ДекорацияУспешно.Видимость = Истина;
		Ошибки = "";
	
		МенеджерКонстанты = Константы[ПрефиксПродукта+"НомерОсновногоКлюча"].СоздатьМенеджерЗначения();
		МенеджерКонстанты.ДополнительныеСвойства.Вставить("ВнешняяОбработка",Истина);
		МенеджерКонстанты.Значение = Объект.НомерОсновногоКлюча;
		МенеджерКонстанты.Записать();
		
		МенеджерКонстанты = Константы[ПрефиксПродукта+"НомерРегистрационнойАнкеты"].СоздатьМенеджерЗначения();
		МенеджерКонстанты.ДополнительныеСвойства.Вставить("ВнешняяОбработка",Истина);
		МенеджерКонстанты.Значение = Объект.НомерРегистрационнойАнкеты;
		МенеджерКонстанты.Записать();
		
		ОбновитьИнформациюОПодписке();
	Иначе
		СостояниеПодписки = "Не удалось обновить данные Лицензии!";
		Элементы.Внимание.Видимость = Ложь;
		Элементы.ДекорацияУспешно.Видимость = Ложь;
		Элементы.ДанныеДляАктивацииВДругихИБЛокальнойСети.Видимость = Ложь;
		Элементы.Ошибки.Видимость = Истина;
		Ошибки = ОписаниеОшибки;
	КонецЕсли;
КонецФункции

&НаСервере
Функция ЗаполнитьСлужебнуюИнформацию()
	Выполнить(ПрефиксПродукта+"ЗащитаКлиентСервер.ПолучитьСлужебноеПредставление(Объект)");	
КонецФункции

&НаКлиенте
Функция ПроверитьДанные()
	НетОшибок = Истина;
	Если НЕ ЗначениеЗаполнено(Объект.НомерОсновногоКлюча) Тогда
		СообщитьПользователю("Не выбран основной ключ защиты, к которому будет привязана активация!");
		НетОшибок = Ложь;
	ИначеЕсли Элементы.НомерОсновногоКлюча.СписокВыбора.НайтиПоЗначению(Объект.НомерОсновногоКлюча)=Неопределено Тогда
		СообщитьПользователю("Не выбран основной ключ защиты, к которому будет привязана активация!");
		НетОшибок = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.НомерРегистрационнойАнкеты) Тогда
		СообщитьПользователю("Не указан номер регистрационной анкеты!");
		НетОшибок = Ложь;
	ИначеЕсли СтрДлина(СокрЛП(Объект.НомерРегистрационнойАнкеты))<>11 Тогда
		СообщитьПользователю("Номер регистрационной анкеты должен состоять из 11 символов!");
		НетОшибок = Ложь;
	КонецЕсли;
	
	Если СпособАктивацииПоддержки=0 Тогда
		Если НЕ СогласиеНаПередачуДанных Тогда
			СообщитьПользователю("Не подтверждено согласие на передачу данных");	
			НетОшибок = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НетОшибок;
КонецФункции

&НаКлиенте
// Формирует и выводит сообщение, которое может быть связано с элементом 
// управления формы.
//
//  Параметры
//  ТекстСообщенияПользователю - Строка - текст сообщения.
//  КлючДанных                - Любая ссылка на объект информационной базы.
//                               Ссылка на объект информационной базы, к которому это сообщение относится,
//                               или ключ записи.
//  Поле                       - Строка - наименование реквизита формы
//  ПутьКДанным                - Строка - путь к данным (путь к реквизиту формы)
//  Отказ                      - Булево - Выходной параметр
//                               Всегда устанавливается в значение Истина
//
//	Примеры использования:
//
//	1. Для вывода сообщения у поля управляемой формы, связанного с реквизитом объекта:
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), ,
//		"ПолеВРеквизитеФормыОбъект",
//		"Объект");
//
//	Альтернативный вариант использования в форме объекта:
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), ,
//		"Объект.ПолеВРеквизитеФормыОбъект");
//
//	2. Для вывода сообщения рядом с полем управляемой формы, связанным с реквизитом формы:
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), ,
//		"ИмяРеквизитаФормы");
//
//	3. Для вывода сообщения связанного с объектом информационной базы
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), ОбъектИнформационнойБазы, "Ответственный",,Отказ);
//
// 4. Для вывода сообщения по ссылке на объект информационной базы
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), Ссылка, , , Отказ);
//
// Случаи некорректного использования:
//  1. Передача одновременно параметров КлючДанных и ПутьКДанным
//  2. Передача в параметре КлючДанных значения типа отличного от допустимых
//  3. Установка ссылки без установки поля (и/или пути к данным)
//
Процедура СообщитьПользователю(
		Знач ТекстСообщенияПользователю,
		Знач КлючДанных = Неопределено,
		Знач Поле = "",
		Знач ПутьКДанным = "",
		Отказ = Ложь) Экспорт
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщенияПользователю;
	Сообщение.Поле = Поле;
	
	ЭтоОбъект = Ложь;
	
	Сообщение.КлючДанных = КлючДанных;
	
	Если НЕ ПустаяСтрока(ПутьКДанным) Тогда
		Сообщение.ПутьКДанным = ПутьКДанным;
	КонецЕсли;
		
	Сообщение.Сообщить();
	
	Отказ = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ИнструкцияПоАктивацииВДругихИБЛокальнойСетиНаСервере()
	ПолеHTML = Обработки[ПрефиксПродукта+"СведенияОЛицензииНаПоддержку"].ПолучитьМакет("МакетАктивацияВЛокальнойСети").ПолучитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияПоАктивацииВДругихИБЛокальнойСети(Команда)
	ИнструкцияПоАктивацииВДругихИБЛокальнойСетиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИнструкцияПоАктивацииНаСервере()
	ПолеHTML = Обработки[ПрефиксПродукта+"СведенияОЛицензииНаПоддержку"].ПолучитьМакет("МакетИнструкцияПоАктивации").ПолучитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияПоАктивации(Команда)
	ИнструкцияПоАктивацииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СохранитьСведенияОбАктивацииНаСервере()
	МенеджерКонстанты = Константы[ПрефиксПродукта+"СведенияОПодпискеНаПоддержку"].СоздатьМенеджерЗначения();
	МенеджерКонстанты.ДополнительныеСвойства.Вставить("ВнешняяОбработка",Истина);
	МенеджерКонстанты.Значение = Новый ХранилищеЗначения(Объект.ОтветЦентраЛицензирования);
	МенеджерКонстанты.Записать();
	
	МенеджерКонстанты = Константы[ПрефиксПродукта+"НомерОсновногоКлюча"].СоздатьМенеджерЗначения();
	МенеджерКонстанты.ДополнительныеСвойства.Вставить("ВнешняяОбработка",Истина);
	МенеджерКонстанты.Значение = Объект.НомерОсновногоКлюча;
	МенеджерКонстанты.Записать();
	
	МенеджерКонстанты = Константы[ПрефиксПродукта+"НомерРегистрационнойАнкеты"].СоздатьМенеджерЗначения();
	МенеджерКонстанты.ДополнительныеСвойства.Вставить("ВнешняяОбработка",Истина);
	МенеджерКонстанты.Значение = Объект.НомерРегистрационнойАнкеты;
	МенеджерКонстанты.Записать();
	
	ОбновитьИнформациюОПодписке();	
КонецПроцедуры

&НаКлиенте
Процедура СпособАктивацииПоддержкиПриИзменении(Элемент)
	Элементы.СогласиеНаПередачуДанных.Видимость = СпособАктивацииПоддержки=0;
КонецПроцедуры

&НаСервере
Функция ИмяФормыОбработки()
	ОбщийМодуль = Вычислить(ПрефиксПродукта+"ЗащитаКлиентСервер");
	Возврат ОбщийМодуль.ПолучитьИмяФормыДляОткрытия();	
КонецФункции




