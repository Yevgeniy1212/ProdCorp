////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

// Формирует запрос по регистру НамеченныеСобытияПоПерсоналуОрганизаций
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапрос(Организации = Неопределено, ДатаНачала, ДатаОкончания, МаксимальноеКоличествоСобытий = 0) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала",		ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Организации",	Организации);
	Запрос.УстановитьПараметр("ПланируемоеУвольнение",	Перечисления.НамеченныеСобытияПоПерсоналуОрганизаций.Увольнение);
	Запрос.УстановитьПараметр("ПланируемоеПеремещение",	Перечисления.НамеченныеСобытияПоПерсоналуОрганизаций.Перемещение);
	Запрос.УстановитьПараметр("Увольнение",		Перечисления.ПричиныИзмененияСостояния.Увольнение);
	Запрос.УстановитьПараметр("Работает",		Перечисления.СостоянияРаботникаОрганизации.Работает);
	Запрос.УстановитьПараметр("НеРаботает",		Перечисления.СостоянияРаботникаОрганизации.НеРаботает);
	Запрос.УстановитьПараметр("ПустаяОрганизация",	Справочники.Организации.ПустаяСсылка());
	
	ПолеТекущееСостояние = "
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = &Увольнение ТОГДА &НеРаботает
	|		КОГДА НЕ (НамеченныеИзменения.Состояние ЕСТЬ NULL) ТОГДА НамеченныеИзменения.Состояние
	|		ИНАЧЕ &Работает
	|	КОНЕЦ";
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ" + ?(МаксимальноеКоличествоСобытий = 0, "", " ПЕРВЫЕ " + МаксимальноеКоличествоСобытий) + "
	|	НамеченныеИзменения.Сотрудник,
	|	НамеченныеИзменения.Сотрудник.Наименование КАК СотрудникНаименование,
	|	НамеченныеИзменения.Организация,
	|	НамеченныеИзменения.Организация.Наименование КАК ОрганизацияНаименование,
	|	НамеченныеИзменения.ДатаИзменения,
	|	НамеченныеИзменения.ПланируемоеСобытие,
	|	НамеченныеИзменения.Подразделение,
	|	НамеченныеИзменения.Должность,
	|	НамеченныеИзменения.ТарифныйРазряд,
	|	НамеченныеИзменения.ГрафикРаботы,
	|	НамеченныеИзменения.СтатьяЗаконаОТрудеРК,
	|	НамеченныеИзменения.ЗанимаемыхСтавок,
	|	НамеченныеИзменения.ЗаниматьСтавку,
	|	" + ПолеТекущееСостояние + " КАК ПроверяемоеЗначение,
	|	РаботникиОрганизации.ТарифныйРазряд КАК ТекущийТарифныйРазряд,
	|	РаботникиОрганизации.Должность КАК ТекущаяДолжность,
	|	РаботникиОрганизации.ПодразделениеОрганизации КАК ТекущееПодразделение,
	|	РаботникиОрганизации.Сотрудник.Код КАК ТабельныйНомер,
	|	РаботникиОрганизации.ГрафикРаботы КАК ТекущийГрафикРаботы,
	|	РаботникиОрганизации.ЗанимаемыхСтавок КАК ТекущихСтавок
	|ИЗ
	|	(ВЫБРАТЬ
	|		НамеченныеИзмененияСДаннымиДляПроверок.ДатаИзменения КАК ДатаИзменения,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Сотрудник КАК Сотрудник,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ПланируемоеСобытие КАК ПланируемоеСобытие,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Подразделение КАК Подразделение,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Должность КАК Должность,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ТарифныйРазряд КАК ТарифныйРазряд,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ЗаниматьСтавку КАК ЗаниматьСтавку,
	|		МАКСИМУМ(РаботникиОрганизации.Период) КАК ДатаВРаботники,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Состояние КАК Состояние,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Организация КАК Организация,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ГрафикРаботы КАК ГрафикРаботы,
	|		НамеченныеИзмененияСДаннымиДляПроверок.СтатьяЗаконаОТрудеРК КАК СтатьяЗаконаОТрудеРК
	|	ИЗ
	|		(ВЫБРАТЬ
	|			СписокИзменений.ДатаИзменения КАК ДатаИзменения,
	|			СписокИзменений.Сотрудник КАК Сотрудник,
	|			СписокИзменений.ПланируемоеСобытие КАК ПланируемоеСобытие,
	|			СписокИзменений.Подразделение КАК Подразделение,
	|			СписокИзменений.Должность КАК Должность,
	|			СписокИзменений.ТарифныйРазряд КАК ТарифныйРазряд,
	|			СписокИзменений.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	|			СписокИзменений.ЗаниматьСтавку КАК ЗаниматьСтавку,
	|			ВЫБОР 
	|				КОГДА СостояниеРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|						И СостояниеРаботниковОрганизации.ПериодЗавершения  < СписокИзменений.ДатаИзменения
	|					ТОГДА СостояниеРаботниковОрганизации.СостояниеЗавершения
	|				ИНАЧЕ СостояниеРаботниковОрганизации.Состояние
	|			КОНЕЦ КАК Состояние,
	|			СписокИзменений.ДатаЗначения КАК ДатаПоследнегоИзменения,
	|			СписокИзменений.Организация КАК Организация,
	|			СписокИзменений.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|			СписокИзменений.ГрафикРаботы КАК ГрафикРаботы,
	|			СписокИзменений.СтатьяЗаконаОТрудеРК КАК СтатьяЗаконаОТрудеРК
	|		ИЗ
	|			(ВЫБРАТЬ
	|				НамеченныеСобытияПоПерсоналу.ДатаИзменения КАК ДатаИзменения,
	|				НамеченныеСобытияПоПерсоналу.Сотрудник КАК Сотрудник,
	|				НамеченныеСобытияПоПерсоналу.ПланируемоеСобытие КАК ПланируемоеСобытие,
	|				НамеченныеСобытияПоПерсоналу.ПодразделениеОрганизации КАК Подразделение,
	|				НамеченныеСобытияПоПерсоналу.Должность КАК Должность,
	|				НамеченныеСобытияПоПерсоналу.ТарифныйРазряд КАК ТарифныйРазряд,
	|				НамеченныеСобытияПоПерсоналу.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	|				НамеченныеСобытияПоПерсоналу.ЗаниматьСтавку КАК ЗаниматьСтавку,
	|				МАКСИМУМ(СостояниеРаботниковОрганизации.Период) КАК ДатаЗначения,
	|				НамеченныеСобытияПоПерсоналу.Организация КАК Организация,
	|				ВЫБОР
	|					КОГДА НамеченныеСобытияПоПерсоналу.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|						ТОГДА НамеченныеСобытияПоПерсоналу.Организация
	|					ИНАЧЕ НамеченныеСобытияПоПерсоналу.Организация.ГоловнаяОрганизация
	|				КОНЕЦ КАК ГоловнаяОрганизация,
	|				НамеченныеСобытияПоПерсоналу.ГрафикРаботы КАК ГрафикРаботы,
	|				НамеченныеСобытияПоПерсоналу.СтатьяЗаконаОТрудеРК КАК СтатьяЗаконаОТрудеРК
	|			ИЗ
	|				РегистрСведений.НамеченныеСобытияПоПерсоналуОрганизаций КАК НамеченныеСобытияПоПерсоналу
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботниковОрганизации
	|					ПО СостояниеРаботниковОрганизации.Сотрудник = НамеченныеСобытияПоПерсоналу.Сотрудник 
	|						И ВЫБОР
	|							КОГДА НамеченныеСобытияПоПерсоналу.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|								ТОГДА НамеченныеСобытияПоПерсоналу.Организация
	|							ИНАЧЕ НамеченныеСобытияПоПерсоналу.Организация.ГоловнаяОрганизация
	|						КОНЕЦ = СостояниеРаботниковОрганизации.Организация 
	|						И (ВЫБОР 
	|								КОГДА НамеченныеСобытияПоПерсоналу.ПланируемоеСобытие = &ПланируемоеУвольнение 
	|									ТОГДА ДОБАВИТЬКДАТЕ(НамеченныеСобытияПоПерсоналу.ДатаИзменения, ДЕНЬ, 1) 
	|								ИНАЧЕ НамеченныеСобытияПоПерсоналу.ДатаИзменения 
	|							КОНЕЦ >= СостояниеРаботниковОрганизации.Период)
	|			
	|			ГДЕ
	|				НамеченныеСобытияПоПерсоналу.ДатаИзменения >= &ДатаНачала И
	|				НамеченныеСобытияПоПерсоналу.ДатаИзменения <= &ДатаОкончания И
	|				НамеченныеСобытияПоПерсоналу.Организация В (&Организации)
	|			
	|			СГРУППИРОВАТЬ ПО
	|				НамеченныеСобытияПоПерсоналу.ДатаИзменения,
	|				НамеченныеСобытияПоПерсоналу.Сотрудник,
	|				НамеченныеСобытияПоПерсоналу.ПланируемоеСобытие,
	|				НамеченныеСобытияПоПерсоналу.ПодразделениеОрганизации,
	|				НамеченныеСобытияПоПерсоналу.Должность,
	|				НамеченныеСобытияПоПерсоналу.ТарифныйРазряд,
	|				НамеченныеСобытияПоПерсоналу.ЗанимаемыхСтавок,
	|				НамеченныеСобытияПоПерсоналу.ЗаниматьСтавку,
	|				НамеченныеСобытияПоПерсоналу.Организация,
	|				ВЫБОР
	|					КОГДА НамеченныеСобытияПоПерсоналу.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|						ТОГДА НамеченныеСобытияПоПерсоналу.Организация
	|					ИНАЧЕ НамеченныеСобытияПоПерсоналу.Организация.ГоловнаяОрганизация
	|				КОНЕЦ,
	|				НамеченныеСобытияПоПерсоналу.ГрафикРаботы,
	|				НамеченныеСобытияПоПерсоналу.СтатьяЗаконаОТрудеРК) КАК СписокИзменений
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботниковОрганизации
	|				ПО СписокИзменений.Сотрудник = СостояниеРаботниковОрганизации.Сотрудник 
	|					И СписокИзменений.ГоловнаяОрганизация = СостояниеРаботниковОрганизации.Организация 
	|					И СписокИзменений.ДатаЗначения = СостояниеРаботниковОрганизации.Период) КАК НамеченныеИзмененияСДаннымиДляПроверок
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|			ПО НамеченныеИзмененияСДаннымиДляПроверок.Сотрудник = РаботникиОрганизации.Сотрудник 
	|				И НамеченныеИзмененияСДаннымиДляПроверок.ГоловнаяОрганизация = РаботникиОрганизации.Организация 
	|				И НамеченныеИзмененияСДаннымиДляПроверок.ДатаИзменения >= ВЫБОР
	|																			КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = &Увольнение
	|																				ТОГДА ДОБАВИТЬКДАТЕ(РаботникиОрганизации.Период, ДЕНЬ, -1)
	|																			ИНАЧЕ РаботникиОрганизации.Период
	|																		  КОНЕЦ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		НамеченныеИзмененияСДаннымиДляПроверок.ДатаИзменения,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Сотрудник,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ПланируемоеСобытие,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Подразделение,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Должность,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ТарифныйРазряд,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ЗанимаемыхСтавок,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ЗаниматьСтавку,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Состояние,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ДатаПоследнегоИзменения,
	|		НамеченныеИзмененияСДаннымиДляПроверок.Организация,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ГоловнаяОрганизация,
	|		НамеченныеИзмененияСДаннымиДляПроверок.ГрафикРаботы,
	|		НамеченныеИзмененияСДаннымиДляПроверок.СтатьяЗаконаОТрудеРК) КАК НамеченныеИзменения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ПО НамеченныеИзменения.Сотрудник = РаботникиОрганизации.Сотрудник 
	|			И НамеченныеИзменения.ГоловнаяОрганизация = РаботникиОрганизации.Организация 
	|			И НамеченныеИзменения.ДатаВРаботники = РаботникиОрганизации.Период
	|                              
	|ГДЕ
	|	ВЫБОР 
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Командировка)
	|				И  НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.Командировка)
	|			ТОГДА ИСТИНА
	|
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускЕжегодный)
	|				И  НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.ОтпускЕжегодный)
	|			ТОГДА ИСТИНА
	|
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|				И  НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.ОтпускУчебный)
	|			ТОГДА ИСТИНА
	|
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебныйНеоплачиваемый)
	|				И  НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.ОтпускУчебныйНеоплачиваемый)
	|			ТОГДА ИСТИНА
	|
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускПоУходуЗаРебенком)
	|				И  НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.ОтпускПоУходуЗаРебенком)
	|			ТОГДА ИСТИНА
	|
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускПоБеременностиИРодам)
	|				И  НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.ОтпускПоБеременностиИРодам)
	|			ТОГДА ИСТИНА
	|
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускБезСохраненияЗарплаты)
	|				И  НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.ОтпускБезСохраненияЗарплаты)
	|			ТОГДА ИСТИНА
	|
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает)
	|			ТОГДА ВЫБОР
	|					КОГДА НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.Увольнение)
	|							И НамеченныеИзменения.ПланируемоеСобытие <> ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.Перемещение)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НамеченныеИзменения.ПланируемоеСобытие = ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналуОрганизаций.Перемещение) 
	|									И НамеченныеИзменения.Подразделение = РаботникиОрганизации.ПодразделениеОрганизации
	|									И НамеченныеИзменения.Должность = РаботникиОрганизации.Должность
	|									И НамеченныеИзменения.ТарифныйРазряд = РаботникиОрганизации.Должность
	|									И НамеченныеИзменения.ЗанимаемыхСтавок = РаботникиОрганизации.ЗанимаемыхСтавок
	|									И НамеченныеИзменения.ГрафикРаботы = РаботникиОрганизации.ГрафикРаботы
	|								ТОГДА ИСТИНА
	|							ИНАЧЕ ЛОЖЬ
	|						  КОНЕЦ
	|				  КОНЕЦ
	|
	|		КОГДА " + ПолеТекущееСостояние + " = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.НеРаботает)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ = ЛОЖЬ
	|";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапрос()	

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ

// Заполняет т.ч. обработки намеченными, но не выполненными ранее кадровыми изменениями
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
Процедура Автозаполнение() Экспорт

	РезультатЗапроса = СформироватьЗапрос(Организация, ДатаНачала, ДатаОкончания);
	ВыборкаЗапроса   = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		НоваяСтрока = Результаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапроса);
	КонецЦикла;
	
КонецПроцедуры

// Разбирает отмеченные строки т.ч. обработки по видам намеченных кадровых изменений
//
// Параметры: 
//  ТабУвольнение, 
//  ТабПеремещение, 
//  ТабВозвратНаРаботу, 
//  ТабВводСведенийОПлановыхУдержанияхРаботниковОрганизации 
//			- таблицы значений в них помещаются разобранные 
//			по видам намеченные кадровые изменения
//
// Возвращаемое значение:
//  Нет
//
Процедура РазобратьТаблицуИзменений(ТабУвольнение, ТабПеремещение, ТабВозвратНаРаботу) Экспорт
	
	Массив = Новый Массив();
	Массив.Добавить(Тип("СправочникСсылка.СотрудникиОрганизаций"));
	ОписаниеТиповСотрудник =  Новый ОписаниеТипов(Массив);
	
	Массив.Очистить();
	Массив.Добавить(Тип("ДокументСсылка.ПриемНаРаботуВОрганизацию"));
	ОписаниеТиповПирказ =  Новый ОписаниеТипов(Массив);

	Массив.Очистить();
	Массив.Добавить(Тип("Дата"));
	ОписаниеТиповДата =  Новый ОписаниеТипов(Массив);

	Массив.Очистить();
	Массив.Добавить(Тип("СправочникСсылка.ПодразделенияОрганизаций"));
	ОписаниеТиповПодразделение =  Новый ОписаниеТипов(Массив);

	Массив.Очистить();
	Массив.Добавить(Тип("СправочникСсылка.ДолжностиОрганизаций"));
	ОписаниеТиповДолжность =  Новый ОписаниеТипов(Массив);

	Массив.Очистить();
	Массив.Добавить(Тип("СправочникСсылка.ТарифныеРазряды"));
	ОписаниеТиповТарифныйРазряд =  Новый ОписаниеТипов(Массив);

	Массив.Очистить();
	Массив.Добавить(Тип("СправочникСсылка.ГрафикиРаботы"));
	ОписаниеТиповГрафик =  Новый ОписаниеТипов(Массив);
	
	Массив.Очистить();
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧисло =  Новый ОписаниеТипов(Массив);

	Массив.Очистить();
	Массив.Добавить(Тип("Строка"));
	ОписаниеТиповСтрока =  Новый ОписаниеТипов(Массив);
	
	ТабУвольнение.Колонки.Добавить("Сотрудник");
	ТабУвольнение.Колонки.Добавить("ДатаУвольнения");
	ТабУвольнение.Колонки.Добавить("ОснованиеУвольнения");
	
	ТабПеремещение.Колонки.Добавить("Сотрудник",				ОписаниеТиповСотрудник);
	ТабПеремещение.Колонки.Добавить("ДатаНачала",				ОписаниеТиповДата);
	ТабПеремещение.Колонки.Добавить("ПодразделениеОрганизации",	ОписаниеТиповПодразделение);
	ТабПеремещение.Колонки.Добавить("Должность",				ОписаниеТиповДолжность);
	ТабПеремещение.Колонки.Добавить("ТарифныйРазряд",			ОписаниеТиповТарифныйРазряд);
	ТабПеремещение.Колонки.Добавить("ГрафикРаботы",				ОписаниеТиповГрафик);
	ТабПеремещение.Колонки.Добавить("ЗанимаемыхСтавок",			ОписаниеТиповЧисло);
	ТабПеремещение.Колонки.Добавить("ТабельныйНомер",			ОписаниеТиповСтрока);
	ТабПеремещение.Колонки.Добавить("СтатьяЗаконаОТрудеРК",		ОписаниеТиповСтрока);
    ТабПеремещение.Колонки.Добавить("СпособРасчета");

	ТабВозвратНаРаботу.Колонки.Добавить("Сотрудник");
	ТабВозвратНаРаботу.Колонки.Добавить("ДатаВозврата");
	ТабВозвратНаРаботу.Колонки.Добавить("ЗаниматьСтавку");
	
    // рассортируем необходимые движения
	Для Каждого Строка Из Результаты Цикл
		Если НЕ Строка.ФормироватьДокумент Тогда
            Продолжить
		КонецЕсли;	
		Если Строка.ПланируемоеСобытие = Перечисления.НамеченныеСобытияПоПерсоналуОрганизаций.Увольнение Тогда
			СтрокаТаб = ТабУвольнение.Добавить();
			СтрокаТаб.Сотрудник 		= Строка.Сотрудник;
			СтрокаТаб.ДатаУвольнения    = Строка.ДатаИзменения;
			СтрокаТаб.ОснованиеУвольнения = Строка.ПланируемоеСобытие;
		ИначеЕсли Строка.ПланируемоеСобытие = Перечисления.НамеченныеСобытияПоПерсоналуОрганизаций.Перемещение Тогда
			СтрокаТаб = ТабПеремещение.Добавить();
			СтрокаТаб.Сотрудник			= Строка.Сотрудник;
			СтрокаТаб.ДатаНачала 		= Строка.ДатаИзменения;
			СтрокаТаб.ПодразделениеОрганизации = Строка.Подразделение;
			СтрокаТаб.Должность		 	= Строка.Должность;
			СтрокаТаб.ТарифныйРазряд 	= Строка.ТарифныйРазряд;
			СтрокаТаб.ГрафикРаботы		= Строка.ГрафикРаботы;
			СтрокаТаб.ЗанимаемыхСтавок  = Строка.ЗанимаемыхСтавок;
			СтрокаТаб.ТабельныйНомер	= Строка.ТабельныйНомер;
			СтрокаТаб.СтатьяЗаконаОТрудеРК	= Строка.СтатьяЗаконаОТрудеРК;
		Иначе 
			СтрокаТаб = ТабВозвратНаРаботу.Добавить();
			СтрокаТаб.Сотрудник 		= Строка.Сотрудник;
			СтрокаТаб.ДатаВозврата		= Строка.ДатаИзменения;
			СтрокаТаб.ЗаниматьСтавку	= Строка.ЗаниматьСтавку;
		КонецЕсли;
		
	КонецЦикла;	

КонецПроцедуры
