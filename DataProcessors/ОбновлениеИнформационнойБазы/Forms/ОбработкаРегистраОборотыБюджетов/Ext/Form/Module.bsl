
&НаСервере
Процедура ВыполнитьОбработкуНаСервере()
	
	СоответствиеСхемам = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Удалить_фин_ШаблоныОтраженияДанныхРегистровПоБюджетам.Ссылка,
		|	Удалить_фин_ШаблоныОтраженияДанныхРегистровПоБюджетам.ПравилоНовогоФормата
		|ИЗ
		|	Справочник.Удалить_фин_ШаблоныОтраженияДанныхРегистровПоБюджетам КАК Удалить_фин_ШаблоныОтраженияДанныхРегистровПоБюджетам
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Удалить_фин_ШаблоныОтраженияФактическихДанныхПоБюджетам.Ссылка,
		|	Удалить_фин_ШаблоныОтраженияФактическихДанныхПоБюджетам.ПравилоНовогоФормата
		|ИЗ
		|	Справочник.Удалить_фин_ШаблоныОтраженияФактическихДанныхПоБюджетам КАК Удалить_фин_ШаблоныОтраженияФактическихДанныхПоБюджетам";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СоответствиеСхемам.Вставить(ВыборкаДетальныеЗаписи.Ссылка,ВыборкаДетальныеЗаписи.ПравилоНовогоФормата);
	КонецЦикла;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НаборПравилОтражения.Ссылка КАК Шаблон,
		|	НаборПравилОтражения.БазовыйИдентификатор КАК ИдентификаторПравила,
		|	НаборПравилОтражения.Комментарий,
		|	НаборПравилОтражения.Правило
		|ИЗ
		|	Справочник.Удалить_фин_ШаблоныОтраженияФактическихДанныхПоБюджетам.НаборПравилОтражения КАК НаборПравилОтражения
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НаборПравилОтражения.Ссылка,
		|	НаборПравилОтражения.БазовыйИдентификатор,
		|	НаборПравилОтражения.Комментарий,
		|	НаборПравилОтражения.Правило
		|ИЗ
		|	Справочник.Удалить_фин_ШаблоныОтраженияДанныхРегистровПоБюджетам.НаборПравилОтражения КАК НаборПравилОтражения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаПравил = РезультатЗапроса.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(фин_ОборотыБюджетов.Период) КАК Период,
		|	фин_ОборотыБюджетов.Регистратор
		|ИЗ
		|	РегистрНакопления.фин_ОборотыБюджетов КАК фин_ОборотыБюджетов
		|ГДЕ
		|	фин_ОборотыБюджетов.Правило = ЗНАЧЕНИЕ(Справочник.фин_ПравилаОтраженияФактическихДанных.ПустаяСсылка)
		|	И НЕ фин_ОборотыБюджетов.Регистратор ССЫЛКА Документ.фин_УчетФактическихДанныхПоБюджетам
		|			И НЕ фин_ОборотыБюджетов.Регистратор ССЫЛКА Документ.КорректировкаЗаписейРегистров
		|
		|СГРУППИРОВАТЬ ПО
		|	фин_ОборотыБюджетов.Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Документ  = ВыборкаДетальныеЗаписи.Регистратор;
		НаборЗаписей = РегистрыНакопления.фин_ОборотыБюджетов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Документ);
		НаборЗаписей.Прочитать();
		Для Каждого СтрокаНабора Из НаборЗаписей Цикл
			СтрокаНабора.Схема = СоответствиеСхемам.Получить(СтрокаНабора.УдалитьШаблон);
			Если ЗначениеЗаполнено(СтрокаНабора.УдалитьИдентификаторПравила) Тогда
				СтрокиПравил = ТаблицаПравил.НайтиСтроки(Новый Структура("Шаблон,ИдентификаторПравила",СтрокаНабора.УдалитьШаблон,СтрокаНабора.УдалитьИдентификаторПравила));
				Если СтрокиПравил.Количество()>0 Тогда
					СтрокаНабора.Правило = СтрокиПравил[0].Правило;
				КонецЕсли;
			Иначе	
				СтрокиПравил = ТаблицаПравил.НайтиСтроки(Новый Структура("Шаблон,Комментарий",СтрокаНабора.УдалитьШаблон,СтрокаНабора.УдалитьКомментарийШаблона));
				Если СтрокиПравил.Количество()>0 Тогда
					СтрокаНабора.Правило = СтрокиПравил[0].Правило;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось обновить движения документа "+Документ);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработкуРегистра(Команда)
	ВыполнитьОбработкуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработку() Экспорт
	ВыполнитьОбработкуНаСервере();
КонецПроцедуры
