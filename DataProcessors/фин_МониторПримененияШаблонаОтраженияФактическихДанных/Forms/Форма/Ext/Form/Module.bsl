
&НаКлиенте
Процедура ДекорацияЗакрытьНажатие(Элемент)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура НадписьПроверкаНажатие(Элемент)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	ВыполнитьПроверку();
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПроверку()
	МассивУдалить = Новый Массив;
	Для Каждого РеквизитФормы Из ЭтотОбъект.ПолучитьРеквизиты() Цикл
		Если Найти(РеквизитФормы.Имя,"Таблица_")<>0 Тогда
			МассивУдалить.Добавить(РеквизитФормы.Имя);
			Если Элементы.Найти(РеквизитФормы.Имя) <>Неопределено Тогда
				Элементы.Удалить(Элементы[РеквизитФормы.Имя]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Попытка
		ИзменитьРеквизиты(,МассивУдалить);
	Исключение
	КонецПопытки;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Документ.Ссылка КАК Документ,
		|	НаборПравилОтражения.Ссылка КАК Правило
		|ПОМЕСТИТЬ ВТ_Кортеж
		|ИЗ
		|	Документ."+Объект.ВидДокументов.ПрограммныйИдентификатор+" КАК Документ,
		|	Справочник.фин_ПравилаОтраженияФактическихДанных КАК НаборПравилОтражения
		|ГДЕ
		|	Документ.Проведен
		|	И Документ.Дата >= &ДатаНачала
		|	И Документ.Дата <= &ДатаОкончания
		|	И (НаборПравилОтражения.Владелец = &Шаблон ИЛИ НаборПравилОтражения.Владелец.Владелец = &Шаблон)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВТ_Кортеж.Документ, ОборотыБюджетов.Регистратор) КАК Документ,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ОборотыБюджетов.НомерСтроки ЕСТЬ NULL 
		|				ТОГДА 0
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК ПризнакОтражения,
		|	ЕСТЬNULL(ВТ_Кортеж.Правило, ОборотыБюджетов.Правило) КАК Правило,
		|	ЕСТЬNULL(ВТ_Кортеж.Документ.Дата, ОборотыБюджетов.Регистратор.Дата) КАК Дата
		|ПОМЕСТИТЬ ВТ_Результат
		|ИЗ
		|	ВТ_Кортеж КАК ВТ_Кортеж
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления."+фин_ОбщегоНазначенияВызовСервераПовтИсп.РегистрФактическихДанныхДляДокумента(Объект.ВидДокументов).ИмяРегистра+" КАК ОборотыБюджетов
		|		ПО ВТ_Кортеж.Документ = ОборотыБюджетов.Регистратор
		|			И ВТ_Кортеж.Правило = ОборотыБюджетов.Правило
		|ГДЕ ОборотыБюджетов.Регистратор ЕСТЬ NULL ИЛИ
		|	(ОборотыБюджетов.Регистратор ССЫЛКА Документ."+Объект.ВидДокументов.ПрограммныйИдентификатор+"
		|	И ОборотыБюджетов.Регистратор.Дата >= &ДатаНачала
		|	И ОборотыБюджетов.Регистратор.Дата <= &ДатаОкончания
		|	И ОборотыБюджетов.Схема = &Шаблон)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(ВТ_Кортеж.Документ, ОборотыБюджетов.Регистратор),
		|	ЕСТЬNULL(ВТ_Кортеж.Правило, ОборотыБюджетов.Правило),
		|	ЕСТЬNULL(ВТ_Кортеж.Документ.Дата, ОборотыБюджетов.Регистратор.Дата)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Документ КАК Документ,
		|	ВТ.ПризнакОтражения КАК ПризнакОтражения,
		|	ВТ.Правило,
		|	ВТ.Дата КАК Дата
		|ИЗ
		|	ВТ_Результат КАК ВТ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата
		|ИТОГИ
		|	СУММА(ПризнакОтражения)
		|ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Кортежи.Правило
		|ИЗ
		|	(ВЫБРАТЬ
		|		Т1.Правило КАК Правило
		|	ИЗ
		|		ВТ_Кортеж КАК Т1
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Т2.Правило
		|	ИЗ
		|		ВТ_Результат КАК Т2) КАК Кортежи
		|
		|СГРУППИРОВАТЬ ПО
		|	Кортежи.Правило";

	Запрос.УстановитьПараметр("ДатаНачала", 	Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	Период.ДатаОкончания);
 	Запрос.УстановитьПараметр("Шаблон", 		Объект.Шаблон);

	Результат = Запрос.ВыполнитьПакет();
	
	СписокТаблиц 		= Результат[3].Выгрузить();
	СписокТаблиц.Колонки.Добавить("Номер",Новый ОписаниеТипов("Число"));
 	ВыборкаДокументов 	= Результат[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТаблицаРезультата = Новый ТаблицаЗначений;

	РеквизитТаблица 	= Новый РеквизитФормы("Таблица_Контроль",Новый ОписаниеТипов("ТаблицаЗначений"),,"Контрольная ведомость");
	МассивДобавить = Новый Массив;
	МассивДобавить.Добавить(РеквизитТаблица);
	ИзменитьРеквизиты(МассивДобавить);
	МассивДобавить = Новый Массив;
	РеквизитКолонка 	= Новый РеквизитФормы("Документ",Новый ОписаниеТипов("ДокументСсылка."+Объект.ВидДокументов.ПрограммныйИдентификатор),"Таблица_Контроль","Документ");
	ТаблицаРезультата.Колонки.Добавить("Документ",Новый ОписаниеТипов("ДокументСсылка."+Объект.ВидДокументов.ПрограммныйИдентификатор));
	МассивДобавить.Добавить(РеквизитКолонка);
	РеквизитКолонка 	= Новый РеквизитФормы("Отражен",Новый ОписаниеТипов("Булево"),"Таблица_Контроль","Отражен");
	ТаблицаРезультата.Колонки.Добавить("Отражен",Новый ОписаниеТипов("Булево"));
	МассивДобавить.Добавить(РеквизитКолонка);
	Номер = 1;
	Для Каждого Таблица Из СписокТаблиц Цикл
		Таблица.Номер = Номер;
		РеквизитКолонка 	= Новый РеквизитФормы("К"+Строка(Номер),Новый ОписаниеТипов("Булево"),"Таблица_Контроль",Таблица.Правило);
		ТаблицаРезультата.Колонки.Добавить("К"+Строка(Номер),Новый ОписаниеТипов("Булево"));
		МассивДобавить.Добавить(РеквизитКолонка);
		Номер = Номер+1;
	КонецЦикла;
	ИзменитьРеквизиты(МассивДобавить);

	Пока ВыборкаДокументов.Следующий() Цикл
		НоваяСтрока = ТаблицаРезультата.Добавить();
		НоваяСтрока.Документ = ВыборкаДокументов.Документ;
		НоваяСтрока.Отражен = ?(ВыборкаДокументов.ПризнакОтражения>0,Истина,Ложь);
		ВложеннаяВыборка = ВыборкаДокументов.Выбрать();
		Пока ВложеннаяВыборка.Следующий() Цикл
			Номер = СписокТаблиц.НайтиСтроки(Новый Структура("Правило",ВложеннаяВыборка.Правило))[0].Номер;
			НоваяСтрока["К"+Строка(Номер)]=?(ВложеннаяВыборка.ПризнакОтражения>0,Истина,Ложь)
		КонецЦикла;
	КонецЦикла;
	
	ЭлементФормы = Элементы.Добавить("Таблица_Контроль",Тип("ТаблицаФормы"),Элементы.РезультатыПроверки);
	ЭлементФормы.ПутьКДанным = "Таблица_Контроль";
	Для Каждого Колонка Из ТаблицаРезультата.Колонки Цикл
		РеквизитКолонка 			= Элементы.Добавить("Таблица_Контроль"+Колонка.Имя,Тип("ПолеФормы"),ЭлементФормы);
		РеквизитКолонка.Вид			=?(Колонка.Имя="Документ",ВидПоляФормы.ПолеВвода,ВидПоляФормы.ПолеФлажка);
		РеквизитКолонка.ПутьКДанным	= "Таблица_Контроль"+"."+Колонка.Имя;
		Если Колонка.Имя="Документ" Тогда
			РеквизитКолонка.КнопкаВыбора = Ложь;
			РеквизитКолонка.РедактированиеТекста = Ложь;
		Иначе
			РеквизитКолонка.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЦикла;
	// перенесем данные в таблицу
	ЗначениеВРеквизитФормы(ТаблицаРезультата,"Таблица_Контроль");
	ЭлементФормы.ИзменятьСоставСтрок = Ложь;
	ЭлементФормы.КоманднаяПанель.Видимость = Ложь;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Период.Вариант=ВариантСтандартногоПериода.ЭтотГод;
	
	РеквизитТаблица 	= Новый РеквизитФормы("Таблица_Контроль",Новый ОписаниеТипов("ТаблицаЗначений"),,"Контрольная ведомость");
	МассивДобавить = Новый Массив;
	МассивДобавить.Добавить(РеквизитТаблица);
	ИзменитьРеквизиты(МассивДобавить);
	МассивДобавить = Новый Массив;
	РеквизитКолонка 	= Новый РеквизитФормы("Документ",Новый ОписаниеТипов("Неопределено"),"Таблица_Контроль","Документ");
	МассивДобавить.Добавить(РеквизитКолонка);
	РеквизитКолонка 	= Новый РеквизитФормы("Отражен",Новый ОписаниеТипов("Булево"),"Таблица_Контроль","Отражен");
	МассивДобавить.Добавить(РеквизитКолонка);
	ИзменитьРеквизиты(МассивДобавить);

	ЭлементФормы = Элементы.Добавить("Таблица_Контроль",Тип("ТаблицаФормы"),Элементы.РезультатыПроверки);
	ЭлементФормы.ПутьКДанным = "Таблица_Контроль";
	РеквизитКолонка 			= Элементы.Добавить("Таблица_КонтрольДокумент",Тип("ПолеФормы"),ЭлементФормы);
	РеквизитКолонка.Вид			=ВидПоляФормы.ПолеВвода;
	РеквизитКолонка.ПутьКДанным	= "Таблица_Контроль.Документ";
	РеквизитКолонка 			= Элементы.Добавить("Таблица_КонтрольОтражен",Тип("ПолеФормы"),ЭлементФормы);
	РеквизитКолонка.Вид			=ВидПоляФормы.ПолеФлажка;
	РеквизитКолонка.ПутьКДанным	= "Таблица_Контроль.Отражен";
КонецПроцедуры
