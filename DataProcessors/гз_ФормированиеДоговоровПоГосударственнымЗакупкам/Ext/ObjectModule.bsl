////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит значение признака ведения учета по спецификам
Перем ВедениеУчетаПоСпецификам Экспорт; // экспортная переменная

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда

	// Производит автозаполнение табличной части РКО
	//
	Процедура Автозаполнение() Экспорт
			
		ТекстЗапрос = 	
		"ВЫБРАТЬ
		|	РезультатИтогов.Победитель КАК Контрагент,
		|	СУММА(РезультатИтогов.Сумма) КАК Сумма,
		|	гз_РегистрацияДоговораПоГосударственнымЗакупкам.Ссылка КАК ДокументРегистрацияДоговоров,
		|	РезультатИтогов.Ссылка КАК Ссылка,"+?(Не ВедениеУчетаПоСпецификам,"","
		|	РезультатИтогов.Специфика,")+"
		|	РезультатИтогов.СпособЗакупки
		|Из
		|	(ВЫБРАТЬ
		|		гз_ИтогиГосударственныхЗакупокТовары.Победитель,
		|		гз_ИтогиГосударственныхЗакупокТовары.Цена * гз_ИтогиГосударственныхЗакупокТовары.Количество КАК Сумма,
		|		гз_ИтогиГосударственныхЗакупокТовары.Ссылка.Ссылка,
		|		Характеристики.Специфика,
		|		гз_ИтогиГосударственныхЗакупокТовары.СпособЗакупки
		|	Из
		|		Документ.гз_ИтогиГосударственныхЗакупок.Товары КАК гз_ИтогиГосударственныхЗакупокТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гз_СвойстваНоменклатуры КАК Характеристики
		|				ПО Характеристики.Номенклатура = гз_ИтогиГосударственныхЗакупокТовары.Номенклатура
		|	ГДЕ
		|		гз_ИтогиГосударственныхЗакупокТовары.Ссылка.Ссылка = &Ссылка) КАК РезультатИтогов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гз_РегистрацияДоговораПоГосударственнымЗакупкам КАК гз_РегистрацияДоговораПоГосударственнымЗакупкам
		|		ПО РезультатИтогов.Победитель = гз_РегистрацияДоговораПоГосударственнымЗакупкам.Контрагент
		|			И РезультатИтогов.Ссылка = гз_РегистрацияДоговораПоГосударственнымЗакупкам.ДокументОснование"+?(Не ВедениеУчетаПоСпецификам,"","
		|			И РезультатИтогов.Специфика = гз_РегистрацияДоговораПоГосударственнымЗакупкам.Специфика")+"
		|			И РезультатИтогов.СпособЗакупки = гз_РегистрацияДоговораПоГосударственнымЗакупкам.СпособЗакупки
		|
		|СГРУППИРОВАТЬ ПО
		|	РезультатИтогов.Победитель,
		|	РезультатИтогов.Ссылка,
		|	гз_РегистрацияДоговораПоГосударственнымЗакупкам.Ссылка,"+?(Не ВедениеУчетаПоСпецификам,"","
		|	РезультатИтогов.Специфика,")+"
		|	РезультатИтогов.СпособЗакупки";
		
		Запрос = Новый Запрос(ТекстЗапрос);
		Запрос.УстановитьПараметр("Ссылка",ИтогиГосЗакупок);
		         
		РегистрацияДоговоров.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецПроцедуры // Автозаполнение()

	// Создает документы регистрации договоров по отмеченным строкам
	//
	// Параметры
	//  нет
	//
	Процедура СоздатьДокументы() Экспорт
		гз_ПроцедурыОперативногоУчетаЗакупок.СоздатьДокументыРегистрацииДоговоров(ЭтотОбъект);		
	КонецПроцедуры // СоздатьДокументы()

	//Процедура ЗаполнитьТЧДокумента
	//
	Процедура ЗаполнитьТЧДокумента(ДокументРег, Контрагент, Специфика, СпособЗакупки,СуммаАвансаПоДоговору) Экспорт 
		ВедениеУчетаПоСпецификам  = Константы.гз_ВестиУчетПоСпецификам.Получить();

		гз_ПроцедурыОперативногоУчетаЗакупок.ЗаполнитьТЧДокументаРегистрацияДоговора(ИтогиГосЗакупок,ДокументРег, Контрагент,
										Специфика, СпособЗакупки,СуммаАвансаПоДоговору,ВедениеУчетаПоСпецификам);
	КонецПроцедуры //ЗаполнитьТЧДокумента

#КонецЕсли

// Создает документы РКО по отмеченным строкам
//
// Параметры
//  нет
//
Функция СформироватьЗапросДляОтображенияДанных(ДокументРегистрацияДоговоров) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Док.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Док.Проведен
	|					ТОГДА 0
	|				ИНАЧЕ 2
	|			КОНЕЦ
	|	КОНЕЦ КАК ИндексКартинки
	|Из
	|	Документ.гз_ИтогиГосударственныхЗакупок КАК Док
	|ГДЕ
	|	Док.Ссылка = &Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДокументРегистрацияДоговоров);
	Возврат Запрос.Выполнить();

КонецФункции // СоздатьДокументы()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ВедениеУчетаПоСпецификам  = Константы.гз_ВестиУчетПоСпецификам.Получить();
