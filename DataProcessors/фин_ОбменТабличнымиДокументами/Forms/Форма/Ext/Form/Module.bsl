
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ФайловаяИБ = фин_ОбщегоНазначенияСервер.ИнформационнаяБазаФайловая();
	Параметры.Свойство("ДляЗагрузкиФактическихИПрогнозныхДанных",Объект.ДляЗагрузкиФактическихИПрогнозныхДанных);
	Параметры.Свойство("Бюджет",Объект.Бюджет);
	Параметры.Свойство("ЗаголовокФайла",Объект.ЗаголовокФайла);
	Параметры.Свойство("Загрузка",Объект.Загрузка);
	Параметры.Свойство("АдресДанных",Объект.АдресДанных);
	Если НЕ Параметры.Свойство("Бюджет") И НЕ Параметры.Свойство("ДляЗагрузкиФактическихИПрогнозныхДанных") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обработка """"Обмен табличными документами"""" не предназначена для самостоятельного использования!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Параметры.Свойство("НастройкаОбмена",Объект.НастройкаОбмена) Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	фин_НастройкиОбменаТабличнымиДокументами.Ссылка
			|ИЗ
			|	Справочник.фин_НастройкиОбменаТабличнымиДокументами КАК фин_НастройкиОбменаТабличнымиДокументами
			|ГДЕ
			|	фин_НастройкиОбменаТабличнымиДокументами.ДляЗагрузкиФактическихИПрогнозныхДанных = &ДляЗагрузкиФактическихИПрогнозныхДанных
			|	И фин_НастройкиОбменаТабличнымиДокументами.Бюджет = &Бюджет
			|	И НЕ фин_НастройкиОбменаТабличнымиДокументами.ПометкаУдаления"+?(Объект.Загрузка," И фин_НастройкиОбменаТабличнымиДокументами.ИспользоватьПоУмолчаниюДляЗагрузки"," И фин_НастройкиОбменаТабличнымиДокументами.ИспользоватьПоУмолчаниюДляВыгрузки");
		
		Запрос.УстановитьПараметр("Бюджет", Объект.Бюджет);
		Запрос.УстановитьПараметр("ДляЗагрузкиФактическихИПрогнозныхДанных", Объект.ДляЗагрузкиФактическихИПрогнозныхДанных);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Объект.НастройкаОбмена = ВыборкаДетальныеЗаписи.Ссылка;
		КонецЕсли;
	
	КонецЕсли;
	Если Объект.Загрузка Тогда
		Элементы.ФормаСохранитьТабличныйДокумент.Видимость=Ложь;
		Элементы.ФормаЗагрузитьДанныеВДокумент.КнопкаПоУмолчанию=Истина;
	Иначе
		Элементы.ФормаЗагрузитьДанныеВДокумент.Видимость=Ложь;
		Элементы.ФормаСохранитьТабличныйДокумент.КнопкаПоУмолчанию=Истина;
		Если Объект.Загрузка=Ложь И ЗначениеЗаполнено(Объект.НастройкаОбмена) Тогда
			ПодготовитьДанныеФайла();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицу(Команда)
		Если Объект.Загрузка Тогда
			Если ЗначениеЗаполнено(Объект.ПутьКФайлу) тогда
				ПрочитатьДанныеФайла();
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Объект.АдресДанных) И ЗначениеЗаполнено(Объект.НастройкаОбмена) тогда
				ПодготовитьДанныеФайла();
			КонецЕсли;
		КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьДанныеФайла()
	Если Объект.ФайловаяИБ Тогда
		ПрочитатьДанныеФайлаНаСервере();
	Иначе
		ОписаниеЗавершенияПомещения = Новый ОписаниеОповещения("ОбработкаПомещенияФайла",ЭтотОбъект);
		НачатьПомещениеФайла(ОписаниеЗавершенияПомещения,,Объект.ПутьКФайлу,Ложь,УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПомещенияФайла(Результат,Адрес,ВыбранноеИмяФайла,ДополнительныеПараметры) Экспорт
	Если Результат Тогда
	    Объект.АдресФайлаНаСервере = Адрес;
		ПрочитатьДанныеФайлаНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеФайлаНаСервере()
	Обработки.фин_ОбменТабличнымиДокументами.ПрочитатьСодержимоеТабличногоДокумента(Объект,ТабличныйДокумент);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаОбменаПриИзменении(Элемент)
	Если Объект.Загрузка=Ложь И ЗначениеЗаполнено(Объект.НастройкаОбмена) Тогда
		ПодготовитьДанныеФайла();
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ИмяФайлаДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Режим = ?(Объект.Загрузка,РежимДиалогаВыбораФайла.Открытие,РежимДиалогаВыбораФайла.Сохранение);
	Диалог = Новый ДиалогВыбораФайла(Режим);
	Диалог.Заголовок = "Выбор файла для загрузки данных";
	Диалог.Фильтр = "Табличный документ 1С:Предприятие (*.mxl)|*.mxl|Табличный документ Microsoft Excel 97-2003 (*.xls)|*.xls|Табличный документ Microsoft Excel (*.xlsx)|*.xlsx|электронная таблица OpenOffice Calc (*.ods)|*.ods";
	ОписаниеОбработкаВыбораФайла = Новый ОписаниеОповещения("ОбработкаВыбораФайла",ЭтотОбъект);
	Диалог.Показать(ОписаниеОбработкаВыбораФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораФайла(МассивФайлов,ДополнительныеПараметры) Экспорт
	Если МассивФайлов<>Неопределено И МассивФайлов.Количество()>0 Тогда
		Объект.ПутьКФайлу = МассивФайлов[0];
		Если Объект.Загрузка Тогда
			ПрочитатьДанныеФайла();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеФайла()
	Обработки.фин_ОбменТабличнымиДокументами.ПодготовитьДанныеТабличногоДокумента(Объект,ТабличныйДокумент);
КонецПроцедуры


&НаКлиенте
Процедура СохранитьТабличныйДокумент(Команда)
	Если ПроверитьЗаполнение() Тогда
		Файл = Новый Файл(Объект.ПутьКФайлу);
		ТабличныйДокумент.Записать(Объект.ПутьКФайлу,ТипФайлаТабличногоДокумента[СтрЗаменить(Файл.Расширение,".","")]);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьДанныеВДокумент(Команда)
	ЗагрузитьДанныеВДокументНаСервере();
	Закрыть(Новый Структура("Адрес,Добавлять",Объект.АдресДанных,Ложь));
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьДанныеВДокументНаСервере()
	ДанныеТаблицы = Обработки.фин_ОбменТабличнымиДокументами.ПолучитьТаблицуДанных(Объект,ТабличныйДокумент);
	ПоместитьВоВременноеХранилище(ДанныеТаблицы,Объект.АдресДанных);
КонецПроцедуры

