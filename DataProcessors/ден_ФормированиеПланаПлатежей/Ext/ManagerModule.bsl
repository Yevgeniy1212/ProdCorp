#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ВывестиОтчетПоДням(Форма) Экспорт
	
	ДетализацияПоДням = Форма.Элементы.ДеревоОстатковДетализацияПоДням.Пометка;
	
	Если ДетализацияПоДням = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	УчитыватьПлановыеПоступления = Форма.УчитыватьПлановыеПоступления; 	
	ДвиженияРасчетные = Форма.ДвиженияРасчетные;
	Движения = Форма.Движения;
	Остатки = Форма.Остатки;
	Период = Форма.Период;
	Отчет = Форма.Отчет;
	
	Отчет.Очистить();
	Макет = Обработки.ден_ФормированиеПланаПлатежей.ПолучитьМакет("Макет");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка|База");
	Отчет.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка|ПоДням");
	МассивДней = Новый Массив;
	тДата = НачалоДня(ТекущаяДата());
	ОбластьМакета.Параметры.Дата = формат(тДата,"ДФ=dd.ММ");
	МассивДней.Добавить(НачалоДня(ТекущаяДата()));
	Отчет.Присоединить(ОбластьМакета);
	Если Период.ДатаОкончания > тДата Тогда
		КоличествоДней = (НачалоДня(Период.ДатаОкончания)-НачалоДня(тДата))/(24*60*60);
		Для Инд = 1 По КоличествоДней Цикл
			Дата = тДата + Инд*24*60*60;
			МассивДней.Добавить(Дата);
			ОбластьМакета.Параметры.Дата = формат(Дата,"ДФ=dd.ММ");
			Отчет.Присоединить(ОбластьМакета);
		КонецЦикла;
	КонецЕсли;
	ДвиженияРасчетные.Очистить();
	мДерево = ДанныеФормыВЗначение(Форма.ДеревоПлатежей, Тип("ДеревоЗначений"));
	Для Каждого СтрокаДерева Из мДерево.Строки Цикл
		РасчетСтроки(СтрокаДерева, ДвиженияРасчетные);
	КонецЦикла;
	ТЗ = ДвиженияРасчетные.Выгрузить();
	ТЗ.Свернуть("КассаСчет,Дата","Платеж");
	ОбластьСчет = Макет.ПолучитьОбласть("Строка|База");
	ОбластьДата = Макет.ПолучитьОбласть("Строка|ПоДням");
	ТипДС = Неопределено;
	Отчет.НачатьАвтогруппировкуСтрок();
	Для Каждого СтрокаОстатков Из Остатки Цикл
		СчетКасса = СтрокаОстатков.КассаСчет;
	Если ТипЗнч(СчетКасса)<>ТипДС Тогда
			ОбластьСчет.Параметры.СчетКасса = ?(ТипЗнч(СчетКасса)=Тип("СправочникСсылка.Кассы"),"Наличные","Безналичные");
			ОбластьСчет.Параметры.СчетКассаТекст = ?(ТипЗнч(СчетКасса)=Тип("СправочникСсылка.Кассы"),"Наличные","Безналичные");
			Отчет.Вывести(ОбластьСчет,1);
			Для Каждого Дата Из МассивДней Цикл
				ОбластьДата.Параметры.Сумма = 0;
				ОбластьДата.Параметры.ТекстСумма = "";
				ОбластьДата.Область().ЦветФона = Новый Цвет(255,255,255);
				Отчет.Присоединить(ОбластьДата);
			КонецЦикла;
			ТипДС = ТипЗнч(СчетКасса);
		КонецЕсли;
		ОбластьСчет.Параметры.СчетКасса = СчетКасса;
		ОбластьСчет.Параметры.СчетКассаТекст = "    "+Строка(СчетКасса);
		Отчет.Вывести(ОбластьСчет,2);
		ВходящийОстаток = СтрокаОстатков.Сумма;
		Для Каждого Дата Из МассивДней Цикл
			Поступления 	= 0;
			ПлатежиПрочее 	= 0;
			ПлатежиЗаявки 	= 0;
			СтрокиДвижений = Движения.НайтиСтроки(Новый Структура("Дата,КассаСчет",Дата,СчетКасса));
			Если СтрокиДвижений.Количество()>0 Тогда
				ПлатежиПрочее = СтрокиДвижений[0].Платеж;
				Если УчитыватьПлановыеПоступления Тогда
					Поступления = СтрокиДвижений[0].Поступление;
				КонецЕсли;
			КонецЕсли;
			СтрокиДвижений = ТЗ.НайтиСтроки(Новый Структура("Дата,КассаСчет",Дата,СчетКасса));
			Если СтрокиДвижений.Количество() > 0 Тогда
				ПлатежиЗаявки = СтрокиДвижений[0].Платеж;
			КонецЕсли;
			ОстатокДаты = ВходящийОстаток + Поступления - ПлатежиПрочее - ПлатежиЗаявки;
			ОбластьДата.Параметры.Сумма = ОстатокДаты;
			ОбластьДата.Параметры.ТекстСумма = ?(ОстатокДаты>ВходящийОстаток,"+",?(ОстатокДаты<ВходящийОстаток,"-",""));
			ОбластьДата.Область().ЦветФона = ?(ОстатокДаты<0,Новый Цвет(255,160,122),?(ОстатокДаты=0,Новый Цвет(255,215,0),Новый Цвет(192,220,192)));
			ОбластьДата.Область("R1C1:R1C1").АвтоОтступ = 2;
			ОбластьДата.Область("R1C1:R1C1").Отступ = 2;
			ВходящийОстаток = ОстатокДаты;
			Отчет.Присоединить(ОбластьДата);
		КонецЦикла;
	КонецЦикла;
	
	Отчет.ЗакончитьАвтогруппировкуСтрок();
	Отчет.ФиксацияСверху = 1;
	Отчет.ФиксацияСлева = 2;
	
КонецПроцедуры

Процедура ЗаполнитьОстатки(Форма) Экспорт

	Объект = Форма.Объект;
	
	Период = Форма.Период;
	ДеревоПлатежей = Форма.ДеревоПлатежей;
	ДеревоОстатков = Форма.ДеревоОстатков;
	УчитыватьПлановыеПоступления = Форма.УчитыватьПлановыеПоступления;
	
    ДатаНачала = Период.ДатаНачала;
	ДатаОкончания = КонецДня(Период.ДатаОкончания);
	Счета = ПолучитьСписокСчетов(ДеревоПлатежей);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДенежныеСредстваОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ДенежныеСредстваОстатки.ВидДенежныхСредств КАК ВидДенежныхСредств,
		|	ДенежныеСредстваОстатки.БанковскийСчетКасса КАК БанковскийСчетКасса,
		|	ДенежныеСредстваОстатки.Организация КАК Организация
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.ден_ДенежныеСредства.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурноеПодразделение = &СтруктурноеПодразделение
		|				И ВЫБОР
		|					КОГДА &ВсеСчета
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ БанковскийСчетКасса В (&Счета)
		|				КОНЕЦ) КАК ДенежныеСредстваОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидДенежныхСредств,
		|	БанковскийСчетКасса,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДенежныйЧек.Касса КАК КассаСчет,
		|	СУММА(ДенежныйЧек.СуммаДокумента) КАК СуммаДокумента
		|ПОМЕСТИТЬ ВТ_ПоступленияВКассуПоДенежнымЧекам
		|ИЗ
		|	Документ.ден_ДенежныйЧек КАК ДенежныйЧек
		|ГДЕ
		|	ДенежныйЧек.Проведен
		|	И НЕ ДенежныйЧек.Оплачено
		|	И ДенежныйЧек.Организация = &Организация
		|	И ДенежныйЧек.СтруктурноеПодразделениеПолучатель = &СтруктурноеПодразделение
		|	И ДенежныйЧек.ДатаОплаты <= &ГраницаПлановПоступления
		|
		|СГРУППИРОВАТЬ ПО
		|	ДенежныйЧек.Касса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДенежныйЧек.СчетОрганизации КАК КассаСчет,
		|	СУММА(ДенежныйЧек.СуммаДокумента) КАК СуммаДокумента
		|ПОМЕСТИТЬ ВТ_СписаниеСоСчетаПоДенежнымЧекам
		|ИЗ
		|	Документ.ден_ДенежныйЧек КАК ДенежныйЧек
		|ГДЕ
		|	ДенежныйЧек.Проведен
		|	И НЕ ДенежныйЧек.Оплачено
		|	И ДенежныйЧек.Организация = &Организация
		|	И ДенежныйЧек.СтруктурноеПодразделениеОтправитель = &СтруктурноеПодразделение
		|	И ДенежныйЧек.ДатаОплаты <= &ГраницаПлановПоступления
		|
		|СГРУППИРОВАТЬ ПО
		|	ДенежныйЧек.СчетОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбъявлениеНаВзносНаличными.Касса КАК КассаСчет,
		|	СУММА(ОбъявлениеНаВзносНаличными.СуммаДокумента) КАК СуммаДокумента
		|ПОМЕСТИТЬ ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос
		|ИЗ
		|	Документ.ден_ОбъявлениеНаВзносНаличными КАК ОбъявлениеНаВзносНаличными
		|ГДЕ
		|	ОбъявлениеНаВзносНаличными.Проведен
		|	И НЕ ОбъявлениеНаВзносНаличными.Оплачено
		|	И ОбъявлениеНаВзносНаличными.СтруктурноеПодразделениеОтправитель = &СтруктурноеПодразделение
		|	И ОбъявлениеНаВзносНаличными.Организация = &Организация
		|	И ОбъявлениеНаВзносНаличными.ДатаОплаты <= &ГраницаПлановПоступления
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъявлениеНаВзносНаличными.Касса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбъявлениеНаВзносНаличными.СчетОрганизации КАК КассаСчет,
		|	СУММА(ОбъявлениеНаВзносНаличными.СуммаДокумента) КАК СуммаДокумента
		|ПОМЕСТИТЬ ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос
		|ИЗ
		|	Документ.ден_ОбъявлениеНаВзносНаличными КАК ОбъявлениеНаВзносНаличными
		|ГДЕ
		|	ОбъявлениеНаВзносНаличными.Проведен
		|	И НЕ ОбъявлениеНаВзносНаличными.Оплачено
		|	И ОбъявлениеНаВзносНаличными.СтруктурноеПодразделениеПолучатель = &СтруктурноеПодразделение
		|	И ОбъявлениеНаВзносНаличными.Организация = &Организация
		|	И ОбъявлениеНаВзносНаличными.ДатаОплаты <= &ГраницаПлановПоступления
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъявлениеНаВзносНаличными.СчетОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Платежи.ЗРС.ФормаОплаты КАК ВидОплаты,
		|	Платежи.ЗРС.БанковскийСчетКасса КАК СчетКасса,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСТЬNULL(ЗаявкиОстатки.СуммаОстаток, 0) > Платежи.Сумма
		|				ТОГДА Платежи.Сумма
		|			ИНАЧЕ ЕСТЬNULL(ЗаявкиОстатки.СуммаОстаток, 0)
		|		КОНЕЦ) КАК КОплате
		|ПОМЕСТИТЬ ВТ_ПоПрочимПланамПлатежей
		|ИЗ
		|	Документ.ден_ПланПлатежей.Платежи КАК Платежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ден_ЗаявкиНаРасходованиеСредств.Остатки(
		|				,
		|				Организация = &Организация
		|					И СтруктурноеПодразделение = &СтруктурноеПодразделение) КАК ЗаявкиОстатки
		|		ПО Платежи.ЗРС = ЗаявкиОстатки.ЗаявкаНаРасходование
		|ГДЕ
		|	Платежи.Ссылка.Проведен
		|	И Платежи.Ссылка.Организация = &Организация
		|	И Платежи.Ссылка.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|
		|СГРУППИРОВАТЬ ПО
		|	Платежи.ЗРС.ФормаОплаты,
		|	Платежи.ЗРС.БанковскийСчетКасса
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидОплаты,
		|	СчетКасса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ПланируемыеПоступленияОстатки.СуммаОстаток) КАК Поступление,
		|	ПланируемоеПоступлениеДенежныхСредств.ФормаОплаты КАК ФормаОплаты,
		|	ПланируемоеПоступлениеДенежныхСредств.БанковскийСчетКасса КАК БанковскийСчетКасса
		|ПОМЕСТИТЬ ВТ_ПланыПоступления
		|ИЗ
		|	РегистрНакопления.ден_ПланируемыеПоступленияДенежныхСредств.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурноеПодразделение = &СтруктурноеПодразделение) КАК ПланируемыеПоступленияОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ден_ПланируемоеПоступлениеДенежныхСредств КАК ПланируемоеПоступлениеДенежныхСредств
		|		ПО ПланируемыеПоступленияОстатки.ДокументПланирования = ПланируемоеПоступлениеДенежныхСредств.Ссылка
		|ГДЕ
		|	ПланируемоеПоступлениеДенежныхСредств.Организация = &Организация
		|	И ПланируемоеПоступлениеДенежныхСредств.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И ПланируемоеПоступлениеДенежныхСредств.ДатаПоступления <= &ГраницаПлановПоступления
		|	И ПланируемоеПоступлениеДенежныхСредств.ВключатьВПлатежныйКалендарь
		|
		|СГРУППИРОВАТЬ ПО
		|	ПланируемоеПоступлениеДенежныхСредств.ФормаОплаты,
		|	ПланируемоеПоступлениеДенежныхСредств.БанковскийСчетКасса
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФормаОплаты,
		|	БанковскийСчетКасса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаОстаток, 0) КАК ОстНаНач,
		|	ДенежныеСредстваОстатки.ВидДенежныхСредств КАК ФормаОплаты,
		|	ДенежныеСредстваОстатки.БанковскийСчетКасса КАК СчетКасса,
		|	ЕСТЬNULL(ВТ_ПопрочимПланамПлатежей.КОплате, 0) + ЕСТЬNULL(ВТ_СписаниеСоСчетаПоДенежнымЧекам.СуммаДокумента, 0) + ЕСТЬNULL(ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос.СуммаДокумента, 0) КАК ПоПрочимПланамПлатежей,
		|	ЕСТЬNULL(ВТ_ПланыПоступления.Поступление, 0) + ЕСТЬNULL(ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос.СуммаДокумента, 0) + ЕСТЬNULL(ВТ_ПоступленияВКассуПоДенежнымЧекам.СуммаДокумента, 0) КАК Поступления
		|ИЗ
		|	ВТ_Остатки КАК ДенежныеСредстваОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоПрочимПланамПлатежей КАК ВТ_ПопрочимПланамПлатежей
		|		ПО ДенежныеСредстваОстатки.ВидДенежныхСредств = ВТ_ПопрочимПланамПлатежей.ВидОплаты
		|			И ДенежныеСредстваОстатки.БанковскийСчетКасса = ВТ_ПопрочимПланамПлатежей.СчетКасса
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПланыПоступления КАК ВТ_ПланыПоступления
		|		ПО ДенежныеСредстваОстатки.ВидДенежныхСредств = ВТ_ПланыПоступления.ФормаОплаты
		|			И ДенежныеСредстваОстатки.БанковскийСчетКасса = ВТ_ПланыПоступления.БанковскийСчетКасса
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоступленияВКассуПоДенежнымЧекам КАК ВТ_ПоступленияВКассуПоДенежнымЧекам
		|		ПО ДенежныеСредстваОстатки.БанковскийСчетКасса = ВТ_ПоступленияВКассуПоДенежнымЧекам.КассаСчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписаниеСоСчетаПоДенежнымЧекам КАК ВТ_СписаниеСоСчетаПоДенежнымЧекам
		|		ПО ДенежныеСредстваОстатки.БанковскийСчетКасса = ВТ_СписаниеСоСчетаПоДенежнымЧекам.КассаСчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос КАК ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос
		|		ПО ДенежныеСредстваОстатки.БанковскийСчетКасса = ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос.КассаСчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос КАК ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос
		|		ПО ДенежныеСредстваОстатки.БанковскийСчетКасса = ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос.КассаСчет
		|ИТОГИ ПО
		|	ФормаОплаты";

	Запрос.УстановитьПараметр("ВсеСчета",		 			Не Форма.ПоказыватьОстаткиТолькоПоЗадействованнымСчетамКассам);
	Запрос.УстановитьПараметр("Счета",		 				Счета);
	Запрос.УстановитьПараметр("ДатаНачала", 				ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 				ДатаОкончания);
	Запрос.УстановитьПараметр("Контрагент", 				Объект.Контрагент);
	Запрос.УстановитьПараметр("Организация", 				Объект.Организация);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", 	Объект.СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("ОтборКонтр", 				ЗначениеЗаполнено(Объект.Контрагент));
	Запрос.УстановитьПараметр("ОтборОрг", 					ЗначениеЗаполнено(Объект.Организация));
    Запрос.УстановитьПараметр("ТолькоНовые", 				Форма.ТолькоНовые);
    Запрос.УстановитьПараметр("ГраницаПлановПоступления",	?(ДатаОкончания>ТекущаяДата(),ДатаОкончания,ТекущаяДата()));
	
	Результат = Запрос.Выполнить();

    ДеревоОст = ДанныеФормыВЗначение(ДеревоОстатков, Тип("ДеревоЗначений"));
	ДеревоЗаявок = ДанныеФормыВЗначение(ДеревоПлатежей, Тип("ДеревоЗначений"));
	ДеревоОст.Строки.Очистить();
    ВыборкаФО = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаФО.Следующий() Цикл
		СтрокаФО = ДеревоОст.Строки.Добавить();	
		СтрокаФО.ВидДСКасса = ВыборкаФО.ФормаОплаты;
		ВыборкаДетали = ВыборкаФО.Выбрать();
		Пока ВыборкаДетали.Следующий() Цикл
			Платеж = 0;
			Для Каждого СтрокаЗаявок Из ДеревоЗаявок.Строки Цикл
				ВычислитьСуммуПлатежаПоКассеСчету(СтрокаЗаявок,Платеж,ВыборкаДетали.СчетКасса);
			КонецЦикла;
			СтрокаД = СтрокаФО.Строки.Добавить();
			СтрокаД.ВидДСКасса = ВыборкаДетали.СчетКасса;
			СтрокаД.ОстатокНаНачало = ВыборкаДетали.ОстНаНач;
			СтрокаД.Платеж = Платеж;
			СтрокаД.ПоПрочимПланамПлатежей = ВыборкаДетали.ПоПрочимПланамПлатежей;
			СтрокаД.Поступления = ВыборкаДетали.Поступления;
			СтрокаД.ОстатокНаКонец = ВыборкаДетали.ОстНаНач -СтрокаД.Платеж- СтрокаД.ПоПрочимПланамПлатежей + ?(УчитыватьПлановыеПоступления,ВыборкаДетали.Поступления,0);
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВДанныеФормы(ДеревоОст, ДеревоОстатков);
	
КонецПроцедуры

Процедура ПолучитьДанныеПоДням(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Период = Форма.Период;
	Остатки = Форма.Остатки;
	Движения = Форма.Движения;
	ДеревоПлатежей = Форма.ДеревоПлатежей;
	
	ДатаНачала = Период.ДатаНачала;
	ДатаОкончания = КонецДня(Период.ДатаОкончания);
	Счета = ПолучитьСписокСчетов(ДеревоПлатежей);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДенежныеСредстваОстатки.СуммаОстаток КАК Сумма,
		|	ДенежныеСредстваОстатки.БанковскийСчетКасса КАК КассаСчет,
		|	ДенежныеСредстваОстатки.ВидДенежныхСредств КАК ВидДенежныхСредств
		|ИЗ
		|	РегистрНакопления.ден_ДенежныеСредства.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурноеПодразделение = &СтруктурноеПодразделение
		|				И ВЫБОР
		|					КОГДА &ВсеСчета
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ БанковскийСчетКасса В (&Счета)
		|				КОНЕЦ) КАК ДенежныеСредстваОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидДенежныхСредств,
		|	КассаСчет";

	Запрос.УстановитьПараметр("ВсеСчета",		 			Не Форма.ПоказыватьОстаткиТолькоПоЗадействованнымСчетамКассам);
	Запрос.УстановитьПараметр("Счета", 						Счета);
	Запрос.УстановитьПараметр("ДатаНачала", 				НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаРасчета", 				ТекущаяДата());
	Запрос.УстановитьПараметр("ДатаОкончания", 				ДатаОкончания);
	Запрос.УстановитьПараметр("Контрагент", 				Объект.Контрагент);
	Запрос.УстановитьПараметр("Организация", 				Объект.Организация);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", 	Объект.СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("ОтборКонтр", 				ЗначениеЗаполнено(Объект.Контрагент));
	Запрос.УстановитьПараметр("ОтборОрг", 					ЗначениеЗаполнено(Объект.Организация));
    Запрос.УстановитьПараметр("ТолькоНовые", 				Форма.ТолькоНовые);
    Запрос.УстановитьПараметр("ГраницаПлановПоступления",	?(ДатаОкончания>ТекущаяДата(),ДатаОкончания,ТекущаяДата()));
	
	Остатки.Очистить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НС = Остатки.Добавить();
		ЗаполнитьЗначенияСвойств(НС,Выборка);
	КонецЦикла;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Платежи.ЗРС.БанковскийСчетКасса КАК СчетКасса,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ЗаявкиОстатки.СуммаОстаток, 0) > Платежи.Сумма
		|			ТОГДА Платежи.Сумма
		|		ИНАЧЕ ЕСТЬNULL(ЗаявкиОстатки.СуммаОстаток, 0)
		|	КОНЕЦ КАК КОплате,
		|	ВЫБОР
		|		КОГДА Платежи.ДатаПлатежа < &ДатаРасчета
		|			ТОГДА &ДатаРасчета
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(Платежи.ДатаПлатежа, ДЕНЬ)
		|	КОНЕЦ КАК ДатаПлатежа
		|ПОМЕСТИТЬ ВТ_ПоПрочимПланамПлатежей
		|ИЗ
		|	Документ.ден_ПланПлатежей.Платежи КАК Платежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ден_ЗаявкиНаРасходованиеСредств.Остатки(
		|				,
		|				Организация = &Организация
		|					И СтруктурноеПодразделение = &СтруктурноеПодразделение) КАК ЗаявкиОстатки
		|		ПО Платежи.ЗРС = ЗаявкиОстатки.ЗаявкаНаРасходование
		|ГДЕ
		|	Платежи.Ссылка.Проведен
		|	И Платежи.Ссылка.Организация = &Организация
		|	И Платежи.Ссылка.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И Платежи.ДатаПлатежа <= &ГраницаПлановПоступления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СчетКасса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПланируемыеПоступленияОстатки.СуммаОстаток КАК Поступление,
		|	ПланируемоеПоступлениеДенежныхСредств.БанковскийСчетКасса КАК БанковскийСчетКасса,
		|	ВЫБОР
		|		КОГДА ПланируемоеПоступлениеДенежныхСредств.ДатаПоступления < &ДатаРасчета
		|			ТОГДА &ДатаРасчета
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ПланируемоеПоступлениеДенежныхСредств.ДатаПоступления, ДЕНЬ)
		|	КОНЕЦ КАК ДатаПоступления
		|ПОМЕСТИТЬ ВТ_ПланыПоступления
		|ИЗ
		|	РегистрНакопления.ден_ПланируемыеПоступленияДенежныхСредств.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурноеПодразделение = &СтруктурноеПодразделение) КАК ПланируемыеПоступленияОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ден_ПланируемоеПоступлениеДенежныхСредств КАК ПланируемоеПоступлениеДенежныхСредств
		|		ПО ПланируемыеПоступленияОстатки.ДокументПланирования = ПланируемоеПоступлениеДенежныхСредств.Ссылка
		|ГДЕ
		|	ПланируемоеПоступлениеДенежныхСредств.Организация = &Организация
		|	И ПланируемоеПоступлениеДенежныхСредств.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И ПланируемоеПоступлениеДенежныхСредств.ДатаПоступления <= &ГраницаПлановПоступления
		|	И ПланируемоеПоступлениеДенежныхСредств.ВключатьВПлатежныйКалендарь
		|	И ПланируемоеПоступлениеДенежныхСредств.ДатаПоступления <= &ГраницаПлановПоступления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	БанковскийСчетКасса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДенежныйЧек.Касса,
		|	ДенежныйЧек.СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ДенежныйЧек.ДатаОплаты < &ДатаРасчета
		|			ТОГДА &ДатаРасчета
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДенежныйЧек.ДатаОплаты, ДЕНЬ)
		|	КОНЕЦ КАК ДатаОплаты
		|ПОМЕСТИТЬ ВТ_ПоступленияВКассуПоДенежнымЧекам
		|ИЗ
		|	Документ.ден_ДенежныйЧек КАК ДенежныйЧек
		|ГДЕ
		|	ДенежныйЧек.Проведен
		|	И НЕ ДенежныйЧек.Оплачено
		|	И ДенежныйЧек.Организация = &Организация
		|	И ДенежныйЧек.СтруктурноеПодразделениеПолучатель = &СтруктурноеПодразделение
		|	И ДенежныйЧек.ДатаОплаты <= &ГраницаПлановПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДенежныйЧек.СчетОрганизации,
		|	ДенежныйЧек.СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ДенежныйЧек.ДатаОплаты < &ДатаРасчета
		|			ТОГДА &ДатаРасчета
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДенежныйЧек.ДатаОплаты, ДЕНЬ)
		|	КОНЕЦ КАК ДатаОплаты
		|ПОМЕСТИТЬ ВТ_СписаниеСоСчетаПоДенежнымЧекам
		|ИЗ
		|	Документ.ден_ДенежныйЧек КАК ДенежныйЧек
		|ГДЕ
		|	ДенежныйЧек.Проведен
		|	И НЕ ДенежныйЧек.Оплачено
		|	И ДенежныйЧек.Организация = &Организация
		|	И ДенежныйЧек.СтруктурноеПодразделениеОтправитель = &СтруктурноеПодразделение
		|	И ДенежныйЧек.ДатаОплаты <= &ГраницаПлановПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбъявлениеНаВзносНаличными.Касса,
		|	ОбъявлениеНаВзносНаличными.СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ОбъявлениеНаВзносНаличными.ДатаОплаты < &ДатаРасчета
		|			ТОГДА &ДатаРасчета
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ОбъявлениеНаВзносНаличными.ДатаОплаты, ДЕНЬ)
		|	КОНЕЦ КАК ДатаОплаты
		|ПОМЕСТИТЬ ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос
		|ИЗ
		|	Документ.ден_ОбъявлениеНаВзносНаличными КАК ОбъявлениеНаВзносНаличными
		|ГДЕ
		|	ОбъявлениеНаВзносНаличными.Проведен
		|	И НЕ ОбъявлениеНаВзносНаличными.Оплачено
		|	И ОбъявлениеНаВзносНаличными.СтруктурноеПодразделениеОтправитель = &СтруктурноеПодразделение
		|	И ОбъявлениеНаВзносНаличными.Организация = &Организация
		|	И ОбъявлениеНаВзносНаличными.ДатаОплаты <= &ГраницаПлановПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбъявлениеНаВзносНаличными.СчетОрганизации,
		|	ОбъявлениеНаВзносНаличными.СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ОбъявлениеНаВзносНаличными.ДатаОплаты < &ДатаРасчета
		|			ТОГДА &ДатаРасчета
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ОбъявлениеНаВзносНаличными.ДатаОплаты, ДЕНЬ)
		|	КОНЕЦ КАК ДатаОплаты
		|ПОМЕСТИТЬ ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос
		|ИЗ
		|	Документ.ден_ОбъявлениеНаВзносНаличными КАК ОбъявлениеНаВзносНаличными
		|ГДЕ
		|	ОбъявлениеНаВзносНаличными.Проведен
		|	И НЕ ОбъявлениеНаВзносНаличными.Оплачено
		|	И ОбъявлениеНаВзносНаличными.СтруктурноеПодразделениеПолучатель = &СтруктурноеПодразделение
		|	И ОбъявлениеНаВзносНаличными.Организация = &Организация
		|	И ОбъявлениеНаВзносНаличными.ДатаОплаты <= &ГраницаПлановПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПоПрочимПланамПлатежей.СчетКасса,
		|	ВТ_ПоПрочимПланамПлатежей.ДатаПлатежа КАК Дата,
		|	ВТ_ПоПрочимПланамПлатежей.КОплате КАК Оплата,
		|	NULL КАК Поступление
		|ПОМЕСТИТЬ ВТ_Свод
		|ИЗ
		|	ВТ_ПоПрочимПланамПлатежей КАК ВТ_ПоПрочимПланамПлатежей
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ПланыПоступления.БанковскийСчетКасса,
		|	ВТ_ПланыПоступления.ДатаПоступления,
		|	NULL,
		|	ВТ_ПланыПоступления.Поступление
		|ИЗ
		|	ВТ_ПланыПоступления КАК ВТ_ПланыПоступления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ПоступленияВКассуПоДенежнымЧекам.Касса,
		|	ВТ_ПоступленияВКассуПоДенежнымЧекам.ДатаОплаты,
		|	NULL,
		|	ВТ_ПоступленияВКассуПоДенежнымЧекам.СуммаДокумента
		|ИЗ
		|	ВТ_ПоступленияВКассуПоДенежнымЧекам КАК ВТ_ПоступленияВКассуПоДенежнымЧекам
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_СписаниеСоСчетаПоДенежнымЧекам.СчетОрганизации,
		|	ВТ_СписаниеСоСчетаПоДенежнымЧекам.ДатаОплаты,
		|	ВТ_СписаниеСоСчетаПоДенежнымЧекам.СуммаДокумента,
		|	NULL
		|ИЗ
		|	ВТ_СписаниеСоСчетаПоДенежнымЧекам КАК ВТ_СписаниеСоСчетаПоДенежнымЧекам
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос.Касса,
		|	ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос.ДатаОплаты,
		|	ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос.СуммаДокумента,
		|	NULL
		|ИЗ
		|	ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос КАК ВТ_СписаниеИзКассыПоОбъявлениямНаВзнос
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос.СчетОрганизации,
		|	ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос.ДатаОплаты,
		|	NULL,
		|	ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос.СуммаДокумента
		|ИЗ
		|	ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос КАК ВТ_ПоступленияНаСчетПоОбъявлениямНаВзнос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Свод.СчетКасса КАК КассаСчет,
		|	ВТ_Свод.Дата,
		|	СУММА(ВТ_Свод.Оплата) КАК Платеж,
		|	СУММА(ВТ_Свод.Поступление) КАК Поступление
		|ИЗ
		|	ВТ_Свод КАК ВТ_Свод
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Свод.СчетКасса,
		|	ВТ_Свод.Дата";

		Выборка = Запрос.Выполнить().Выбрать();
		Движения.Очистить();
		Пока Выборка.Следующий() Цикл
			НС = Движения.Добавить();
			ЗаполнитьЗначенияСвойств(НС,Выборка);
		КонецЦикла;
		
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияПлановПлатежей(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	ТаблицаПланаПлатежей = Форма.ТаблицаПланаПлатежей;
	ДеревоПлатежей = Форма.ДеревоПлатежей;
	
	ДеревоПл = ДанныеФормыВЗначение(ДеревоПлатежей, Тип("ДеревоЗначений"));
	ТаблицаДанных = ДанныеФормыВЗначение(ТаблицаПланаПлатежей, Тип("ТаблицаЗначений"));
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДанных.ВидОплаты КАК ВидОплаты,
		|	ТаблицаДанных.Организация КАК Организация,
		|	ТаблицаДанных.СчетКасса КАК СчетКасса,
		|	ТаблицаДанных.Сумма КАК Сумма,
		|	ТаблицаДанных.Валюта КАК Валюта,
		|	ТаблицаДанных.ДатаПлатежа КАК ДатаПлатежа,
		|	ТаблицаДанных.ЗРС,
		|	ТаблицаДанных.СуммаПоЗаявке
		|ПОМЕСТИТЬ ВТ_ТаблицаДанных
		|ИЗ
		|	&ТаблицаДанных КАК ТаблицаДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДанных.ВидОплаты КАК ВидОплаты,
		|	ТаблицаДанных.Организация КАК Организация,
		|	ТаблицаДанных.СчетКасса КАК СчетКасса,
		|	ТаблицаДанных.Сумма КАК Сумма,
		|	ТаблицаДанных.Валюта КАК Валюта,
		|	ТаблицаДанных.ДатаПлатежа КАК ДатаПлатежа,
		|	ТаблицаДанных.ЗРС,
		|	ТаблицаДанных.СуммаПоЗаявке
		|ИЗ
		|	ВТ_ТаблицаДанных КАК ТаблицаДанных
		|ИТОГИ
		|ПО
		|	Организация";
		
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		ВыборкаЗаявки = Выборка.Выбрать();
		ПланСсылка = СоздатьПланПлатежей(ВыборкаЗаявки, Выборка.Организация, Объект.СтруктурноеПодразделение);
		ВыборкаЗаявки.Сбросить();
		Пока ВыборкаЗаявки.Следующий() Цикл
			СтрокаТЗ = ТаблицаДанных.НайтиСтроки(Новый Структура("ЗРС", ВыборкаЗаявки.ЗРС));
			Если СтрокаТЗ.Количество() > 0 Тогда
				СтрокаТЗ[0].ПланПлатежей = ПланСсылка;	
			КонецЕсли;
			
		КонецЦикла;	
	КонецЦикла;
	
	УстановитьПланПлатежейВДеревоПлатежей(ДеревоПл.Строки, ТаблицаДанных);
	
	ЗначениеВДанныеФормы(ДеревоПл, ДеревоПлатежей);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура РасчетСтроки(СтрокаДерева, ДвиженияРасчетные)
	
	Если СтрокаДерева.Применить И ЗначениеЗаполнено(СтрокаДерева.СчетКасса) Тогда
		НС 				= ДвиженияРасчетные.Добавить();
		НС.КассаСчет 	= СтрокаДерева.СчетКасса;
		НС.Дата 		= ?(СтрокаДерева.ДатаПлатежа<НачалоДня(ТекущаяДата()),НачалоДня(ТекущаяДата()),НачалоДня(СтрокаДерева.ДатаПлатежа));
		НС.Платеж 		= СтрокаДерева.Сумма;
	КонецЕсли;
	
	Для Каждого СтрокаДочерняя Из СтрокаДерева.Строки Цикл
		РасчетСтроки(СтрокаДочерняя, ДвиженияРасчетные);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСчета(СтрокаДерева, Счета)
	
	Счета.Добавить(СтрокаДерева.СчетКасса);
	
	Для Каждого мСтрокаДерева Из СтрокаДерева.Строки Цикл
		ЗаполнитьСчета(мСтрокаДерева,Счета);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВычислитьСуммуПлатежаПоКассеСчету(СтрокаДерева, Сумма, СчетКасса)
	
	Если ЗначениеЗаполнено(СтрокаДерева.СчетКасса) Тогда
		Если СтрокаДерева.Применить И СтрокаДерева.СчетКасса = СчетКасса Тогда
			Сумма = Сумма+СтрокаДерева.Сумма;
		КонецЕсли;
	Иначе
		Для Каждого СтрокаЗаявок Из СтрокаДерева.Строки Цикл
			ВычислитьСуммуПлатежаПоКассеСчету(СтрокаЗаявок,Сумма,СчетКасса);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПланПлатежейВДеревоПлатежей(Строки, ТаблицаПП)
	
	Для Каждого СтрокаД Из Строки Цикл
		Если ЗначениеЗаполнено(СтрокаД.Организация) 
			И СтрокаД.Применить 
			И НЕ ЗначениеЗаполнено(СтрокаД.ПланПлатежей) Тогда
			СтрокаТЗ = ТаблицаПП.Найти(СтрокаД.ОргКонтр, "ЗРС");
			Если СтрокаТЗ <> Неопределено Тогда
				СтрокаД.ПланПлатежей = СтрокаТЗ.ПланПлатежей;		 
			КонецЕсли;
			 
		КонецЕсли;
		УстановитьПланПлатежейВДеревоПлатежей(СтрокаД.Строки, ТаблицаПП);	
	КонецЦикла;
		
КонецПроцедуры

Функция ПолучитьСписокСчетов(ДеревоПлатежей)
	
	Счета = Новый Массив;
	
	ДеревоОстатков = ДанныеФормыВЗначение(ДеревоПлатежей, Тип("ДеревоЗначений"));
	
	Для Каждого СтрокаДерева Из ДеревоОстатков.Строки Цикл
		ЗаполнитьСчета(СтрокаДерева, Счета);
	КонецЦикла;
	
	Возврат Счета;
	
КонецФункции

Функция СоздатьПланПлатежей(Выборка, Организация, СтруктурноеПодразделение)
	
	Если Выборка.Количество() = 0 Тогда	
		Возврат Документы.ден_ПланПлатежей.ПустаяСсылка();
	КонецЕсли;
	
	Попытка
		Документ = Документы.ден_ПланПлатежей.СоздатьДокумент();
		Документ.Дата = ТекущаяДата();
	    Документ.Автор = ПользователиКлиентСервер.АвторизованныйПользователь();
		Документ.НеУчитыватьГрафикПлатежныхДней = Истина;
		Документ.Ответственный = фин_ОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию(, "ОсновнойОтветственный");
		Документ.Организация = Организация;
		Документ.СтруктурноеПодразделение = СтруктурноеПодразделение;
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Документ.Платежи.Добавить(),Выборка);		
		КонецЦикла;
		Документ.Платежи.Свернуть("ЗРС,ДатаПлатежа,СуммаПоЗаявке","Сумма");
		Документ.Записать();
		Возврат Документ.Ссылка;
	Исключение
		Возврат Документы.ден_ПланПлатежей.ПустаяСсылка();
	КонецПопытки;
	
КонецФункции

#КонецЕсли