
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПрименяемаяСхема = "<укажите название>";
	Период = ТекущаяДата();
	Если Параметры.Свойство("ОбъектИБ") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,Параметры,"ОбъектИБ,ЭтоДокументы,Набор");
		ТипыДляВыбора = Новый Массив;
		Если Набор = Ложь Тогда
			ТипыДляВыбора.Добавить(Тип("СправочникСсылка.фин_СхемыОтраженияФактическихДанных"));
			ТипыДляВыбора.Добавить(Тип("Строка"));
		КонецЕсли;
		Если ЭтоДокументы Тогда
			ТипыДляВыбора.Добавить(Тип("ПеречислениеСсылка.фин_АльтернативныеСпособыОтраженияФактическихДанных"));
		КонецЕсли;
		Если ЭтоДокументы И Набор Тогда
			ТипыДляВыбора.Добавить(Тип("СправочникСсылка.фин_НаборыШаблоновОтраженияПервичныхДанныхВБюджетах"));
		КонецЕсли;
		Элементы.ПрименяемаяСхема.ОграничениеТипа=Новый ОписаниеТипов(ТипыДляВыбора);
		ПрименяемаяСхема = Элементы.ПрименяемаяСхема.ОграничениеТипа.ПривестиЗначение(ПрименяемаяСхема);
		Элементы.ИспользоватьСовместноСШаблонамиРегистров.Видимость=ЭтоДокументы;
	Иначе
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		ПоказатьПредупреждение(,"Не указана дата ввода в действие!");
	ИначеЕсли НЕ ЗначениеЗаполнено(ПрименяемаяСхема) Тогда
		ПоказатьПредупреждение(,"Не указана применяемая схема!");
	Иначе
		Если ТипЗнч(ПрименяемаяСхема)=Тип("Строка") Тогда
			Схема = СоздатьСхему(ОбъектИБ,ПрименяемаяСхема);
			Если Схема<>Неопределено Тогда
				ПрименяемаяСхема = Схема;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Закрыть(Новый Структура("Настройка,Период,ИспользоватьСовместноСШаблонамиРегистров",ПрименяемаяСхема,Период,ИспользоватьСовместноСШаблонамиРегистров));
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьСхему(Владелец,Название)
	НЭ = Справочники.фин_СхемыОтраженияФактическихДанных.СоздатьЭлемент();
	НЭ.Владелец = Владелец;
	НЭ.Наименование = Название;
	НЭ.УстановитьНовыйКод();
	Попытка
		НЭ.Записать();
		Возврат НЭ.Ссылка;
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции
