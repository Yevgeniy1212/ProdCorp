
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("Проект",Проект);
	Параметры.Свойство("УправленческоеПодразделение",УправленческоеПодразделение);
	Параметры.Свойство("ЭтапБюджетногоПроцесса",ЭтапБюджетногоПроцесса);
	Если ЗначениеЗаполнено(УправленческоеПодразделение) Тогда
		УровеньДетализации = Перечисления.фин_УровниДетализацииБюджета.БюджетПодразделения;	
	ИначеЕсли ЗначениеЗаполнено(Проект) Тогда
		УровеньДетализации = Перечисления.фин_УровниДетализацииБюджета.БюджетПроекта;	
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Форма.Элементы.Проект.ТолькоПросмотр = Форма.УровеньДетализации <> ПредопределенноеЗначение("Перечисление.фин_УровниДетализацииБюджета.БюджетПроекта");
	Форма.Элементы.УправленческоеПодразделение.ТолькоПросмотр = Форма.УровеньДетализации <> ПредопределенноеЗначение("Перечисление.фин_УровниДетализацииБюджета.БюджетПодразделения");
КонецПроцедуры

&НаКлиенте
Процедура УровеньДетализацииПриИзменении(Элемент)
	Если УровеньДетализации <> ПредопределенноеЗначение("Перечисление.фин_УровниДетализацииБюджета.БюджетПроекта") Тогда
		Проект = фин_ОбщегоНазначенияКлиентПовтИсп.ПустоеЗначениеПроект();
	КонецЕсли;
	Если УровеньДетализации <> ПредопределенноеЗначение("Перечисление.фин_УровниДетализацииБюджета.БюджетПодразделения") Тогда
		УправленческоеПодразделение = фин_ОбщегоНазначенияКлиентПовтИсп.ПустоеЗначениеУправленческоеПодразделение();
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Если УровеньДетализации = ПредопределенноеЗначение("Перечисление.фин_УровниДетализацииБюджета.БюджетПроекта") Тогда
		СтруктураРезультат = Новый Структура("Проект,УправленческоеПодразделение,ЭтапБюджетногоПроцесса",Проект,фин_ОбщегоНазначенияКлиентПовтИсп.ПустоеЗначениеУправленческоеПодразделение(),ЭтапБюджетногоПроцесса);
	ИначеЕсли УровеньДетализации = ПредопределенноеЗначение("Перечисление.фин_УровниДетализацииБюджета.БюджетПодразделения") Тогда
		СтруктураРезультат = Новый Структура("Проект,УправленческоеПодразделение,ЭтапБюджетногоПроцесса",фин_ОбщегоНазначенияКлиентПовтИсп.ПустоеЗначениеПроект(),УправленческоеПодразделение,ЭтапБюджетногоПроцесса);
	Иначе
		СтруктураРезультат = Новый Структура("Проект,УправленческоеПодразделение,ЭтапБюджетногоПроцесса",фин_ОбщегоНазначенияКлиентПовтИсп.ПустоеЗначениеПроект(),фин_ОбщегоНазначенияКлиентПовтИсп.ПустоеЗначениеУправленческоеПодразделение(),ЭтапБюджетногоПроцесса);
	КонецЕсли;
	Закрыть(СтруктураРезультат);
КонецПроцедуры
