
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("ФинансовыйПоказатель") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Объект,Параметры);
	Если Параметры.Свойство("РежимВыбораИзСтатейБюджетов") Тогда
		Элементы.ФормаРежимВыбораИзСтатейБюджетов.Пометка = Параметры.РежимВыбораИзСтатейБюджетов;
	КонецЕсли;
	Структура = Новый Структура;
	Для Каждого Реквизит Из Метаданные.Обработки.фин_РедактированиеСтрокиТабличнойЧастиДокумента.Реквизиты Цикл
		Структура.Вставить(Реквизит.Имя);	
	КонецЦикла;
	СтруктураРезультат = Новый ФиксированнаяСтруктура(Структура);
	ВалютаРегл = фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ВалютаРегламентированногоУчета");
	Разрезы = фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ПолучитьПолныйСписокРазрезов");
	Для Каждого ЭлементРазрез Из Разрезы Цикл
		НовыйЭлемент = ПолныйСписокРазрезов.Добавить(ЭлементРазрез.Значение,фин_РаботаСДополнительнымиРазрезамиБюджетирования.ИмяРазреза(ЭлементРазрез.Значение));
		ЭлементФормы = Элементы[НовыйЭлемент.Представление];
		ЭлементФормы.Видимость = Истина;
		ЭлементФормы.Заголовок = фин_РаботаСДополнительнымиРазрезамиБюджетирования.ПредставлениеРазреза(ЭлементРазрез.Значение);
		Если НовыйЭлемент.Представление="Номенклатура" Тогда
			ЭлементФормы.ОграничениеТипа = фин_РаботаСДополнительнымиРазрезамиБюджетирования.ОписаниеТиповНоменклатурногоПеречня(Объект.ФинансовыйПоказатель);
	    Иначе
			ЭлементФормы.ОграничениеТипа = фин_РаботаСДополнительнымиРазрезамиБюджетирования.РазрезПоИзмерению(ЭлементРазрез.Значение).ТипЗначения;
		КонецЕсли;
	КонецЦикла;
	ОпределитьУчетныеПризнакиПоказателя();
	УправлениеФормой(ЭтотОбъект);
	Цена = ?(Объект.Количество=0,0,Объект.Сумма/Объект.Количество);
КонецПроцедуры

&НаСервере
Процедура ОпределитьУчетныеПризнакиПоказателя()
	СтруктураРазрезовУчетаПоказателя = Новый ФиксированнаяСтруктура(фин_ОбщегоНазначенияВызовСервераПовтИсп.СтруктураРазрезовФинансовогоПоказателя(Объект.ФинансовыйПоказатель));
	УчетПоКоличеству = Объект.ФинансовыйПоказатель.УчетПоКоличеству;
	УчетПоСумме = Объект.ФинансовыйПоказатель.УчетПоСумме;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Структура = Новый Структура(СтруктураРезультат);
	ЗаполнитьЗначенияСвойств(Структура,Объект);
	Закрыть(Структура);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Для Каждого ЭлементРазрез Из Форма.ПолныйСписокРазрезов Цикл
		Форма.Элементы[ЭлементРазрез.Представление].Видимость = Форма.СтруктураРазрезовУчетаПоказателя.Свойство(ЭлементРазрез.Представление);
	КонецЦикла;
	Форма.Элементы.Количество.Видимость = Форма.УчетПоКоличеству;
	Форма.Элементы.ГруппаСуммаВалюта.Видимость = Форма.УчетПоСумме;
	Форма.Элементы.Цена.Видимость = Форма.УчетПоСумме И Форма.УчетПоКоличеству;
КонецПроцедуры

&НаКлиенте
Процедура ФинансовыйПоказательПриИзменении(Элемент)
	ОпределитьУчетныеПризнакиПоказателя();
	КорректироватьЗаполнениеПравил();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура КорректироватьЗаполнениеПравил() Экспорт
	Для Каждого ЭлементБазовыйПараметр Из ПолныйСписокРазрезов Цикл
		Очищать = Ложь;
		Если НЕ СтруктураРазрезовУчетаПоказателя.Свойство(ЭлементБазовыйПараметр.Представление) Тогда
			Очищать = Истина;
		КонецЕсли;
		Если Очищать Тогда
			Объект[ЭлементБазовыйПараметр.Представление] = фин_ОбщегоНазначенияВызовСервераПовтИсп.ПустоеЗначениеРазреза(ЭлементБазовыйПараметр.Представление,Объект.ФинансовыйПоказатель);
		КонецЕсли;
	КонецЦикла;
	ОписаниеТипов = фин_РаботаСДополнительнымиРазрезамиБюджетирования.ОписаниеТиповНоменклатурногоПеречня(Объект.ФинансовыйПоказатель);
	Объект.Номенклатура = ОписаниеТипов.ПривестиЗначение(Объект.Номенклатура);
КонецПроцедуры


&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	Если Объект.Количество<>0 И Цена<>0 Тогда
		Объект.Сумма = Объект.Количество * Цена;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФинансовыйПоказательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Элементы.ФормаРежимВыбораИзСтатейБюджетов.Пометка Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.фин_СтатьиБюджета.ФормаВыбора",ПараметрыВыбораСтатьи(Объект.ФинансовыйПоказатель),Элемент,УникальныйИдентификатор,ВариантОткрытияОкна.ОтдельноеОкно);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФинансовыйПоказательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение)=Тип("СправочникСсылка.фин_СтатьиБюджета") Тогда
		СтандартнаяОбработка = Ложь;
		Объект.ФинансовыйПоказатель = ФинансовыйПоказатель(ВыбранноеЗначение);
		ФинансовыйПоказательПриИзменении(Элемент);
	КонецЕсли;
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПараметрыВыбораСтатьи(ФинансовыйПоказатель)
	Статья = фин_ПроцедурыМеханизмовБюджетирования.ОпределитьСтатьюБюджетаПоПоказателю(ФинансовыйПоказатель);
	Структура = Новый Структура("ТекущаяСтрока",Статья);
	Если ЗначениеЗаполнено(Статья) Тогда
		Структура.Вставить("Отбор",Новый Структура("Владелец",Статья.Владелец));
	КонецЕсли;
	Возврат структура;
КонецФункции

&НаСервереБезКонтекста
Функция ФинансовыйПоказатель(СтатьяБюджета)
	Возврат СтатьяБюджета.ФинансовыйПоказатель;	
КонецФункции


&НаКлиенте
Процедура РежимВыбораИзСтатейБюджетов(Команда)
	Элементы.ФормаРежимВыбораИзСтатейБюджетов.Пометка = НЕ Элементы.ФормаРежимВыбораИзСтатейБюджетов.Пометка;
КонецПроцедуры
