#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


// Процедура ОбработкаПроведения
// 
Процедура ОбработкаПроведения(Отказ)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	фин_ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПараметрыПроведения = Документы.фин_КорректировкаОстатковПоПрогнознымБалансам.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	фин_УправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ТаблицаНовыеОстаткиПоСчетамБюджетирования, Движения, Отказ,"ТаблицаНовыеОстаткиПоСчетамБюджетирования","фин_ПроцедурыУчетаПоУправленческомуПлануСчетовСервер","КорректировкаОстатков");	

КонецПроцедуры //ОбработкаПроведения

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Проверим балансы корректировок. Он должен быть равным 0
	
	БалансКорректировкиУпр=0;
	БалансКорректировкиСценарий=0;
	
	Для Каждого Строка Из НовыеОстаткиПоСчетамБюджетирования Цикл
		
		Если Строка.Счет.Забалансовый Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка.Счет.Вид=ВидСчета.Пассивный Тогда
			БалансКорректировкиУпр=БалансКорректировкиУпр-Строка.СуммаУпр;
			Если Не Сценарий.Пустая() Тогда
				БалансКорректировкиСценарий=БалансКорректировкиСценарий-Строка.СуммаСценария;
			КонецЕсли;
		Иначе
			БалансКорректировкиУпр=БалансКорректировкиУпр+Строка.СуммаУпр;
			Если Не Сценарий.Пустая() Тогда
				БалансКорректировкиСценарий=БалансКорректировкиСценарий+Строка.СуммаСценария;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Окр(?(БалансКорректировкиУпр>0,БалансКорректировкиУпр,БалансКорректировкиУпр*(-1)),1,1)>0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Суммы корректировок остатка в валюте упр. учета по активу и пассиву должны быть равны!",,,,Отказ);
	КонецЕсли;
	
	Если Окр(?(БалансКорректировкиСценарий>0,БалансКорректировкиСценарий,БалансКорректировкиСценарий*(-1)),1,1)>0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Суммы корректировок остатка в валюте сценария по активу и пассиву должны быть равны!",,,,Отказ);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	фин_ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект,фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ВалютаРегламентированногоУчета"));
КонецПроцедуры
#КонецЕсли