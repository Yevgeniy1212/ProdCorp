#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт 
	
	Реквизиты = "ДенежныеСредстваВКассах, ДенежныеСредстваНаБанковскихСчетах, Организация, СтруктурноеПодразделение, ПериодРегистрации";
	ДокЗначенияРеквизитов = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДокументСсылка, Реквизиты);
	
	ДополнительныеЗапросы = Новый Массив;
	ДополнительныеЗапросы.Добавить("ТаблицаПоДенежнымСредствам");
	ДополнительныеЗапросы.Добавить("ТаблицаПоДенежнымСредствамКПолучению");
	
	ДопПараметрыЗапросов = Новый Структура;
	
	ВалютаУправленческогоУчета 	= фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ВалютаУправленческогоУчета");
	ДатаДокумента 				= Новый Граница(КонецМесяца(ДокЗначенияРеквизитов.ПериодРегистрации), ВидГраницы.Включая);

	ДопПараметрыЗапросов.Вставить("ДатаДокумента", 				ДатаДокумента);
	ДопПараметрыЗапросов.Вставить("Организация", 				ДокЗначенияРеквизитов.Организация);
	ДопПараметрыЗапросов.Вставить("СтруктурноеПодразделение",	ДокЗначенияРеквизитов.СтруктурноеПодразделение);
	ДопПараметрыЗапросов.Вставить("ВалютаУправленческогоУчета", ВалютаУправленческогоУчета);
	
	ПараметрыПроведения = фин_УправлениеПроведениемДокументовСервер.ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ,,,,,,,ДопПараметрыЗапросов,ДополнительныеЗапросы);

	Реквизиты = ПараметрыПроведения.Реквизиты;
	
	// Денежные средства
	ДенежныеСредства = ПараметрыПроведения.ТаблицаДенежныеСредства;
	ДенежныеСредства.Колонки.Добавить("СуммаУпр", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
    МассивДляУдаления = Новый Массив;
	Для каждого СтрокаТаблицы из ДенежныеСредства Цикл
		Если Не СтрокаТаблицы.КурсВалютыДенежныхСредств = NULL Тогда 
			СтрокаТаблицы.СуммаУпр = фин_ОбщегоНазначенияСервер.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.СуммаОстаток, СтрокаТаблицы.Валюта, 
				ВалютаУправленческогоУчета, СтрокаТаблицы.КурсВалютыДенежныхСредств, СтрокаТаблицы.КурсВалютыУправленческогоУчета, 
				СтрокаТаблицы.КратностьВалютыДенежныхСредств, СтрокаТаблицы.КратностьВалютыУправленческогоУчета) - СтрокаТаблицы.СуммаУпрОстаток;
		КонецЕсли;
		Если СтрокаТаблицы.СуммаУпр = 0 Тогда
			МассивДляУдаления.Добавить(СтрокаТаблицы);
		КонецЕсли;		
	КонецЦикла;  	
	Для Каждого Строка ИЗ МассивДляУдаления Цикл
		ДенежныеСредства.Удалить(Строка);
	КонецЦикла;

	// Денежные средства к получению
	ДенежныеСредстваКПолучению = ПараметрыПроведения.ТаблицаДенежныеСредстваКПолучению;
	ДенежныеСредстваКПолучению.Колонки.Добавить("СуммаУпр", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
    МассивДляУдаления = Новый Массив;	
	Для каждого СтрокаТаблицы из ДенежныеСредстваКПолучению Цикл
		Если Не СтрокаТаблицы.КурсВалютыДенежныхСредств = NULL Тогда 
		СтрокаТаблицы.СуммаУпр = фин_ОбщегоНазначенияСервер.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.СуммаОстаток, СтрокаТаблицы.Валюта,
			ВалютаУправленческогоУчета, СтрокаТаблицы.КурсВалютыДенежныхСредств, СтрокаТаблицы.КурсВалютыУправленческогоУчета,
			СтрокаТаблицы.КратностьВалютыДенежныхСредств, СтрокаТаблицы.КратностьВалютыУправленческогоУчета) - СтрокаТаблицы.СуммаУпрОстаток;
		КонецЕсли;
		Если СтрокаТаблицы.СуммаУпр = 0 Тогда
			МассивДляУдаления.Добавить(СтрокаТаблицы);
		КонецЕсли;		
	КонецЦикла;
	Для Каждого Строка ИЗ МассивДляУдаления Цикл
		ДенежныеСредстваКПолучению.Удалить(Строка);
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаПоДенежнымСредствам(НомераТаблиц, ПараметрыПроведения, Реквизиты) Экспорт

	НомераТаблиц.Вставить("ТаблицаДенежныеСредства", НомераТаблиц.Количество());

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаДенежныеСредства.БанковскийСчетКасса,
	|	ТаблицаДенежныеСредства.Организация,
	|	ТаблицаДенежныеСредства.СтруктурноеПодразделение,
	|	ТаблицаДенежныеСредства.ВидДенежныхСредств,
	|	ТаблицаДенежныеСредства.СуммаОстаток КАК СуммаОстаток,
	|	ТаблицаДенежныеСредства.СуммаУпрОстаток КАК СуммаУпрОстаток,
	|	ТаблицаДенежныеСредства.БанковскийСчетКасса.ВалютаДенежныхСредств КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК КурсВалютыДенежныхСредств,
	|	КурсыВалютСрезПоследних.Кратность КАК КратностьВалютыДенежныхСредств,
	|	КурсыВалютСрезПоследнихУпр.Курс КАК КурсВалютыУправленческогоУчета,
	|	КурсыВалютСрезПоследнихУпр.Кратность КАК КратностьВалютыУправленческогоУчета
	|ИЗ
	|	РегистрНакопления.ден_ДенежныеСредства.Остатки(
	|			&ДатаДокумента,
	|			Организация = &Организация
	|				И СтруктурноеПодразделение = &СтруктурноеПодразделение) КАК ТаблицаДенежныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДокумента, ) КАК КурсыВалютСрезПоследних
	|		ПО (КурсыВалютСрезПоследних.Валюта = ТаблицаДенежныеСредства.БанковскийСчетКасса.ВалютаДенежныхСредств)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДокумента, ) КАК КурсыВалютСрезПоследнихУпр
	|		ПО (КурсыВалютСрезПоследнихУпр.Валюта = &ВалютаУправленческогоУчета)";

	Если Не Реквизиты.ДенежныеСредстваВКассах Тогда

		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ТаблицаДенежныеСредства.БанковскийСчетКасса ССЫЛКА Справочник.БанковскиеСчета";

	ИначеЕсли Не Реквизиты.ДенежныеСредстваНаБанковскихСчетах Тогда

		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ТаблицаДенежныеСредства.БанковскийСчетКасса ССЫЛКА Справочник.Кассы";

	КонецЕсли;

	Возврат ТекстЗапроса + фин_УправлениеПроведениемДокументовСервер.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаТаблицаПоДенежнымСредствамКПолучению(НомераТаблиц, ПараметрыПроведения, Реквизиты) Экспорт

	НомераТаблиц.Вставить("ТаблицаДенежныеСредстваКПолучению", НомераТаблиц.Количество());

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаДенежныеСредстваКПолучению.Организация,
	|	ТаблицаДенежныеСредстваКПолучению.СтруктурноеПодразделение,
	|	ТаблицаДенежныеСредстваКПолучению.БанковскийСчетКасса,
	|	ТаблицаДенежныеСредстваКПолучению.ВидДенежныхСредств,
	|	ТаблицаДенежныеСредстваКПолучению.ДокументПолучения,
	|	ТаблицаДенежныеСредстваКПолучению.СуммаОстаток КАК СуммаОстаток,
	|	ТаблицаДенежныеСредстваКПолучению.СуммаУпрОстаток КАК СуммаУпрОстаток,
	|	ТаблицаДенежныеСредстваКПолучению.БанковскийСчетКасса.ВалютаДенежныхСредств КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК КурсВалютыДенежныхСредств,
	|	КурсыВалютСрезПоследних.Кратность КАК КратностьВалютыДенежныхСредств,
	|	КурсыВалютСрезПоследнихУпр.Курс КАК КурсВалютыУправленческогоУчета,
	|	КурсыВалютСрезПоследнихУпр.Кратность КАК КратностьВалютыУправленческогоУчета
	|ИЗ
	|	РегистрНакопления.ден_ДенежныеСредстваКПолучению.Остатки(
	|			&ДатаДокумента,
	|			Организация = &Организация
	|				И СтруктурноеПодразделение = &СтруктурноеПодразделение) КАК ТаблицаДенежныеСредстваКПолучению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДокумента, ) КАК КурсыВалютСрезПоследних
	|		ПО (КурсыВалютСрезПоследних.Валюта = ТаблицаДенежныеСредстваКПолучению.БанковскийСчетКасса.ВалютаДенежныхСредств)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДокумента, ) КАК КурсыВалютСрезПоследнихУпр
	|		ПО (КурсыВалютСрезПоследнихУпр.Валюта = &ВалютаУправленческогоУчета)";

	Если Не Реквизиты.ДенежныеСредстваВКассах Тогда

		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ТаблицаДенежныеСредстваКПолучению.БанковскийСчетКасса ССЫЛКА Справочник.БанковскиеСчета";

	ИначеЕсли Не Реквизиты.ДенежныеСредстваНаБанковскихСчетах Тогда

		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ТаблицаДенежныеСредстваКПолучению.БанковскийСчетКасса ССЫЛКА Справочник.Кассы";

	КонецЕсли;
		
	Возврат ТекстЗапроса + фин_УправлениеПроведениемДокументовСервер.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Подготовка табличных печатных документов.

#КонецЕсли