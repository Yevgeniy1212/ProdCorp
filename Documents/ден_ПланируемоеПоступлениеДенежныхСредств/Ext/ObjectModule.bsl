#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Состояние = фин_ОбщегоНазначенияСервер.СостояниеДокументаПоУмолчанию(ЭтотОбъект.Метаданные().Имя);	
	
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура") 
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		Документы.ден_ПланируемоеПоступлениеДенежныхСредств.ЗаполнитьПоДокументуОснованию(ЭтотОбъект, ДанныеЗаполнения);
		Возврат;
	КонецЕсли;
	
	фин_ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ВалютаРегламентированногоУчета"));
	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЕстьРасчетыПоКредитам = ден_ПроцедурыКазначейства.ЕстьРасчетыПоКредитам(ВидОперации);
	ЕстьРасчетыСКонтрагентами = ден_ПроцедурыКазначейства.ЕстьРасчетыСКонтрагентами(ВидОперации);

	МассивНепроверяемыхРеквизитов = Новый Массив();

	Если ЕстьРасчетыСКонтрагентами ИЛИ ЕстьРасчетыПоКредитам Тогда
		Для Каждого Платеж Из РасшифровкаПлатежа Цикл
			Если ЗначениеЗаполнено(Организация) 
				И Организация <> Платеж.ДоговорКонтрагента.Организация Тогда
				ТекстСообщения = НСтр("ru = 'Строка %1 - выбран договор контрагента, не соответствующий организации, указанной в документе'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Платеж.НомерСтроки);
				Поле = "РасшифровкаПлатежа[" + (Платеж.НомерСтроки - 1) + "].ДоговорКонтрагента";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,Поле,, Отказ);
			КонецЕсли;
			Если ЗначениеЗаполнено(БанковскийСчетКасса) И Платеж.ДоговорКонтрагента.ВалютаВзаиморасчетов <> БанковскийСчетКасса.ВалютаДенежныхСредств Тогда
				ТекстСообщения = НСтр("ru = 'Строка %1 - валюта взаиморасчетов по договору контрагента не соответствуюет валюте документа'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Платеж.НомерСтроки);
				Поле = "РасшифровкаПлатежа[" + (Платеж.НомерСтроки - 1) + "].ДоговорКонтрагента";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,Поле,, Отказ);
			КонецЕсли;
		КонецЦикла;
		Если Не ВключатьВПлатежныйКалендарь Тогда
			МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаПлатежа");
		КонецЕсли;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ден_ВидыОперацийПланируемоеПоступлениеДС.ПрочееПоступлениеДенежныхСредств Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаПлатежа");
	КонецЕсли;
	
	фин_ЗаполнениеДокументов.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	фин_ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект, Ложь);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.ден_ПланируемоеПоступлениеДенежныхСредств.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ

	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ	
	фин_УправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ТаблицаРасшифровкаПлатежа, Движения, Отказ, "РасшифровкаПлатежа", "ден_ПроцедурыКазначейства", "СформироватьДвиженияПланируемоеПоступлениеДенежныхСредств",,);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#КонецЕсли










