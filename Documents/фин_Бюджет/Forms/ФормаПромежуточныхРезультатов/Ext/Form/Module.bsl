
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Адрес",АдресДанных)=Ложь Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Данные = ПолучитьИзВременногоХранилища(АдресДанных);
	ДеревоРасчета = ДанныеФормыВЗначение(Дерево,Тип("ДеревоЗначений"));
	Соответствие = Новый Соответствие;
	Для Каждого ЭлементСтруктуры Из Данные Цикл
		НоваяСтрока = ДеревоРасчета.Строки.Добавить();
		НоваяСтрока.Пункт = "Расчеты";
		Обработать(ЭлементСтруктуры.Значение,НоваяСтрока,Соответствие);
	КонецЦикла;
	АдресДанных = ПоместитьВоВременноеХранилище(Соответствие,УникальныйИдентификатор);
	ЗначениеВДанныеФормы(ДеревоРасчета,Дерево);
КонецПроцедуры

&НаСервере
Процедура Обработать(Структура,Строка,Соответствие)
	Если Структура.Свойство("Наименование") И Структура.Свойство("Данные") Тогда
		Строка.Пункт = Структура.Наименование;
		Обработать(Структура.Данные,Строка,Соответствие);
		Если Структура.Свойство("Движения") Тогда
			Обработать(Структура.Движения,Строка,Соответствие);
		КонецЕсли;
	ИначеЕсли Структура.Свойство("Данные") Тогда
		Обработать(Структура.Данные,Строка,Соответствие);
	ИначеЕсли Структура.Свойство("ДанныеДокумента") ИЛИ Структура.Свойство("ДвиженияОбороты") Тогда
		Для Каждого ЭлементСтруктуры Из Структура Цикл
			НоваяСтрока = Строка.Строки.Добавить();
			НоваяСтрока.Пункт = ?(ЭлементСтруктуры.Ключ="ДанныеДокумента","Расчет по моделям",
								 ?(ЭлементСтруктуры.Ключ="ИсходныеДанные","Данные источника расчетов (по текущему расчету)",
								 ?(ЭлементСтруктуры.Ключ="ПолныеИсходныеДанные","Данные источника расчетов (полные)",
								 ?(ЭлементСтруктуры.Ключ="ДвиженияОбороты","Движения: Обороты бюджетов","Движения: Остатки бюджетов"))));
			НоваяСтрока.Номер = Соответствие.Количество()+1;
			Соответствие.Вставить(Соответствие.Количество()+1,ЭлементСтруктуры.Значение);
		КонецЦикла;
	ИначеЕсли Структура.Количество()<2 Тогда
		Для Каждого ЭлементСтруктуры Из Структура Цикл
			Обработать(ЭлементСтруктуры.Значение,Строка,Соответствие);
		КонецЦикла;
	Иначе
		Для Каждого ЭлементСтруктуры Из Структура Цикл
			НоваяСтрока = Строка.Строки.Добавить();
			Обработать(ЭлементСтруктуры.Значение,НоваяСтрока,Соответствие);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ДеревоВыборНаСервере(ВыбраннаяСтрока);
КонецПроцедуры

&НаСервере
Процедура ДеревоВыборНаСервере(ВыбраннаяСтрока)
	НомерСтроки 	= Дерево.НайтиПоИдентификатору(ВыбраннаяСтрока).Номер;
	Данные 			= ПолучитьИзВременногоХранилища(АдресДанных);
	Таблица 		= Данные.Получить(НомерСтроки);
	ТабличныйДокумент.Очистить();
	Если Таблица = Неопределено ИЛИ ТипЗнч(Таблица)<>Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;
	Элементы.ДекорацияДанные.Заголовок = Дерево.НайтиПоИдентификатору(ВыбраннаяСтрока).Пункт;
	Инд = 1;
	Для Каждого Колонка Из Таблица.Колонки Цикл
		ТабличныйДокумент.Область("R1C"+Формат(Инд,"ЧГ=")).Текст = Колонка.Имя;
		Для Н = 1 По Таблица.Количество() Цикл
			ТабличныйДокумент.Область("R"+Формат(Н+1,"ЧГ=")+"C"+Формат(Инд,"ЧГ=")).Текст = Таблица[Н-1][Колонка.Имя];
			ТабличныйДокумент.Область("R"+Формат(Н+1,"ЧГ=")+"C"+Формат(Инд,"ЧГ=")).Расшифровка = Таблица[Н-1][Колонка.Имя];
		КонецЦикла;
		Инд = Инд + 1;
	КонецЦикла;
КонецПроцедуры
