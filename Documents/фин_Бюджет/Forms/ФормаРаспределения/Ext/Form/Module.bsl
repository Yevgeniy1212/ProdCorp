
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("СтруктураПоказателей")=Ложь Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Параметры.Свойство("ТочностьКоличественныхПоказателей",ТочностьКоличественныхПоказателей);
	Если Параметры.СтруктураПоказателей.Сумма = Истина Тогда
		Показатель 							= "Сумма";
		Если Параметры.СтруктураПоказателей.Количество = Ложь Тогда
			Элементы.Показатель.Видимость	= Ложь;
		КонецЕсли;
	Иначе
		Показатель 							= "Количество";
		Элементы.Показатель.Видимость		= Ложь;
	КонецЕсли;
	Для Инд = 1 По Параметры.КоличествоПериодов Цикл
		НС = ТаблицаДоли.Добавить();
		НС.НомерПериода	= Инд;
		НС.Доля 		= 1;
	КонецЦикла;
	СпособРаспределенияПоПериодам = "Умножать";
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДолиСПриращением(Команда)
	ТаблицаДоли[0].Доля = 1;
	ТекДоля = 1;
	Для Инд = 1 По ТаблицаДоли.Количество()-1 Цикл
		ТекДоля = ?(ВидПриращения=0,ТекДоля + Приращение,ТекДоля * (1+Приращение));
		ТаблицаДоли[Инд].Доля = ТекДоля;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДолиПоПрофилю(Команда)
	ОповещениеВыбораПрофиля = Новый ОписаниеОповещения("ОбработчикВыбораПрофиля",ЭтотОбъект);
	ОткрытьФорму("Справочник.фин_ПрофилиИзмененияПлановПоПериодам.ФормаВыбора",,ЭтотОбъект,,ВариантОткрытияОкна.ОтдельноеОкно,,ОповещениеВыбораПрофиля);
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораПрофиля(ВыбранноеЗначение,ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение=Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОбработчикВыбораПрофиляСервер(ВыбранноеЗначение);
КонецПроцедуры

&НаСервере
Процедура ОбработчикВыбораПрофиляСервер(Профиль)
	Для Инд = 0 По ТаблицаДоли.Количество()-1 Цикл
		ТаблицаДоли[Инд].Доля = 0;
	КонецЦикла;
	Для Каждого СтрокаПрофиля Из Профиль.ПрофильИзменения Цикл
		Если СтрокаПрофиля.НомерПериода>=0 И СтрокаПрофиля.НомерПериода<=ТаблицаДоли.Количество()-1 Тогда
			ТаблицаДоли[СтрокаПрофиля.НомерПериода].Доля = СтрокаПрофиля.Коэффициент;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Итог 			= ТаблицаДоли.Итог("Доля");
	МассивРезультат = Новый Массив;
	Буфер 			= ИсходноеЗначение;
	Инд 			= 1;
	Для Каждого СтрокаДоля Из ТаблицаДоли Цикл
		Если СпособРаспределенияПоПериодам = "Распределять" И Инд=ТаблицаДоли.Количество() Тогда
			МассивРезультат.Добавить(Буфер);
		Иначе
			МассивРезультат.Добавить(Окр(ИсходноеЗначение*СтрокаДоля.Доля*?(СпособРаспределенияПоПериодам="Умножать",1,1/?(Итог=0,1,Итог)),?(Показатель="Сумма",2,ТочностьКоличественныхПоказателей)));
		КонецЕсли;
		Буфер = Буфер - МассивРезультат[Инд-1];
		Инд = Инд + 1;
	КонецЦикла;
	Закрыть(Новый Структура("Значения,Поле",МассивРезультат,Показатель));
КонецПроцедуры
