
&НаКлиенте
Процедура БюджетНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.фин_Бюджеты.ФормаВыбора",Новый Структура("ЗначениеОтбора,ВидОтбора",СписокИспользованных,"НеВСписке"),Элементы.Бюджет,УникальныйИдентификатор,ВариантОткрытияОкна.ОтдельноеОкно,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("Адрес") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Параметры.Свойство("РежимДобавления",РежимДобавления);
	Параметры.Свойство("Ранг",			Ранг);
	Параметры.Свойство("Ячейка",		Ячейка);
	Параметры.Свойство("ПоЦентру",		ПоЦентру);
	Параметры.Свойство("Бюджет",		Бюджет);
	Параметры.Свойство("ПодачаЗаявок",	ПодачаЗаявок);
	Параметры.Свойство("СогласованиеЗаявок",СогласованиеЗаявок);
	Параметры.Свойство("ФормированиеБюджета",ФормированиеБюджета);
	Параметры.Свойство("СогласованиеБюджета",СогласованиеБюджета);
	Данные = ПолучитьИзВременногоХранилища(Параметры.Адрес);
	Для Каждого СтрокаРегламент Из Данные.Регламент Цикл
		НС = Регламент.Добавить();
		ЗаполнитьЗначенияСвойств(НС,СтрокаРегламент);
		СписокИспользованных.Добавить(НС.Бюджет);
	КонецЦикла;
	Для Каждого СтрокаПредшествующиеБюджеты Из Данные.ПредшествующиеБюджеты Цикл
		НС = ВсеПредшествующиеБюджеты.Добавить();
		ЗаполнитьЗначенияСвойств(НС,СтрокаПредшествующиеБюджеты);
		Если ЗначениеЗаполнено(Бюджет) И НС.Бюджет = Бюджет Тогда
			НС = ПредшествующиеБюджеты.Добавить();
			ЗаполнитьЗначенияСвойств(НС,СтрокаПредшествующиеБюджеты);
		КонецЕсли;
	КонецЦикла;
	Если РежимДобавления И Параметры.Свойство("ПредшествующийБюджет") Тогда
		НС = ПредшествующиеБюджеты.Добавить();
		НС.Предшественник = Параметры.ПредшествующийБюджет;
	КонецЕсли;
	Если Ранг = 0 Тогда
		Максимум = 0;
		Для Каждого СтрокаРегламент Из Регламент Цикл
			Максимум = Макс(Максимум,СтрокаРегламент.Ранг);
		КонецЦикла;
		Ранг = Максимум + 1;
	КонецЕсли;
	Если Ячейка = 0 Тогда
		Максимум = 0;
		Для Каждого СтрокаРегламент Из Регламент Цикл
			Максимум = Макс(Максимум,СтрокаРегламент.Ячейка);
		КонецЦикла;
		Ячейка = Максимум + 1;
	КонецЕсли;
	Элементы.Бюджет.ТолькоПросмотр = НЕ РежимДобавления;
	ЭтотОбъект.Заголовок = ?(РежимДобавления,"Добавление бюджета в регламент","Редактирование пункта регламента");
КонецПроцедуры

&НаКлиенте
Процедура ПредшествующиеБюджетыПредшественникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	Для Каждого СтрокаРегламент Из Регламент Цикл
		Если СтрокаРегламент.Ранг < Ранг И СтрокаРегламент.Бюджет<>Бюджет Тогда
			ДанныеВыбора.Добавить(СтрокаРегламент.Бюджет);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Если НЕ ЗначениеЗаполнено(Бюджет) Тогда
		ПоказатьПредупреждение(,"Не указан бюджет");
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Ранг) Тогда
		ПоказатьПредупреждение(,"Не указана строка (уровень) регламента");
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Ячейка) Тогда
		ПоказатьПредупреждение(,"Не указан столбец регламента");
		Возврат;
	КонецЕсли;
	Для Каждого СтрокаПредшествующий Из ПредшествующиеБюджеты Цикл
		СтрокиРегламента = Регламент.НайтиСтроки(Новый Структура("Бюджет",СтрокаПредшествующий.Предшественник));
		Если СтрокиРегламента.Количество()=0 Тогда
			ПоказатьПредупреждение(,"Бюджет """+СтрокаПредшествующий.Предшественник+""", указанный в качестве предшествующего, не найден в регламенте!");
			Возврат;
		ИначеЕсли СтрокиРегламента[0].Ранг>=Ранг Тогда
			ПоказатьПредупреждение(,"Бюджет не может формироваться раньше (или одновременно) бюджета """+СтрокаПредшествующий.Предшественник+""", указанного в качестве предшествующего!");
			Возврат;
		КонецЕсли;
	КонецЦикла;
	СтрокиПреемников = ВсеПредшествующиеБюджеты.НайтиСтроки(Новый Структура("Предшественник",Бюджет));
	Для Каждого СтрокаПреемник Из СтрокиПреемников Цикл
		СтрокиРегламента = Регламент.НайтиСтроки(Новый Структура("Бюджет",СтрокаПреемник.Бюджет));
		Если СтрокиРегламента.Количество()=0 Тогда
			
		ИначеЕсли СтрокиРегламента[0].Ранг<=Ранг Тогда
			ПоказатьПредупреждение(,"Бюджет не может формироваться позже (или одновременно) бюджета """+СтрокаПреемник.Бюджет+""", для которого текущий указан в качестве предшествующего!");
			Возврат;
		КонецЕсли;
	КонецЦикла;
	СтрокиКонфликт = Регламент.НайтиСтроки(Новый Структура("Ранг,Ячейка",Ранг,Ячейка));
	Для Каждого СтрокаКонфликт Из СтрокиКонфликт Цикл
		Если СтрокаКонфликт.Бюджет<> Бюджет Тогда
			ПоказатьПредупреждение(,"В указанных строке и столбце регламента уже располагается бюджет """+СтрокаКонфликт.Бюджет+"""!");
			Возврат;
		КонецЕсли;
	КонецЦикла;
	СтрокиКонфликт = Регламент.НайтиСтроки(Новый Структура("Бюджет",Бюджет));
	Если СтрокиКонфликт.Количество()>?(РежимДобавления,0,1) Тогда
		ПоказатьПредупреждение(,"Указанный бюджет не может больше одного раза быть включен в регламент!");
		Возврат;
	КонецЕсли;
	ПоместитьДанные();
	Закрыть(Параметры.Адрес);
КонецПроцедуры

&НаСервере
Процедура ПоместитьДанные()
	СтруктураДанных = Новый Структура("Бюджет,Ранг,Ячейка,ПоЦентру,РежимДобавления,ПредшествующиеБюджеты,ПодачаЗаявок,СогласованиеЗаявок,ФормированиеБюджета,СогласованиеБюджета",Бюджет,Ранг,Ячейка,ПоЦентру,РежимДобавления,ПредшествующиеБюджеты.Выгрузить(),ПодачаЗаявок,СогласованиеЗаявок,ФормированиеБюджета,СогласованиеБюджета);
	ПоместитьВоВременноеХранилище(СтруктураДанных,Параметры.Адрес);	
КонецПроцедуры
