Перем мВалютаРегламентированногоУчета Экспорт;
Перем мИмяВалютыРегламентированногоУчета Экспорт;
Перем мПрежнееОтражатьВУправленческомУчете;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда
	
// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураПечатныхФорм = Новый Структура();
	СтруктураПечатныхФорм.Вставить("ИсторияПогашенияЗайма", "Отчет о погашении займа");
		
	Возврат СтруктураПечатныхФорм;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "ИсторияПогашенияЗайма" Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		Макет = ПолучитьОбщийМакет("ИсторияПогашенияЗайма");
		СекцияШапка = Макет.ПолучитьОбласть("Шапка");
		СекцияМесяц = Макет.ПолучитьОбласть("Месяц");
		СекцияИтогоПогашено = Макет.ПолучитьОбласть("ИтогоПогашено");
		СекцияПодвал = Макет.ПолучитьОбласть("Подвал");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
	    Запрос.УстановитьПараметр("ФизЛицо",ФизЛицо);
	    Запрос.УстановитьПараметр("Организация",Организация);
		Запрос.УстановитьПараметр("Тенге", Константы.ВалютаРегламентированногоУчета.Получить());
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорЗаймаСРаботником.Дата,
		|	ДоговорЗаймаСРаботником.Номер,
		|	ДоговорЗаймаСРаботником.ФизЛицо,
		|	ВЫБОР
		|		КОГДА ДоговорЗаймаСРаботником.ВалютаДокумента = &Тенге
		|			ТОГДА ""тг.""
		|		ИНАЧЕ ДоговорЗаймаСРаботником.ВалютаДокумента.Наименование
		|	КОНЕЦ КАК НаименованиеВалюты,
		|	ДоговорЗаймаСРаботником.СуммаЗайма,
		|	ДоговорЗаймаСРаботником.СрокПогашения,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ДоговорЗаймаСРаботником.ФизЛицо.Наименование) КАК Работник,
		|	ДоговорЗаймаСРаботником.Организация
		|ИЗ
		|	Документ.ДоговорЗаймаСРаботником КАК ДоговорЗаймаСРаботником
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ДоговорЗаймаСРаботником.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		СекцияШапка.Параметры.Заполнить(Выборка);
		СекцияШапка.Параметры.Номер = ОбщегоНазначения.ПолучитьНомерНаПечать(ЭтотОбъект, глСписокПрефиксовУзлов);
		СекцияШапка.Параметры.ОписаниеНачалаПогашения = НРег(ПорядокПогашенияЗайма);// + ?(ПорядокПогашенияЗайма = Перечисления.ПорядокПогашенияЗаймаПроцентов.Ежемесячно," с " + Формат(НачалоПогашения,"ДФ='ММММ гггг'") + " г.","");
		ВалютаУчета = ?(ОтражатьВУправленческомУчете,Константы.ВалютаУправленческогоУчета.Получить(),Константы.ВалютаРегламентированногоУчета.Получить());
		СекцияШапка.Параметры.ИмяВалютыУчета = ВалютаУчета.НаименованиеПолное;
		ТабДокумент.Вывести(СекцияШапка);

		Если ОтражатьВУправленческомУчете Тогда
			ИмяРегистраУчета = "ПогашениеЗаймовРаботниками";
			ТекстНачислений = 
			"ВЫБРАТЬ
			|	СУММА(УправленческиеНачисления.Результат) КАК Результат,
			|	УправленческиеНачисления.ПериодРегистрации КАК ПериодРегистрации
			|ИЗ
			|	РегистрРасчета.УправленческиеНачисления КАК УправленческиеНачисления
			|ГДЕ
			|	УправленческиеНачисления.ФизЛицо = &ФизЛицо
			|
			|СГРУППИРОВАТЬ ПО
			|	УправленческиеНачисления.ПериодРегистрации";
		Иначе
			ИмяРегистраУчета = "ПогашениеЗаймовРаботникамиОрганизаций";
			ТекстНачислений = 
			"ВЫБРАТЬ
			|	СУММА(РезультатыНачислений.Результат) КАК Результат,
			|	РезультатыНачислений.ПериодРегистрации КАК ПериодРегистрации
			|ИЗ
			|	(ВЫБРАТЬ
			|		ДополнительныеНачисленияРаботниковОрганизаций.Результат КАК Результат,
			|		НАЧАЛОПЕРИОДА(ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации
			|	ИЗ
			|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисленияРаботниковОрганизаций
			|	ГДЕ
			|		ДополнительныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
			|		И ДополнительныеНачисленияРаботниковОрганизаций.ОбособленноеПодразделение = &Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ОсновныеНачисленияРаботниковОрганизаций.Результат,
			|		ОсновныеНачисленияРаботниковОрганизаций.ПериодРегистрации
			|	ИЗ
			|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
			|	ГДЕ
			|		ОсновныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
			|		И ОсновныеНачисленияРаботниковОрганизаций.ОбособленноеПодразделение = &Организация) КАК РезультатыНачислений
			|
			|СГРУППИРОВАТЬ ПО
			|	РезультатыНачислений.ПериодРегистрации";
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПогашениеЗаймов.Период КАК Период,
		|	ПогашениеЗаймов.ОсновнойДолгРасход КАК ОсновнойДолгРасход,
		|	ПогашениеЗаймов.ОсновнойДолгКонечныйОстаток КАК ОсновнойДолгКонечныйОстаток,
		|	ПогашениеЗаймов.ОсновнойДолгКонечныйОстаток КАК ОстатокЗайма,
		|	НачисленныеСуммы.Результат КАК Зарплата
		|ИЗ
		|	РегистрНакопления." + ИмяРегистраУчета + ".ОстаткиИОбороты(
		|		,
		|		,
		|		Месяц,
		|		,
		|		ДоговорЗайма = &Ссылка
		|		    И ФизЛицо = &ФизЛицо) КАК ПогашениеЗаймов
		|		ЛЕВОЕ СОЕДИНЕНИЕ (" + ТекстНачислений + ") КАК НачисленныеСуммы
		|		ПО ПогашениеЗаймов.Период = НачисленныеСуммы.ПериодРегистрации
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ
		|	СУММА(ОсновнойДолгРасход)
		|ПО
		|	ОБЩИЕ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ОбщийИтог Тогда
				СекцияИтогоПогашено.Параметры.Заполнить(Выборка);
			Иначе
				СекцияМесяц.Параметры.Заполнить(Выборка);
				СекцияПодвал.Параметры.Заполнить(Выборка);
				ТабДокумент.Вывести(СекцияМесяц);
			КонецЕсли;
		КонецЦикла;
		СекцияИтогоПогашено.Параметры.ДатаОкончания = Формат(ДобавитьМесяц(НачалоПогашения, СрокПогашения),"ДЛФ=DD");
		ТабДокумент.Вывести(СекцияИтогоПогашено);
		ТабДокумент.Вывести(СекцияПодвал);
		УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, РаботаСДиалогами.СформироватьЗаголовокДокумента(ЭтотОбъект,"Отчет о погашении займа"));
		
	КонецЕсли;
	
КонецФункции // Печать

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапки(Отказ, Заголовок)

	// ФизЛицо
	Если НЕ ЗначениеЗаполнено(ФизЛицо) Тогда
		ОбщегоНазначения.ОшибкаПриПроведении("Не указан работник!", Отказ, Заголовок);
	КонецЕсли;

	Если СрокПогашения = 0 Тогда
		ОбщегоНазначения.ОшибкаПриПроведении("Не указан срок погашения займа!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НачалоПогашения) Тогда
		ОбщегоНазначения.ОшибкаПриПроведении("Не указано начало погашения займа!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ОбщегоНазначения.ОшибкаПриПроведении("Не указана валюта займа!", Отказ, Заголовок);
	ИначеЕсли КурсДокумента = 0 Тогда 
		ОбщегоНазначения.ОшибкаПриПроведении("Не указан курс валюты займа!", Отказ, Заголовок);
	КонецЕсли;
	
    ПроверитьЗаполнениеШапкиРегл(Отказ, Заголовок)
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапкиРегл(Отказ, Заголовок)

	//  Организация
	Если ОтражатьВБухгалтерскомУчете И НЕ ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.ОшибкаПриПроведении("Не указана организация!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Процедура формирования движения регистров
//
Процедура ДвиженияРегистров()
	
	ДвиженияРегистровСведений();
	ДвиженияРегистровУпр();
	ДвиженияРегистровРегл();
	
КонецПроцедуры // ДвиженияРегистров()

Процедура ДвиженияРегистровУпр()
	
	Если ОтражатьВУправленческомУчете Тогда
		
		Движение = Движения.ПогашениеЗаймовРаботниками.Добавить();

		// Свойства
		Движение.Период			= Дата;
		Движение.ВидДвижения	= ВидДвиженияНакопления.Приход;
		
		// Измерения
		Движение.ФизЛицо		= ФизЛицо;
		Движение.ДоговорЗайма	= Ссылка;
		
		ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
		СтруктураКурса = ОбщегоНазначения.ПолучитьКурсВалюты(ВалютаУправленческогоУчета,Дата);
		
		// Ресурсы
		Движение.ОсновнойДолг	= ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(СуммаЗайма,ВалютаДокумента,ВалютаУправленческогоУчета,
																		КурсДокумента,СтруктураКурса.Курс,
																		КратностьДокумента,СтруктураКурса.Кратность);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвиженияРегистровРегл()
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		
		Движение = Движения.ПогашениеЗаймовРаботникамиОрганизаций.Добавить();

		// Свойства
		Движение.Период			= Дата;
		Движение.ВидДвижения	= ВидДвиженияНакопления.Приход;
		
		// Измерения
		Движение.ФизЛицо		= ФизЛицо;
		Движение.Организация	= Организация;
		Движение.ДоговорЗайма	= Ссылка;
		
		// Ресурсы
		Движение.ОсновнойДолг	= ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(СуммаЗайма,ВалютаДокумента,мВалютаРегламентированногоУчета,
																		КурсДокумента,1,
																		КратностьДокумента,1);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвиженияРегистровСведений()
	
	НовыеСведения = Движения.СведенияОЗаймах.Добавить();
	ЗаполнитьЗначенияСвойств(НовыеСведения, ЭтотОбъект);
	НовыеСведения.ДатаДокумента = Дата;
	НовыеСведения.ДоговорЗайма = Ссылка;
	НовыеСведения.НомерДокумента = Номер;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);	
	
	// Проверка ручной корректировки
	Если ОбщегоНазначения.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект) Тогда
		Возврат
	КонецЕсли;

	
	//Надо позвать проверку заполнения реквизитов шапки
	ПроверитьЗаполнениеШапки(Отказ, Заголовок);
	
	// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
	Если НЕ Отказ Тогда
		
		ДвиженияРегистров()

	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	// Установим прежнее состояние ОтражатьВУправленческомУчете
	мПрежнееОтражатьВУправленческомУчете = Ссылка.ОтражатьВУправленческомУчете;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ ОБЪЕКТА 

Запрос = Новый Запрос("ВЫБРАТЬ
                      |	Константы.ВалютаРегламентированногоУчета.Ссылка КАК Ссылка,
                      |	Константы.ВалютаРегламентированногоУчета.Наименование КАК Наименование
                      |ИЗ
                      |	Константы КАК Константы");

Выборка = Запрос.Выполнить().Выбрать();
Выборка.Следующий();
мИмяВалютыРегламентированногоУчета = Выборка.Наименование;
мВалютаРегламентированногоУчета = Выборка.Ссылка;