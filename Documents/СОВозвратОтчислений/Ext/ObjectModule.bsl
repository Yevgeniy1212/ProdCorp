// - БК - начало
Перем мОтображатьСтруктурныеПодразделения Экспорт;
// - БК - конец

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

Процедура ЗаполнитьПоДокументуОснования(Основание) Экспорт
	
	ТипОснования = ТипЗнч(Основание);
	Если ТипОснования = Тип("ДокументСсылка.СОПеречислениеВФонды") Тогда
		
		// Заполним реквизиты из стандартного набора.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СОПеречислениеВФонды.Контрагент КАК Контрагент,
		|	СОПеречислениеВФонды.ВидПлатежа КАК ВидПлатежа,
		|	СОПеречислениеВФонды.ПериодРегистрации КАК ПериодРегистрации,
		|	СОПеречислениеВФонды.СоциальныеОтчисления.(
		|		Ссылка,
		|		НомерСтроки,
		|		ФизЛицо,
		|		Сумма,
		|		Авторасчет
		|	) КАК СоциальныеОтчисления,
		|	СОПеречислениеВФонды.ВидОперации
		|ИЗ
		|	Документ.СОПеречислениеВФонды КАК СОПеречислениеВФонды
		|ГДЕ
		|	СОПеречислениеВФонды.Ссылка = &парамСсылка";
		
		Запрос.УстановитьПараметр("парамСсылка", Основание);
		РезультатЗапроса = Запрос.Выполнить().Выбрать();
		Если РезультатЗапроса.Следующий() Тогда
			Контрагент		  = РезультатЗапроса.Контрагент;
			ВидПлатежа		  = РезультатЗапроса.ВидПлатежа;
			ПериодРегистрации = РезультатЗапроса.ПериодРегистрации;
			Если РезультатЗапроса.ВидОперации = Перечисления.ВидыОперацийСОПеречислениеВФонды.ПеречислениеОбязательныхСоциальныхОтчислений Тогда
				ВидОперации = Перечисления.ВидыОперацийСОВозвратОтчислений.ВозвратОбязательныхСоциальныхОтчислений;
			ИначеЕсли РезультатЗапроса.ВидОперации = Перечисления.ВидыОперацийСОПеречислениеВФонды.ПеречислениеВзносовОСМС Тогда
				ВидОперации = Перечисления.ВидыОперацийСОВозвратОтчислений.ВозвратВзносовОСМС;
			Иначе
				ВидОперации = Перечисления.ВидыОперацийСОВозвратОтчислений.ВозвратОтчисленийОСМС;						
			КонецЕсли; 
			СоциальныеОтчисления.Загрузить(РезультатЗапроса.СоциальныеОтчисления.Выгрузить());
		КонецЕсли;
		
	КонецЕсли;
	
	ДокументОснование = Основание;
	
КонецПроцедуры // ОбработкаЗаполненияУпр()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(Основание)
	
	 ЗаполнитьПоДокументуОснования(Основание)
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(СоциальныеОтчисления);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "ФизЛицо");
	СуммаДокумента = СоциальныеОтчисления.Итог("Сумма");
	
КонецПроцедуры

// - БК - начало
мОтображатьСтруктурныеПодразделения = ОбщегоНазначения.ПолучитьПризнакОтображенияСтруктурныхПодразделенийВЗарплате();
// - БК - конец
