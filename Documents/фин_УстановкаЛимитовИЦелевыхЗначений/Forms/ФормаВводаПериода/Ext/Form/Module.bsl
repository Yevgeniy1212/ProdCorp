
&НаКлиенте
Процедура ПериодНазад(Команда)
	ДобавитьПериод(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПериод(Добавить)
	ПериодичностьСдвига = фин_ОбщегоНазначенияВызовСервераПовтИсп.ПолучитьПериодичностьСценария(Сценарий);
	НачалоПериодаПланирования=фин_ПроцедурыМеханизмовБюджетированияКлиентСервер.СледующийПериодПланирования(НачалоПериодаПланирования,ПериодичностьСдвига,Добавить);
	фин_ПроцедурыМеханизмовБюджетированияКлиентСервер.УстановитьГоризонтПланированияИПредставлениеБюджетногоПериода(НадписьПериодПланирования,,НачалоПериодаПланирования,Сценарий);
КонецПроцедуры

&НаКлиенте
Процедура ПериодВперед(Команда)
	ДобавитьПериод(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПериодВыбрать(Команда)
	ВыбратьПериодПланирования(НачалоПериодаПланирования,Элементы.ПериодВыбрать);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодПланирования(Период,Элемент)
	ОбъектПериодичность = Сценарий;
	СписокПериодов = ПолучитьСписокПериодов(Период,ОбъектПериодичность);
	Если СписокПериодов.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	ОписаниеВыбораПериодаИзСписка = Новый ОписаниеОповещения("ОбработкаВыбораПериодаИзСписка",ЭтотОбъект,Элемент);
	ПоказатьВыборИзСписка(ОписаниеВыбораПериодаИзСписка,СписокПериодов,Элемент,СписокПериодов[0]);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораПериодаИзСписка(мРезультат,Элемент) Экспорт
	Если мРезультат<>Неопределено Тогда
		Если ТипЗнч(мРезультат.Значение) = Тип("Число") Тогда
			ВыбратьПериодПланирования(Дата(мРезультат.Значение,1,1),Элемент);
		Иначе
			НачалоПериодаПланирования = мРезультат.Значение;
			фин_ПроцедурыМеханизмовБюджетированияКлиентСервер.УстановитьГоризонтПланированияИПредставлениеБюджетногоПериода(НадписьПериодПланирования,,НачалоПериодаПланирования,Сценарий);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСписокПериодов(ПериодПланирования,Сценарий)
	Возврат фин_ПроцедурыМеханизмовБюджетированияКлиентСервер.ПолучитьПериодыДляВыбора(ПериодПланирования,Сценарий);	
КонецФункции


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Сценарий") Тогда
		Сценарий = Параметры.Сценарий;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если Параметры.Свойство("ПериодПланирования") Тогда
		НачалоПериодаПланирования = Параметры.ПериодПланирования;
	Иначе
		НачалоПериодаПланирования = ТекущаяДата();
	КонецЕсли;
	НачалоПериодаПланирования = фин_ПроцедурыМеханизмовБюджетированияКлиентСервер.ДатаНачалаПериода(НачалоПериодаПланирования,Сценарий);
	фин_ПроцедурыМеханизмовБюджетированияКлиентСервер.УстановитьГоризонтПланированияИПредставлениеБюджетногоПериода(НадписьПериодПланирования,,НачалоПериодаПланирования,Сценарий);
КонецПроцедуры


&НаКлиенте
Процедура Ок(Команда)
	Закрыть(НачалоПериодаПланирования);
КонецПроцедуры


&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть();
КонецПроцедуры

