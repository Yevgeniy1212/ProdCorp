Перем мСписокНедоступныхРасчетов;

// Заполняет начисления и удержания по схемам мотивации
// Параметры:
//   ВидТаблицы - Имя табличной части, в которую заполняются схемы мотивации
//   ТабличнаяЧасть - заполняемая табличная часть (без заменяемых записей)
// 	 Период - Дата документа
//   Физлица - СправочникСсылка.Физлица
//				
Процедура ЗаполнениеПоСхемамМотивации(ВидТаблицы, ТабличнаяЧасть, Период, Сотрудники = Неопределено) Экспорт
	
	Если ВидТаблицы = "Начисления" Тогда	
		ТекстЗапросаПоСхемамМотивации = 
		"ВЫБРАТЬ
		| 	Сотрудники.ФизЛицо,
		|   Сотрудники.Сотрудник
		|ПОМЕСТИТЬ ВТ_Сотрудники
		|ИЗ &Сотрудники КАК Сотрудники
		|;
		|//////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СхемыМотивации.Показатель1 КАК Показатель1,
		|	СхемыМотивации.Валюта1 КАК Валюта1,
		|	СхемыМотивации.Показатель2 КАК Показатель2,
		|	СхемыМотивации.Валюта2 КАК Валюта2,
		|	СхемыМотивации.Показатель3 КАК Показатель3,
		|	СхемыМотивации.Валюта3 КАК Валюта3,
		|	СхемыМотивации.Показатель4 КАК Показатель4,
		|	СхемыМотивации.Валюта4 КАК Валюта4,
		|	ВЫБОР КОГДА (СхемыМотивации.ВидРасчета) ЕСТЬ NULL ТОГДА &Прекратить КОГДА (ПлановыеРасчеты.ВидРасчета) ЕСТЬ NULL  ТОГДА &Начать ИНАЧЕ &Изменить КОНЕЦ КАК Действие,
		|	ВЫБОР КОГДА (ПлановыеРасчеты.ФизЛицо) ЕСТЬ NULL ТОГДА СхемыМотивации.ФизЛицо ИНАЧЕ ПлановыеРасчеты.ФизЛицо КОНЕЦ КАК ФизЛицо,
		|	ВЫБОР КОГДА (ПлановыеРасчеты.ВидРасчета) ЕСТЬ NULL ТОГДА СхемыМотивации.ВидРасчета ИНАЧЕ ПлановыеРасчеты.ВидРасчета КОНЕЦ КАК ВидРасчета,
		|	Сотрудники.Сотрудник
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ПлановыеВРРаботниковСрезПоследних.ФизЛицо КАК ФизЛицо,
		|		ПлановыеВРРаботниковСрезПоследних.ВидРасчета КАК ВидРасчета,
		|		ПлановыеВРРаботниковСрезПоследних.Показатель1 КАК Показатель1,
		|		ПлановыеВРРаботниковСрезПоследних.Валюта1 КАК Валюта1,
		|		ПлановыеВРРаботниковСрезПоследних.Показатель2 КАК Показатель2,
		|		ПлановыеВРРаботниковСрезПоследних.Валюта2 КАК Валюта2,
		|		ПлановыеВРРаботниковСрезПоследних.Показатель3 КАК Показатель3,
		|		ПлановыеВРРаботниковСрезПоследних.Валюта3 КАК Валюта3,
		|		ПлановыеВРРаботниковСрезПоследних.Показатель4 КАК Показатель4,
		|		ПлановыеВРРаботниковСрезПоследних.Валюта4 КАК Валюта4
		|	ИЗ
		|		РегистрСведений.ПлановыеНачисленияРаботников.СрезПоследних(&Период, Физлицо В (&ФизлицА)) КАК ПлановыеВРРаботниковСрезПоследних
		|	
		|	ГДЕ
		|		(ПлановыеВРРаботниковСрезПоследних.Действует)) КАК ПлановыеРасчеты
		|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			СхемыМотивации.ВидРасчета КАК ВидРасчета,
		|			СхемыМотивации.Показатель1 КАК Показатель1,
		|			СхемыМотивации.Валюта1 КАК Валюта1,
		|			СхемыМотивации.Показатель2 КАК Показатель2,
		|			СхемыМотивации.Валюта2 КАК Валюта2,
		|			СхемыМотивации.Показатель3 КАК Показатель3,
		|			СхемыМотивации.Валюта3 КАК Валюта3,
		|			СхемыМотивации.Показатель4 КАК Показатель4,
		|			СхемыМотивации.Валюта4 КАК Валюта4,
		|			РаботникиСрезПоследних.ФизЛицо КАК ФизЛицо
		|		ИЗ
		|			РегистрСведений.Работники.СрезПоследних(&Период, Физлицо В (&ФизлицА)) КАК РаботникиСрезПоследних
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СхемыМотивацииРаботников.ВидРасчета КАК ВидРасчета,
		|					СхемыМотивацииРаботников.Показатель1 КАК Показатель1,
		|					СхемыМотивацииРаботников.Валюта1 КАК Валюта1,
		|					СхемыМотивацииРаботников.Показатель2 КАК Показатель2,
		|					СхемыМотивацииРаботников.Валюта2 КАК Валюта2,
		|					СхемыМотивацииРаботников.Показатель3 КАК Показатель3,
		|					СхемыМотивацииРаботников.Валюта3 КАК Валюта3,
		|					СхемыМотивацииРаботников.Показатель4 КАК Показатель4,
		|					СхемыМотивацииРаботников.Валюта4 КАК Валюта4,
		|					СхемыМотивацииРаботников.Подразделение КАК Подразделение,
		|					СхемыМотивацииРаботников.Должность КАК Должность
		|				ИЗ
		|					РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
		|				
		|				ГДЕ
		|					(СхемыМотивацииРаботников.ВидРасчета ССЫЛКА ПланВидовРасчета.УправленческиеНачисления)) КАК СхемыМотивации
		|				ПО РаботникиСрезПоследних.Должность = СхемыМотивации.Должность И РаботникиСрезПоследних.Подразделение = СхемыМотивации.Подразделение) КАК СхемыМотивации
		|		ПО ПлановыеРасчеты.ВидРасчета = СхемыМотивации.ВидРасчета И ПлановыеРасчеты.ФизЛицо = СхемыМотивации.ФизЛицо
		|       ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Сотрудники КАК Сотрудники
		|		ПО ПлановыеРасчеты.ФизЛицо = Сотрудники.ФизЛицо
		|ГДЕ
		|	((ПлановыеРасчеты.ВидРасчета) ЕСТЬ НЕ NULL  ИЛИ (СхемыМотивации.ВидРасчета) ЕСТЬ НЕ NULL )
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизЛицо,
		|	ВидРасчета";
	Иначе 
		ТекстЗапросаПоСхемамМотивации = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СхемыМотивации.Показатель1 КАК Показатель1,
		|	СхемыМотивации.Валюта1 КАК Валюта1,
		|	СхемыМотивации.Показатель2 КАК Показатель2,
		|	СхемыМотивации.Валюта2 КАК Валюта2,
		|	СхемыМотивации.Показатель3 КАК Показатель3,
		|	СхемыМотивации.Валюта3 КАК Валюта3,
		|	СхемыМотивации.Показатель4 КАК Показатель4,
		|	СхемыМотивации.Валюта4 КАК Валюта4,
		|	ВЫБОР КОГДА (СхемыМотивации.ВидРасчета) ЕСТЬ NULL  ТОГДА &Прекратить КОГДА (ПлановыеРасчеты.ВидРасчета) ЕСТЬ NULL  ТОГДА &Начать ИНАЧЕ &Изменить КОНЕЦ КАК Действие,
		|	ВЫБОР КОГДА (ПлановыеРасчеты.ФизЛицо) ЕСТЬ NULL  ТОГДА СхемыМотивации.ФизЛицо ИНАЧЕ ПлановыеРасчеты.ФизЛицо КОНЕЦ КАК ФизЛицо,
		|	ВЫБОР КОГДА (ПлановыеРасчеты.ВидРасчета) ЕСТЬ NULL  ТОГДА СхемыМотивации.ВидРасчета ИНАЧЕ ПлановыеРасчеты.ВидРасчета КОНЕЦ КАК ВидРасчета
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ПлановыеУдержанияРаботниковСрезПоследних.ФизЛицо КАК ФизЛицо,
		|		ПлановыеУдержанияРаботниковСрезПоследних.ВидРасчета КАК ВидРасчета,
		|		ПлановыеУдержанияРаботниковСрезПоследних.Показатель1 КАК Показатель1,
		|		ПлановыеУдержанияРаботниковСрезПоследних.Валюта1 КАК Валюта1,
		|		ПлановыеУдержанияРаботниковСрезПоследних.Показатель2 КАК Показатель2,
		|		ПлановыеУдержанияРаботниковСрезПоследних.Валюта2 КАК Валюта2,
		|		ПлановыеУдержанияРаботниковСрезПоследних.Показатель3 КАК Показатель3,
		|		ПлановыеУдержанияРаботниковСрезПоследних.Валюта3 КАК Валюта3,
		|		ПлановыеУдержанияРаботниковСрезПоследних.Показатель4 КАК Показатель4,
		|		ПлановыеУдержанияРаботниковСрезПоследних.Валюта4 КАК Валюта4
		|	ИЗ
		|		РегистрСведений.ПлановыеУдержанияРаботников.СрезПоследних(&Период, Физлицо В (&ФизлицА)) КАК ПлановыеУдержанияРаботниковСрезПоследних
		|	
		|	ГДЕ
		|		(ПлановыеУдержанияРаботниковСрезПоследних.Действует)) КАК ПлановыеРасчеты
		|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			СхемыМотивации.ВидРасчета КАК ВидРасчета,
		|			СхемыМотивации.Показатель1 КАК Показатель1,
		|			СхемыМотивации.Валюта1 КАК Валюта1,
		|			СхемыМотивации.Показатель2 КАК Показатель2,
		|			СхемыМотивации.Валюта2 КАК Валюта2,
		|			СхемыМотивации.Показатель3 КАК Показатель3,
		|			СхемыМотивации.Валюта3 КАК Валюта3,
		|			СхемыМотивации.Показатель4 КАК Показатель4,
		|			СхемыМотивации.Валюта4 КАК Валюта4,
		|			РаботникиСрезПоследних.ФизЛицо КАК ФизЛицо
		|		ИЗ
		|			РегистрСведений.Работники.СрезПоследних(&Период, Физлицо В (&ФизлицА)) КАК РаботникиСрезПоследних
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СхемыМотивацииРаботников.ВидРасчета КАК ВидРасчета,
		|					СхемыМотивацииРаботников.Показатель1 КАК Показатель1,
		|					СхемыМотивацииРаботников.Валюта1 КАК Валюта1,
		|					СхемыМотивацииРаботников.Показатель2 КАК Показатель2,
		|					СхемыМотивацииРаботников.Валюта2 КАК Валюта2,
		|					СхемыМотивацииРаботников.Показатель3 КАК Показатель3,
		|					СхемыМотивацииРаботников.Валюта3 КАК Валюта3,
		|					СхемыМотивацииРаботников.Показатель4 КАК Показатель4,
		|					СхемыМотивацииРаботников.Валюта4 КАК Валюта4,
		|					СхемыМотивацииРаботников.Подразделение КАК Подразделение,
		|					СхемыМотивацииРаботников.Должность КАК Должность
		|				ИЗ
		|					РегистрСведений.СхемыМотивацииРаботников КАК СхемыМотивацииРаботников
		|				
		|				ГДЕ
		|					(СхемыМотивацииРаботников.ВидРасчета ССЫЛКА ПланВидовРасчета.УправленческиеУдержания)) КАК СхемыМотивации
		|				ПО РаботникиСрезПоследних.Должность = СхемыМотивации.Должность И РаботникиСрезПоследних.Подразделение = СхемыМотивации.Подразделение) КАК СхемыМотивации
		|		ПО ПлановыеРасчеты.ВидРасчета = СхемыМотивации.ВидРасчета И ПлановыеРасчеты.ФизЛицо = СхемыМотивации.ФизЛицо
		|
		|ГДЕ
		|	((ПлановыеРасчеты.ВидРасчета) ЕСТЬ НЕ NULL  ИЛИ (СхемыМотивации.ВидРасчета) ЕСТЬ НЕ NULL )
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизЛицо,
		|	ВидРасчета";
		
	КонецЕсли;
	
	ЗапросПоСхемамМотивации = Новый Запрос(ТекстЗапросаПоСхемамМотивации);
	ЗапросПоСхемамМотивации.УстановитьПараметр("Физлица",Сотрудники.ВыгрузитьКолонку("ФизЛицо"));
	ЗапросПоСхемамМотивации.УстановитьПараметр("Сотрудники",Сотрудники);
	ЗапросПоСхемамМотивации.УстановитьПараметр("Период",Период);
	ЗапросПоСхемамМотивации.УстановитьПараметр("Начать",Перечисления.ВидыДействияСНачислением.Начать);
	ЗапросПоСхемамМотивации.УстановитьПараметр("Изменить",Перечисления.ВидыДействияСНачислением.Изменить);
	ЗапросПоСхемамМотивации.УстановитьПараметр("Прекратить",Перечисления.ВидыДействияСНачислением.Прекратить);
	
	Выборка = ЗапросПоСхемамМотивации.Выполнить().Выбрать();
	                                      
	Пока Выборка.Следующий() Цикл
	    СтрокаТЧ = ТабличнаяЧасть.Добавить();
	    ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по таблице "Начисления" и таблице "Удержания" документа
//
// Параметры: 
//  нет
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапрос()

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("парамСсылка", Ссылка);
	Запрос.УстановитьПараметр("ПустаяСсылка", Документы.ВводСведенийОПлановыхНачисленияхУдержанияхРаботников.ПустаяСсылка());

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|	ТЧНачисления.ВидРасчета,
	|	МАКСИМУМ(Показатели.НомерСтроки) КАК КоличествоПоказателей,
	|	Показатели1.Показатель.Наименование КАК Показатель1Наименование,
	|	Показатели2.Показатель.Наименование КАК Показатель2Наименование,
	|	Показатели3.Показатель.Наименование КАК Показатель3Наименование,
	|	Показатели4.Показатель.Наименование КАК Показатель4Наименование,
	|	Показатели5.Показатель.Наименование КАК Показатель5Наименование,
	|	Показатели6.Показатель.Наименование КАК Показатель6Наименование,
	|	Показатели1.Показатель.ТипПоказателя КАК Показатель1ТипПоказателя,
	|	Показатели2.Показатель.ТипПоказателя КАК Показатель2ТипПоказателя,
	|	Показатели3.Показатель.ТипПоказателя КАК Показатель3ТипПоказателя,
	|	Показатели4.Показатель.ТипПоказателя КАК Показатель4ТипПоказателя,
	|	Показатели5.Показатель.ТипПоказателя КАК Показатель5ТипПоказателя,
	|	Показатели6.Показатель.ТипПоказателя КАК Показатель6ТипПоказателя,
	|	Показатели1.Показатель.ВозможностьИзменения КАК Показатель1ВозможностьИзменения,
	|	Показатели2.Показатель.ВозможностьИзменения КАК Показатель2ВозможностьИзменения,
	|	Показатели3.Показатель.ВозможностьИзменения КАК Показатель3ВозможностьИзменения,
	|	Показатели4.Показатель.ВозможностьИзменения КАК Показатель4ВозможностьИзменения,
	|	Показатели5.Показатель.ВозможностьИзменения КАК Показатель5ВозможностьИзменения,
	|	Показатели6.Показатель.ВозможностьИзменения КАК Показатель6ВозможностьИзменения,
	|	Показатели1.Показатель.Валюта КАК Показатель1Валюта,
	|	Показатели2.Показатель.Валюта КАК Показатель2Валюта,
	|	Показатели3.Показатель.Валюта КАК Показатель3Валюта,
	|	Показатели4.Показатель.Валюта КАК Показатель4Валюта,
	|	Показатели5.Показатель.Валюта КАК Показатель5Валюта,
	|	Показатели6.Показатель.Валюта КАК Показатель6Валюта,
	|	Показатели1.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель1ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели2.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель2ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели3.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель3ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели4.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель4ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели5.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель5ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели6.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель6ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели1.Показатель.Предопределенный КАК Показатель1Предопределенный,
	|	Показатели2.Показатель.Предопределенный КАК Показатель2Предопределенный,
	|	Показатели3.Показатель.Предопределенный КАК Показатель3Предопределенный,
	|	Показатели4.Показатель.Предопределенный КАК Показатель4Предопределенный,
	|	Показатели5.Показатель.Предопределенный КАК Показатель5Предопределенный,
	|	Показатели6.Показатель.Предопределенный КАК Показатель6Предопределенный
	|ПОМЕСТИТЬ ВТ_ПоказателиНачислений
	|ИЗ
	|	Документ.ВводСведенийОПлановыхНачисленияхУдержанияхРаботников.Начисления КАК ТЧНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели
	|		ПО ТЧНачисления.ВидРасчета = Показатели.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели1
	|		ПО ТЧНачисления.ВидРасчета = Показатели1.Ссылка
	|			И (Показатели1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели2
	|		ПО ТЧНачисления.ВидРасчета = Показатели2.Ссылка
	|			И (Показатели2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели3
	|		ПО ТЧНачисления.ВидРасчета = Показатели3.Ссылка
	|			И (Показатели3.НомерСтроки = 3)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели4
	|		ПО ТЧНачисления.ВидРасчета = Показатели4.Ссылка
	|			И (Показатели4.НомерСтроки = 4)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели5
	|		ПО ТЧНачисления.ВидРасчета = Показатели5.Ссылка
	|			И (Показатели5.НомерСтроки = 5)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели6
	|		ПО ТЧНачисления.ВидРасчета = Показатели6.Ссылка
	|			И (Показатели6.НомерСтроки = 6)
	|ГДЕ
	|	ТЧНачисления.Ссылка = &парамСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧНачисления.НомерСтроки,
	|	ТЧНачисления.ВидРасчета,
	|	Показатели1.Показатель.Наименование,
	|	Показатели2.Показатель.Наименование,
	|	Показатели3.Показатель.Наименование,
	|	Показатели4.Показатель.Наименование,
	|	Показатели5.Показатель.Наименование,
	|	Показатели6.Показатель.Наименование,
	|	Показатели1.Показатель.ТипПоказателя,
	|	Показатели2.Показатель.ТипПоказателя,
	|	Показатели3.Показатель.ТипПоказателя,
	|	Показатели4.Показатель.ТипПоказателя,
	|	Показатели5.Показатель.ТипПоказателя,
	|	Показатели6.Показатель.ТипПоказателя,
	|	Показатели1.Показатель.ВозможностьИзменения,
	|	Показатели2.Показатель.ВозможностьИзменения,
	|	Показатели3.Показатель.ВозможностьИзменения,
	|	Показатели4.Показатель.ВозможностьИзменения,
	|	Показатели5.Показатель.ВозможностьИзменения,
	|	Показатели6.Показатель.ВозможностьИзменения,
	|	Показатели1.Показатель.Валюта,
	|	Показатели2.Показатель.Валюта,
	|	Показатели3.Показатель.Валюта,
	|	Показатели4.Показатель.Валюта,
	|	Показатели5.Показатель.Валюта,
	|	Показатели6.Показатель.Валюта,
	|	Показатели1.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели2.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели3.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели4.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели5.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели6.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели1.Показатель.Предопределенный,
	|	Показатели2.Показатель.Предопределенный,
	|	Показатели3.Показатель.Предопределенный,
	|	Показатели4.Показатель.Предопределенный,
	|	Показатели5.Показатель.Предопределенный,
	|	Показатели6.Показатель.Предопределенный
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЧУдержания.НомерСтроки КАК НомерСтроки,
	|	ТЧУдержания.ВидРасчета,
	|	МАКСИМУМ(Показатели.НомерСтроки) КАК КоличествоПоказателей,
	|	Показатели1.Показатель.Наименование КАК Показатель1Наименование,
	|	Показатели2.Показатель.Наименование КАК Показатель2Наименование,
	|	Показатели3.Показатель.Наименование КАК Показатель3Наименование,
	|	Показатели4.Показатель.Наименование КАК Показатель4Наименование,
	|	Показатели5.Показатель.Наименование КАК Показатель5Наименование,
	|	Показатели6.Показатель.Наименование КАК Показатель6Наименование,
	|	Показатели1.Показатель.ТипПоказателя КАК Показатель1ТипПоказателя,
	|	Показатели2.Показатель.ТипПоказателя КАК Показатель2ТипПоказателя,
	|	Показатели3.Показатель.ТипПоказателя КАК Показатель3ТипПоказателя,
	|	Показатели4.Показатель.ТипПоказателя КАК Показатель4ТипПоказателя,
	|	Показатели5.Показатель.ТипПоказателя КАК Показатель5ТипПоказателя,
	|	Показатели6.Показатель.ТипПоказателя КАК Показатель6ТипПоказателя,
	|	Показатели1.Показатель.ВозможностьИзменения КАК Показатель1ВозможностьИзменения,
	|	Показатели2.Показатель.ВозможностьИзменения КАК Показатель2ВозможностьИзменения,
	|	Показатели3.Показатель.ВозможностьИзменения КАК Показатель3ВозможностьИзменения,
	|	Показатели4.Показатель.ВозможностьИзменения КАК Показатель4ВозможностьИзменения,
	|	Показатели5.Показатель.ВозможностьИзменения КАК Показатель5ВозможностьИзменения,
	|	Показатели6.Показатель.ВозможностьИзменения КАК Показатель6ВозможностьИзменения,
	|	Показатели1.Показатель.Валюта КАК Показатель1Валюта,
	|	Показатели2.Показатель.Валюта КАК Показатель2Валюта,
	|	Показатели3.Показатель.Валюта КАК Показатель3Валюта,
	|	Показатели4.Показатель.Валюта КАК Показатель4Валюта,
	|	Показатели5.Показатель.Валюта КАК Показатель5Валюта,
	|	Показатели6.Показатель.Валюта КАК Показатель6Валюта,
	|	Показатели1.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель1ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели2.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель2ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели3.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель3ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели4.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель4ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели5.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель5ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели6.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель6ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели1.Показатель.Предопределенный КАК Показатель1Предопределенный,
	|	Показатели2.Показатель.Предопределенный КАК Показатель2Предопределенный,
	|	Показатели3.Показатель.Предопределенный КАК Показатель3Предопределенный,
	|	Показатели4.Показатель.Предопределенный КАК Показатель4Предопределенный,
	|	Показатели5.Показатель.Предопределенный КАК Показатель5Предопределенный,
	|	Показатели6.Показатель.Предопределенный КАК Показатель6Предопределенный
	|ПОМЕСТИТЬ ВТ_ПоказателиУдержаний
	|ИЗ
	|	Документ.ВводСведенийОПлановыхНачисленияхУдержанияхРаботников.Удержания КАК ТЧУдержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеУдержания.Показатели КАК Показатели
	|		ПО ТЧУдержания.ВидРасчета = Показатели.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеУдержания.Показатели КАК Показатели1
	|		ПО ТЧУдержания.ВидРасчета = Показатели1.Ссылка
	|			И (Показатели1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеУдержания.Показатели КАК Показатели2
	|		ПО ТЧУдержания.ВидРасчета = Показатели2.Ссылка
	|			И (Показатели2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеУдержания.Показатели КАК Показатели3
	|		ПО ТЧУдержания.ВидРасчета = Показатели3.Ссылка
	|			И (Показатели3.НомерСтроки = 3)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеУдержания.Показатели КАК Показатели4
	|		ПО ТЧУдержания.ВидРасчета = Показатели4.Ссылка
	|			И (Показатели4.НомерСтроки = 4)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеУдержания.Показатели КАК Показатели5
	|		ПО ТЧУдержания.ВидРасчета = Показатели5.Ссылка
	|			И (Показатели5.НомерСтроки = 5)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеУдержания.Показатели КАК Показатели6
	|		ПО ТЧУдержания.ВидРасчета = Показатели6.Ссылка
	|			И (Показатели6.НомерСтроки = 6)
	|ГДЕ
	|	ТЧУдержания.Ссылка = &парамСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧУдержания.НомерСтроки,
	|	ТЧУдержания.ВидРасчета,
	|	Показатели1.Показатель.Наименование,
	|	Показатели2.Показатель.Наименование,
	|	Показатели3.Показатель.Наименование,
	|	Показатели4.Показатель.Наименование,
	|	Показатели5.Показатель.Наименование,
	|	Показатели6.Показатель.Наименование,
	|	Показатели1.Показатель.ТипПоказателя,
	|	Показатели2.Показатель.ТипПоказателя,
	|	Показатели3.Показатель.ТипПоказателя,
	|	Показатели4.Показатель.ТипПоказателя,
	|	Показатели5.Показатель.ТипПоказателя,
	|	Показатели6.Показатель.ТипПоказателя,
	|	Показатели1.Показатель.ВозможностьИзменения,
	|	Показатели2.Показатель.ВозможностьИзменения,
	|	Показатели3.Показатель.ВозможностьИзменения,
	|	Показатели4.Показатель.ВозможностьИзменения,
	|	Показатели5.Показатель.ВозможностьИзменения,
	|	Показатели6.Показатель.ВозможностьИзменения,
	|	Показатели1.Показатель.Валюта,
	|	Показатели2.Показатель.Валюта,
	|	Показатели3.Показатель.Валюта,
	|	Показатели4.Показатель.Валюта,
	|	Показатели5.Показатель.Валюта,
	|	Показатели6.Показатель.Валюта,
	|	Показатели1.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели2.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели3.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели4.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели5.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели6.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели1.Показатель.Предопределенный,
	|	Показатели2.Показатель.Предопределенный,
	|	Показатели3.Показатель.Предопределенный,
	|	Показатели4.Показатель.Предопределенный,
	|	Показатели5.Показатель.Предопределенный,
	|	Показатели6.Показатель.Предопределенный
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачисленияРаботникам.НомерСтроки,
	|	НачисленияРаботникам.Физлицо,
	|	НачисленияРаботникам.ВидРасчета.ЗачетОтработанногоВремени КАК ОсновноеНачисление,
	|	""Начисление"" КАК ТипТаблицы,
	|	НачисленияРаботникам.ВидРасчета,
	|	ПоказателиНачислений.КоличествоПоказателей КАК КоличествоПоказателей,
	|	ИСТИНА КАК ПроизвольнаяФормулаРасчета,
	|	НачисленияРаботникам.Действие,
	|	НачисленияРаботникам.Показатель1,
	|	НачисленияРаботникам.Показатель2,
	|	НачисленияРаботникам.Показатель3,
	|	НачисленияРаботникам.Показатель4,
	|	НачисленияРаботникам.Показатель5,
	|	НачисленияРаботникам.Показатель6,
	|	НачисленияРаботникам.Валюта1,
	|	НачисленияРаботникам.Валюта2,
	|	НачисленияРаботникам.Валюта3,
	|	НачисленияРаботникам.Валюта4,
	|	НачисленияРаботникам.Валюта5,
	|	НачисленияРаботникам.Валюта6,
	|	ПоказателиНачислений.Показатель1Наименование КАК Показатель1Наименование,
	|	ПоказателиНачислений.Показатель2Наименование КАК Показатель2Наименование,
	|	ПоказателиНачислений.Показатель3Наименование КАК Показатель3Наименование,
	|	ПоказателиНачислений.Показатель4Наименование КАК Показатель4Наименование,
	|	ПоказателиНачислений.Показатель5Наименование КАК Показатель5Наименование,
	|	ПоказателиНачислений.Показатель6Наименование КАК Показатель6Наименование,
	|	ПоказателиНачислений.Показатель1ТипПоказателя КАК Показатель1ТипПоказателя,
	|	ПоказателиНачислений.Показатель2ТипПоказателя КАК Показатель2ТипПоказателя,
	|	ПоказателиНачислений.Показатель3ТипПоказателя КАК Показатель3ТипПоказателя,
	|	ПоказателиНачислений.Показатель4ТипПоказателя КАК Показатель4ТипПоказателя,
	|	ПоказателиНачислений.Показатель5ТипПоказателя КАК Показатель5ТипПоказателя,
	|	ПоказателиНачислений.Показатель6ТипПоказателя КАК Показатель6ТипПоказателя,
	|	ПоказателиНачислений.Показатель1ВозможностьИзменения КАК Показатель1ВозможностьИзменения,
	|	ПоказателиНачислений.Показатель2ВозможностьИзменения КАК Показатель2ВозможностьИзменения,
	|	ПоказателиНачислений.Показатель3ВозможностьИзменения КАК Показатель3ВозможностьИзменения,
	|	ПоказателиНачислений.Показатель4ВозможностьИзменения КАК Показатель4ВозможностьИзменения,
	|	ПоказателиНачислений.Показатель5ВозможностьИзменения КАК Показатель5ВозможностьИзменения,
	|	ПоказателиНачислений.Показатель6ВозможностьИзменения КАК Показатель6ВозможностьИзменения,
	|	ПоказателиНачислений.Показатель1ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиНачислений.Показатель2ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиНачислений.Показатель3ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиНачислений.Показатель4ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиНачислений.Показатель5ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиНачислений.Показатель6ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиНачислений.Показатель1Предопределенный,
	|	ПоказателиНачислений.Показатель2Предопределенный,
	|	ПоказателиНачислений.Показатель3Предопределенный,
	|	ПоказателиНачислений.Показатель4Предопределенный,
	|	ПоказателиНачислений.Показатель5Предопределенный,
	|	ПоказателиНачислений.Показатель6Предопределенный
	|ИЗ
	|	Документ.ВводСведенийОПлановыхНачисленияхУдержанияхРаботников.Начисления КАК НачисленияРаботникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоказателиНачислений КАК ПоказателиНачислений
	|		ПО НачисленияРаботникам.НомерСтроки = ПоказателиНачислений.НомерСтроки
	|ГДЕ
	|	НачисленияРаботникам.Ссылка = &парамСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УдержанияРаботникам.НомерСтроки,
	|	УдержанияРаботникам.Физлицо,
	|	NULL,
	|	""Удержание"",
	|	УдержанияРаботникам.ВидРасчета,
	|	ПоказателиУдержаний.КоличествоПоказателей,
	|	ИСТИНА,
	|	УдержанияРаботникам.Действие,
	|	УдержанияРаботникам.Показатель1,
	|	УдержанияРаботникам.Показатель2,
	|	УдержанияРаботникам.Показатель3,
	|	УдержанияРаботникам.Показатель4,
	|	УдержанияРаботникам.Показатель5,
	|	УдержанияРаботникам.Показатель6,
	|	УдержанияРаботникам.Валюта1,
	|	УдержанияРаботникам.Валюта2,
	|	УдержанияРаботникам.Валюта3,
	|	УдержанияРаботникам.Валюта4,
	|	УдержанияРаботникам.Валюта5,
	|	УдержанияРаботникам.Валюта6,
	|	ПоказателиУдержаний.Показатель1Наименование,
	|	ПоказателиУдержаний.Показатель2Наименование,
	|	ПоказателиУдержаний.Показатель3Наименование,
	|	ПоказателиУдержаний.Показатель4Наименование,
	|	ПоказателиУдержаний.Показатель5Наименование,
	|	ПоказателиУдержаний.Показатель6Наименование,
	|	ПоказателиУдержаний.Показатель1ТипПоказателя,
	|	ПоказателиУдержаний.Показатель2ТипПоказателя,
	|	ПоказателиУдержаний.Показатель3ТипПоказателя,
	|	ПоказателиУдержаний.Показатель4ТипПоказателя,
	|	ПоказателиУдержаний.Показатель5ТипПоказателя,
	|	ПоказателиУдержаний.Показатель6ТипПоказателя,
	|	ПоказателиУдержаний.Показатель1ВозможностьИзменения,
	|	ПоказателиУдержаний.Показатель2ВозможностьИзменения,
	|	ПоказателиУдержаний.Показатель3ВозможностьИзменения,
	|	ПоказателиУдержаний.Показатель4ВозможностьИзменения,
	|	ПоказателиУдержаний.Показатель5ВозможностьИзменения,
	|	ПоказателиУдержаний.Показатель6ВозможностьИзменения,
	|	ПоказателиУдержаний.Показатель1ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиУдержаний.Показатель2ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиУдержаний.Показатель3ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиУдержаний.Показатель4ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиУдержаний.Показатель5ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиУдержаний.Показатель6ЗапрашиватьПриКадровыхПеремещениях,
	|	ПоказателиУдержаний.Показатель1Предопределенный,
	|	ПоказателиУдержаний.Показатель2Предопределенный,
	|	ПоказателиУдержаний.Показатель3Предопределенный,
	|	ПоказателиУдержаний.Показатель4Предопределенный,
	|	ПоказателиУдержаний.Показатель5Предопределенный,
	|	ПоказателиУдержаний.Показатель6Предопределенный
	|ИЗ
	|	Документ.ВводСведенийОПлановыхНачисленияхУдержанияхРаботников.Удержания КАК УдержанияРаботникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоказателиУдержаний КАК ПоказателиУдержаний
	|		ПО УдержанияРаботникам.НомерСтроки = ПоказателиУдержаний.НомерСтроки
	|ГДЕ
	|	УдержанияРаботникам.Ссылка = &парамСсылка";
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапрос

// Проверяет правильность заполнения реквизитов в строке ТЧ "Начисления" документа.
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
//
// Параметры: 
//  ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//  							  из результата запроса
//  Отказ 						- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеСтроки(ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части "+?(ВыборкаПоСтрокамДокумента.ТипТаблицы = "Удержание", "Удержания", "Начисления")+": ";
									
	// ФизЛицо
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ФизЛицо) Тогда
		ОбщегоНазначения.ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не выбран работник!", Отказ, Заголовок);
	КонецЕсли;
		
	// ВидРасчета
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета) Тогда
		ОбщегоНазначения.ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не указано " + ?(ВыборкаПоСтрокамДокумента.ТипТаблицы = "Удержание", "удержание!", "начисление!"), Отказ, Заголовок);
	ИначеЕсли мСписокНедоступныхРасчетов.НайтиПоЗначению(ВыборкаПоСтрокамДокумента.ВидРасчета) <> Неопределено Тогда
		ОбщегоНазначения.ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "указанный вид расчета нельзя назначать в качестве планового!", Отказ, Заголовок);
	ИначеЕсли (ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.Начать или ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.Изменить) Тогда
		
		ПроведениеРасчетов.ПроверкаПоказателейВПлановыхНачислениях(ВыборкаПоСтрокамДокумента, СтрокаНачалаСообщенияОбОшибке, Ложь, Отказ, Заголовок, Истина);
		
	КонецЕсли;

	// Действие
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Действие) Тогда
		ОбщегоНазначения.ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не указано действие!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиНачисления()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//  ВыборкаПоРаботникиОрганизации 	- выборка из результата запроса по табличной чавсти документа,
//
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоСтрокамДокумента)

	Если ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.НеИзменять Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.ТипТаблицы = "Удержание" Тогда
		Движение = Движения.ПлановыеУдержанияРаботников.Добавить();
	Иначе
		Движение = Движения.ПлановыеНачисленияРаботников.Добавить();
	КонецЕсли;
	
	// Свойства
	Движение.Период                     = Дата;

	// Измерения
	Движение.ФизЛицо                    = ВыборкаПоСтрокамДокумента.ФизЛицо;
	
	// для основного начисления измерение ВидРасчетаИзмерение остается пустым - нельзя ввести 
	// более одного основных видов расчета
	
	Если (ВыборкаПоСтрокамДокумента.ОсновноеНачисление <> Null) и (ВыборкаПоСтрокамДокумента.ОсновноеНачисление = Ложь) Тогда
		
		Движение.ВидРасчетаИзмерение    = ВыборкаПоСтрокамДокумента.ВидРасчета;
	КонецЕсли;

	// Ресурсы
	Движение.ВидРасчета                 = ВыборкаПоСтрокамДокумента.ВидРасчета;
	Если ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.Прекратить Тогда
		Движение.Действует				= Ложь;
	Иначе				
		Движение.Действует				= Истина;
		Движение.Показатель1			= ВыборкаПоСтрокамДокумента.Показатель1;
		Движение.Валюта1				= ВыборкаПоСтрокамДокумента.Валюта1;
		Движение.Показатель2			= ВыборкаПоСтрокамДокумента.Показатель2;
		Движение.Валюта2				= ВыборкаПоСтрокамДокумента.Валюта2;
		Движение.Показатель3			= ВыборкаПоСтрокамДокумента.Показатель3;
		Движение.Валюта3				= ВыборкаПоСтрокамДокумента.Валюта3;		
		Движение.Показатель4			= ВыборкаПоСтрокамДокумента.Показатель4;
		Движение.Валюта4				= ВыборкаПоСтрокамДокумента.Валюта4;	
		Движение.Показатель5			= ВыборкаПоСтрокамДокумента.Показатель5;
		Движение.Валюта5				= ВыборкаПоСтрокамДокумента.Валюта5;		
		Движение.Показатель6			= ВыборкаПоСтрокамДокумента.Показатель6;
		Движение.Валюта6				= ВыборкаПоСтрокамДокумента.Валюта6;	
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Проверка ручной корректировки
	Если ОбщегоНазначения.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект) Тогда
		Возврат
	КонецЕсли;

	// получим реквизиты табличной части Начисления
	ВыборкаПоСтрокамДокумента = СформироватьЗапрос().Выбрать();

	Пока ВыборкаПоСтрокамДокумента.Следующий() Цикл 

		// проверим очередную строку табличной части
		ПроверитьЗаполнениеСтроки(ВыборкаПоСтрокамДокумента, Отказ, Заголовок);

		Если НЕ Отказ Тогда
			// Заполним записи в наборах записей регистров
			ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоСтрокамДокумента);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(Начисления);
	МассивТЧ.Добавить(Удержания);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "ФизЛицо");

КонецПроцедуры


мСписокНедоступныхРасчетов = Новый СписокЗначений;
мСписокНедоступныхРасчетов.Добавить(ПланыВидовРасчета.УправленческиеУдержания.УдержаноПоБухгалтерии);
мСписокНедоступныхРасчетов.Добавить(ПланыВидовРасчета.УправленческиеНачисления.НачисленоПоБухгалтерии);
мСписокНедоступныхРасчетов.Добавить(ПланыВидовРасчета.УправленческиеНачисления.НачисленоПоБухгалтерииСдельно);
