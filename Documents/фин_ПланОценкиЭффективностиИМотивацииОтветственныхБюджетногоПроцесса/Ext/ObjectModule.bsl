#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Процедура - обработчик события "ОбработкаПроведения".
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	фин_ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПараметрыПроведения = Документы.фин_ПланОценкиЭффективностиИМотивацииОтветственныхБюджетногоПроцесса.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	фин_УправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ТаблицаПереченьПравилРасчета, Движения, Отказ,"ПереченьПравилРасчета",,,"фин_ПравилаРасчетаЭффективностиРаботыОтветственныхБюджетногоПроцесса");	
	фин_УправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ТаблицаМатериальноеСтимулирование, Движения, Отказ,"МатериальноеСтимулирование",,,"фин_ПравилаМотивацииОтветственныхБюджетногоПроцесса");	
	
	Движения.фин_ПрименяемыеПланыОценкиЭффективностиИМотивации.Записывать=Истина;
	
	Движение = Движения.фин_ПрименяемыеПланыОценкиЭффективностиИМотивации.Добавить();
	Движение.Период 			= ПараметрыПроведения.Реквизиты[0].ПериодДействия;
	Движение.ПрименяемыйПлан 	= ПараметрыПроведения.Реквизиты[0].Ссылка;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПроверитьЗаполнениеТабличныхЧастей(Отказ);
КонецПроцедуры

Процедура ПроверитьЗаполнениеТабличныхЧастей(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		//|	ПравилРасчета.Ссылка,
		|	ПравилРасчета.НомерСтроки,
		|	ПравилРасчета.ФинансовыйПоказатель,
		|	ПравилРасчета.УправленческоеПодразделение,
		|	ПравилРасчета.Цель,
		|	ПравилРасчета.ДанныеПоказателя,
		|	ПравилРасчета.ИспользуемаяВеличина,
		|	ПравилРасчета.Формула,
		|	ПравилРасчета.УдельныйВес,
		|	ПравилРасчета.ОграничитьПодразделением
		|ПОМЕСТИТЬ ВТ_ТЗ
		|ИЗ
		|	&ПереченьПравилРасчета КАК ПравилРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПереченьПравилРасчета.НомерСтроки,
		|	ПереченьПравилРасчета2.НомерСтроки КАК НомерСтроки1
		|ИЗ
		|	ВТ_ТЗ КАК ПереченьПравилРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТЗ КАК ПереченьПравилРасчета2
		|		ПО ПереченьПравилРасчета.ФинансовыйПоказатель = ПереченьПравилРасчета2.ФинансовыйПоказатель
		|			И ПереченьПравилРасчета.УправленческоеПодразделение = ПереченьПравилРасчета2.УправленческоеПодразделение
		|			И ПереченьПравилРасчета.ДанныеПоказателя = ПереченьПравилРасчета2.ДанныеПоказателя
		|			И ПереченьПравилРасчета.ОграничитьПодразделением = ПереченьПравилРасчета2.ОграничитьПодразделением
		|			И ПереченьПравилРасчета.НомерСтроки < ПереченьПравилРасчета2.НомерСтроки";

	Запрос.УстановитьПараметр("ПереченьПравилРасчета", ПереченьПравилРасчета.Выгрузить());

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("в правилах расчета коэффициентов эффективности строка № "+Строка(Выборка.НомерСтроки)+" дублируется строкой № "+Строка(Выборка.НомерСтроки1),,,,Отказ);
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	фин_ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект,фин_ОбщегоНазначенияВызовСервераПовтИсп.ЗначениеПеременной("ВалютаРегламентированногоУчета"));
КонецПроцедуры

#КонецЕсли