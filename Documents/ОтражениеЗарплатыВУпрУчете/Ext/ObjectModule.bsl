Процедура Автозаполнение() Экспорт

	ОтражениеВУчете.Очистить();
	
	// ДанныеУпрУчета
	//	Поля:
	//		СтатьяЗатрат           	- статья затрат управленческго учета
	//		НоменклатурнаяГруппа	- номенклатурная группа управленческго учета
	//		Подразделение
	//		Результат
	//
	//	Описание:
	//	Содержит данные для отражения начисленй в упр. учете	
	
	ДанныеУпрУчетаТекст = 
	"ВЫБРАТЬ
 	|	ВЫБОР КОГДА (Начисления.ВидРасчета.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ) ТОГДА Начисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат 					КОГДА (УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ И НЕ УчетЗаработкаРаботников.СпособОтраженияВУпрУчете ЕСТЬ NULL) ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат			ИНАЧЕ &парамСпособПоУмолчаниюСтатьяЗатрат 		КОНЕЦ КАК СтатьяЗатрат,
 	|	ВЫБОР КОГДА (Начисления.ВидРасчета.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ) ТОГДА Начисления.ВидРасчета.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа 			КОГДА (УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ И НЕ УчетЗаработкаРаботников.СпособОтраженияВУпрУчете ЕСТЬ NULL) ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа  		ИНАЧЕ &парамСпособПоУмолчаниюНоменклатурнаяГруппа 	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ВЫБОР КОГДА (Начисления.ВидРасчета.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ) ТОГДА Начисления.ВидРасчета.СпособОтраженияВУпрУчете.ОбъектСтроительства 			КОГДА (УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ И НЕ УчетЗаработкаРаботников.СпособОтраженияВУпрУчете ЕСТЬ NULL) ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства  		ИНАЧЕ &парамСпособПоУмолчаниюОбъектСтроительства	КОНЕЦ КАК ОбъектСтроительства,
	|	Начисления.Подразделение КАК Подразделение,
	|	СУММА(Начисления.Результат) КАК Сумма
	|ИЗ
	|	РегистрРасчета.УправленческиеНачисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				Начисления.ФизЛицо КАК ФизЛицо,
	|				МАКСИМУМ(УчетЗаработкаРаботников.Период) КАК Период
	|			ИЗ
	|				РегистрРасчета.УправленческиеНачисления КАК Начисления
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|					ПО Начисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо И УчетЗаработкаРаботников.Период <= Начисления.ПериодДействияНачало
	|			
	|			ГДЕ
	|				Начисления.ПериодРегистрации = &парамПериодРегистрации
	|			
	|			СГРУППИРОВАТЬ ПО
	|				Начисления.ФизЛицо) КАК СведенияОРаботникахСрез
	|			ПО СведенияОРаботникахСрез.ФизЛицо = УчетЗаработкаРаботников.Физлицо И СведенияОРаботникахСрез.Период = УчетЗаработкаРаботников.Период
	|		ПО УчетЗаработкаРаботников.Физлицо = Начисления.ФизЛицо
	|
	|ГДЕ
	|	Начисления.ПериодРегистрации = &парамПериодРегистрации
	|	И (ВЫБОР КОГДА (Начисления.ВидРасчета.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ) ТОГДА Начисления.ВидРасчета.СпособОтраженияВУпрУчете КОГДА (УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ И НЕ УчетЗаработкаРаботников.СпособОтраженияВУпрУчете ЕСТЬ NULL) ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете КОНЕЦ <> &парамСпособНеОтражать)
	|
	|СГРУППИРОВАТЬ ПО
 	|	ВЫБОР КОГДА (Начисления.ВидРасчета.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ) ТОГДА Начисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат 				КОГДА (УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ И НЕ УчетЗаработкаРаботников.СпособОтраженияВУпрУчете ЕСТЬ NULL) ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат 		ИНАЧЕ &парамСпособПоУмолчаниюСтатьяЗатрат 		КОНЕЦ,
 	|	ВЫБОР КОГДА (Начисления.ВидРасчета.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ) ТОГДА Начисления.ВидРасчета.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа 		КОГДА (УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ И НЕ УчетЗаработкаРаботников.СпособОтраженияВУпрУчете ЕСТЬ NULL) ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа  	ИНАЧЕ &парамСпособПоУмолчаниюНоменклатурнаяГруппа 	КОНЕЦ,
	|	ВЫБОР КОГДА (Начисления.ВидРасчета.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ) ТОГДА Начисления.ВидРасчета.СпособОтраженияВУпрУчете.ОбъектСтроительства 		КОГДА (УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> &парамПустойСпособУУ И НЕ УчетЗаработкаРаботников.СпособОтраженияВУпрУчете ЕСТЬ NULL) ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства  	ИНАЧЕ &парамСпособПоУмолчаниюОбъектСтроительства	КОНЕЦ,
	|	Начисления.Подразделение";
	
	Запрос = Новый Запрос(ДанныеУпрУчетаТекст);
	
	Запрос.УстановитьПараметр("парамПустойСпособУУ", Справочники.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка());
	Запрос.УстановитьПараметр("парамСпособНеОтражать", Справочники.СпособыОтраженияЗарплатыВУпрУчете.НеОтражатьВУпручете);
	Запрос.УстановитьПараметр("парамСпособПоУмолчаниюСтатьяЗатрат", Справочники.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию.СтатьяЗатрат);
	Запрос.УстановитьПараметр("парамСпособПоУмолчаниюНоменклатурнаяГруппа", Справочники.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию.НоменклатурнаяГруппа);
	Запрос.УстановитьПараметр("парамСпособПоУмолчаниюОбъектСтроительства", Справочники.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию.ОбъектСтроительства);
	Запрос.УстановитьПараметр("парамПериодРегистрации", ПериодРегистрации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Строка = ОтражениеВУчете.Добавить();
		
		Строка.Подразделение = Выборка.Подразделение;
		Строка.СтатьяЗатрат = Выборка.СтатьяЗатрат;
		Строка.НоменклатурнаяГруппа = Выборка.НоменклатурнаяГруппа;
		Строка.ОбъектСтроительства = Выборка.ОбъектСтроительства;
		Строка.Сумма = Выборка.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры //  Автозаполнение
