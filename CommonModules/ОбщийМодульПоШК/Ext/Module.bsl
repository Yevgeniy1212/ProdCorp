Функция глДополнитьСтрокуЛ(Стр,Чем,Длина) Экспорт
	
	Добавить = Длина - СтрДлина(Стр);
	Добавок = "";
	
	СчетчикЦикла = 0;
	Для СчетчикЦикла = 1 По Добавить Цикл
		Добавок = Добавок + Чем;
	КонецЦикла;
	
	Возврат Добавок + Стр;
	
КонецФункции //глДополнитьСтрокуЛ()

Функция СимволыВЦифры(СтрСимв) 
	
	СтрЦифр = "";
	
	Для Индекс = 1 По СтрДлина(СтрСимв) Цикл
		ТекСимв = Сред(СтрСимв, Индекс, 1);
		
		Если Найти("1234567890", ТекСимв) = 0 Тогда
			// преобразуем символ
			ТекСимв = Строка(КодСимвола(ТекСимв,1) % 10);
		КонецЕсли;
		
		СтрЦифр = СтрЦифр + ТекСимв;
	КонецЦикла;
	
	Возврат СтрЦифр;
	
КонецФункции // СимволыВЦифры()

Функция глСформироватьШтрихкод(Префикс, ПредвКод, ТипКода=13) Экспорт
	
	Если ТипКода = 13 Тогда
		ИспКолво = 10;
	Иначе
		ИспКолво = 6;
	КонецЕсли;
	
	Штрихкод = СимволыВЦифры(Прав(СокрЛП(ПредвКод), ИспКолво));
	Штрихкод = Префикс + глДополнитьСтрокуЛ(Штрихкод, "0", ИспКолво);
				 
	Возврат Штрихкод + глКонтрольныйСимволEAN(Штрихкод, ТипКода);
					
КонецФункции // глСформироватьШтрихкод()

// глКонтрольныйСимволEAN(ШтрКод, Тип)
//
// Параметры:
//  ШтрКод - 12-символьный штрих-код (без контрольной цифры)
//  Тип    - тип штрихкода: 13 - EAN13, 8 - EAN8

Функция глКонтрольныйСимволEAN(ШтрКод, Тип) Экспорт
	
	Четн = 0;
	Нечетн = 0;
	
	Если Тип = 13 Тогда
		КолвоИтераций = 6;
	Иначе
		КолвоИтераций = 4;
	КонецЕсли;
	
	Для Индекс = 1 По КолвоИтераций Цикл
		Четн = Четн + Сред(ШтрКод, 2 * Индекс, 1);
		Нечетн = Нечетн + Сред(ШтрКод, 2 * Индекс - 1, 1);
	КонецЦикла;
	
	Если Тип = 13 Тогда
		Четн = Четн * 3;
	Иначе
		Нечетн = Нечетн * 3;
	КонецЕсли;
    
	КонтЦифра = 10 - (Четн + Нечетн) % 10;
	
	Возврат ?(КонтЦифра = 10, "0", Строка(КонтЦифра));
	
КонецФункции // глКонтрольныйСимволEAN()



