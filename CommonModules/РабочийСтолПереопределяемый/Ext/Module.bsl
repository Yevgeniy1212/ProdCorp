
Функция ФормаРабочегоСтола(ТекущийПользователь) Экспорт
	
	Возврат Обработки.РабочийСтол.ПолучитьФорму();
	
КонецФункции

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ОткрытьРабочийСтол() Экспорт

	ТекущийПользователь = глЗначениеПеременной("глТекущийПользователь");
	ФормаРабочегоСтола = РабочийСтолПереопределяемый.ФормаРабочегоСтола(ТекущийПользователь);
	ФормаРабочегоСтола.Открыть();
	ИспользоватьРабочийСтол = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОткрыватьПриЗапускеПанельФункций");
	Если Не ИспользоватьРабочийСтол Тогда
		// для текущего пользователя установим использование рабочего стола
		НаборЗаписей = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
		НаборЗаписей.Отбор.Настройка.Установить(ПланыВидовХарактеристик.НастройкиПользователей.ОткрыватьПриЗапускеПанельФункций);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Пользователь = ТекущийПользователь;
			НоваяЗапись.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОткрыватьПриЗапускеПанельФункций;
			НоваяЗапись.Значение = Истина;
		ИначеЕсли Не НаборЗаписей[0].Значение Тогда
			НаборЗаписей[0].Значение = Истина;
		КонецЕсли;
		
		Попытка
			НаборЗаписей.Записать();
			НаборЗаписан = Истина;
		Исключение
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПолучитьПричинуОшибки(ИнформацияОбОшибке()).Описание);
			НаборЗаписан = Ложь;
		КонецПопытки;
		
		Если НаборЗаписан Тогда
			// оповестим форму настройки параметров пользователя
			Оповестить("ИзмененаНастройкаОткрыватьПриЗапускеРабочийСтол", Новый Структура("ТекущийПользователь, ОткрыватьПриЗапускеПанельФункций", ТекущийПользователь, Истина));
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры

Процедура ФормаНачисленийПередОткрытием(Форма) Экспорт
	
	// В этой конфигурации дополнительных действий не предусмотрено
	
КонецПроцедуры

Процедура ИнициализироватьРабочийСтол() Экспорт
 	
	Если УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОткрыватьПриЗапускеПанельФункций") Тогда
		ФормаРабочегоСтола = РабочийСтолПереопределяемый.ФормаРабочегоСтола(глЗначениеПеременной("глТекущийПользователь"));
		ФормаРабочегоСтола.Открыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли