////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА УПРАВЛЕНИЯ НАСТРОЙКАМИ

Функция ЗаполнитьНастройкиПриОткрытииОтчета(ОтчетОбъект) Экспорт
	
	Если ТиповыеОтчеты.ОтчетПодсистемыЗУП(ОтчетОбъект) Тогда
		//Пользователи = УправлениеПользователями.СписокГруппВключающихПользователя();
		ПользователиДляОтбора = Новый Массив;
		ПользователиДляОтбора.Добавить(ПараметрыСеанса.ТекущийПользователь);
		ПользователиДляОтбора.Добавить(Справочники.ГруппыПользователей.ВсеПользователи);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
		|	СохраненныеНастройки.Ссылка КАК СохраненнаяНастройка
		|ИЗ
		|	Справочник.СохраненныеНастройки.Пользователи КАК СохраненныеНастройкиПользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СохраненныеНастройки КАК СохраненныеНастройки
		|		ПО СохраненныеНастройкиПользователи.Ссылка = СохраненныеНастройки.Ссылка
		|ГДЕ
		|	СохраненныеНастройки.ИспользоватьПриОткрытии = ИСТИНА
		|	И СохраненныеНастройки.НастраиваемыйОбъект = &НастраиваемыйОбъект
		|	И СохраненныеНастройкиПользователи.Пользователь В(&Пользователи)";
		
		Запрос.УстановитьПараметр("Пользователи", ПользователиДляОтбора);
		Запрос.УстановитьПараметр("НастраиваемыйОбъект", "ОтчетОбъект." + ОтчетОбъект.Метаданные().Имя);
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаРезультата.Количество() > 0 Тогда
			ОтчетОбъект.СохраненнаяНастройка = ТаблицаРезультата[0].СохраненнаяНастройка;
			ОтчетОбъект.ПрименитьНастройку();
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		//Пользователи = УправлениеПользователями.СписокГруппВключающихПользователя();
		ПользователиДляОтбора = Новый Массив;
		ПользователиДляОтбора.Добавить(ПараметрыСеанса.ТекущийПользователь);
		ПользователиДляОтбора.Добавить(Справочники.ГруппыПользователей.ВсеПользователи);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиОтчета.Ссылка КАК СохраненнаяНастройкаОтчета,
		|	НастройкиОтчета.НастраиваемыйОбъект
		|ПОМЕСТИТЬ СохраненныеНастройкиОтчета
		|ИЗ
		|	Справочник.СохраненныеНастройки.Пользователи КАК СохраненныеНастройкиПользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СохраненныеНастройки КАК НастройкиОтчета
		|		ПО СохраненныеНастройкиПользователи.Ссылка = НастройкиОтчета.Ссылка
	    |ГДЕ
		|	(НЕ НастройкиОтчета.ПометкаУдаления)
		|	И НастройкиОтчета.ТипНастройки = ЗНАЧЕНИЕ(Перечисление.ТипыНастроек.НастройкиОтчета)
		|	И СохраненныеНастройкиПользователи.Пользователь В(&Пользователи)
		|	И НастройкиОтчета.ИспользоватьПриОткрытии = ИСТИНА
		|	И НастройкиОтчета.НастраиваемыйОбъект = &НастраиваемыйОбъект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиФормы.Ссылка КАК СохраненнаяНастройкаФормы,
		|	НастройкиФормы.НастраиваемыйОбъект
		|ПОМЕСТИТЬ СохраненныеНастройкиФормы
		|ИЗ
		|	Справочник.СохраненныеНастройки.Пользователи КАК СохраненныеНастройкиПользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СохраненныеНастройки КАК НастройкиФормы
		|		ПО СохраненныеНастройкиПользователи.Ссылка = НастройкиФормы.Ссылка
	 	|ГДЕ
		|	(НЕ НастройкиФормы.ПометкаУдаления)
		|	И НастройкиФормы.ТипНастройки = ЗНАЧЕНИЕ(Перечисление.ТипыНастроек.НастройкиПользователяНастройкиОтчета)
		|	И СохраненныеНастройкиПользователи.Пользователь В(&Пользователи)
		|	И НастройкиФормы.ИспользоватьПриОткрытии = ИСТИНА
		|	И НастройкиФормы.НастраиваемыйОбъект = &НастраиваемыйОбъект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СохраненныеНастройкиОтчета.СохраненнаяНастройкаОтчета, ЗНАЧЕНИЕ(Справочник.СохраненныеНастройки.ПустаяСсылка)) КАК НастройкаОтчета,
		|	ЕСТЬNULL(СохраненныеНастройкиФормы.СохраненнаяНастройкаФормы, ЗНАЧЕНИЕ(Справочник.СохраненныеНастройки.ПустаяСсылка)) КАК НастройкаФормы
		|ИЗ
		|	СохраненныеНастройкиОтчета КАК СохраненныеНастройкиОтчета
		|		ПОЛНОЕ СОЕДИНЕНИЕ СохраненныеНастройкиФормы КАК СохраненныеНастройкиФормы
		|		ПО СохраненныеНастройкиОтчета.НастраиваемыйОбъект = СохраненныеНастройкиФормы.НастраиваемыйОбъект";
		
		Запрос.УстановитьПараметр("Пользователи", ПользователиДляОтбора);
		Запрос.УстановитьПараметр("НастраиваемыйОбъект", "ОтчетОбъект." + ОтчетОбъект.Метаданные().Имя);
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаРезультата.Количество() > 0 Тогда

			СохраненнаяНастройка = ТаблицаРезультата[0].НастройкаОтчета;
			СохраненнаяНастройкаФормы = ТаблицаРезультата[0].НастройкаФормы;
			
			Если ЗначениеЗаполнено(СохраненнаяНастройка) Тогда
				ОтчетОбъект.СохраненнаяНастройка = СохраненнаяНастройка;
			Иначе
				ОтчетОбъект.СохраненнаяНастройка = СохраненнаяНастройкаФормы;
			КонецЕсли;
			
			ОтчетОбъект.ПрименитьНастройку();
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
			
	КонецЕсли;
	
КонецФункции

// Процедура сохраняет настройки формы.
//
// Параметры:
//  СохраненнаяНастройка       - СправочникСсылка.СохраненныеНастройки - сохраняемая настройка.
//  СохраняемыеНастройки - параметры настройки формы.
//
Процедура СохранитьНастройкуОбъекта(СохраненнаяНастройка, СохраняемыеНастройки) Экспорт

	ОбъектСохраненнаяНастройка = СохраненнаяНастройка.ПолучитьОбъект();
	
	//СохраняемыеНастройки.Удалить("НастройкаПериода");
	//СохраняемыеНастройки.Удалить("ДанныеОтчета");
	//Если СохраняемыеНастройки.Свойство("ДанныеОтчета") И СохраняемыеНастройки.ДанныеОтчета.Свойство("Группировка") Тогда
	//	СохраняемыеНастройки.ДанныеОтчета.Удалить("Группировка");
	//КонецЕсли;
	//Если СохраняемыеНастройки.Свойство("ДанныеОтчета") И СохраняемыеНастройки.ДанныеОтчета.Свойство("ДополнительныеПоля") Тогда
	//	СохраняемыеНастройки.ДанныеОтчета.Удалить("ДополнительныеПоля");
	//КонецЕсли;
	
Если СохраняемыеНастройки.Свойство("ДанныеОтчета") Тогда
Если СохраняемыеНастройки.ДанныеОтчета <> неопределено Тогда
		
	Если СохраняемыеНастройки.Свойство("ДанныеОтчета") И СохраняемыеНастройки.ДанныеОтчета.Свойство("МакетШапкиОтчета") Тогда
		СохраняемыеНастройки.ДанныеОтчета.Удалить("МакетШапкиОтчета");
	КонецЕсли;
	Если СохраняемыеНастройки.Свойство("ДанныеОтчета") И СохраняемыеНастройки.ДанныеОтчета.Свойство("МакетСчет") Тогда
		СохраняемыеНастройки.ДанныеОтчета.Удалить("МакетСчет");
	КонецЕсли;
	Если СохраняемыеНастройки.Свойство("ДанныеОтчета") И СохраняемыеНастройки.ДанныеОтчета.Свойство("МакетПодвал") Тогда
		СохраняемыеНастройки.ДанныеОтчета.Удалить("МакетПодвал");
	КонецЕсли;
	Если СохраняемыеНастройки.Свойство("ДанныеОтчета") И СохраняемыеНастройки.ДанныеОтчета.Свойство("ГруппировкаДоп") И СохраняемыеНастройки.ДанныеОтчета.ГруппировкаДоп.Колонки.Найти("Оформление")<>Неопределено Тогда
		СохраняемыеНастройки.ДанныеОтчета.ГруппировкаДоп.Колонки.Удалить("Оформление");
		Для Каждого ЭлементСтруктуры Из СохраняемыеНастройки.ДанныеОтчета Цикл
			ЗначениеСтруктуры =  СохраняемыеНастройки.ДанныеОтчета[ЭлементСтруктуры.Ключ];
			Попытка
				ХранилищеЗ = Новый ХранилищеЗначения(ЗначениеСтруктуры);
			Исключение
//				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("ДанныеОтчета."+ЭлементСтруктуры.Ключ);
				СохраняемыеНастройки.ДанныеОтчета.Удалить(ЭлементСтруктуры.Ключ);
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
КонецЕсли;
КонецЕсли;


	Если СохраняемыеНастройки.Свойство("История") Тогда
		СохраняемыеНастройки.Удалить("История");
	КонецЕсли;
	Для Каждого ЭлементСтруктуры Из СохраняемыеНастройки Цикл
		ЗначениеСтруктуры =  СохраняемыеНастройки[ЭлементСтруктуры.Ключ];
		Попытка
			ХранилищеЗ = Новый ХранилищеЗначения(ЗначениеСтруктуры);
		Исключение
//			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ЭлементСтруктуры.Ключ);
			СохраняемыеНастройки.Удалить(ЭлементСтруктуры.Ключ);
		КонецПопытки;
	КонецЦикла;
	
	ОбъектСохраненнаяНастройка.ХранилищеНастроек = Новый ХранилищеЗначения(СохраняемыеНастройки);
	
	Попытка
		ОбъектСохраненнаяНастройка.Записать();
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке("Настройка формы не записана:" + Символы.ПС + "- " + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

#Если Клиент Тогда
	
// Процедура открывает форму выбора настройки форм.
//
// Параметры:
//  ФормаВладелец            - Форма - форма, которая является владельцем формы выбора.
//  НастраиваемыйОбъект       - объект, для которой выбирается настройка.
//  РежимСохраненияНастройки - Истина - форма открыта для выбора сохраняемой настройки.
//
Процедура ВыбратьНастройкуФормы(СохраненнаяНастройка, ФормаВладелец, НастраиваемыйОбъект, РежимСохраненияНастройки, Наименование = "") Экспорт

	ФормаВыбора = Справочники.СохраненныеНастройки.ПолучитьФормуВыбора(?(ТиповыеОтчеты.ОтчетПодсистемыЗУП(Сред(НастраиваемыйОбъект,Найти(НастраиваемыйОбъект,".")+1)),"ФормаВыбораЗУП","ФормаВыбора"), ФормаВладелец);
	ФормаВыбора.Отбор.НастраиваемыйОбъект.Установить(НастраиваемыйОбъект);
	Если Найти(НастраиваемыйОбъект, "ОтчетОбъект") > 0 Тогда
		ФормаВыбора.Отбор.ТипНастройки.Установить(Перечисления.ТипыНастроек.НастройкиОтчета);
	КонецЕсли;
	ФормаВыбора.Отбор.НастраиваемыйОбъект.Установить(НастраиваемыйОбъект);
	ФормаВыбора.Отбор.Ссылка.Использование = Истина;
	ФормаВыбора.Отбор.Ссылка.ВидСравнения = ВидСравнения.ВСписке;
	СписокДоступныхВариантов = ТиповыеОтчеты.ПолучитьСписокДоступныхВариантов(НастраиваемыйОбъект);
	Для Каждого ДоступныйВариант Из СписокДоступныхВариантов Цикл
		Если ДоступныйВариант.Пометка Тогда
			ФормаВыбора.Отбор.Ссылка.Значение.Добавить(ДоступныйВариант.Значение);
		КонецЕсли;
	КонецЦикла;
	// - БК - начало
	ФормаВыбора.Наименование             = Наименование;
	// - БК - конец
	ФормаВыбора.ПараметрТекущаяСтрока    = СохраненнаяНастройка;
	ФормаВыбора.РежимСохраненияНастройки = РежимСохраненияНастройки;
	ФормаВыбора.ОткрытьМодально();
	ФормаВыбора.Активизировать();

КонецПроцедуры

// Возвращает настройки отбора списка в виде таблицы
//
// Параметры:
//  Отбор - (Отбор) - отбор, по которому строится таблица
//
// Возвращаемое значение:
//  (ТаблицаЗначений) - таблица с значениями отбора
//
Функция ПолучитьНастройкуОтбораСписка(Отбор) Экспорт

	// Сохранение настроек отборов
	ТаблицаНастроек = Новый ТаблицаЗначений();

	ТаблицаНастроек.Колонки.Добавить("ИмяОтбора");
	ТаблицаНастроек.Колонки.Добавить("Использование");
	ТаблицаНастроек.Колонки.Добавить("ВидСравнения");
	ТаблицаНастроек.Колонки.Добавить("Значение");
	ТаблицаНастроек.Колонки.Добавить("ЗначениеС");
	ТаблицаНастроек.Колонки.Добавить("ЗначениеПо");

	Для Каждого ЭлементОтбора Из Отбор Цикл
		СтрокаПараметров = ТаблицаНастроек.Добавить();

		СтрокаПараметров.ИмяОтбора     = ЭлементОтбора.Имя;
		СтрокаПараметров.Использование = ЭлементОтбора.Использование;
		СтрокаПараметров.ВидСравнения  = ЭлементОтбора.ВидСравнения;
		СтрокаПараметров.Значение      = ЭлементОтбора.Значение;
		СтрокаПараметров.ЗначениеС     = ЭлементОтбора.ЗначениеС;
		СтрокаПараметров.ЗначениеПо    = ЭлементОтбора.ЗначениеПо;
	КонецЦикла;

	Возврат ТаблицаНастроек;

КонецФункции

// Возвращает натсройки сортировок списка в виде таблицы
//
// Параметры:
//  Порядок - (Порядок) - Порядок, по которому строится таблица
//
// Возвращаемое значение:
//  (ТаблицаЗначений) - таблица с значениями сортировок
//
Функция ПолучитьНастройкуПорядкаСписка(Порядок) Экспорт

	// Сохранение настроек сортировок
	ТаблицаНастроек = Новый ТаблицаЗначений();

	ТаблицаНастроек.Колонки.Добавить("Данные");
	ТаблицаНастроек.Колонки.Добавить("Направление");

	Для Каждого ЭлементПорядка Из Порядок Цикл
		СтрокаПараметров = ТаблицаНастроек.Добавить();

		СтрокаПараметров.Данные      = ЭлементПорядка.Данные;
		СтрокаПараметров.Направление = ЭлементПорядка.Направление;
	КонецЦикла;

	Возврат ТаблицаНастроек;

КонецФункции

// Возвращает натсройки колонок списка в виде таблицы.
//
// Параметры:
//  Колонки - (Колонки) - колонки списка, по которым строится таблица
//
// Возвращаемое значение:
//  (ТаблицаЗначений) - таблица с значениями настройк
//
Функция ПолучитьНастройкуКолонокСписка(Колонки) Экспорт

	// Сохранение настроек отборов
	ТаблицаНастроек = Новый ТаблицаЗначений();

	ТаблицаНастроек.Колонки.Добавить("ИмяКолонки");
	ТаблицаНастроек.Колонки.Добавить("Видимость");
	ТаблицаНастроек.Колонки.Добавить("Положение");
	ТаблицаНастроек.Колонки.Добавить("ИзменениеРазмера");
	ТаблицаНастроек.Колонки.Добавить("Ширина");
	ТаблицаНастроек.Колонки.Добавить("ВысотаЯчейки");
	ТаблицаНастроек.Колонки.Добавить("АвтоВысотаЯчейки");

	Для Каждого Колонка Из Колонки Цикл
		СтрокаПараметров = ТаблицаНастроек.Добавить();

		СтрокаПараметров.ИмяКолонки       = Колонка.Имя;
		СтрокаПараметров.Видимость        = Колонка.Видимость;
		СтрокаПараметров.Положение        = Колонка.Положение;
		СтрокаПараметров.ИзменениеРазмера = Колонка.ИзменениеРазмера;
		СтрокаПараметров.Ширина           = Колонка.Ширина;
		СтрокаПараметров.ВысотаЯчейки     = Колонка.ВысотаЯчейки;
		СтрокаПараметров.АвтоВысотаЯчейки = Колонка.АвтоВысотаЯчейки;
	КонецЦикла;

	Возврат ТаблицаНастроек;

КонецФункции
 
// Устанавливает настройки отбора списка по сохраненным значениям из таблицы.
//
// Параметры:
//  СтруктураНастроек - Структура - структура применяемых настроек.
//  КлючЗначения      - Строка - ключ применяемой настройки.
//  Отбор             - (Отбор) - Настройка отбора формы
//
Процедура ПрименитьНастройкуОтбораСписка(СтруктураНастроек, КлючЗначения, Отбор) Экспорт
	
	Перем ТаблицаНастроек;
	
	Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
		СтруктураНастроек.Свойство(КлючЗначения, ТаблицаНастроек);
	КонецЕсли;
	
	Если ТаблицаНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементОтбора Из Отбор Цикл
		СтрокаТаблицы = ТаблицаНастроек.Найти(ЭлементОтбора.Имя , "ИмяОтбора");
		
		Если СтрокаТаблицы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначения.УстановитьЗначение(ЭлементОтбора.Использование, СтрокаТаблицы.Использование);
		Если ЭлементОтбора.ТипЗначения.СодержитТип(Тип("Строка")) И ЭлементОтбора.ТипЗначения.КвалификаторыСтроки.Длина = 0 Тогда
			Если СтрокаТаблицы.ВидСравнения = ВидСравнения.НеРавно или СтрокаТаблицы.ВидСравнения = ВидСравнения.НеСодержит Тогда
				ОбщегоНазначения.УстановитьЗначение(ЭлементОтбора.ВидСравнения,  ВидСравнения.НеСодержит);
			Иначе
				ОбщегоНазначения.УстановитьЗначение(ЭлементОтбора.ВидСравнения,  ВидСравнения.Содержит);
			КонецЕсли;
		Иначе
			ОбщегоНазначения.УстановитьЗначение(ЭлементОтбора.ВидСравнения,  СтрокаТаблицы.ВидСравнения);
		КонецЕсли;
		
		Если ТипЗнч(ЭлементОтбора.Значение) = Тип("СписокЗначений") Тогда
			Если ТипЗнч(СтрокаТаблицы.Значение) = Тип("СписокЗначений") И СтрокаТаблицы.Значение.ТипЗначения = ЭлементОтбора.ТипЗначения Тогда
				ЭлементОтбора.Значение = СтрокаТаблицы.Значение;
			КонецЕсли;
		Иначе
			ОбщегоНазначения.УстановитьЗначение(ЭлементОтбора.Значение, ЭлементОтбора.ТипЗначения.ПривестиЗначение(СтрокаТаблицы.Значение));
		КонецЕсли;
		
		ОбщегоНазначения.УстановитьЗначение(ЭлементОтбора.ЗначениеС,  ЭлементОтбора.ТипЗначения.ПривестиЗначение(СтрокаТаблицы.ЗначениеС));
		ОбщегоНазначения.УстановитьЗначение(ЭлементОтбора.ЗначениеПо, ЭлементОтбора.ТипЗначения.ПривестиЗначение(СтрокаТаблицы.ЗначениеПо));
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает настройки сортировки списка по сохраненным значениям из таблицы
//
// Параметры:
//  СтруктураНастроек - Структура - структура применяемых настроек.
//  КлючЗначения      - Строка - ключ применяемой настройки.
//  Порядок           - (Порядок) - настройка порядка формы.
//
Процедура ПрименитьНастройкуПорядкаСписка(СтруктураНастроек, КлючЗначения, Порядок, НастройкаПорядка) Экспорт

	Перем ТаблицаНастроек;
	
	Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
		СтруктураНастроек.Свойство(КлючЗначения, ТаблицаНастроек);
	КонецЕсли;

	Если ТаблицаНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтрокаПорядка = "";

	Для каждого СтрокаТаблицы Из ТаблицаНастроек Цикл
		Если НастройкаПорядка.Найти(СтрокаТаблицы.Данные) = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		СтрокаПорядка = СтрокаПорядка + СтрокаТаблицы.Данные;
		
		Если СтрокаТаблицы.Направление = НаправлениеСортировки.Убыв Тогда
			СтрокаПорядка = СтрокаПорядка + " Убыв";
		КонецЕсли;
		
		СтрокаПорядка = СтрокаПорядка + ",";
	КонецЦикла;

	СтрокаПорядка = Лев(СтрокаПорядка, СтрДлина(СтрокаПорядка) - 1);

	Если СтрокаПорядка <> "" Тогда
		Порядок.Установить(СтрокаПорядка);
	КонецЕсли;

КонецПроцедуры

// Устанавливает настройки колонок списка по сохраненным значениям из таблицы.
//
// Параметры:
//  СтруктураНастроек - Структура - структура применяемых настроек.
//  КлючЗначения      - Строка - ключ применяемой настройки.
//  Колонки           - (Колонки) - настраиваемые колонки списка.
//
Процедура ПрименитьНастройкуКолонокСписка(СтруктураНастроек, КлючЗначения, Колонки) Экспорт

	Перем ТаблицаНастроек;
	
	Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
		СтруктураНастроек.Свойство(КлючЗначения, ТаблицаНастроек);
	КонецЕсли;

	Если ТаблицаНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Для каждого СтрокаТаблицы Из ТаблицаНастроек Цикл
		Колонка = Колонки.Найти(СтрокаТаблицы.ИмяКолонки);
		
		Если Колонка = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Колонки.Сдвинуть(Колонка, ТаблицаНастроек.Индекс(СтрокаТаблицы) - Колонки.Индекс(Колонка));
		
		Колонка.Видимость        = СтрокаТаблицы.Видимость;
		Колонка.Положение        = СтрокаТаблицы.Положение;
		Колонка.ИзменениеРазмера = ИзменениеРазмераКолонки.Изменять; // это чтобы сработало присвоение ширины
		Колонка.Ширина           = СтрокаТаблицы.Ширина;
		Колонка.ИзменениеРазмера = СтрокаТаблицы.ИзменениеРазмера;
		Колонка.ВысотаЯчейки     = СтрокаТаблицы.ВысотаЯчейки;
		Колонка.АвтоВысотаЯчейки = СтрокаТаблицы.АвтоВысотаЯчейки;
	КонецЦикла;

КонецПроцедуры

// Проверяет, были ли изменены указанные реквизиты объекта.
// Предназначена для установки Отказ в проверках перед записью объекта.
//
// Параметры:
//  Объект    - проверяемый объект.
//  Реквизиты - строка - список проверяемых реквизитов через запятую.
//  Отказ     - устанавливается в Истина, если реквизит был изменен.
//
Процедура ОтказПриИзмененииРеквизитов(Объект, Знач Реквизиты, Отказ) Экспорт
	
	Если ПустаяСтрока(Реквизиты) Тогда
		Возврат;
		
	ИначеЕсли Объект.Ссылка.Пустая() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура(Реквизиты);
	
	Для каждого ЭлементСтруктуры Из СтруктураРеквизитов Цикл
		ИмяРеквизита = ЭлементСтруктуры.Ключ;
		
		Если Объект[ИмяРеквизита] <> Объект.Ссылка[ИмяРеквизита] Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьВидСписка(ТабличноеПоле, Пользователь) Экспорт
	
	ТабличноеПоле.РежимВыделения = РежимВыделенияТабличногоПоля.Одиночный;
	
КонецПроцедуры

// Функция проверяет, переданный параметр, пустой он, или нет.
//
// Параметры:
//  Параметр - параметр, значение которого проверяется.
//
// Возвращаемое значение:
//  Истина - параметр пустой;
//  Ложь   - параметр не пустой.
//
Функция ПустоеЗначениеЗначения(Значение) Экспорт

	МассивТипов    = Новый Массив(1);
	МассивТипов[0] = ТипЗнч(Значение);

	ОписаниеТипов  = Новый ОписаниеТипов(МассивТипов,,,);

	Если Значение = ОписаниеТипов.ПривестиЗначение() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Процедура УстановитьДоступностьОтбора(ТабличноеПоле, Знач ОбязательныеОтборы = "") Экспорт
	
	ОбязательныеОтборы = Врег(ОбязательныеОтборы) + ", ";

	Для каждого ЭлементУправленияОтбором Из ТабличноеПоле.НастройкаОтбора Цикл
		ОбязательныйОтбор = Найти(ОбязательныеОтборы, Врег(ЭлементУправленияОтбором.Имя)) > 0;
		
		ЭлементУправленияОтбором.Доступность = НЕ ОбязательныйОтбор;
	КонецЦикла;

КонецПроцедуры

// Процедура устанавливает доступность всех элементов управления порядком списка.
//
// Параметры:
//  ТабличноеПоле - табличное поле списка.
//
Процедура УстановитьДоступностьПорядка(ТабличноеПоле, Доступность = Истина) Экспорт

	Для каждого ЭлементУправленияПорядком Из ТабличноеПоле.НастройкаПорядка Цикл
		ЭлементУправленияПорядком.Доступность = Доступность;
	КонецЦикла;

КонецПроцедуры

#КонецЕсли