Процедура ПослеЗакрытияВопросаЗаполнитьПоСпецификацииОчиститьТабличныеЧасти(Результат, ПараметрыДиалога) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;

	дог_УправлениеДоговорами.ОбработкаЗаполненияПоДоговоруОбработкаЗаполнения(ПараметрыДиалога.Объект, ПараметрыДиалога.Объект.ДоговорКонтрагента, Истина);
	
КонецПроцедуры

Процедура ПослеЗакрытияВопросаЗаполнитьПоСпецификации(Результат, ПараметрыДиалога) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	СписокТЧ = Новый Массив;
	Если ПараметрыДиалога.Объект.Свойство("Товары") Тогда 
		СписокТЧ.Добавить("Товары");
	КонецЕсли;
	Если ПараметрыДиалога.Объект.Свойство("Услуги") Тогда 
		СписокТЧ.Добавить("Услуги");
	КонецЕсли;
	Если ПараметрыДиалога.Объект.Свойство("ОС") Тогда 
		СписокТЧ.Добавить("ОС");
	КонецЕсли;
	Если ПараметрыДиалога.Объект.Свойство("НМА") Тогда 
		СписокТЧ.Добавить("НМА");
	КонецЕсли;
	
	Если СписокТЧ.Количество()>0 Тогда
		Для Каждого ТЧ Из СписокТЧ Цикл
			Если ПараметрыДиалога.Объект[ТЧ].Количество()>0 Тогда
				
				ОписаниеЗакрытия = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаполнитьПоСпецификацииОчиститьТабличныеЧасти", дог_УправлениеДоговорамиКлиент, ПараметрыДиалога);
				ПоказатьВопрос(ОписаниеЗакрытия, НСтр("ru = 'Табличные части при заполнении по спецификации будут очищены. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
				Возврат;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	дог_УправлениеДоговорами.ОбработкаЗаполненияПоДоговоруОбработкаЗаполнения(ПараметрыДиалога.Объект, ПараметрыДиалога.Объект.ДоговорКонтрагента, Истина);

КонецПроцедуры

Процедура ОбработатьВыборДоговора(Договор, Объект) Экспорт
	
	Если ЗначениеЗаполнено(Договор) И дог_УправлениеДоговорами.ДоговорЗарегистрирован(Договор) Тогда
		
		ПараметрыДиалога = Новый Структура("Объект", Объект);
		ОписаниеЗакрытия = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаполнитьПоСпецификации", дог_УправлениеДоговорамиКлиент, ПараметрыДиалога);
		ПоказатьВопрос(ОписаниеЗакрытия, НСтр("ru = 'Заполнить по спецификации зарегистрированного товарного договора?'"), РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;		

КонецПроцедуры


Процедура ОбработатьОтказОтПересчетаДокументаПоДоговору(ФормаДокумента, Параметры) Экспорт 
	
	Если Параметры = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеОбъекта = ФормаДокумента.Объект;
	
	// Если в форме "Цены и валюта" была нажата кнопка "Отмена" 
	// или при ответе на вопрос пересчета документа была нажата кнопка "Нет",
	// то заполним реквизиты ВалютаДокумента, КурсВзаиморасчетов и КратностьВзаиморасчетов из переданной структуры
	// и пересчитаем суммы в реквизитах, которые всегда рассчитываются в валюте регламентированного учета
	Если Параметры.Свойство("НоваяВалютаДокумента") И ДанныеОбъекта.Свойство("ВалютаДокумента")
		И Параметры.НоваяВалютаДокумента <> ДанныеОбъекта.ВалютаДокумента Тогда 
		
		СтруктураПересчетаСумм = Новый Структура;
		СтруктураПересчетаСумм.Вставить("ПерезаполнитьЦеныПоТипуЦен", Ложь);
		СтруктураПересчетаСумм.Вставить("ПересчитатьЦеныПоВалютеДокумента", Ложь);
		
		СтруктураЗначенияПриОткрытии = Новый Структура("УчитыватьНДС, СуммаВключаетНДС, УчитыватьАкциз, СуммаВключаетАкциз, КурсДокумента, КратностьДокумента", Ложь, Ложь, Ложь, Ложь, 1, 1);
		ЗаполнитьЗначенияСвойств(СтруктураЗначенияПриОткрытии, ДанныеОбъекта);
		
		Если Параметры.Свойство("ЕстьКурсВзаиморасчетов") И Параметры.ЕстьКурсВзаиморасчетов Тогда
			СтруктураЗначенияПриОткрытии.КурсДокумента      = ДанныеОбъекта.КурсВзаиморасчетов;
		КонецЕсли;
		Если Параметры.Свойство("ЕстьКратностьВзаиморасчетов") И Параметры.ЕстьКратностьВзаиморасчетов Тогда
			СтруктураЗначенияПриОткрытии.КратностьДокумента = ДанныеОбъекта.КратностьВзаиморасчетов;
		КонецЕсли;
		
		СтруктураПересчетаСумм.Вставить("ЗначенияПриОткрытии", СтруктураЗначенияПриОткрытии);
		
		СтруктураЗначенияПриОткрытии = Новый Структура("УчитыватьНДС, СуммаВключаетНДС, УчитыватьАкциз, СуммаВключаетАкциз, КурсДокумента, КратностьДокумента", Ложь, Ложь, Ложь, Ложь, 1, 1);
		ЗаполнитьЗначенияСвойств(СтруктураЗначенияПриОткрытии, ДанныеОбъекта);
		
		Если Параметры.Свойство("ЕстьКурсВзаиморасчетов") И Параметры.ЕстьКурсВзаиморасчетов Тогда
			СтруктураЗначенияПриОткрытии.КурсДокумента  	= Параметры.СтруктураКурсаВзаиморасчетов.Курс;
			ДанныеОбъекта.КурсВзаиморасчетов      			= Параметры.СтруктураКурсаВзаиморасчетов.Курс;
		КонецЕсли;
		Если Параметры.Свойство("ЕстьКратностьВзаиморасчетов") И Параметры.ЕстьКратностьВзаиморасчетов Тогда
			СтруктураЗначенияПриОткрытии.КратностьДокумента = Параметры.СтруктураКурсаВзаиморасчетов.Кратность;
			ДанныеОбъекта.КратностьВзаиморасчетов			= Параметры.СтруктураКурсаВзаиморасчетов.Кратность;
		КонецЕсли;
		
		СтруктураПересчетаСумм.Вставить("ЗначенияПриЗакрытии", СтруктураЗначенияПриОткрытии);
		ДанныеОбъекта.ВалютаДокумента      = Параметры.НоваяВалютаДокумента;
		
		// Необходимо пересчитать реквизиты, которые указываются всегда в валюте регламентированного учета, к примеру "ОборотПоРеализации"
		ИзменитьЦеныВалюту(ДанныеОбъекта, Неопределено, СтруктураПересчетаСумм);
		
	Иначе 
		Если Параметры.Свойство("ЕстьКурсВзаиморасчетов") И Параметры.ЕстьКурсВзаиморасчетов Тогда
			ДанныеОбъекта.КурсВзаиморасчетов      = Параметры.СтруктураКурсаВзаиморасчетов.Курс;
		КонецЕсли;
		
		Если Параметры.Свойство("ЕстьКратностьВзаиморасчетов") И Параметры.ЕстьКратностьВзаиморасчетов Тогда
			ДанныеОбъекта.КратностьВзаиморасчетов = Параметры.СтруктураКурсаВзаиморасчетов.Кратность;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ИзменитьЦеныВалюту(ДанныеОбъекта, СпособЗаполненияЦен, СтруктураЗначений,
							   СтруктураНеобрабатываемыхТабличныхЧастей = Неопределено) Экспорт

	// Все изменения по кнопке цена и валюта должны произойти в форме документа до вызова этой процедуры.
	// Предполагается, что ее вызов будет происходить из процедуры документа "ПослеЗакрытияФормыЦеныИВалюта"
	СписокТЧ = Новый Массив;
	
	Если ДанныеОбъекта.Свойство("Товары")
		И ТипЗнч(ДанныеОбъекта["Товары"]) = Тип("ДанныеФормыКоллекция")
		И ДанныеОбъекта["Товары"].Количество() > 0 Тогда
		СписокТЧ.Добавить("Товары");
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("Услуги")
		И ТипЗнч(ДанныеОбъекта["Услуги"]) = Тип("ДанныеФормыКоллекция")
		И ДанныеОбъекта["Услуги"].Количество() > 0 Тогда
		СписокТЧ.Добавить("Услуги");
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("ОС")
		И ТипЗнч(ДанныеОбъекта["ОС"]) = Тип("ДанныеФормыКоллекция")
		И ДанныеОбъекта["ОС"].Количество() > 0 Тогда
		СписокТЧ.Добавить("ОС");
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("НМА")
		И ТипЗнч(ДанныеОбъекта["НМА"]) = Тип("ДанныеФормыКоллекция")
		И ДанныеОбъекта["НМА"].Количество() > 0 Тогда
		СписокТЧ.Добавить("НМА");
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("ОплатаПоставщикам")
		И ТипЗнч(ДанныеОбъекта["ОплатаПоставщикам"]) = Тип("ДанныеФормыКоллекция")
		И ДанныеОбъекта["ОплатаПоставщикам"].Количество() > 0 Тогда
		СписокТЧ.Добавить("ОплатаПоставщикам");
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("Прочее")
		И ТипЗнч(ДанныеОбъекта["Прочее"]) = Тип("ДанныеФормыКоллекция")
		И ДанныеОбъекта["Прочее"].Количество() > 0 Тогда
		СписокТЧ.Добавить("Прочее");
	КонецЕсли;
	
	//обрабатываем изменения Товаров, Услуг, ВозворатнойТары и Оборудования
	Для Каждого ИмяТабличнойЧасти Из СписокТЧ Цикл

		Если ДанныеОбъекта[ИмяТабличнойЧасти].Количество() > 0 Тогда
		
			СтрокаПроверкиМетаданных = ДанныеОбъекта[ИмяТабличнойЧасти][0];
			
			Если (СтруктураНеобрабатываемыхТабличныхЧастей = Неопределено
				  ИЛИ НЕ СтруктураНеобрабатываемыхТабличныхЧастей.Свойство(ИмяТабличнойЧасти))
				  И (СтрокаПроверкиМетаданных.Свойство("Цена")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("Сумма")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("ФактурнаяСтоимость")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("ЦенаПоступления")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("СуммаПоступления")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("ЦенаПередачи")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("СуммаПередачи")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("ЦенаСтарая")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("СуммаСтарая")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("СуммаНДС")
				  ИЛИ СтрокаПроверкиМетаданных.Свойство("СуммаАкциза")) Тогда

				дог_УправлениеЦенообразованиемКлиент.ОбработатьИзмененияПоКнопкеЦеныВалюта(ДанныеОбъекта,
													  СпособЗаполненияЦен,
													  ИмяТабличнойЧасти,
													  СтруктураЗначений);
													  
			КонецЕсли;
												  
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры
